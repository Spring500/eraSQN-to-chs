;[ライセンス]パブリックドメイン
;本ファイルに係る著作権を放棄する。
;本ファイルに係る著作人格権は行使しない。
;2009/6/13 MinorShift(Emuera作者)
;2010/4/2 MinorShift(Emuera作者) ver 1.736に合わせて一部修正。

;独自のタイトル画面の例（Emuera ver 1.736以降対象）
;LOADGAME_EXが定義されていることが前提なので、SAVELOAD.ERBと同時に使用してください。
;SAVELOAD.ERBを使用しない場合、CALL LOADGAME_EXをLOADGAMEに置き換えてください。
;なお、SYSTEM_TITLEが定義されているとTITLE_LOADGAMEは呼ばれなくなります。
;使用している命令の詳細については下記のEmueraWikiを参照してください。
;http://sourceforge.jp/projects/emuera/wiki/FrontPage

@SYSTEM_TITLE
;@SYSTEM_TITLEが定義されていると、標準のタイトル画面の代わりにSYSTEM_TITLEが呼ばれる。

;このタイミングでグローバル変数を読んでおけば取りこぼしが無くなる。
;GLOBALはRESETDATAやLOADDATAによって初期化・上書きされない。
;必要に応じてコメントアウトを解除すること。
;LOADGLOBAL

;バージョン表記をLOCALS:0に作成。
;1001なら1.001、1100なら1.10と表示されるように。
;ここで使用した変数はRESETDATAかLOADDATAによってクリアされるのでローカル変数を使わなくてもよい。
LOCAL:0 = GAMEBASE_VERSION / 1000
LOCAL:1 = GAMEBASE_VERSION % 1000 / 10
LOCAL:2 = GAMEBASE_VERSION % 10
LOCALS:0 = {LOCAL:0}.%TOSTR(LOCAL:1,"00")%
SIF LOCAL:2 != 0
	LOCALS:0 = %LOCALS:0%{LOCAL:2}

;タイトル表示。
$TITLE_SELECT
DRAWLINE

ALIGNMENT CENTER
PRINTFORML %GAMEBASE_TITLE%
PRINTFORML %LOCALS%
PRINTFORML %GAMEBASE_AUTHOR%
PRINTFORML (%GAMEBASE_YEAR%)
PRINTFORML %GAMEBASE_INFO%
PRINTL
PRINTFORML %STRNAME:0%
PRINTFORML %STRNAME:1%
PRINTFORML %STRNAME:2%
PRINTFORML (%STRNAME:3%)
PRINTFORML %STRNAME:4%
ALIGNMENT LEFT

DRAWLINE

;選択肢表示。_Rename.csvで指定した値を読むことはできないのでここで指定する必要がある。
PRINTL [0] 从最初开始
PRINTL [1] 读取故事进度

$TITLE_INPUT
INPUT
IF RESULT == 0
	DRAWLINE
	RESETDATA
	;ADDDEFCHARAはeramakerの動作を再現するために存在する専用の関数です
	;他の場面ではADDCHARA、ADDSPCHARA等を使うことを勧めます
	ADDDEFCHARA
	BEGIN FIRST
	RETURN
ELSEIF RESULT == 1
	CALL LOADGAME_EX
	GOTO TITLE_SELECT
	;LOADGAME_EXでLOADを行わずに戻ってきた場合、もう一度選択しなおす。
ELSE
	CLEARLINE 1
	REUSELASTLINE 数值无效
	GOTO TITLE_INPUT
ENDIF

;SYSTEM_TITLE中でRETURNすると次に実行する行が無くなり、エラーになる。
;必ずBEGIN命令かLOADGAMEを行うこと。
