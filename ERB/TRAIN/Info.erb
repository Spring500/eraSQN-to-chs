@SHOW_STATUS
REDRAW 0

;この値をもとにCLEARLINEを行う
TFLAG:31 = LINECOUNT

;レベルリミッターチェック
CALL SETFLAG, "等级限制器"

;靴の自動脱ぎ
IF CONFIG("室内や屋外での靴の扱い") && TEQUIP:野外Play == 0 && (NUM("ターン数") == 0 || PREVCOM == 171)
	CALL SET_SHOES, TARGET
	SIF ASSI
		CALL SET_SHOES, ASSI
ENDIF

DRAWLINE
CALL SHOW_STATUS_MAIN

CALL SETFLAG, "助手相手の恋慕外し開始"

;@COMORDERで求める実行値の初期化
CVARSET TCVAR, 26, 0
CVARSET TCVAR, 27, 0

FLAG:238 = 1


@SHOW_STATUS_MAIN
#DIM LCOUNT

;季節、年表示部分
PRINTFORM {FLAG:872}年{FLAG:871}日目 %TEXTS("日付表示")%
;クリスマスツリーチェック
IF DAYEV("クリスマス期間") && NUM("クリスマスツリー")
	SETCOLOR DEF_COLOR("ハートピンク")
	PRINTFORM %UNICODE(0xD83C)%%UNICODE(0xDF84)%
	RESETCOLOR
ENDIF
PRINT  ムード：
CALL TEXT, "ムードハート"
;ムード記録
TFLAG:34 = MOOD()
PRINTL
PRINTFORM 調教者:%NAME:PLAYER%
;危険日
CALL TEXT, "生理周期", PLAYER

SIF STATE("具現", PLAYER)
	PRINT [具現]
IF PENIS(PLAYER)
	SELECTCASE SIZE("ペニス", PLAYER)
	CASE 0
		PRINTFORM [小
	CASE 1
		PRINTFORM [並
	CASE 2
		PRINTFORM [巨根
	CASE 3
		PRINTFORM [馬並み
	CASE 4
		PRINTFORM [腕並み
	CASE 5
		PRINTFORM [竜並み
	CASEELSE
		PRINTFORM [規格外
	ENDSELECT

	PRINTFORM (大きさLV{SIZE("ペニス", PLAYER)})
	SIF TALENT:PLAYER:龙之血
		PRINTFORM %UNI("龙")%
	PRINTFORM ]

	PRINTFORM Ｐ潤%TEXTS("調教パラメータのＬＶ", GET_PALAMLV(PALAM:PLAYER:Ｐ润) )%
	CALL PRINT_BAR, "ＰＡＬＡＭ", PALAM:PLAYER:Ｐ润, 0, 4
	;PRINTFORM %PALAM_NUM(PALAM:PLAYER:Ｐ润)%
ENDIF
;陥没乳首が勃起
IF COND("陥没乳首", PLAYER)
	IF CALCF("Ｂ発情", PLAYER)
		CALL PRINT_STR, "ハートピンク_[乳首露出]"
	ELSE
		PRINTFORM [陥没乳首]
	ENDIF
ENDIF
IF INSERT("インサート", PLAYER)
	VARSET LOCALS
	IF INSERT("Ｖ二本挿入", PLAYER)
		LOCALS = Ｖ二本挿入
	ELSEIF INSERT("Ａ二本挿入", PLAYER)
		LOCALS = Ａ二本挿入
	ELSEIF INSERT("二穴挿入", PLAYER)
		LOCALS = 二穴挿入
	ENDIF
	IF STRLENS(LOCALS) == 0
		IF INSERT("Ａインサート", PLAYER)
			PRINTFORM [Ａ%CONDS("体位", PLAYER)%]
		ELSEIF INSERT("尿道インサート", PLAYER)
			PRINTFORM [尿道%CONDS("体位", PLAYER)%]
		ELSE
			PRINTFORM [%CONDS("体位", PLAYER)%]
		ENDIF
	ENDIF
ENDIF
IF TEQUIP:Ｗ推到
	PRINT [Ｗ押し倒され中]
ELSEIF TEQUIP:６９式
	PRINTFORM [\@ TEQUIP:推到 ? 押し倒し # \@６９(%TRAIN_NAME_STANDARD(TEQUIP:PLAYER:６９式)%)中]
ELSEIF TEQUIP:推到 == 0
	SIF TEQUIP:亲吻
		PRINT [隙あらばキス]
ENDIF
SIF TEQUIP:高潮管理
	PRINT [てかげん]
SIF STATE("恍惚", PLAYER)
	PRINT [恍惚]
SIF STATE("ふにゃふにゃ", PLAYER)
	PRINT [ふにゃふにゃ]
IF STATE("束缚术", PLAYER)
	PRINT [バインド:
	SIF STATE("バインド：手", PLAYER)
		PRINT 手
	SIF STATE("バインド：脚", PLAYER)
		PRINT 脚
	SIF STATE("バインド：口", PLAYER)
		PRINT 口
	SIF STATE("バインド：Ｐ", PLAYER)
		PRINT Ｐ
	PRINT ]
ENDIF
SIF STATE("小根术", PLAYER)
	PRINT [ミニマム]
SIF STATE("大根术", PLAYER)
	PRINTFORM [エンラジ]
SIF STATE("高敏术", PLAYER)
	PRINT [センシブ]
SIF STATE("快感术", PLAYER)
	PRINT [ヴィジョン]
IF STATE("精强术", PLAYER)
	PRINT [ウルズグン
	;SIF STATE("精强术", PLAYER) == 2
	;	PRINT *2
	;SIF STATE("精强术", PLAYER) >= 3
	;	PRINT *3
	PRINT ]
ENDIF
IF STATE("魅强术", PLAYER)
	PRINT [シギルグン
	;SIF STATE("魅强术", PLAYER) == 2
	;	PRINT *2
	;SIF STATE("魅强术", PLAYER) >= 3
	;	PRINT *3
	PRINT ]
ENDIF
IF STATE("防强术", PLAYER)
	PRINT [エオルグン
	;SIF STATE("防强术", PLAYER) == 2
	;	PRINT *2
	;SIF STATE("防强术", PLAYER) >= 3
	;	PRINT *3
	PRINT ]
ENDIF
SIF STATE("ハードスキン", PLAYER)
	PRINT [ハードスキン]
SIF STATE("フィーブル", PLAYER)
	PRINT [フィーブル]
SIF STATE("倍化术", PLAYER)
	PRINT [トワイス]
IF STATE("悪酔い", PLAYER)
	PRINT [悪酔い]
ELSEIF STATE("ドランク", PLAYER)
	PRINT [ドランク]
ENDIF
SELECTCASE TEQUIP:PLAYER:媚药
CASE IS >= 200
	PRINT [媚薬(激)]
CASE IS >= 150
	PRINT [媚薬(強)]
CASE IS > 50
	PRINT [媚薬]
CASE IS > 0
	PRINT [媚薬(弱)]
ENDSELECT

SIF GETBIT(TEQUIP:PLAYER:药品, 0)
	PRINT [鎮静剤]
SIF TEQUIP:PLAYER:排卵诱发剂
	PRINT [排卵誘発剤]
IF TEQUIP:命运检阅
	PRINT [運命検閲:
	LOCALS =
	SIF COND("運命検閲：愛撫")
		LOCALS += "愛撫＆"
	SIF COND("運命検閲：アソコ")
		LOCALS += "アソコ＆"
	SIF COND("運命検閲：胸")
		LOCALS += "胸＆"
	SIF COND("運命検閲：キス")
		LOCALS += "キス＆"
	SIF COND("運命検閲：アナル")
		LOCALS += "アナル＆"
	SIF COND("運命検閲：詠唱")
		LOCALS += "詠唱＆"
	STRLENS LOCALS
	SUBSTRING LOCALS, 0, RESULT - 2
	PRINTFORM %RESULTS%]
ENDIF
SIF TEQUIP:MASTER:薰香
	PRINTFORM [%TEXTS("薰香", TEQUIP:MASTER:薰香)%]

;SIF ITEM:精液储存量
;	PRINTFORM 　（魔法瓶：{ITEM:精液储存量}）
PRINTL

;射精ゲージ設定
FOR LCOUNT, 1 , CHARANUM
	SELECTCASE LCOUNT
	CASE MASTER, ASSI, PLAYER, PLAYER:1, TARGET, TARGET:1
		CALL SETFLAG, "射精ゲージ", LCOUNT
	ENDSELECT
NEXT

CALL SHOW_STATUS_CHARA, TARGET
IF TEQUIP:Ｗ推到
	PRINTL
	CALL SHOW_STATUS_CHARA, TARGET:1
ENDIF

IF PENIS(MASTER)
	PRINT 主人：射精
ELSE
	PRINT 主人：絶頂
ENDIF
CALL PRINT_BAR, "溜めゲージ", BASE:MASTER:绝顶, MAXBASE:MASTER:绝顶, 26
PRINTFORM ({BASE:MASTER:绝顶}/{MAXBASE:MASTER:绝顶})

;主人精力表示
PRINTFORM 　精力({BASE:MASTER:精力}/{MAXBASE:MASTER:精力})
SIF MAXBASE:MASTER:STOCK >= 1
	PRINTFORM ×{BASE:MASTER:STOCK}/{MAXBASE:MASTER:STOCK}
;溜まっている
SIF BASE:MASTER:精力 >= MAXBASE:MASTER:精力 && BASE:MASTER:STOCK >= MAXBASE:MASTER:STOCK && PENIS(MASTER)
	CALL PRINT_COLORTEXT, "(たまっている) ", DEF_COLOR("ハートピンク")
PRINTL
;助手がいて、ダウンしていないなら表示
IF ASSI && STATE("撃沈", ASSI) == 0 && TEQUIP:Ｗ推到 == 0
	IF PENIS(ASSI)
		PRINTFORM %TEXTS("助手の名称")%：射精
	ELSE
		PRINTFORM %TEXTS("助手の名称")%：絶頂
	ENDIF
	CALL PRINT_BAR, "溜めゲージ", BASE:ASSI:绝顶, MAXBASE:ASSI:绝顶, 26
	PRINTFORM ({BASE:ASSI:绝顶}/{MAXBASE:ASSI:绝顶})
	;助手精力表示
	PRINTFORM 　精力({BASE:ASSI:精力}/{MAXBASE:ASSI:精力})
	;溜まっている
	SIF BASE:ASSI:精力 >= MAXBASE:ASSI:精力 && BASE:ASSI:STOCK >= MAXBASE:ASSI:STOCK && PENIS(ASSI)
		CALL PRINT_COLORTEXT, "(たまっている) ", DEF_COLOR("ハートピンク")
	PRINTL
ENDIF

IF TEQUIP:触手 && FLAG:87 != 1
	PRINT 触手：射精
	CALL PRINT_BAR, "溜めゲージ", BASE:MASTER:触手, MAXBASE:MASTER:触手, 32
	PRINTFORML ({BASE:MASTER:触手}/{MAXBASE:MASTER:触手})
ENDIF

IF TALENT:龙骑兵 + TALENT:MASTER:龙骑兵
	SIF MAXBASE:MASTER:龙 != 50000
		MAXBASE:MASTER:龙 = 50000
	IF BASE:MASTER:龙
		PRINT 射精（ドラ）
		CALL PRINT_BAR, "溜めゲージ", BASE:MASTER:龙, MAXBASE:MASTER:龙, 32
		PRINTFORML ({BASE:MASTER:龙}/{MAXBASE:MASTER:龙})
	ENDIF
ELSEIF ASSI
	IF TALENT:ASSI:龙骑兵
		SIF MAXBASE:MASTER:龙 != 50000
			MAXBASE:MASTER:龙 = 50000
		IF BASE:MASTER:龙
			PRINT 射精（ドラ）
			CALL PRINT_BAR, "溜めゲージ", BASE:MASTER:龙, MAXBASE:MASTER:龙, 32
			PRINTFORML ({BASE:MASTER:龙}/{MAXBASE:MASTER:龙})
		ENDIF
	ENDIF
ENDIF


@SHOW_STATUS_CHARA, ARG
PRINTFORM %NAME:ARG%
CALL TEXT, "生理周期", ARG
;ご機嫌
SELECTCASE MARK:ARG:情绪？
CASE 1
	PRINT (様子見)
CASE 2
	PRINT (やる気なし)
ENDSELECT
;これはTARGETの数値を見ればよい
IF TEQUIP:Ｗ推到
	PRINTFORM (好:{CFLAG:2} 奉:{EXP:ARG:侍奉快乐经验})
ELSE
	PRINTFORM (好感度:{CFLAG:2} 奉仕快楽:{EXP:ARG:侍奉快乐经验})
ENDIF

CALL SHOW_EQUIP_2, ARG

BASE:ARG:体力 = MAX(BASE:ARG:体力, 0)
BASE:ARG:气力 = MAX(BASE:ARG:气力, 0)

PRINT 体力
CALL PRINT_BAR, "減少ゲージ", BASE:ARG:体力, MAXBASE:ARG:体力, 32
PRINTFORML ({BASE:ARG:体力}/{MAXBASE:ARG:体力})
PRINT 気力
CALL PRINT_BAR, "減少ゲージ", BASE:气力, MAXBASE:气力, 32
PRINTFORM ({BASE:ARG:气力}/{MAXBASE:ARG:气力})
;精力表示
PRINTFORML 精力({BASE:ARG:精力}/{MAXBASE:ARG:精力})

CALL NEW_PRINT_PALAM, ARG

IF PENIS(ARG)
	PRINTFORM 射精
	CALL PRINT_BAR, "溜めゲージ", BASE:ARG:绝顶, MAXBASE:ARG:绝顶, 24
	PRINTFORM ({BASE:ARG:绝顶}/{MAXBASE:ARG:绝顶})　
ENDIF
IF TALENT:ARG:母乳体质
	SIF MAXBASE:ARG:喷乳 == 0
		MAXBASE:ARG:喷乳 = 10000
	PRINTFORM 母乳
	CALL PRINT_BAR, "溜めゲージ", BASE:ARG:喷乳, MAXBASE:ARG:喷乳, 24
	PRINTFORM ({BASE:ARG:喷乳}/{MAXBASE:ARG:喷乳})
ENDIF

SIF PENIS(ARG) || TALENT:ARG:母乳体质
	PRINTL

IF EX:ARG:处女丧失 || EX:ARG:Ｃ绝顶 || EX:ARG:尿道绝顶 || EX:ARG:Ｖ绝顶 || EX:ARG:Ａ绝顶 || EX:ARG:Ｂ绝顶 || EX:ARG:Ｍ绝顶 || EX:ARG:射精 || EX:ARG:喷乳 || EX:ARG:能量吸收
	PRINT <
	SIF EX:ARG:处女丧失
		CALL PRINT_COLORTEXT, "処女喪失 ", DEF_COLOR("ハートピンク")
	SIF EX:ARG:尿道绝顶
		PRINTFORM 尿道絶頂:{EX:ARG:尿道绝顶}回
	SIF EX:ARG:Ｃ绝顶
		PRINTFORM Ｃ絶頂:{EX:ARG:Ｃ绝顶}回
	SIF EX:ARG:Ｖ绝顶
		PRINTFORM Ｖ絶頂:{EX:ARG:Ｖ绝顶}回
	SIF EX:ARG:Ａ绝顶
		PRINTFORM Ａ絶頂:{EX:ARG:Ａ绝顶}回
	SIF EX:ARG:Ｂ绝顶
		PRINTFORM Ｂ絶頂:{EX:ARG:Ｂ绝顶}回
	SIF EX:ARG:Ｍ绝顶
		PRINTFORM Ｍ絶頂:{EX:ARG:Ｍ绝顶}回
	SIF EXP_GAIN(3, ARG)
		PRINTFORM 射精:{EXP_GAIN(3, ARG)}回
	SIF EXP_GAIN(54, ARG)
		PRINTFORM 噴乳:{EXP_GAIN(54, ARG)}回
	SIF EX:ARG:能量吸收
		PRINTFORM ＥＤ:{EX:ARG:能量吸收}回
	PRINT >
	PRINTL
ENDIF

IF PALAM:ARG:Extacy
	;オバフロ対策
	IF PALAM:Extacy >= 10000000000000000
		PRINTFORM Extacy:１京ＯＶＥＲ！
	ELSE
		PRINTFORM Extacy:{PALAM:ARG:Extacy}
	ENDIF
	SELECTCASE PALAM:ARG:连续绝顶COMBO数
	CASE IS >= 100
		PRINTFORM … 連続絶頂 {PALAM:ARG:连续绝顶COMBO数}COMBO！！！！
	CASE IS >= 50
		PRINTFORM … 連続絶頂 {PALAM:ARG:连续绝顶COMBO数}COMBO！！！
	CASE IS >= 20
		PRINTFORM … 連続絶頂 {PALAM:ARG:连续绝顶COMBO数}COMBO！！
	CASE IS >= 10
		PRINTFORM … 連続絶頂 {PALAM:ARG:连续绝顶COMBO数}COMBO！！
	CASE IS >= 2
		PRINTFORM … 連続絶頂 {PALAM:ARG:连续绝顶COMBO数}COMBO！
	ENDSELECT
	PRINTL
ENDIF

CALL CHECK_SATISFACTION, ARG

;性欲
IF MAXBASE:ARG:性欲
	PRINT 性欲
	IF BASE:ARG:性欲 >= MAXBASE:ARG:性欲
		PRINT [
		IF BASE:ARG:性欲 >= MAXBASE:ARG:性欲*2
			SETCOLOR DEF_COLOR("ハートピンク")
			PRINT 大満足！
		ELSE
			SETCOLOR DEF_COLOR("イエロー")
			PRINT 満足！
		ENDIF
		RESETCOLOR
		PRINT ]
	ELSE
		CALL PRINT_BAR, "桃色ゲージ", BASE:ARG:性欲, MAXBASE:ARG:性欲, 4
	ENDIF
	PRINTFORM ({BASE:ARG:性欲}/{MAXBASE:ARG:性欲})
ENDIF

;精液中毒
IF MAXBASE:ARG:精液欲
	;PRINT 精液欲
	PRINT 精欲
	IF BASE:ARG:精液欲 >= MAXBASE:ARG:精液欲
		PRINT [
		IF BASE:ARG:精液欲 >= MAXBASE:ARG:精液欲*2
			SETCOLOR DEF_COLOR("ハートピンク")
			PRINT 大満足！
		ELSE
			SETCOLOR DEF_COLOR("イエロー")
			PRINT 満足！
		ENDIF
		RESETCOLOR
		PRINT ]
	ELSE
		CALL PRINT_BAR, "白色ゲージ", BASE:ARG:精液欲, MAXBASE:ARG:精液欲, 4
	ENDIF
	PRINTFORM ({BASE:ARG:精液欲}/{MAXBASE:ARG:精液欲})
ENDIF
;胎内精液量
IF TALENT:ARG:男性 == 0
	;PRINTFORM 胎内精液量
	PRINTFORM 胎内
	IF COND("胎内の精液量", ARG) >= 100000
		PRINT [
		SETCOLOR DEF_COLOR("ハートピンク")
		PRINT いっぱい
		RESETCOLOR
		PRINT ]
	ELSE
		CALL PRINT_BAR, "白色ゲージ", COND("胎内の精液量", ARG), 100000, 4
	ENDIF
	SELECTCASE COND("胎内の精液量", ARG)
	CASE IS >= 10000
		PRINTFORM ({COND("胎内の精液量", ARG)/1000}kp)
	CASEELSE
		PRINTFORM ({COND("胎内の精液量", ARG)}p)
	ENDSELECT
	;PRINTFORM (妊娠確率{COND("受精確率", ARG)}％)
ENDIF

IF CONFIG("画面表示：尿意ゲージ")
	PRINT 尿意
	IF BASE:ARG:尿意 >= MAXBASE:ARG:尿意
		PRINT [
		IF BASE:ARG:尿意 >= MAXBASE:ARG:尿意*2
			SETCOLOR DEF_COLOR("ハートピンク")
			PRINT 限界！
		ELSE
			SETCOLOR DEF_COLOR("イエロー")
			PRINT 漏れそう
		ENDIF
		RESETCOLOR
		PRINT ]
	ELSE
		CALL PRINT_BAR, "白色ゲージ", BASE:ARG:尿意, MAXBASE:ARG:尿意, 4
	ENDIF
	;PRINTFORM ({BASE:ARG:尿意}/{MAXBASE:ARG:尿意})
ENDIF
IF CONFIG("画面表示：排泄欲ゲージ")
	;PRINT 排泄欲
	PRINT 排泄
	IF BASE:ARG:排泄欲 >= MAXBASE:ARG:排泄欲
		PRINT [
		SETCOLOR DEF_COLOR("ハートピンク")
		PRINT 限界！
		RESETCOLOR
		PRINT ]
	ELSE
		CALL PRINT_BAR, "白色ゲージ", BASE:ARG:排泄欲, MAXBASE:ARG:排泄欲, 4
	ENDIF
	;PRINTFORM ({BASE:ARG:排泄欲}/{MAXBASE:ARG:排泄欲})
ENDIF

PRINTL

CALL SHOW_EQUIP_1, ARG


@SHOW_EQUIP_1, ARG
;ARGが使用中の道具を表示する
VARSET LOCALS
SIF TEQUIP:ARG:调情
	LOCALS = %LOCALS%[フラート]
SIF TEQUIP:ARG:甜言蜜语 >= 1 && TEQUIP:ARG:甜言蜜语 <= 8
	LOCALS = %LOCALS%[甘い言葉]
SIF TEQUIP:ARG:排卵诱发剂
	LOCALS = %LOCALS%[排卵誘発剤]

SELECTCASE TEQUIP:ARG:媚药
CASE IS >= 200
	LOCALS = %LOCALS%[媚薬(激)]
CASE IS >= 150
	LOCALS = %LOCALS%[媚薬(強)]
CASE IS > 50
	LOCALS = %LOCALS%[媚薬]
CASE IS > 0
	LOCALS = %LOCALS%[媚薬(弱)]
ENDSELECT

SIF TEQUIP:ARG:利尿剂
	LOCALS = %LOCALS%[利尿剤]
IF TEQUIP:ARG:肌肉松弛剂
	LOCALS = %LOCALS%[筋弛緩剤
	SIF GETBIT(TEQUIP:ARG:肌肉松弛剂, 0)
		LOCALS = %LOCALS%(Ａ)
	SIF GETBIT(TEQUIP:ARG:肌肉松弛剂, 1)
		LOCALS = %LOCALS%(尿道)
	SIF GETBIT(TEQUIP:ARG:肌肉松弛剂, 2)
		LOCALS = %LOCALS%(Ｖ)
	SIF GETBIT(TEQUIP:ARG:肌肉松弛剂, 3)
		LOCALS = %LOCALS%(子宮口)
	LOCALS = %LOCALS%]
ENDIF
IF TEQUIP:ARG:按摩棒 && TEQUIP:ARG:触手
	IF SIZE("按摩棒", ARG) >= 3
		LOCALS = %LOCALS%[極太触手挿入]
	ELSE
		LOCALS = %LOCALS%[触手挿入]
	ENDIF
ELSEIF TEQUIP:ARG:按摩棒
	IF COND("双頭バイブ", ARG)
		LOCALS = %LOCALS%[双頭バイブ]
	ELSE
		SELECTCASE SIZE("按摩棒", ARG)
		CASE 5
			LOCALS = %LOCALS%[馬並バイブ]
		CASE 4
			LOCALS = %LOCALS%[腕型バイブ]
		CASE 3
			LOCALS = %LOCALS%[極太バイブ]
		CASEELSE
			LOCALS = %LOCALS%[バイブ]
		ENDSELECT
	ENDIF
ENDIF

SIF TEQUIP:ARG:导管
	LOCALS = %LOCALS%[カテーテル]
SIF TEQUIP:ARG:尿道扩张棒
	LOCALS = %LOCALS%[尿道ブジー]
SIF TEQUIP:ARG:尿道按摩棒
	LOCALS = %LOCALS%[尿道バイブ]

IF TEQUIP:ARG:肛用按摩棒 && TEQUIP:ARG:触手
	LOCALS = %LOCALS%[Ａ触手挿入]
ELSEIF TEQUIP:ARG:肛用按摩棒
	IF COND("アナルパール", ARG)
		LOCALS = %LOCALS%[アナルパール]
	ELSE
		SELECTCASE SIZE("Ａバイブ", ARG)
		CASE 5
			LOCALS = %LOCALS%[Ａ馬並バイブ]
		CASE 4
			LOCALS = %LOCALS%[Ａ腕型バイブ]
		CASE 3
			LOCALS = %LOCALS%[Ａ極太バイブ]
		CASEELSE
			LOCALS = %LOCALS%[アナルバイブ]
		ENDSELECT
	ENDIF
ENDIF

IF TEQUIP:ARG:震动式阴蒂夹 && TEQUIP:ARG:触手
	LOCALS = %LOCALS%[触手クリ責め]
ELSEIF TEQUIP:ARG:震动式阴蒂夹
	LOCALS = %LOCALS%[クリキャップ]
ENDIF

IF TEQUIP:ARG:震动式乳头夹 && TEQUIP:ARG:触手
	LOCALS = %LOCALS%[触手乳首責め]
ELSEIF TEQUIP:ARG:震动式乳头夹
	LOCALS = %LOCALS%[ニプルキャップ]
ENDIF

IF TEQUIP:ARG:榨乳机 && TEQUIP:ARG:触手
	LOCALS = %LOCALS%[触手搾乳]
ELSEIF TEQUIP:ARG:榨乳机
	LOCALS = %LOCALS%[搾乳器]
ENDIF

IF TEQUIP:ARG:飞机杯 && TEQUIP:ARG:触手
	LOCALS = %LOCALS%[触手ペニス責め]
ELSEIF TEQUIP:ARG:飞机杯
	LOCALS = %LOCALS%[オナホール]
ENDIF

SIF TEQUIP:ARG:跳蛋插入
	LOCALS = %LOCALS%[ローター挿入中]

IF TEQUIP:ARG:束缚 && TEQUIP:ARG:触手
	LOCALS = %LOCALS%[触手緊縛]
ELSEIF TEQUIP:ARG:束缚
	LOCALS = %LOCALS%[%BINDS(TEQUIP:ARG:束缚)%]
ENDIF

IF COND("精液浣腸", ARG)
	LOCALS = %LOCALS%[精液浣腸]
ELSEIF TEQUIP:ARG:灌肠 && TEQUIP:ARG:触手
	LOCALS = %LOCALS%[触手浣腸]
ELSEIF TEQUIP:ARG:灌肠
	IF COND("温泉浣腸", ARG)
		LOCALS = %LOCALS%[温泉浣腸
	ELSEIF COND("空気浣腸", ARG)
		LOCALS = %LOCALS%[空気浣腸
	ELSE
		LOCALS = %LOCALS%[浣腸
	ENDIF
	SIF COND("アナルプラグ", ARG)
		LOCALS = %LOCALS%＋アナルプラグ
	SIF TEQUIP:ARG:扩张气囊 == 2
		LOCALS = %LOCALS%＋アナル拡張バルーン
	LOCALS = %LOCALS%]
ELSEIF TEQUIP:ARG:扩张气囊 == 2
	LOCALS = %LOCALS%[Ａ拡張バルーン]
ENDIF

SIF TEQUIP:ARG:扩张气囊 == 1
	LOCALS = %LOCALS%[Ｖ拡張バルーン]
SIF TEQUIP:ARG:触手口辱
	LOCALS = %LOCALS%[触手口辱]
SIF TEQUIP:ARG:眼罩
	LOCALS = %LOCALS%[アイマスク]
IF TEQUIP:ARG:口塞 == 1
	LOCALS = %LOCALS%[ボールギャグ]
ELSEIF TEQUIP:ARG:口塞 == 2
	LOCALS = %LOCALS%[リングギャグ]
ENDIF
SIF TEQUIP:ARG:淋浴
	LOCALS = %LOCALS%[シャワー]
IF TEQUIP:ARG:演奏中
	IF TEQUIP:ARG:演奏中 == 1
		LOCALS = %LOCALS%[平穏の歌]
	ELSEIF TEQUIP:ARG:演奏中 == 2
		LOCALS = %LOCALS%[ロマンスソング]
	ENDIF
ENDIF

SIF STRLENS(LOCALS) == 0
	RETURN 0

PRINTFORML 使用中(%CALLNAME:ARG%):%LOCALS%
PRINTL

@SHOW_EQUIP_2, ARG
;ARGの状況の表示
;調教対象には使わない、特殊な着脱式の道具を追加した場合はここに追加をしてください
;（場所変更、マスターや助手が使用する道具等）

IF PENIS(ARG)
	SELECTCASE SIZE("ペニス", ARG)
	CASE 0
		PRINTFORM [小
	CASE 1
		PRINTFORM [並
	CASE 2
		PRINTFORM [巨根
	CASE 3
		PRINTFORM [馬並み
	CASE 4
		PRINTFORM [腕並み
	CASE 5
		PRINTFORM [竜並み
	CASEELSE
		PRINTFORM [規格外
	ENDSELECT

	PRINTFORM (大きさLV{SIZE("ペニス", ARG)})
	SIF TALENT:ARG:龙之血
		PRINTFORM %UNI("龙")%
	PRINTFORM ]

	;PRINTFORM Ｐ潤%TEXTS("調教パラメータのＬＶ", GET_PALAMLV(PALAM:ARG:Ｐ润) )%
	;CALL PRINT_BAR, "ＰＡＬＡＭ", PALAM:ARG:Ｐ润, 0, 4
ENDIF

;陥没乳首が勃起
IF COND("陥没乳首", ARG)
	IF CALCF("Ｂ発情", ARG)
		CALL PRINT_STR, "ハートピンク_[乳首露出]"
	ELSE
		PRINTFORM [陥没乳首]
	ENDIF
ENDIF

SIF COND("発情期", ARG)
	CALL PRINT_STR, "ハートピンク_[発情中]"

SIF STATE("小根术", ARG)
	PRINT [ミニマム]
SIF STATE("大根术", ARG)
	PRINTFORM [エンラジ]
SIF STATE("高敏术", ARG)
	PRINT [センシブ]
SIF STATE("快感术", ARG)
	PRINT [ヴィジョン]
SIF STATE("精强术", ARG)
	PRINT [ウルズグン]
SIF STATE("魅强术", ARG)
	PRINT [シギルグン]
SIF STATE("防强术", ARG)
	PRINT [エオルグン]
SIF STATE("ハードスキン", ARG)
	PRINT [ハードスキン]
SIF STATE("フィーブル", ARG)
	PRINT [フィーブル]
SIF STATE("倍化术", ARG)
	PRINT [トワイス]

SIF STATE("具現", ARG)
	PRINT [具現]
IF NAMES("食事", FLAG:40) == "マタタビワイン" && BASE:ARG:种族 == 6
	PRINT [マタタビ]
ELSEIF STATE("悪酔い", ARG)
	PRINT [悪酔い]
ELSEIF STATE("ドランク", ARG)
	PRINT [ドランク]
ENDIF
SIF TEQUIP:ARG:称赞
	CALL PRINT_COLORTEXT, "[ご機嫌]", DEF_COLOR("ハートピンク")
SIF EX:ARG:忍住了没去
	CALL PRINT_COLORTEXT, "[イきそう]", DEF_COLOR("ハートピンク")
SIF TEQUIP:ARG:录像带
	PRINTFORM [撮影中(残{10 - TEQUIP:ARG:录像带 + 1}回)]
IF TEQUIP:ARG:野外Play
	SIF COND("人里", ARG)
		PRINT [人里]
	SIF COND("森林", ARG)
		PRINT [森林]
	SIF COND("花園", ARG)
		PRINT [花園]
	SIF COND("水辺", ARG)
		PRINT [水辺]
	SIF COND("海", ARG)
		PRINT [海]
ENDIF
SIF TEQUIP:ARG:馆内露出Play
	PRINT [館内]
IF TEQUIP:ARG:女王大人Play
	IF TALENT:ARG:男性
		PRINTFORM [ＳＭプレイ LV{TEQUIP:ARG:女王大人Play}]
	ELSE
		PRINTFORM [女王様プレイ LV{TEQUIP:ARG:女王大人Play}]
	ENDIF
ENDIF
SIF TEQUIP:ARG:羞耻Play
	PRINT [羞恥プレイ中]
IF TEQUIP:ARG:沐浴Play
	SELECTCASE TEQUIP:MASTER:沐浴乳
	CASE 1
		PRINT [泡風呂]
	CASE 2
		PRINT [秘湯風呂]
	CASE 3
		PRINT [炭酸風呂]
	CASE 4
		PRINT [バラ風呂]
	CASE 5
		PRINT [塩水風呂]
	CASE 6
		PRINT [ひのき風呂]
	CASE 7
		PRINT [神秘風呂]
	CASE 30
		PRINT [ローション風呂]
	CASE 31
		PRINT [媚薬風呂]
	CASE 79
		PRINT [精液風呂]
	CASEELSE
		PRINT [お風呂場]
	ENDSELECT
ENDIF
SIF TEQUIP:ARG:新妻Play
	PRINT [新妻]
SIF TEQUIP:ARG:触手
	PRINT [触手]
SIF TEQUIP:ARG:爱情圈套？
	PRINT [ラブトラップ中]
IF TEQUIP:ARG:深喉 == PLAYER
	PRINT [ディープスロート中]
ELSEIF TEQUIP:ARG:深喉
	PRINTFORM [ディープスロート中(%CALLNAME:(TEQUIP:ARG:深喉)%)]
ENDIF
;これはTARGETの値を見る
IF TEQUIP:Ｗ推到
	;PRINT [Ｗ押し倒し中]
ELSEIF TEQUIP:ARG:６９式
	PRINTFORM [\@ TEQUIP:ARG:推到 ? 押し倒し # \@６９(%TRAIN_NAME_STANDARD(TEQUIP:ARG:６９式)%)中]
ELSEIF TEQUIP:ARG:推到
	PRINT [押し倒し
	IF TEQUIP:PLAYER:束缚
		PRINT 拘束
	ELSEIF TEQUIP:ARG:颜面骑乘
		PRINT 顔面騎乗
	ENDIF
	PRINT 中]

	SIF TEQUIP:ARG:亲吻
		PRINT [隙あらばキス]
ENDIF

SIF STATE("ふにゃふにゃ", ARG)
	PRINT [ふにゃふにゃ]

IF INSERT("インサート", ARG)
	PRINT [
	VARSET LOCALS
	IF INSERT("Ｖ二本挿入", ARG)
		LOCALS = Ｖ二本挿入
	ELSEIF INSERT("Ａ二本挿入", ARG)
		LOCALS = Ａ二本挿入
	ELSEIF INSERT("二穴挿入", ARG)
		LOCALS = 二穴挿入
	ENDIF
	IF STRLENS(LOCALS) == 0
		SELECTCASE INSERT("インサート相手", ARG)
		CASE PLAYER
		CASE ASSI
			LOCALS:1 = (%TEXTS("助手の名称")%)
		CASE MASTER
			LOCALS:1 = (主人)
		ENDSELECT

		IF INSERT("Ａインサート", ARG)
			LOCALS = Ａ%CONDS("体位", ARG)%%LOCALS:1%
		ELSEIF INSERT("尿道インサート", ARG)
			LOCALS = 尿道%CONDS("体位", ARG)%%LOCALS:1%
		ELSE
			LOCALS = %CONDS("体位", ARG)%%LOCALS:1%
		ENDIF
	ENDIF
	IF COND("お気に入りの体位中", ARG)
		SETCOLOR DEF_COLOR("ハートピンク")
		LOCALS = %LOCALS%☆
	ENDIF
	PRINTFORM %LOCALS%
	RESETCOLOR
	PRINT ]
ELSEIF INSERT("挿入中", ARG)
	IF INSERT("Ａ挿入中", ARG)
		PRINTFORM [Ａ挿入中(%CALLNAME:INSERT("挿入中", ARG)%)]
	ELSEIF INSERT("尿道挿入中", ARG)
		LOCALS = [尿道挿入中(%CALLNAME:INSERT("挿入中", ARG)%)]
	ELSE
		LOCALS = [Ｖ挿入中(%CALLNAME:INSERT("挿入中", ARG)%)]
	ENDIF
ENDIF
PRINTL
;着せ替え表示
CALL PRINT_DRESS, ARG, "短縮表示"
PRINTL



@STAIN_INFO
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0
PRINTL
PRINTFORML 以下が各々の汚れの状態です。
SIF TEQUIP:推到 == 0
	PRINTFORML お望みならば、手や口の汚れ等を綺麗にすることもできます。
;誰かが真性包茎
IF COND("真性包茎") || COND("真性包茎", MASTER) || COND("真性包茎", ASSI)
	SETCOLOR DEF_COLOR("イエロー")
	PRINTFORML ※真性包茎の人はＰに常に<Ｐ恥垢>汚れが有ります。洗っても掃除しても落ちません
	RESETCOLOR
ENDIF
PRINTFORML （入浴中・お風呂上り状態だと汚れが移りづらく、奉仕調教も実行しやすくなります）
CALL STAIN_INFO_SHOW, MASTER
CALL STAIN_INFO_SHOW, TARGET
CALL STAIN_INFO_SHOW, TARGET:1
DRAWLINE
IF TEQUIP:推到
	WAIT
ELSE
	PRINTBUTTON "[Enter] 閉じる", "Enter"

	ONEINPUTS

	SELECTCASE RESULTS
	CASE "0"
		STAIN:MASTER:Ｍ = 0
	CASE "1"
		STAIN:MASTER:手 = 0
	CASE "2"
		IF CHECK_ADD_STAIN("Ｐ", MASTER)
			CALL RESET_STAIN_EX, "Ｐ", MASTER
			SIF INSERT("挿入中", MASTER)
				CALL RESET_INSERT, MASTER
		ENDIF
	CASE "3"
		STAIN:Ｍ = 0
	CASE "4"
		STAIN:手 = 0
	CASE "5"
		IF CHECK_ADD_STAIN("Ｐ", TARGET)
			CALL RESET_STAIN_EX, "Ｐ", TARGET
			SIF INSERT("挿入中", TARGET)
				CALL RESET_INSERT, TARGET
		ENDIF
	CASE "6"
		SIF ASSI
			STAIN:ASSI:Ｍ = 0
	CASE "7"
		SIF ASSI
			STAIN:ASSI:手 = 0
	CASE "8"
		IF ASSI
			IF CHECK_ADD_STAIN("Ｐ", ASSI)
				CALL RESET_STAIN_EX, "Ｐ", ASSI
				SIF INSERT("挿入中", ASSI)
					CALL RESET_INSERT, ASSI
			ENDIF
		ENDIF
	CASEELSE
		REDRAW 1
		RETURN 0
	ENDSELECT
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

REDRAW 1

@STAIN_INFO_SHOW, ARG
#DIM LCOUNT

SIF ARG <= 0
	RETURN 0
IF TEQUIP:ARG:结束淋浴
	;ここはTARGETの値を見る
	IF TARGET && TEQUIP:沐浴Play
		LOCALS = □ %NAME:ARG%（入浴中）□
	ELSE
		LOCALS = □ %NAME:ARG%（お風呂上り）□
	ENDIF
ELSE
	LOCALS = □ %NAME:ARG% □
ENDIF
CALL DRAWLINES, LOCALS

;選択肢に使う
SELECTCASE ARG
CASE MASTER
	LOCAL = 0
CASE TARGET
	LOCAL = 3
CASE TARGET:1
	LOCAL = 6
ENDSELECT

FOR LCOUNT, 0, 6
	LOCALS =
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ｖ")
		LOCALS += "<Ｖ>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ｖ恥垢")
		LOCALS += "<Ｖ恥垢>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ｐ")
		LOCALS += "<Ｐ>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ｐ恥垢")
		LOCALS += "<Ｐ恥垢>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "精液")
		LOCALS += "<精>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ａ")
		LOCALS += "<Ａ>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "乳")
		LOCALS += "<乳>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "尿")
		LOCALS += "<尿>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "触手")
		LOCALS += "<触手>"
	;SIF LCOUNT == 4 && (COND("身嗜み：腸洗浄", ARG) || EX:ARG:排泄 >= 2)
	;	LOCALS += "（洗浄済み）"

	SELECTCASE LCOUNT
	CASE 0
		PRINTFORM 　口：%TEXT_LJ(LOCALS, 32)%
		SIF TEQUIP:推到 == 0 && LOCALS != ""
			PRINTFORM [{LOCAL + LCOUNT}] 口を\@ ARG == MASTER ? すすぐ # すすがせる \@
	CASE 1
		PRINTFORM 　手：%TEXT_LJ(LOCALS, 32)%
		SIF TEQUIP:推到 == 0 && LOCALS != ""
			PRINTFORM [{LOCAL + LCOUNT}] 指を拭う
	CASE 2
		SIF PENIS(ARG) == 0
			CONTINUE
		PRINTFORM 　Ｐ：%TEXT_LJ(LOCALS, 32)%
		SIF TEQUIP:推到 == 0 && CHECK_ADD_STAIN("Ｐ", ARG)
			PRINTFORM [{LOCAL + LCOUNT}] ペニスを拭う \@ INSERT("挿入中", ARG) ? (インサートは解除) # \@
	CASE 3
		SIF TALENT:ARG:男性
			CONTINUE
		PRINTFORM 　Ｖ：%TEXT_LJ(LOCALS, 32)%
	CASE 4
		PRINTFORM 　Ａ：%TEXT_LJ(LOCALS, 32)%
	CASE 5
		;SIF TALENT:ARG:男性
		;	CONTINUE
		PRINTFORM 　Ｂ：%TEXT_LJ(LOCALS, 32)%
	ENDSELECT


	PRINTL
NEXT
PRINTL
