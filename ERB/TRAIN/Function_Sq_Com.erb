;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;主に調教中に使用する関数
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;-------------------------------------------------
;ひょっとしたら最も重要かもしれない値であるムードの計算
;最低が０で最高は160
;-------------------------------------------------
@MOOD()
#FUNCTION

;基本はTFLAG:33
LOCAL = TFLAG:33

;恭順と欲情で25まで
LOCAL:1 = PALAM:恭順 + PALAM:欲情
SELECTCASE LOCAL:1
CASE IS > 240000
	LOCAL += 25
;MAX25
CASE IS > 120000
	LOCAL += (LOCAL:1 - 120000)/24000 + 20
;MAX20
CASE IS > 60000
	LOCAL += (LOCAL:1 - 60000)/12000 + 15
;MAX15
CASE IS > 30000
	LOCAL += (LOCAL:1 - 30000)/6000 + 10
;MAX10
CASE IS > 10000
	LOCAL += (LOCAL:1 - 10000)/4000 + 5
;MAX5
CASEELSE
	LOCAL += LOCAL:1/2000
ENDSELECT

;恐怖、反感でキツいマイナス補正。抑鬱はちょっと軽いけどやはりマイナス
LOCAL -= (UP_PALAM(10) + UP_PALAM(11) )/100 + GET_PALAMLV(UP_PALAM(13) )*2

;好感度で30まで補正
SELECTCASE CFLAG:2
CASE IS > 3000
	LOCAL += LIMIT( (CFLAG:2 - 3000)/1000 + 15, 15, 30)
CASE IS > 1500
	LOCAL += (CFLAG:2 - 1500)/300 + 10
CASE IS > 500
	LOCAL += (CFLAG:2 - 500)/200 + 5
CASEELSE
	LOCAL += CFLAG:2/100
ENDSELECT

LOCAL = LIMIT(LOCAL, 0, 160)

RETURNF LOCAL

;-------------------------------------------------
;ムードの値をハートマークの個数に変換
;-------------------------------------------------
@MOOD_HEART()
#FUNCTION
LOCAL = (MOOD() + 20) / 30
RETURNF LOCAL

;-------------------------------------------------
;コマンド選択によるムード増減のチェック
;ARGにはコマンド番号が代入される（通常はSELECTCOM）
;-------------------------------------------------
@MOOD_COM, ARG
VARSET LOCAL

SELECTCASE ARG
;愛撫
CASE 0
	LOCAL += 5
;愛撫コマンド
CASE 5, 7
	LOCAL += 3
;キス
CASE 6
	LOCAL += 5
	;開幕キスはボーナス
	SIF NUM("ターン数") == 0
		LOCAL += 25
;インサート
CASE 60 TO 89
	SELECTCASE ARG
	;対面座位
	CASE 63, 73
		LOCAL += 4
	ENDSELECT

	SIF COND("お気に入りの体位中")
		LOCAL += 4
ENDSELECT

;初回選択で無いなら効果は半減
SIF CHOSECOM(SELECTCOM)
	LOCAL /= 2

TFLAG:33 += LOCAL


;-------------------------------------------------
;主にプレイが可能かどうかだが、他にも様々な内容のものを判定する
;苦痛なく受け入れられるかの判定にも多用する
;-------------------------------------------------
@CHECK(ARGS, ARG, ARG:1)
#FUNCTION
VARSET LOCAL
SIF ARG == 0 && TARGET
	ARG = TARGET

;インサートや挿入系は共通部分が多いので上下に分ける
SELECTCASE ARGS
CASE "指挿入れ", "バイブ"
	LOCAL = MAX(SIZE("バイブ", ARG), 1)
CASE "Ｖインサート"
	LOCAL = MAX(SIZE("ペニス", PLAYER), 1)
	;Ｖ性交経験がある程度必要
	SIF TALENT:ARG:好色 == 0 && EXP:ARG:Ｖ性交经验 < MAX(EXP_LV(2*(LOCAL - TALENT:ARG:好色) - ABL:ARG:Ｖ扩张), 1)
		RETURNF 0
CASE "Ａ指挿入れ", "Ａバイブ"
	LOCAL = MAX(SIZE("Ａバイブ", ARG), 1)
CASE "Ａインサート"
	LOCAL = MAX(SIZE("ペニス", PLAYER), 1)
	;Ａ性交経験がある程度必要
	SIF TALENT:ARG:好色 == 0 && EXP:ARG:Ａ性交经验 < MAX(EXP_LV(LOCAL*2 - ABL:ARG:Ａ扩张), 1)
		RETURNF 0
CASE "尿道インサート"
	LOCAL = MAX(SIZE("ペニス", PLAYER), 1)
	;尿道性交経験がある程度必要
	SIF EXP:ARG:尿道性交经验 < MAX(EXP_LV(LOCAL*2 - ABL:ARG:尿道扩张), 1)
		RETURNF 0
CASE "尿道指挿入れ", "尿道バイブ"
	LOCAL = MAX(SIZE("尿道バイブ", ARG), 1)
CASE "フィスト"
	SELECTCASE SELECTCOM
	CASE 141
		ARGS = Ｖフィスト
		LOCAL = SIZE("フィスト", PLAYER)
	CASE 142
		ARGS = Ａフィスト
		LOCAL = SIZE("フィスト", PLAYER)
	CASE 143
		ARGS = 両穴フィスト
		LOCAL = SIZE("Ｗフィスト", PLAYER)
	ENDSELECT
CASE "Ｖフィスト", "Ａフィスト"
	LOCAL = SIZE("フィスト", PLAYER)
CASE "両穴フィスト"
	LOCAL = SIZE("Ｗフィスト", PLAYER)
CASE "竜姦"
	SELECTCASE SELECTCOM
	CASE 304
		ARGS = Ｖ竜姦
	CASE 305
		ARGS = Ａ竜姦
	ENDSELECT
	LOCAL = SIZE("ドラゴン")
CASE "Ｖ竜姦", "Ａ竜姦"
	LOCAL = SIZE("ドラゴン")
ENDSELECT


SELECTCASE ARGS
CASE "指挿入れ", "バイブ", "Ｖインサート", "Ｖフィスト", "Ｖ竜姦"
	SELECTCASE ARG
	CASE PLAYER
		SIF ABL:ARG:Ｖ扩张 < LOCAL
			RETURNF 0
	CASE TARGET
		SIF CUP:ARG:快Ｖ >= 3000 && MAX(CUP:ARG:苦痛, CUP:ARG:恐怖/10, CUP:ARG:反感) < 300
			RETURNF 1
		;反感が強いとダメ
		SIF CUP:ARG:反感 >= 500
			RETURNF 0
		;好色でない人はうるさい
		IF TALENT:ARG:好色 == 0
			;侵入してくるモノを気持ちよく受け入れられるかを判定
			SIF MIN(MAX(ABL:ARG:Ｖ感觉, ABL:ARG:受虐倾向), ABL:ARG:Ｖ扩张) < MAX(LOCAL, 1)
				RETURNF 0
			;経験がある程度必要
			SIF EXP:ARG:Ｖ经验 < MAX(EXP_LV(LOCAL*2 - ABL:ARG:Ｖ扩张), 1)
				RETURNF 0
			;Ｖ潤がある程度必要
			SELECTCASE ARGS
			CASE "Ｖインサート"
				SIF PALAM:ARG:Ｖ潤 + PALAM:PLAYER:Ｐ潤 < PALAM_LV(LOCAL - ABL:ARG:Ｖ扩张 + 2)
					RETURNF 0
			CASEELSE
				SIF PALAM:ARG:Ｖ潤 < PALAM_LV(LOCAL - ABL:ARG:Ｖ扩张 + 2)
					RETURNF 0
			ENDSELECT
		ENDIF
		;痛みが少ないことが必要
		SIF CUP:ARG:苦痛 >= 500 + ABL:ARG:受虐倾向*100 + TALENT:ARG:抖Ｍ*500
			RETURNF 0
	ENDSELECT
	RETURNF 1
CASE "Ａ指挿入れ", "Ａバイブ", "Ａインサート", "Ａフィスト", "Ａ竜姦"
	SELECTCASE ARG
	CASE PLAYER
		SIF ABL:ARG:Ａ扩张 < LOCAL
			RETURNF 0
	CASE TARGET
		SIF CUP:ARG:快Ａ >= 3000 && MAX(CUP:ARG:苦痛, CUP:ARG:恐怖/10, CUP:ARG:反感) < 300
			RETURNF 1
		;反感が強いとダメ
		SIF CUP:ARG:反感 >= 500
			RETURNF 0
		;好色でない人はうるさい
		IF TALENT:ARG:好色 == 0
			;侵入してくるモノを気持ちよく受け入れられるかを判定
			SIF MIN(MAX(ABL:ARG:Ａ感觉, ABL:ARG:受虐倾向), ABL:ARG:Ａ扩张) < MAX(LOCAL, 1)
				RETURNF 0
			;経験がある程度必要
			SIF EXP:ARG:Ａ经验 < MAX(EXP_LV(LOCAL*2 - ABL:ARG:Ａ扩张), 1)
				RETURNF 0
			;Ａ潤がある程度必要
			SELECTCASE ARGS
			CASE "Ａインサート"
				SIF PALAM:ARG:Ａ潤 + PALAM:PLAYER:Ｐ潤 < PALAM_LV(LOCAL - ABL:ARG:Ａ扩张 + 2)
					RETURNF 0
			CASEELSE
				SIF PALAM:ARG:Ａ潤 < PALAM_LV(LOCAL - ABL:ARG:Ａ扩张 + 2)
					RETURNF 0
			ENDSELECT
		ENDIF
		;痛みが少ないことが必要
		SIF CUP:ARG:苦痛 >= 500 + ABL:ARG:受虐倾向*100 + TALENT:ARG:抖Ｍ*500
			RETURNF 0
	ENDSELECT
	RETURNF 1
CASE "尿道指挿入れ", "尿道バイブ"
	;快楽が強くて苦痛などが少ないならＯＫ
	SIF CUP:ARG:快Ｃ >= 3000 && MAX(CUP:ARG:苦痛, CUP:ARG:恐怖/10, CUP:ARG:反感) < 300
		RETURNF 1
	;反感が強いとダメ
	SIF CUP:ARG:反感 >= 500
		RETURNF 0
	;倒錯的でない人はうるさい
	IF TALENT:ARG:性变态 == 0
		;侵入してくるモノを気持ちよく受け入れられるかを判定
		SIF ABL:ARG:尿道扩张 + TALENT:ARG:尿道狂 <= MAX(LOCAL, 1)
			RETURNF 0
	ENDIF
	;痛みが少ないことが必要
	SIF CUP:ARG:苦痛 >= 500 + ABL:ARG:受虐倾向*100 + TALENT:ARG:抖Ｍ*500
		RETURNF 0
	RETURNF 1
CASE "尿道インサート"
	;快楽が強くて苦痛などが少ないならＯＫ
	SIF CUP:ARG:快Ｃ >= 3000 && MAX(CUP:ARG:苦痛, CUP:ARG:恐怖/10, CUP:ARG:反感) < 300
		RETURNF 1
	;反感が強いとダメ
	SIF CUP:ARG:反感 >= 500
		RETURNF 0
	;倒錯的でない人はうるさい
	IF TALENT:ARG:性变态 == 0
		;侵入してくるモノを気持ちよく受け入れられるかを判定
		SIF ABL:ARG:尿道扩张 + TALENT:ARG:尿道狂 <= MAX(LOCAL, 1)
			RETURNF 0
	ENDIF
	;痛みが少ないことが必要
	SIF CUP:ARG:苦痛 >= 500 + ABL:ARG:受虐倾向*100 + TALENT:ARG:抖Ｍ*500
		RETURNF 0
	RETURNF 1
CASE "両穴フィスト"
	SIF MIN(CUP:ARG:快Ｖ, CUP:ARG:快Ａ) >= 3000 && MAX(CUP:ARG:苦痛, CUP:ARG:恐怖/10, CUP:ARG:反感) < 300
		RETURNF 1
	;反感が強いとダメ
	SIF CUP:ARG:反感 >= 500
		RETURNF 0
	SIF MIN(MAX(ABL:ARG:Ｖ感觉, ABL:ARG:受虐倾向), ABL:ARG:Ｖ扩张) < MAX(LOCAL, 1)
		RETURNF 0
	SIF MIN(MAX(ABL:ARG:Ａ感觉, ABL:ARG:受虐倾向), ABL:ARG:Ａ扩张) < MAX(LOCAL, 1)
		RETURNF 0
	SIF MIN(EXP:ARG:Ｖ经验, EXP:ARG:Ａ经验) < EXP_LV(LOCAL)
		RETURNF 0

	SELECTCASE ARG
	CASE TARGET
		;苦痛が殆どないならＯＫ
		SIF CUP:ARG:苦痛 < 100
			RETURNF 1
		;Ｖ潤がある程度必要
		SIF PALAM:ARG:Ｖ潤 < PALAM_LV(LOCAL - ABL:ARG:Ｖ扩张 + 2)
			RETURNF 0
		;Ａ潤がある程度必要
		SIF PALAM:ARG:Ａ潤 < PALAM_LV(LOCAL - ABL:ARG:Ａ扩张 + 2)
			RETURNF 0
		;痛みが少ないことが必要
		SIF CUP:ARG:苦痛 >= 500 + ABL:ARG:受虐倾向*100 + TALENT:ARG:抖Ｍ*500
			RETURNF 0
	ENDSELECT
	RETURNF 1
CASE "Ｖ二本挿入"
	SIF ABL:Ｖ扩张 <= SIZE("Ｗペニス") || ABL:Ｖ感觉 < 3
		RETURNF 0
	RETURNF 1
CASE "Ａ二本挿入"
	SIF ABL:Ａ扩张 <= SIZE("Ｗペニス") || ABL:Ａ感觉 < 3
		RETURNF 0
	RETURNF 1
CASE "二穴挿入"
	SIF MIN(ABL:Ｖ扩张, ABL:Ａ扩张) < 2 || MIN(ABL:Ｖ感觉, ABL:Ａ感觉) < 3
		RETURNF 0
	RETURNF 1
CASE "尿道プレイ"
	;反感が強いとダメ
	SIF CUP:ARG:反感 >= 500
		RETURNF 0
	;苦痛が殆どないならＯＫ
	SIF CUP:ARG:苦痛 < 100
		RETURNF 1
	;痛みが少ないことが必要
	SIF CUP:ARG:苦痛 >= 500 + ABL:ARG:受虐倾向*100 + TALENT:ARG:抖Ｍ*500
		RETURNF 0
	RETURNF 1
CASE "喉姦"
	SIF ARG:1 == 0 && PLAYER > 0
		ARG:1 = PLAYER
	;喉まで受け入れられるかを判定。
	SIF ABL:ARG:喉扩张 <= SIZE("ペニス", ARG:1)
		RETURNF 0
	SIF TALENT:ARG:呕吐反射耐性 == 0 && EXP:ARG:口交经验 < MIN(EXP_LV(SIZE("ペニス", ARG:1) + 2), 100)
		RETURNF 0
	LOCAL = ABL:ARG:喉扩张 - SIZE("ペニス", ARG:1) + TALENT:ARG:呕吐反射耐性 + 1
CASE "口内放尿", "膣内放尿", "子宮内放尿", "温泉浣腸", "乳内放尿", "膀胱内放尿"
	SIF ABL:ARG:受虐倾向 >= 3
		RETURNF 1
	;SIF ABL:ARG:受虐倾向 + TALENT:ARG:性变态 + TALENT:ARG:淫乱 + TALENT:ARG:无视污臭*2 + TALENT:ARG:污臭钝感 - TALENT:ARG:污臭敏感 >= 5
	;	RETURNF 1
	SIF CUP:ARG:恐怖 + CUP:ARG:反感 + CUP:ARG:抑鬱 < 500
		RETURNF 1
	SIF MAX(CUP:ARG:恐怖, CUP:ARG:反感, CUP:ARG:抑鬱) < 300
		RETURNF 1
CASE "陰毛管理", "腋毛管理"
	SIF MAX(ABL:ARG:露出癖, ABL:ARG:受虐倾向) >= 3
		RETURNF 1
CASE "性別による拒絶反応"
	SIF ARG:1 == 0 && PLAYER > 0
		ARG:1 = PLAYER
	SIF TALENT:ARG:女性 && TALENT:(ARG:1):女性 && ABL:ARG:百合倾向 == 0
		RETURNF 1
	SIF TALENT:ARG:男性 && TALENT:(ARG:1):男性 && ABL:ARG:ＢＬ倾向 == 0
		RETURNF 1
;ARGのＶがARG:1のペニスにジャストフィットしている度合いを判定します
CASE "Ｖジャストフィット"
	SIF ARG:1 == 0 && PLAYER > 0
		ARG:1 = PLAYER
	SIF ABL:ARG:Ｖ技能 < 4 || TALENT:ARG:Ｖ损伤 || CALCF("Ｖ履歴：記憶母数", ARG) < 100
		RETURNF 0

	LOCAL = CALCF("Ｖ履歴：Ｐ記憶", ARG, ARG:1) * 100 / CALCF("Ｖ履歴：記憶母数", ARG)
	;50以上でジャストフィットなので、それ未満は意味が無いので0を返す
	SIF LOCAL < 50
		RETURNF 0
CASE "Ａジャストフィット"
	SIF ARG:1 == 0 && PLAYER > 0
		ARG:1 = PLAYER
	SIF ABL:ARG:Ａ技能 < 4 || TALENT:ARG:Ａ损伤 || CALCF("Ａ履歴：記憶母数", ARG) < 100
		RETURNF 0

	LOCAL = CALCF("Ａ履歴：Ｐ記憶", ARG, ARG:1) * 100 / CALCF("Ａ履歴：記憶母数", ARG)
	;50以上でジャストフィットなので、それ未満は意味が無いので0を返す
	SIF LOCAL < 50
		RETURNF 0
;Ｖに最後に入れたもののサイズを返す。入っていたバイブのサイズ判定などに
CASE "Ｖ最新受け入れサイズ"
	RETURNF TOINT(EXTSTR(CSTR:ARG:Ｖ受け入れ履歴, "：", "＠") )
CASE "Ａ最新受け入れサイズ"
	RETURNF TOINT(EXTSTR(CSTR:ARG:Ａ受け入れ履歴, "：", "＠") )
ENDSELECT
RETURNF LOCAL

;-------------------------------------------------
;コマンドに対応した装備品の値を返す関数
;CHECK_TEQUIP(31)ならTEQUIP:バイブの値を返す
;-------------------------------------------------
@CHECK_TEQUIP(ARG, ARG:1)
#FUNCTION
SIF ARG < 0
	RETURNF 0
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET
SELECTCASE ARG
CASE 31
	RETURNF TEQUIP:(ARG:1):バイブ
CASE 33
	RETURNF TEQUIP:(ARG:1):アナルバイブ
CASE 34
	RETURNF TEQUIP:(ARG:1):クリキャップ
CASE 35
	RETURNF TEQUIP:(ARG:1):ニプルキャップ
CASE 36
	RETURNF TEQUIP:(ARG:1):搾乳機
CASE 37
	RETURNF TEQUIP:(ARG:1):オナホール
CASE 38
	RETURNF TEQUIP:(ARG:1):シャワー
CASE 39
	RETURNF TEQUIP:(ARG:1):ローター挿入
CASE 133
	RETURNF TEQUIP:(ARG:1):アイマスク
CASE 134
	RETURNF TEQUIP:(ARG:1):拘束
CASE 135
	RETURNF TEQUIP:(ARG:1):口枷
CASE 136
	RETURNF TEQUIP:(ARG:1):浣腸
CASE 137
	RETURNF TEQUIP:(ARG:1):拡張バルーン
CASE 149
	RETURNF TEQUIP:(ARG:1):カテーテル
CASE 151
	RETURNF TEQUIP:(ARG:1):尿道ブジー
CASE 152
	RETURNF TEQUIP:(ARG:1):尿道バイブ
ENDSELECT
RETURNF 0

;-------------------------------------------------
;汚れ関連の関数など
;-------------------------------------------------
;-------------------------------------------------
;汚れを数値でなく名称で参照するための関数
;ARGに汚れの数値を入れ、ARGS:0〜にチェックする汚れを代入する
;例）CHECK_STAIN(STAIN:PLAYER:Ｐ, "精液", "Ａ") だとPLAYERのペニスに精液汚れorＡ汚れがあれば０以外を返す
;-------------------------------------------------
@CHECK_STAIN(ARG, ARGS:0, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6)
#FUNCTION
#DIM LCOUNT
VARSET LOCAL
FOR LCOUNT, 0, 7
	SELECTCASE ARGS:LCOUNT
	CASE "Ｖ"
		LOCAL = GETBIT(ARG, 0)
	CASE "Ｐ"
		LOCAL = GETBIT(ARG, 1)
	CASE "精液", "精"
		LOCAL = GETBIT(ARG, 2)
	CASE "Ａ"
		LOCAL = GETBIT(ARG, 3)
	CASE "乳"
		LOCAL = GETBIT(ARG, 4)
	CASE "尿"
		LOCAL = GETBIT(ARG, 5)
	CASE "触手", "触"
		LOCAL = GETBIT(ARG, 6)
	CASE "蒸れ", "蒸"
		LOCAL = GETBIT(ARG, 7)
	CASE "濡れ", "濡"
		LOCAL = GETBIT(ARG, 8)
	CASE "破瓜の血", "血"
		LOCAL = GETBIT(ARG, 10)
	CASE "Ｖ恥垢"
		LOCAL = GETBIT(ARG, 20)
	CASE "Ｐ恥垢"
		LOCAL = GETBIT(ARG, 21)
	ENDSELECT
	SIF LOCAL
		RETURNF LOCAL
NEXT
RETURNF LOCAL

;-------------------------------------------------
;汚れを数値でなく名称で参照するための関数
;キャラARGの部位ARGSに落とせる汚れが付着しているかどうかをチェックする。ARGはTARGETの場合には省略可能
;例）CHECK_ADD_STAIN("Ｖ", PLAYER)は<Ｖ>汚れ以外のものがあったら０より大きい値を返す
;-------------------------------------------------
@CHECK_ADD_STAIN(ARGS, ARG)
#FUNCTION

SIF ARG < 0
	RETURNF 0
SIF ARG == 0 && TARGET
	ARG = TARGET

;値が０なら汚れは無し
SIF STAIN:ARG:ARGS == 0
	RETURNF 0

SELECTCASE ARGS
CASE "Ｐ"
	;真性包茎はＰ恥垢があるのが通常
	IF COND("真性包茎", ARG)
		IF STAIN:ARG:Ｐ == SET_STAIN(0, "Ｐ") || STAIN:ARG:Ｐ == SET_STAIN(0, "Ｐ恥垢") || STAIN:ARG:Ｐ == SET_STAIN(0, "Ｐ") + SET_STAIN(0, "Ｐ恥垢")
		ELSE
			RETURNF 1
		ENDIF
	ELSE
		SIF STAIN:ARG:ARGS != SET_STAIN(0, ARGS)
			RETURNF 1
	ENDIF
CASEELSE
	SIF STAIN:ARG:ARGS != SET_STAIN(0, ARGS)
		RETURNF 1
ENDSELECT
RETURNF 0

;-------------------------------------------------
;汚れを数値でなく名称でセットする関数
;例）STAIN:Ｖ = SET_STAIN(STAIN:Ｖ, "精液") だとTARGETのＶに精液汚れを加える
;これはSETBIT STAIN:Ｖ, 2 や STAIN:Ｖ |= 1p2に等しいが、直感的にわかりやすくするための関数である
;もちろんSTAIN:Ｖ = SET_STAIN(STAIN:PLAYER:Ｐ, "精液")のようにも使えるが今はあまり推奨しない
;-------------------------------------------------
@SET_STAIN(ARG, ARGS)
#FUNCTION

SELECTCASE ARGS
CASE "Ｖ"
	SETBIT ARG, 0
CASE "Ｐ"
	SETBIT ARG, 1
CASE "精液", "精"
	SETBIT ARG, 2
CASE "Ａ"
	SETBIT ARG, 3
CASE "母乳", "乳"
	SETBIT ARG, 4
CASE "尿"
	SETBIT ARG, 5
CASE "触手", "触"
	SETBIT ARG, 6
CASE "蒸れ", "蒸"
	SETBIT ARG, 7
CASE "濡れ", "濡"
	SETBIT ARG, 8
CASE "破瓜の血", "血"
	SETBIT ARG, 10
CASE "Ｖ恥垢"
	SETBIT ARG, 20
CASE "Ｐ恥垢"
	SETBIT ARG, 21
ENDSELECT
RETURNF ARG

;-------------------------------------------------
;汚れをリセットするデフォルトの関数RESET_STAINの拡張版
;ARGSには"全部"、"Ｍ"、"手"、"Ｐ"、"Ｖ"、"Ａ"、"Ｂ"のどれかが使える
;ARGは対象のキャラ
;老廃物については現状だとＰとＶのみ
;-------------------------------------------------
@RESET_STAIN_EX, ARGS, ARG
SELECTCASE ARGS
CASE "全部"
	;ペニスに垢あり
	IF CHECK_STAIN(STAIN:ARG:Ｐ, "Ｐ恥垢")
		RESET_STAIN ARG
		;真性包茎は汚れたまま
		IF COND("真性包茎", ARG)
			STAIN:ARG:Ｐ = SET_STAIN(STAIN:ARG:Ｐ, "Ｐ恥垢")
		ELSE
			CLEARBIT BASE:ARG:老廃物, 0
		ENDIF
	ELSE
		RESET_STAIN ARG
		CLEARBIT BASE:ARG:老廃物, 0
	ENDIF
	CLEARBIT BASE:ARG:老廃物, 1
CASE "Ｍ"
	STAIN:ARG:Ｍ = 0
CASE "手"
	STAIN:ARG:手 = 0
CASE "Ｐ"
	;ペニスに垢あり
	IF CHECK_STAIN(STAIN:ARG:Ｐ, "Ｐ恥垢")
		STAIN:ARG:Ｐ = SET_STAIN(0, "Ｐ")
		;真性包茎は汚れたまま
		IF COND("真性包茎", ARG)
			STAIN:ARG:Ｐ = SET_STAIN(STAIN:ARG:Ｐ, "Ｐ恥垢")
		ELSE
			CLEARBIT BASE:ARG:老廃物, 0
		ENDIF
	ELSE
		STAIN:ARG:Ｐ = SET_STAIN(0, "Ｐ")
		CLEARBIT BASE:ARG:老廃物, 0
	ENDIF
CASE "Ｖ"
	STAIN:ARG:Ｖ = SET_STAIN(0, "Ｖ")
	CLEARBIT BASE:ARG:老廃物, 1
CASE "Ａ"
	STAIN:ARG:Ａ = SET_STAIN(0, "Ａ")
CASE "Ｂ"
	STAIN:ARG:Ｂ = 0
ENDSELECT

;-------------------------------------------------
;汚れを数値でなく名称でセットする関数その２
;ARGに対象のキャラ番号、ARGSに汚れを適用する部位、ARGS:1に汚れを代入して用いる
;これは服の要素を加味して汚れを加える関数だが、加えられる汚れの発生源は外側（服の上から）なのに注意すること
;例）CALL ADD_STAIN, TARGET, "Ｖ", "精液" だとTARGETのＶに精液汚れを加えるが、
;　もしもパンツなどをはいていたらその汚れはパンツのほうにつくこととなる。
;-------------------------------------------------
@ADD_STAIN, ARG, ARGS, ARGS:1
IF ARGS == "手" && CHECK_GLOVE("手露出", ARG) == 0
	SELECTCASE ARGS:1
	CASE "精液", "精"
		STAIN:ARG:手袋 = SET_STAIN(STAIN:ARG:手袋, "精液")
	ENDSELECT
ELSEIF ARGS == "パンツ" || (ARGS == "Ｖ" && CHECK_CLO("Ｖ露出", ARG) == 0) || (ARGS == "Ａ" && CHECK_CLO("Ａ露出", ARG) == 0)
	;精液or尿汚れならパンツなどにつく
	IF CHECK_CLO("ボディスーツ露出", ARG)
		ARGS = ボディスーツボトム
	ELSEIF CHECK_CLO("アウター露出", ARG)
		ARGS = アウター
	ELSEIF CHECK_CLO("パンツ露出", ARG)
		ARGS = パンツ
	ENDIF

	SELECTCASE ARGS:1
	CASE "精液", "精", "尿"
		STAIN:ARG:ARGS = SET_STAIN(STAIN:ARG:ARGS, ARGS:1)
	ENDSELECT
ELSEIF ARGS == "Ｂ" && CHECK_CLO("谷間露出", ARG) == 0
	;精液汚れなら服などにつく
	IF EQUIP:ARG:上着 && CHECK_SHIRT("谷間露出", ARG) == 0
		ARGS = 服
	ELSEIF EQUIP:ARG:ボディスーツ && CHECK_BODYS("谷間露出", ARG) == 0
		ARGS = ボディスーツトップ
	ENDIF

	SELECTCASE ARGS:1
	CASE "精液", "精"
		STAIN:ARG:ARGS = SET_STAIN(STAIN:ARG:ARGS, "精液")
	ENDSELECT
ELSE
	STAIN:ARG:ARGS = SET_STAIN(STAIN:ARG:ARGS, ARGS:1)
ENDIF

@EQ_STAIN, ARG, ARG:1, ARG:2, ARG:3
;ARGのARG:2の汚れと、ARG:1のARG:3の汚れを同じにする。いままでの
;STAIN:Ｍ |= STAIN:PLAYER:Ｐ
;STAIN:PLAYER:Ｐ |= STAIN:Ｍ
;と使い時は一緒。
STAIN:ARG:(ARG:2) |= STAIN_CALC(ARG, ARG:1, ARG:2, ARG:3)
STAIN:(ARG:1):(ARG:3) |= STAIN_CALC(ARG:1, ARG, ARG:3, ARG:2)


;-------------------------------------------------
;汚れが実際に移るかどうかを計算する関数
;-------------------------------------------------
@STAIN_CALC(ARG, ARG:1, ARG:2, ARG:3)
#FUNCTION

VARSET LOCAL
;汚れを与える側の部位が露出しているか？
SELECTCASE ARG:3
;手が露出していない
CASE 1
	IF CHECK_GLOVE("手露出", ARG:1) == 0
		;精液で汚れた手袋など
		IF CHECK_STAIN(STAIN:(ARG:1):手袋, "精液")
			ARG:3 = 18
		ELSE
			RETURNF 0
		ENDIF
	ENDIF
;Ｖが露出していない
CASE 3
	SIF CHECK_CLO("Ｖ露出", ARG:1) == 0
		RETURNF 0
;Ａが露出していない
CASE 4
	SIF CHECK_CLO("Ａ露出", ARG:1) == 0
		RETURNF 0
;胸が露出していない
CASE 5
	SIF CHECK_CLO("谷間露出", ARG:1) == 0
		RETURNF 0
ENDSELECT

;口でペニスをしゃぶっても汚れは移らない
SIF ARG:3 == 0 && ARG:2 == 2
	RETURNF 0

;Ｖ汚れはＡよりも弱い
IF CHECK_STAIN(STAIN:(ARG:1):(ARG:3), "Ｖ")
	;ＡにはＶ汚れのあるＰでないと移らない
	IF ARG:2 == 4
		SIF ARG:3 == 2
			LOCAL = SET_STAIN(LOCAL, "Ｖ")
	;Ｖ汚れはＶorＰからしか付かない
	ELSEIF ARG:3 == 2
		LOCAL = SET_STAIN(LOCAL, "Ｖ")
	ELSEIF ARG:3 == 3
		;お風呂上りだと綺麗
		SIF TEQUIP:(ARG:1):お風呂上り == 0
			LOCAL = SET_STAIN(LOCAL, "Ｖ")
	ENDIF
ENDIF
;Ｐ汚れはＰからしか付かない
IF CHECK_STAIN(STAIN:(ARG:1):(ARG:3), "Ｐ")
	IF ARG:3 == 2
		;お風呂上りだと綺麗
		SIF TEQUIP:(ARG:1):お風呂上り == 0
			LOCAL = SET_STAIN(LOCAL, "Ｐ")
	ENDIF
ENDIF
;精液汚れはＶＡには移らない
IF CHECK_STAIN(STAIN:(ARG:1):(ARG:3), "精液")
	SIF ARG:2 != 3 && ARG:2 != 4
		LOCAL = SET_STAIN(LOCAL, "精液")
ENDIF
;Ａ汚れは強い
IF CHECK_STAIN(STAIN:(ARG:1):(ARG:3), "Ａ")
	;ＶにはＡ汚れのあるＰでないと移らない
	IF ARG:2 == 3
		SIF ARG:3 == 2
			LOCAL = SET_STAIN(LOCAL, "Ａ")
	;舐める際にはお風呂上りなら移らない
	ELSEIF ARG:2 == 0
		;お風呂上りだと綺麗
		SIF TEQUIP:(ARG:1):お風呂上り == 0
			LOCAL = SET_STAIN(LOCAL, "Ａ")
	ELSE
		LOCAL = SET_STAIN(LOCAL, "Ａ")
	ENDIF
ENDIF
;乳汚れは口のみ移る
IF CHECK_STAIN(STAIN:(ARG:1):(ARG:3), "乳")
	SIF ARG:2 == 0
		LOCAL = SET_STAIN(LOCAL, "乳")
ENDIF
;尿汚れは口のみ移る
IF CHECK_STAIN(STAIN:(ARG:1):(ARG:3), "尿")
	SIF ARG:2 == 0
		LOCAL = SET_STAIN(LOCAL, "尿")
ENDIF
;触手汚れは口のみ移る
IF CHECK_STAIN(STAIN:(ARG:1):(ARG:3), "触手")
	SIF ARG:2 == 0
		LOCAL = SET_STAIN(LOCAL, "触手")
ENDIF

;精液汚れが無い場合にはここまで
SIF CHECK_STAIN(LOCAL, "精液") == 0
	RETURNF LOCAL

;汚れが増える側の部位が露出してい無い場合には主に精液汚れのみ移す処理
SELECTCASE ARG:2
;手が露出していない
CASE 1
	IF CHECK_CLO("手露出", ARG) == 0
		STAIN:ARG:手袋 = SET_STAIN(STAIN:ARG:手袋, "精液")
		LOCAL = 0
	ENDIF
;Ｖが露出していない
CASE 3
	IF CHECK_CLO("Ｖ露出", ARG) == 0
		IF CHECK_CLO("ボディスーツ露出", ARG)
			STAIN:ARG:ボディスーツボトム = SET_STAIN(STAIN:ARG:ボディスーツボトム, "精液")
		ELSEIF CHECK_CLO("アウター露出", ARG)
			STAIN:ARG:アウター = SET_STAIN(STAIN:ARG:アウター, "精液")
		ELSEIF CHECK_CLO("パンツ露出", ARG)
			STAIN:ARG:パンツ = SET_STAIN(STAIN:ARG:パンツ, "精液")
		ENDIF
		LOCAL = 0
	ENDIF
;Ａが露出していない
CASE 4
	IF CHECK_CLO("Ａ露出", ARG) == 0
		IF CHECK_CLO("ボディスーツ露出", ARG)
			STAIN:ARG:ボディスーツボトム = SET_STAIN(STAIN:ARG:ボディスーツボトム, "精液")
		ELSEIF CHECK_CLO("アウター露出", ARG)
			STAIN:ARG:アウター = SET_STAIN(STAIN:ARG:アウター, "精液")
		ELSEIF CHECK_CLO("パンツ露出", ARG)
			STAIN:ARG:パンツ = SET_STAIN(STAIN:ARG:パンツ, "精液")
		ENDIF
		LOCAL = 0
	ENDIF
;胸が露出していない
CASE 5
	IF CHECK_CLO("谷間露出", ARG) == 0
		IF EQUIP:ARG:上着 && !CHECK_SHIRT("谷間露出", ARG)
			STAIN:ARG:服 = SET_STAIN(STAIN:ARG:服, "精液")
		ELSEIF EQUIP:ARG:ボディスーツ && !CHECK_BODYS("谷間露出", ARG)
			STAIN:ARG:ボディスーツトップ = SET_STAIN(STAIN:ARG:ボディスーツトップ, "精液")
		ENDIF
		LOCAL = 0
	ENDIF
;パンツの場合には精液汚れ以外は移らなくさせる
CASE 10
	IF CHECK_CLO("ボディスーツ露出", ARG)
		STAIN:ARG:ボディスーツボトム = SET_STAIN(STAIN:ARG:ボディスーツボトム, "精液")
	ELSEIF CHECK_CLO("アウター露出", ARG)
		STAIN:ARG:アウター = SET_STAIN(STAIN:ARG:アウター, "精液")
	ELSEIF CHECK_CLO("パンツ露出", ARG)
		STAIN:ARG:パンツ = SET_STAIN(STAIN:ARG:パンツ, "精液")
	ENDIF
	LOCAL = 0
ENDSELECT

RETURNF LOCAL


@STAIN_CALC_COSPLAY
SELECTCASE CUP:Ｖ潤
CASE IS >= 10000
	LOCAL = 5
CASE IS >= 6000
	LOCAL = 4
CASE IS >= 3000
	LOCAL = 3
CASE IS >= 1500
	LOCAL = 2
CASE IS >= 500
	LOCAL = 1
CASEELSE
	LOCAL = 0
ENDSELECT

SELECTCASE PALAM:Ｖ潤
CASE IS >= 30000
	SIF LOCAL < 3
		LOCAL = 3
CASE IS >= 10000
	SIF LOCAL < 2
		LOCAL = 2
CASE IS >= 3000
	SIF LOCAL < 1
		LOCAL = 1
ENDSELECT
SIF LOCAL < 2 && CALCF("Ｖ発情")
	LOCAL += 1

;パンツの愛液汚れ
IF EQUIP:パンツ && CHECK_PANTIES("Ｖ露出") == 0
	IF CHECK_PANTIES("通気性無し") == 0
		SIF LOCAL >= 3 || (CHECK_STAIN(STAIN:パンツ, "蒸れ") && LOCAL >= 2)
			STAIN:パンツ = SET_STAIN(STAIN:パンツ, "濡れ")
	ENDIF
	SIF LOCAL >= 1 || (NUM("ターン数") == 0 && PALAM:Ｖ潤 >= 100)
		STAIN:パンツ = SET_STAIN(STAIN:パンツ, "蒸れ")
ENDIF
;アウター(直穿き)の愛液汚れ
IF EQUIP:アウター && CHECK_OUTER("Ｖ露出") == 0
	;通気性なし(濡れないけど、蒸れやすい)
	IF CHECK_OUTER("通気性無し")
		SIF LOCAL >= 1 || (NUM("ターン数") == 0 && PALAM:Ｖ潤 >= 50)
			STAIN:アウター = SET_STAIN(STAIN:アウター, "蒸れ")
	;直穿き
	ELSEIF CHECK_CLO("アウター直穿き")
		;ズボン生地
		IF CHECK_OUTER("ズボン")
			SIF LOCAL >= 5 || (CHECK_STAIN(STAIN:アウター, "蒸れ") && LOCAL >= 4)
				STAIN:アウター = SET_STAIN(STAIN:アウター, "濡れ")
			SIF LOCAL >= 3 || (NUM("ターン数") == 0 && PALAM:Ｖ潤 >= 500)
				STAIN:アウター = SET_STAIN(STAIN:アウター, "蒸れ")
		ELSE
			SIF LOCAL >= 4 || (CHECK_STAIN(STAIN:アウター, "蒸れ") && LOCAL >= 3)
				STAIN:アウター = SET_STAIN(STAIN:アウター, "濡れ")
			SIF LOCAL >= 2 || (NUM("ターン数") == 0 && PALAM:Ｖ潤 >= 300)
				STAIN:アウター = SET_STAIN(STAIN:アウター, "蒸れ")
		ENDIF
	;ぱんつ濡れ(ズボン生地と同様の難易度)
	ELSEIF CHECK_STAIN(STAIN:パンツ, "濡れ")
		SIF LOCAL >= 5 || (CHECK_STAIN(STAIN:アウター, "蒸れ") && LOCAL >= 4)
			STAIN:アウター = SET_STAIN(STAIN:アウター, "濡れ")
		SIF LOCAL >= 3 || (NUM("ターン数") == 0 && PALAM:Ｖ潤 >= 500)
			STAIN:アウター = SET_STAIN(STAIN:アウター, "蒸れ")
	;ぱんつ蒸れ
	ELSEIF CHECK_STAIN(STAIN:パンツ, "蒸れ")
		SIF LOCAL >= 3 || (NUM("ターン数") == 0 && PALAM:Ｖ潤 >= 1000)
			STAIN:アウター = SET_STAIN(STAIN:アウター, "蒸れ")
	ENDIF
ENDIF
;ボディスーツ（ボトム）の愛液汚れ
IF EQUIP:ボディスーツ && CHECK_BODYS("Ｖ露出") == 0
	;通気性あり
	IF CHECK_BODYS("通気性無し") == 0
		SIF LOCAL >= 4 || (CHECK_STAIN(STAIN:ボディスーツボトム, "蒸れ") && LOCAL >= 3)
			STAIN:ボディスーツボトム = SET_STAIN(STAIN:ボディスーツボトム, "濡れ")
	ENDIF
	SIF LOCAL >= 3 || (NUM("ターン数") == 0 && PALAM:Ｖ潤 >= 500)
		STAIN:ボディスーツボトム = SET_STAIN(STAIN:ボディスーツボトム, "蒸れ")
ENDIF

;-------------------------------------------------
;〜使いLVによる補正の一括管理
;-------------------------------------------------
@SOURCE_EXABL(ARG, ARG:1, ARG:2)
#FUNCTION
;快感の基礎値を、対応した技術ＬＶ*100だけ追加
LOCAL = ARG + 100 * ABL:(ARG:2):(ARG:1)

IF CONFIG("ＨＡＲＤ")
	LOCAL:1 = 10 * ABL:(ARG:2):(ARG:1)
ELSE
	LOCAL:1 = 20 * ABL:(ARG:2):(ARG:1)
ENDIF
IF ARG:2 == PLAYER
	LOCAL = MULTIPLY(LOCAL, 100 + LOCAL:1)
ELSE
	LOCAL = MULTIPLY(LOCAL, 100 + LOCAL:1 * 2)
ENDIF

RETURNF LOCAL

;-------------------------------------------------
;ペニス判定関数@PENIS
;	引数：対象キャラの登録番号
;式中で使用する関数です。ペニスがある場合1を返します
;-------------------------------------------------
@PENIS(ARG)
#FUNCTION
SIF TALENT:ARG:扶她 || TALENT:ARG:男性
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;膣判定関数
;-------------------------------------------------
@VAGINA(ARG)
#FUNCTION
SIF TALENT:ARG:扶她 || TALENT:ARG:女性
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;インサートの総合管理。ARGの状態ARGSを判定する
;インサートならば、ＶorＡどっちかにインサート、ＶインサートならＶにインサート中、という風に使い分ける
;ARG:1にキャラ番号を代入することでより細かい状態を調べられる
;例）INSERT("インサート", TARGET)だと"TARGETがインサートされてるか"のみをチェックするが、
;　　INSERT("インサート", TARGET, PLAYER)だと"PLAYERがTARGETにインサートしているか"をチェックする
;TEQUIP:体位はビットで状態を管理する
;〜9bit	体位、10bit Ｖ性交、11bit Ａ性交、12bit 尿道性交
;-------------------------------------------------
@INSERT(ARGS, ARG, ARG:1)
#FUNCTION
SIF ARG < 0 || ARG:1 < 0
	RETURNF 0
SIF ARG == 0 && TARGET
	ARG = TARGET

SELECTCASE ARGS
CASE "インサート"
	SIF ARG:1 > 0 && TEQUIP:(ARG:1):Ｖ挿入 != ARG && TEQUIP:(ARG:1):Ａ挿入 != ARG && TEQUIP:(ARG:1):尿道挿入 != ARG
		RETURNF 0
	RETURNF TEQUIP:ARG:体位
CASE "Ｖインサート"
	SIF ARG:1 > 0 && TEQUIP:(ARG:1):Ｖ挿入 != ARG
		RETURNF 0
	SIF GETBIT(TEQUIP:ARG:体位, 10)
		RETURNF TEQUIP:ARG:体位
CASE "Ｖ二本挿入"
	SIF FINDCHARA(TEQUIP:Ｖ挿入, ARG) > 0 && FINDCHARA(TEQUIP:Ｖ挿入, FINDCHARA(TEQUIP:Ｖ挿入, ARG) ) > 0
		RETURNF TEQUIP:ARG:体位
CASE "Ａインサート"
	SIF ARG:1 > 0 && TEQUIP:(ARG:1):Ａ挿入 != ARG
		RETURNF 0
	SIF GETBIT(TEQUIP:ARG:体位, 11)
		RETURNF TEQUIP:ARG:体位
CASE "Ａ二本挿入"
	SIF FINDCHARA(TEQUIP:Ａ挿入, ARG) > 0 && FINDCHARA(TEQUIP:Ａ挿入, FINDCHARA(TEQUIP:Ａ挿入, ARG) ) > 0
		RETURNF TEQUIP:ARG:体位
CASE "尿道インサート"
	SIF ARG:1 > 0 && TEQUIP:(ARG:1):尿道挿入 != ARG
		RETURNF 0
	SIF GETBIT(TEQUIP:ARG:体位, 12)
		RETURNF TEQUIP:ARG:体位
CASE "複数挿入"
	SIF GETBIT(TEQUIP:ARG:体位, 10) && GETBIT(TEQUIP:ARG:体位, 11) && GETBIT(TEQUIP:ARG:体位, 12)
		RETURNF TEQUIP:ARG:体位
	SIF FINDCHARA(TEQUIP:Ｖ挿入, ARG) > 0 && FINDCHARA(TEQUIP:Ｖ挿入, FINDCHARA(TEQUIP:Ｖ挿入, ARG) ) > 0
		RETURNF TEQUIP:ARG:体位
	SIF FINDCHARA(TEQUIP:Ａ挿入, ARG) > 0 && FINDCHARA(TEQUIP:Ａ挿入, FINDCHARA(TEQUIP:Ａ挿入, ARG) ) > 0
		RETURNF TEQUIP:ARG:体位
	SIF FINDCHARA(TEQUIP:尿道挿入, ARG) > 0 && FINDCHARA(TEQUIP:尿道挿入, FINDCHARA(TEQUIP:尿道挿入, ARG) ) > 0
		RETURNF TEQUIP:ARG:体位
;同じ穴に二本挿入してる時には正確ではないです
CASE "インサート相手"
	LOCAL = MAX(FINDCHARA(TEQUIP:Ｖ挿入, ARG), FINDCHARA(TEQUIP:Ａ挿入, ARG), FINDCHARA(TEQUIP:尿道挿入, ARG), 0)
		RETURNF LOCAL
CASE "Ｖインサート相手"
	LOCAL = MAX(FINDCHARA(TEQUIP:Ｖ挿入, ARG), 0)
		RETURNF LOCAL
CASE "Ａインサート相手"
	LOCAL = MAX(FINDCHARA(TEQUIP:Ａ挿入, ARG), 0)
		RETURNF LOCAL
CASE "尿道インサート相手"
	LOCAL = MAX(FINDCHARA(TEQUIP:尿道挿入, ARG), 0)
		RETURNF LOCAL
CASE "挿入中"
	RETURNF MAX(TEQUIP:ARG:Ｖ挿入, TEQUIP:ARG:Ａ挿入, TEQUIP:ARG:尿道挿入)
CASE "Ｖ挿入中"
	RETURNF TEQUIP:ARG:Ｖ挿入
CASE "Ａ挿入中"
	RETURNF TEQUIP:ARG:Ａ挿入
CASE "尿道挿入中"
	RETURNF TEQUIP:ARG:尿道挿入
;現時点ではＶＡ両方の場合を指すこととする
CASE "二穴挿入"
	SIF (GETBIT(TEQUIP:ARG:体位, 10) > 0) + (GETBIT(TEQUIP:ARG:体位, 11) > 0) >= 2
		RETURNF TEQUIP:ARG:体位
;ARGとARG:1が合体している状況を調べる
;INSERT("インサート", ARG, ARG:1) || INSERT("インサート", ARG:1, ARG)と同じ
CASE "合体中"
	SIF ARG:1 == 0
		ARG:1 = PLAYER
	IF TEQUIP:(ARG:1):Ｖ挿入 == ARG || TEQUIP:(ARG:1):Ａ挿入 == ARG || TEQUIP:(ARG:1):尿道挿入 == ARG
		RETURNF TEQUIP:ARG:体位
	ELSEIF TEQUIP:ARG:Ｖ挿入 == ARG:1 || TEQUIP:ARG:Ａ挿入 == ARG:1 || TEQUIP:ARG:尿道挿入 == ARG:1
		RETURNF TEQUIP:(ARG:1):体位
	ENDIF
CASE "正常位"
	SIF GETBIT(TEQUIP:ARG:体位, 0)
		RETURNF 1
CASE "Ｖ正常位"
	SIF GETBIT(TEQUIP:ARG:体位, 0) && GETBIT(TEQUIP:ARG:体位, 10)
		RETURNF 1
CASE "Ａ正常位"
	SIF GETBIT(TEQUIP:ARG:体位, 0) && GETBIT(TEQUIP:ARG:体位, 11)
		RETURNF 1
CASE "尿道正常位"
	SIF GETBIT(TEQUIP:ARG:体位, 0) && GETBIT(TEQUIP:ARG:体位, 12)
		RETURNF 1
CASE "後背位"
	SIF GETBIT(TEQUIP:ARG:体位, 1)
		RETURNF 1
CASE "Ｖ後背位"
	SIF GETBIT(TEQUIP:ARG:体位, 1) && GETBIT(TEQUIP:ARG:体位, 10)
		RETURNF 1
CASE "Ａ後背位"
	SIF GETBIT(TEQUIP:ARG:体位, 1) && GETBIT(TEQUIP:ARG:体位, 11)
		RETURNF 1
CASE "尿道後背位"
	SIF GETBIT(TEQUIP:ARG:体位, 1) && GETBIT(TEQUIP:ARG:体位, 12)
		RETURNF 1
CASE "屈曲位"
	SIF GETBIT(TEQUIP:ARG:体位, 2)
		RETURNF 1
CASE "Ｖ屈曲位"
	SIF GETBIT(TEQUIP:ARG:体位, 2) && GETBIT(TEQUIP:ARG:体位, 10)
		RETURNF 1
CASE "Ａ屈曲位"
	SIF GETBIT(TEQUIP:ARG:体位, 2) && GETBIT(TEQUIP:ARG:体位, 11)
		RETURNF 1
CASE "尿道屈曲位"
	SIF GETBIT(TEQUIP:ARG:体位, 2) && GETBIT(TEQUIP:ARG:体位, 12)
		RETURNF 1
CASE "対面座位"
	SIF GETBIT(TEQUIP:ARG:体位, 3)
		RETURNF 1
CASE "Ｖ対面座位"
	SIF GETBIT(TEQUIP:ARG:体位, 3) && GETBIT(TEQUIP:ARG:体位, 10)
		RETURNF 1
CASE "Ａ対面座位"
	SIF GETBIT(TEQUIP:ARG:体位, 3) && GETBIT(TEQUIP:ARG:体位, 11)
		RETURNF 1
CASE "尿道対面座位"
	SIF GETBIT(TEQUIP:ARG:体位, 3) && GETBIT(TEQUIP:ARG:体位, 12)
		RETURNF 1
CASE "背面座位"
	SIF GETBIT(TEQUIP:ARG:体位, 4)
		RETURNF 1
CASE "Ｖ背面座位"
	SIF GETBIT(TEQUIP:ARG:体位, 4) && GETBIT(TEQUIP:ARG:体位, 10)
		RETURNF 1
CASE "Ａ背面座位"
	SIF GETBIT(TEQUIP:ARG:体位, 4) && GETBIT(TEQUIP:ARG:体位, 11)
		RETURNF 1
CASE "尿道背面座位"
	SIF GETBIT(TEQUIP:ARG:体位, 4) && GETBIT(TEQUIP:ARG:体位, 12)
		RETURNF 1
CASE "騎乗位"
	SIF GETBIT(TEQUIP:ARG:体位, 5)
		RETURNF 1
CASE "Ｖ騎乗位"
	SIF GETBIT(TEQUIP:ARG:体位, 5) && GETBIT(TEQUIP:ARG:体位, 10)
		RETURNF 1
CASE "Ａ騎乗位"
	SIF GETBIT(TEQUIP:ARG:体位, 5) && GETBIT(TEQUIP:ARG:体位, 11)
		RETURNF 1
CASE "尿道騎乗位"
	SIF GETBIT(TEQUIP:ARG:体位, 5) && GETBIT(TEQUIP:ARG:体位, 12)
		RETURNF 1
CASE "Ｖ対応コマンド"
	IF ARG == TARGET
		IF GETBIT(TEQUIP:ARG:体位, 0)
			RETURNF 60
		ELSEIF GETBIT(TEQUIP:ARG:体位, 1)
			RETURNF 61
		ELSEIF GETBIT(TEQUIP:ARG:体位, 2)
			RETURNF 62
		ELSEIF GETBIT(TEQUIP:ARG:体位, 3)
			RETURNF 63
		ELSEIF GETBIT(TEQUIP:ARG:体位, 4)
			RETURNF 64
		ELSEIF GETBIT(TEQUIP:ARG:体位, 5)
			RETURNF 65
		ENDIF
	ELSE
		IF GETBIT(TEQUIP:ARG:体位, 0)
			RETURNF 120
		ELSEIF GETBIT(TEQUIP:ARG:体位, 1)
			RETURNF 121
		ELSEIF GETBIT(TEQUIP:ARG:体位, 5)
			RETURNF 122
		ELSEIF TEQUIP:ARG:体位
			RETURNF 120
		ENDIF
	ENDIF
CASE "Ａ対応コマンド"
	IF ARG == TARGET
		IF GETBIT(TEQUIP:ARG:体位, 0)
			RETURNF 70
		ELSEIF GETBIT(TEQUIP:ARG:体位, 1)
			RETURNF 71
		ELSEIF GETBIT(TEQUIP:ARG:体位, 2)
			RETURNF 72
		ELSEIF GETBIT(TEQUIP:ARG:体位, 3)
			RETURNF 73
		ELSEIF GETBIT(TEQUIP:ARG:体位, 4)
			RETURNF 74
		ELSEIF GETBIT(TEQUIP:ARG:体位, 5)
			RETURNF 75
		ENDIF
	ELSE
		IF GETBIT(TEQUIP:ARG:体位, 0)
			RETURNF 125
		ELSEIF GETBIT(TEQUIP:ARG:体位, 1)
			RETURNF 126
		ELSEIF GETBIT(TEQUIP:ARG:体位, 5)
			RETURNF 127
		ELSEIF TEQUIP:ARG:体位
			RETURNF 125
		ENDIF
	ENDIF
CASE "尿道対応コマンド"
	RETURNF 83
CASE "ワイルドスウィング"
	SIF TEQUIP:ARG:体位
		RETURNF GETBIT(TEQUIP:ARG:特殊行動, 0)
ENDSELECT
RETURNF 0

;-------------------------------------------------
;ARGとARG:1のインサート＆挿入状態を解除させる関数
;通常はARGとARG:1にTARGETとPLAYERを代入する
;高速化のために@INSERTでできることを別の関数で行う事も有る
;-------------------------------------------------
@RESET_INSERT, ARG, ARG:1
#DIM LCOUNT

;ARGに関するTEQUIP:Ｖ挿入とTEQUIP:Ａ挿入を初期化する
FOR LOCAL, 0, (ARG > 0) + (ARG:1 > 0)
	;IF INSERT("インサート", ARG:LOCAL) || INSERT("インサート相手", ARG:LOCAL)
	IF TEQUIP:ARG:体位 || MAX(FINDCHARA(TEQUIP:Ｖ挿入, ARG:LOCAL), FINDCHARA(TEQUIP:Ａ挿入, ARG:LOCAL), FINDCHARA(TEQUIP:尿道挿入, ARG:LOCAL), 0)
		;挿入している相手のフラグを消去
		FOR LCOUNT, 1, CHARANUM
			SELECTCASE LCOUNT
			CASE MASTER, ASSI, PLAYER, PLAYER:1, TARGET, TARGET:1
				SIF TEQUIP:LCOUNT:Ｖ挿入 == ARG:LOCAL
					TEQUIP:LCOUNT:Ｖ挿入 = 0
				SIF TEQUIP:LCOUNT:Ａ挿入 == ARG:LOCAL
					TEQUIP:LCOUNT:Ａ挿入 = 0
				SIF TEQUIP:LCOUNT:尿道挿入 == ARG:LOCAL
					TEQUIP:LCOUNT:尿道挿入 = 0
			ENDSELECT
		NEXT
	ENDIF

	TEQUIP:(ARG:LOCAL):Ｖ挿入 = 0
	TEQUIP:(ARG:LOCAL):Ａ挿入 = 0
	TEQUIP:(ARG:LOCAL):尿道挿入 = 0
NEXT

;インサートされていない全てのキャラのTEQUIP:体位を初期化
FOR LCOUNT, 1, CHARANUM
	SELECTCASE LCOUNT
	CASE MASTER, ASSI, PLAYER, PLAYER:1, TARGET, TARGET:1
		;SIF INSERT("インサート相手", LCOUNT)
		SIF MAX(FINDCHARA(TEQUIP:Ｖ挿入, LCOUNT), FINDCHARA(TEQUIP:Ａ挿入, LCOUNT), FINDCHARA(TEQUIP:尿道挿入, LCOUNT), 0)
			CONTINUE
		TEQUIP:LCOUNT:体位 = 0
		TEQUIP:LCOUNT:ラブトラップ = 0
		;ワイルドスウィング初期化
		CLEARBIT TEQUIP:LCOUNT:特殊行動, 0
	ENDSELECT
NEXT

;-------------------------------------------------
;インサートの関数を設定する関数
;ARGに挿入される側、ARG:1に挿入する側、ARG:2にSELECTCOM
;ARGSに"解除判定"が代入された場合には解除されるかをチェックするのみでインサートフラグの発生は無い
;TEQUIP:体位はビットで状態を管理する
;〜9bit	体位、10bit Ｖ性交、11bit Ａ性交、12bit 尿道性交
;-------------------------------------------------
@SET_INSERT, ARG, ARG:1, ARG:2, ARGS
#DIM LCOUNT

SIF ARG <= 0 || ARG:1 <= 0
	RETURN 0

VARSET LOCAL

SELECTCASE ARG:2
;インサートコマンド
CASE 60 TO 79, 83 TO 88, 120 TO 129, 177
	IF ARGS != "解除判定"
		;これから挿入する側がインサートされている場合はインサートを解除する
		SELECTCASE ARG:2
		CASE 60 TO 79, 177, 83 TO 88
			SIF INSERT("インサート", ARG:1)
				CALL RESET_INSERT, ARG:1
		CASE 120 TO 129
			SIF INSERT("インサート", ARG)
				CALL RESET_INSERT, ARG
		ENDSELECT

		;インサートされる側の体位フラグ
		SELECTCASE ARG:2
		CASE 60, 70, 83, 177
			TEQUIP:ARG:体位 = 1p0
			;ラブトラップ状態の付与
			SIF ARG:2 == 177
				TEQUIP:ARG:ラブトラップ = 1
		CASE 61, 71, 84
			TEQUIP:ARG:体位 = 1p1
		CASE 62, 72, 85
			TEQUIP:ARG:体位 = 1p2
		CASE 63, 73, 86
			TEQUIP:ARG:体位 = 1p3
		CASE 64, 74, 87
			TEQUIP:ARG:体位 = 1p4
		CASE 65, 75, 88
			TEQUIP:ARG:体位 = 1p5
		;子宮挿入
		CASE 68
			IF TEQUIP:ARG:体位 == 0
				IF TEQUIP:ARG:押し倒し
					TEQUIP:ARG:体位 = 1p5
				ELSE
					TEQUIP:ARG:体位 = 1p0
				ENDIF
			ENDIF
		;以下は逆インサート
		CASE 120, 125
			TEQUIP:(ARG:1):体位 = 1p0
		CASE 121, 126
			TEQUIP:(ARG:1):体位 = 1p1
		CASE 122, 127
			TEQUIP:(ARG:1):体位 = 1p5
		ENDSELECT

		;挿入する側がどこに入れたかのフラグ
		SELECTCASE ARG:2
		CASE 60 TO 69, 177
			SETBIT TEQUIP:ARG:体位, 10
			TEQUIP:(ARG:1):Ｖ挿入 = ARG
			TEQUIP:(ARG:1):Ａ挿入 = 0
			TEQUIP:(ARG:1):尿道挿入 = 0
		CASE 70 TO 79
			SETBIT TEQUIP:ARG:体位, 11
			TEQUIP:(ARG:1):Ｖ挿入 = 0
			TEQUIP:(ARG:1):Ａ挿入 = ARG
			TEQUIP:(ARG:1):尿道挿入 = 0
		CASE 83 TO 88
			SETBIT TEQUIP:ARG:体位, 12
			TEQUIP:(ARG:1):Ｖ挿入 = 0
			TEQUIP:(ARG:1):Ａ挿入 = 0
			TEQUIP:(ARG:1):尿道挿入 = ARG

		;以下は逆インサート
		CASE 120 TO 124
			SETBIT TEQUIP:(ARG:1):体位, 10
			TEQUIP:ARG:Ｖ挿入 = ARG:1
			TEQUIP:ARG:Ａ挿入 = 0
			TEQUIP:ARG:尿道挿入 = 0
		CASE 125 TO 129
			SETBIT TEQUIP:(ARG:1):体位, 11
			TEQUIP:ARG:Ｖ挿入 = 0
			TEQUIP:ARG:Ａ挿入 = ARG:1
			TEQUIP:ARG:尿道挿入 = 0
		ENDSELECT
	ENDIF
;以下はコマンド実行時の解除判定。それぞれ逆インサート状態かどうかもチェック
CASE 3
	SIF INSERT("インサート", ARG:1) || CHECK_COM("インサート自慰") == 0
		CALL RESET_INSERT, ARG, ARG:1
CASE 7
	SIF INSERT("インサート", ARG:1) || CHECK_COM("ふきふき", ARG:2)
		CALL RESET_INSERT, ARG, ARG:1
;指挿入れ
CASE 8
	TEQUIP:ARG:Ｖ挿入 = 0
	;ARG:1がARGに挿入している場合には解除
	SIF INSERT("Ｖインサート相手", ARG) == ARG:1
		TEQUIP:(ARG:1):Ｖ挿入 = 0
	TEQUIP:ARG:尿道挿入 = 0
	SIF INSERT("尿道インサート相手", ARG) == ARG:1
		TEQUIP:(ARG:1):尿道挿入 = 0
;尿道指挿入れ(TARGETがペニスを挿入している場合にはインサート解除)
CASE 15
	IF PENIS(ARG)
		TEQUIP:ARG:Ｖ挿入 = 0
		TEQUIP:ARG:Ａ挿入 = 0
		TEQUIP:ARG:尿道挿入 = 0
	ENDIF
	SIF INSERT("尿道インサート相手", ARG) == ARG:1
		TEQUIP:(ARG:1):尿道挿入 = 0
CASE 0, 2, 5 TO 7
	;背中側の人には色々できない(逆インサートの時)
	SIF INSERT("インサート", ARG:1) && INSERT("後背位", ARG:1) + INSERT("背面座位", ARG:1)
		CALL RESET_INSERT, ARG, ARG:1
CASE 13
	TEQUIP:ARG:Ｖ挿入 = 0
	SIF INSERT("Ｖインサート相手", ARG) == ARG:1
		TEQUIP:(ARG:1):Ｖ挿入 = 0
	TEQUIP:ARG:尿道挿入 = 0
	SIF INSERT("尿道インサート相手", ARG) == ARG:1
		TEQUIP:(ARG:1):尿道挿入 = 0
	SIF INSERT("インサート", ARG:1) || INSERT("後背位", ARG) || INSERT("対面座位", ARG) || (INSERT("背面座位", ARG) && TEQUIP:ARG:羞恥プレイ == 0)
		CALL RESET_INSERT, ARG, ARG:1
CASE 14
	TEQUIP:ARG:尿道挿入 = 0
	SIF INSERT("尿道インサート相手", ARG) == ARG:1
		TEQUIP:(ARG:1):尿道挿入 = 0
	TEQUIP:ARG:Ａ挿入 = 0
	SIF INSERT("Ａインサート相手", ARG) == ARG:1
		TEQUIP:(ARG:1):Ａ挿入 = 0
	SIF INSERT("インサート", ARG:1) || (INSERT("後背位", ARG) == 0 && INSERT("騎乗位", ARG) == 0)
		CALL RESET_INSERT, ARG, ARG:1
;ぱふぱふは長身でないとインサート解除
CASE 104
	;後ろからは駄目
	IF INSERT("後背位", ARG) || INSERT("背面座位", ARG) || INSERT("後背位", ARG:1) || INSERT("背面座位", ARG:1)
		CALL RESET_INSERT, ARG, ARG:1
	;騎乗位は相手と同程度の背がないと駄目
	ELSEIF INSERT("騎乗位") || INSERT("騎乗位", ARG:1)
		SIF (TALENT:(ARG:1):高挑体型 && TALENT:高挑体型 == 0) || (TALENT:(ARG:1):高挑体型 == 0 && TALENT:(ARG:1):娇小体型 == 0 && TALENT:娇小体型)
			CALL RESET_INSERT, ARG, ARG:1
	ENDIF
;放尿は尿道挿入されながらは無理
CASE 144
	SIF INSERT("尿道インサート相手", ARG) == ARG:1
		TEQUIP:(ARG:1):尿道挿入 = 0
;そのグループにおいては稀なインサート継続コマンド
CASE 130, 138, 139, 176, 178, 180
;触手など、インサート完全消去
CASE 171 TO 181, 200
	CALL RESET_INSERT, ARG, ARG:1
;媚薬騎乗位
CASE 261
	SIF TEQUIP:押し倒し == 0
		CALL RESET_INSERT, ARG, ARG:1
;問答無用でペニスを抜くコマンド(調教者が別の人ならば、その限りではありません)
CASE IS < 0, 1, 4, 9, 11, 12, 16, 31 TO 59, 80 TO 229, 260 TO 279, 302 TO 305, 307 TO 308, 503 TO 504
	TEQUIP:ARG:Ｖ挿入 = 0
	TEQUIP:ARG:Ａ挿入 = 0
	TEQUIP:ARG:尿道挿入 = 0
	SIF INSERT("Ｖインサート相手", ARG) == ARG:1
		TEQUIP:(ARG:1):Ｖ挿入 = 0
	SIF INSERT("Ａインサート相手", ARG) == ARG:1
		TEQUIP:(ARG:1):Ａ挿入 = 0
	SIF INSERT("尿道インサート相手", ARG) == ARG:1
		TEQUIP:(ARG:1):尿道挿入 = 0
ENDSELECT

FOR LCOUNT, 1, CHARANUM
	SELECTCASE LCOUNT
	CASE MASTER, ASSI, PLAYER, PLAYER:1, TARGET, TARGET:1
		;インサートしている相手がいる
		IF INSERT("インサート相手", LCOUNT)
			;複数挿入の時には体勢が変更される
			IF INSERT("複数挿入", LCOUNT)
				SELECTCASE ARG:2
				CASE 60 TO 69
					TEQUIP:LCOUNT:体位 = 1p4
				CASEELSE
					TEQUIP:LCOUNT:体位 = 1p5
				ENDSELECT
			;押し倒しのときは騎乗位
			ELSEIF TEQUIP:LCOUNT:押し倒し && INSERT("インサート", LCOUNT)
				TEQUIP:LCOUNT:体位 = 1p5
			ENDIF
			;体位変更に伴う挿入フラグ補正
			IF INSERT("Ｖインサート相手", LCOUNT)
				SETBIT TEQUIP:LCOUNT:体位, 10
				SIF CHECK_CLO("Ｖ露出", LCOUNT) == 0
					CALL SETFLAG, "ボトムを露出：テキスト無し", LCOUNT
			ENDIF
			IF INSERT("Ａインサート相手", LCOUNT)
				SETBIT TEQUIP:LCOUNT:体位, 11
				SIF CHECK_CLO("Ａ露出", LCOUNT) == 0
					CALL SETFLAG, "ボトムを露出：テキスト無し", LCOUNT
			ENDIF
			IF INSERT("尿道インサート相手", LCOUNT)
				SETBIT TEQUIP:LCOUNT:体位, 12
				SIF CHECK_CLO("Ｖ露出", LCOUNT) == 0
					CALL SETFLAG, "ボトムを露出：テキスト無し", LCOUNT
			ENDIF
		;誰かに挿入中
		ELSEIF INSERT("挿入中", LCOUNT)
			SIF CHECK_CLO("Ｖ露出", LCOUNT) == 0
				CALL SETFLAG, "ボトムを露出：テキスト無し", LCOUNT
		;インサートも挿入もしていない
		ELSE
			CALL RESET_INSERT, LCOUNT
		ENDIF
	ENDSELECT
NEXT


;-------------------------------------------------
;各キャラのインサートフラグを一括管理する
;ARGSに"解除判定"が代入された場合には解除されるかをチェックするのみでインサートフラグの発生は無い
;-------------------------------------------------
@CHECK_INSERT, ARGS
IF COND("調教側") == 0
	CALL SET_INSERT, TARGET, PLAYER, SELECTCOM, ARGS
	SIF SELECTCOM:1 >= 0
		CALL SET_INSERT, TARGET:1, PLAYER, SELECTCOM:1, ARGS
	SIF SELECTCOM:2 >= 0
		CALL SET_INSERT, TARGET, PLAYER:1, SELECTCOM:2, ARGS
ELSEIF ASSI
	CALL SET_INSERT, ASSI, TARGET, SELECTCOM:1, ARGS
ENDIF

;-------------------------------------------------
;インサートではない特殊な体位フラグの管理
;-------------------------------------------------
@CHECK_EX_EQUIP
;押し倒しクンニorＡ舐めは顔面騎乗フラグON
SELECTCASE SELECTCOM
CASE 1, 9
	SIF TEQUIP:押し倒し
		TEQUIP:颜面骑乘 = SELECTCOM
ENDSELECT


;キス継続…ここではフラグONのみ
;フラグOFFはEVENTCOMで行う
SELECTCASE SELECTCOM
CASE 6, 311
	;連続するほどに消費や効果にボーナス
	TEQUIP:亲吻 += 1
ENDSELECT

;占有不可な場合
IF TEQUIP:Ｗ押し倒し || COND("３Ｐ")
	TEQUIP:亲吻 = 0
	TEQUIP:(TARGET:1):亲吻 = 0
	TEQUIP:(PLAYER:1):亲吻 = 0
	TEQUIP:ASSI:亲吻 = 0
ENDIF

;-------------------------------------------------
;派生コマンドなどを判定する関数
;-------------------------------------------------
@CHECK_COM(ARGS, ARG)
#FUNCTION

VARSET LOCAL

SELECTCASE ARGS
CASE "連続実行"
	SIF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
		RETURNF 1
CASE "ローションマッサージ"
	SIF ITEM:底垫 == 0 && NOITEM == 0
		RETURNF 0
	SIF COND("前Ｔ調教者") != PLAYER || TEQUIP:押し倒し || TEQUIP:触手
		RETURNF 0
	;基本的に１ローションで一回のみ
	SIF COND("ローションマッサージ実行済")
		RETURNF 0
	;細かい条件はあとで詰める
	SELECTCASE ARG
	CASE 0
		SIF PREVCOM != 260
			RETURNF 0
		RETURNF 1
	CASE 260
		SIF PREVCOM != 0
			RETURNF 0
		RETURNF 260
	ENDSELECT
CASE "フラート"
	SIF TEQUIP:押し倒し || TEQUIP:シックスナイン || ABL:PLAYER:技巧 < 3
		RETURNF 0
 	SIF RANDF(3) == 0
		RETURNF 1
CASE "お掃除クンニ"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF CHECK_COM("恥垢お掃除クンニ", ARG)
		RETURNF 1
	SELECTCASE ARG
	CASE 1
		SIF COND("お掃除クンニフラグ", PLAYER)
			RETURNF 1
		SIF TALENT:男性
			RETURNF 0
		;押し倒し状態等は今のところなし
		SIF TEQUIP:押し倒し || TEQUIP:触手 || CHECK_CLO("Ｖ露出可") == 0
			RETURNF 0
		;尿のお掃除orセックスのお掃除
		SIF PREVEX("Ｖ中出し") && PREVPLAY("インサート") && CHECK_STAIN(STAIN:Ｖ, "精液")
			SETBIT LOCAL, 0
		SIF PREVEX("尿道内射精") && PREVPLAY("尿道インサート")
			SETBIT LOCAL, 0
		SIF (PREVPLAY("膣内浴尿") || (PREVPLAY("放尿") && PENIS(TARGET) == 0) ) && CHECK_STAIN(STAIN:Ｖ, "尿") && TEQUIP:カテーテル == 0
			SETBIT LOCAL, 1
	CASE 98
		SIF COND("お掃除クンニフラグ")
			RETURNF 1
		SIF TALENT:PLAYER:男性
			RETURNF 0
		SIF MIN(ABL:技巧, ABL:侍奉精神) < 3
			RETURNF 0
		;尿のお掃除orセックスのお掃除
		SIF PREVEX("Ｖ中出し", PLAYER) && PREVPLAY("インサート", PLAYER) && CHECK_STAIN(STAIN:PLAYER:Ｖ, "精液")
			SETBIT LOCAL, 0
		SIF (PREVPLAY("膣内浴尿", PLAYER) || (PREVPLAY("放尿", PLAYER) && PENIS(PLAYER) == 0) ) && CHECK_STAIN(STAIN:PLAYER:Ｖ, "尿")
			SETBIT LOCAL, 1
	ENDSELECT
	RETURNF LOCAL
CASE "恥垢お掃除クンニ"
	SIF ARG == 0
		ARG = SELECTCOM
	SELECTCASE ARG
	CASE 1
		SIF COND("恥垢お掃除クンニフラグ", PLAYER)
			RETURNF 1
		SIF TALENT:男性
			RETURNF 0
		;押し倒し状態等は今のところなし
		SIF TEQUIP:押し倒し || TEQUIP:触手 || CHECK_CLO("Ｖ露出可") == 0
			RETURNF 0
		;前回お掃除クンニ実行の時に続けてやっちゃう感じで
		SIF PREVPLAY("お掃除クンニ", PLAYER) == 0 || CHECK_STAIN(STAIN:Ｖ, "Ｖ恥垢") == 0
			RETURNF 0
		;お掃除クンニよりは難易度高い
		SIF TALENT:PLAYER:无视污臭 || TALENT:PLAYER:性变态
			RETURNF 1
		IF PLAYER == MASTER
			SIF COND("性癖：においフェチ", PLAYER) && TALENT:偏爱 + TALENT:相思相爱
				RETURNF 1
		ELSE
			SIF ABL:PLAYER:侍奉精神 + TALENT:PLAYER:献身 + TALENT:PLAYER:污臭钝感 + TALENT:PLAYER:弱气味 - TALENT:PLAYER:污臭敏感 >= 5
				RETURNF 1
		ENDIF
	CASE 98
		SIF COND("恥垢お掃除クンニフラグ")
			RETURNF 1
		SIF TALENT:PLAYER:男性
			RETURNF 0
		SIF MIN(ABL:技巧, ABL:侍奉精神) < 3
			RETURNF 0
		;前回お掃除フェラ実行の時に続けてやっちゃう感じで
		SIF PREVPLAY("お掃除クンニ") == 0 || CHECK_STAIN(STAIN:PLAYER:Ｖ, "Ｖ恥垢") == 0
			RETURNF 0
		;お掃除クンニよりは難易度高い
		SIF PLAYER == MASTER && TALENT:恋慕
			RETURNF 1
		SIF ABL:侍奉精神 + TALENT:献身 + TALENT:污臭钝感 + TALENT:弱气味 - TALENT:污臭敏感 >= 5 || TALENT:性变态 || TALENT:无视污臭
			RETURNF 1
	ENDSELECT
CASE "インサート自慰"
	SIF INSERT("インサート") == 0 || INSERT("複数挿入") || INSERT("インサート相手") != PLAYER
		RETURNF 0
	;腕の自由が損なわれない体位
	SIF INSERT("正常位") || INSERT("屈曲位") || INSERT("背面座位") || INSERT("騎乗位")
		RETURNF INSERT("インサート")
CASE "フェラ自慰"
	SIF COND("前回フェラ") && ABL:精液上瘾 >= 3 && ABL:自慰上瘾 && TCVAR:28 >= 40
		RETURNF 1
CASE "オナホ自慰"
	;触手プレイ中は駄目
	SIF TEQUIP:触手
		RETURNF 0
	;前回コマンドがオナホ
	SIF PREVCOM == 37 && ABL:Ｃ感觉 >= 3 && ABL:自慰上瘾
		RETURNF 1
CASE "乳首舐め可能"
	SIF COND("口の使用", PLAYER) == 0
		RETURNF 0
	IF INSERT("インサート")
		IF INSERT("後背位") || INSERT("背面座位")
			RETURNF 0
		ELSEIF TALENT:巨乳 < 3 && INSERT("騎乗位")
			RETURNF 0
		ENDIF
	ENDIF
	SIF INSERT("インサート", PLAYER) && (INSERT("後背位", PLAYER) || INSERT("背面座位", PLAYER) || INSERT("騎乗位", PLAYER) )
		RETURNF 0
	SIF TEQUIP:ニプルキャップ || TEQUIP:搾乳機 || TEQUIP:触手
		RETURNF 0
	IF TALENT:PLAYER:污臭钝感 || TALENT:PLAYER:无视污臭
	ELSEIF PLAYER == MASTER && (TALENT:偏爱 || TALENT:相思相爱)
	ELSEIF PLAYER == ASSI && ABL:ASSI:服从 >= 4
	ELSE
		SIF CHECK_STAIN(STAIN:Ｂ, "Ａ", "破瓜の血")
			RETURNF 0
		SIF TALENT:PLAYER:男性 && ABL:PLAYER:ＢＬ倾向 == 0 && CHECK_STAIN(STAIN:Ｂ, "Ｐ")
			RETURNF 0
		SIF ABL:PLAYER:精液上瘾 == 0 && CHECK_STAIN(STAIN:Ｂ, "精液")
			RETURNF 0
		SIF TALENT:PLAYER:旧支配者的末裔 == 0 && CHECK_STAIN(STAIN:Ｂ, "触手")
			RETURNF 0
	ENDIF
	RETURNF 1
CASE "母乳飲み可能"
	SIF TEQUIP:ニプルキャップ || TEQUIP:搾乳機 || TEQUIP:触手 || TALENT:母乳体质 == 0 || BASE:噴乳 < MAXBASE:噴乳*30/100
		RETURNF 0
	SIF CHECK_COM("乳首舐め可能", ARG)
		RETURNF 1
;これがＯＮだと胸愛撫が乳首舐めに変わる
CASE "乳首舐めに変化"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 5
		RETURNF 0
	SIF CHECK_COM("乳首舐め可能", ARG) == 0
		RETURNF 0
	;手で胸愛撫中
	SIF CHECK_COM("胸指愛撫", ARG)
		RETURNF 1
	;前回コマンドによっては変更される。例外的に乳首舐めの方が先に来る方が嬉しいコマンドとかね
	SELECTCASE PREVCOM
	CASE 5
		;乳首を舐めているが、触るのがまだの際には愛撫優先
		SIF CHECK_COM("胸指愛撫", ARG) == 0
			RETURNF 0
		RETURNF 1
	CASE 0, 2, 7, 8, 10, 11, 15, 60, 62, 63, 70, 72, 73, 90, 97, 104, 105
		RETURNF 1
	ENDSELECT
;胸愛撫コマンドでの内容詳細（TEQUIP:胸愛撫 で行為が記録されている）
;0bitで胸を揉む 1bitで乳首を弄る 3bitでピアスをいじめる 10bitで乳首を舐める 11bitでおっぱいを飲む
CASE "胸指愛撫"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 5
		RETURNF 0
	SIF GETBITS(TEQUIP:胸愛撫, 0, 2)
		RETURNF 1
CASE "胸揉み"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 5
		RETURNF 0
	SIF GETBIT(TEQUIP:胸愛撫, 0)
		RETURNF 1
CASE "乳首弄り"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 5
		RETURNF 0
	SIF GETBIT(TEQUIP:胸愛撫, 1)
		RETURNF 1
CASE "乳首指弄り"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 5
		RETURNF 0
	SIF GETBIT(TEQUIP:胸愛撫, 1) && GETBIT(TEQUIP:胸愛撫, 10) == 0
		RETURNF 1
CASE "Ｂピアス弄り"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 5
		RETURNF 0
	SIF GETBIT(TEQUIP:胸愛撫, 2)
		RETURNF 1
CASE "乳首舐め"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 5
		RETURNF 0
	SIF GETBIT(TEQUIP:胸愛撫, 10)
		RETURNF 1

CASE "キス"
	SIF FIRSTCOM(6) == 0 || TEQUIP:口枷 || TEQUIP:シックスナイン
		RETURNF 0
	SIF PLAYER == MASTER && !TALENT:MASTER:无视污臭 && STAIN:Ｍ&(2+4+8)
		RETURNF 0
	RETURNF 1

CASE "陰核愛撫可能"
	;ふたなりにはクリトリスが無いという事になってます現在は
	SIF PENIS(TARGET)
		RETURNF 0
	;クリトリスが弄れないとダメ
	SIF TEQUIP:クリキャップ
		RETURNF 0
	;ふきふき優先
	SIF CHECK_COM("ふきふき", ARG)
		RETURNF 0
	RETURNF 1
;これがＯＮだとアソコ愛撫が陰核愛撫に変わる
CASE "陰核愛撫に変化"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 7
		RETURNF 0
	SIF CHECK_COM("陰核愛撫可能", ARG) == 0
		RETURNF 0
	;現在陰核愛撫以外のアソコ愛撫実行中
	SIF CHECK_COM("アソコ愛撫", ARG) || CHECK_COM("恥丘愛撫", ARG)
		RETURNF 1
	;Ｖ使用中
	SIF TEQUIP:バイブ || INSERT("Ｖインサート")
		RETURNF 1
	;陰核露出状態だと優遇
	SIF RANDF(2) && CALCF("Ｃ発情") >= 2 && CHECK_CLO("Ｖ露出")
		RETURNF 1
	;前回コマンドによっては変更される。例外的に陰核愛撫の方が先に来る方が嬉しいコマンドとかね
	SELECTCASE PREVCOM
	;まだ未調整
	CASE 7
	;	;乳首を舐めているが、揉むのがまだの際には愛撫優先
	;	SIF CHECK_COM("胸揉み", ARG) == 0
	;		RETURNF 0
		RETURNF 1
	;クンニ、Ｖくぱぁなど陰核を強調したコマンド
	CASE 0, 1, 13, 30, 32, 34
		RETURNF 1
	ENDSELECT

CASE "ふきふき"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF COND("ふきふきフラグ")
		RETURNF 1
	SIF ARG != 7
		RETURNF 0
	;放尿後はどうしようか悩む
	;SIF PREVEX("放尿") && CHECK_STAIN(STAIN:Ｖ, "尿") && CHECK_CLO("Ｖ露出")
	;	RETURNF 1
	SIF PREVEX("Ｖ中出し") == 0 || CHECK_STAIN(STAIN:Ｖ, "精液") == 0 || TEQUIP:触手 || CHECK_CLO("Ｖ露出") == 0
		RETURNF 0
	RETURNF 1
;アソコ愛撫コマンドでの内容詳細（TEQUIP:アソコ愛撫 で行為が記録されている）
;0bitアソコ表層 1bit恥丘 2bitクリを弄る 3bitピアスをいじめる
CASE "アソコ愛撫"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 7
		RETURNF 0
	SIF GETBIT(TEQUIP:アソコ愛撫, 0)
		RETURNF 1
CASE "恥丘愛撫"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 7
		RETURNF 0
	SIF GETBIT(TEQUIP:アソコ愛撫, 1)
		RETURNF 1
CASE "陰核愛撫"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 7
		RETURNF 0
	SIF GETBIT(TEQUIP:アソコ愛撫, 2)
		RETURNF 1
CASE "Ｃピアス弄り"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 7
		RETURNF 0
	SIF GETBIT(TEQUIP:アソコ愛撫, 3)
		RETURNF 1

CASE "両穴くぱぁ"
	;能力と素質で制限
	SIF ABL:露出癖 >= 5 || TALENT:抖Ｍ
		RETURNF 1
	SIF ABL:露出癖 + ABL:受虐倾向 + TALENT:不知羞耻 + TALENT:淫乱*2 + TALENT:抖Ｍ + TALENT:性变态 - TALENT:羞耻心强*2 - TALENT:清纯*2 < 5
		RETURNF 0
	RETURNF 1

CASE "挿入前暴発"
	;既に挿入してるor３Ｐ中orペニバンなら起きない
	SIF INSERT("インサート") || COND("３Ｐ") || PENIS(PLAYER) == 0
		RETURNF 0

	;絶頂ゲージが振りきってないと暴発しない（絶頂ゲージが多いほど暴発率が上がる）
	SELECTCASE BASE:PLAYER:絶頂 / MAXBASE:PLAYER:絶頂
	CASE 0
		RETURNF 0
	CASEELSE
		SIF RANDF(1 + (BASE:PLAYER:絶頂 / MAXBASE:PLAYER:絶頂) ) == 0
			RETURNF 0
	ENDSELECT

	;精力に余裕がある状態じゃないと起きない
	SIF BASE:PLAYER:精力 < MAXBASE:PLAYER:精力*8/10
		RETURNF 0
	;今回、既にＶＡ挿入行為を経験済みだと平気（この条件は消す…かも）
	SIF EXP_GAIN(15, PLAYER) || EXP_GAIN(16, PLAYER)
		RETURNF 0

	;騎乗位以外の体位
	SELECTCASE SELECTCOM
	CASE 60 TO 64, 70 TO 74
		;慣れてなかったり技巧が低い必要がある
		SIF TALENT:PLAYER:童贞 || TALENT:PLAYER:早泄 || ABL:PLAYER:技巧 - MAX(ABL:ＬＶ - ABL:PLAYER:ＬＶ, 0)/2 < 2
			RETURNF 1
	ENDSELECT
CASE "Ｇスポ責め"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 8 || ABL:PLAYER:技巧 + ABL:PLAYER:指技 < 3 || COND("前Ｔ調教者") != PLAYER || PREVCOM != 8 || TEQUIP:バイブ || TEQUIP:触手
		RETURNF 0
	RETURNF 1
CASE "ワイルドスウィング"
	SIF BASE:ARG:気力 <= 0 || TEQUIP:ARG:押し倒し == 0 || INSERT("騎乗位", ARG) == 0 || COND("脚の使用", ARG) == 0
		RETURNF 0
	;連続ではあまりしてこない
	SIF INSERT("ワイルドスウィング", ARG) && RAND:4
		RETURNF 0
	SIF TEQUIP:ARG:Ｗ押し倒し || SELECTCOM:1 >= 0 || COND("前Ｔ調教者") != PLAYER || PENIS(PLAYER) == 0
		RETURNF 0
	;基本値は欲望
	LOCAL = ABL:ARG:欲望
	IF INSERT("Ｖインサート", ARG) && SELECTCOM >= 60 && SELECTCOM <= 69
		SIF EXP:ARG:Ｖ经验 + EXP:ARG:Ｖ性交经验*2 + EXP:ARG:腰技经验 <= 50
			RETURNF 0
		IF TALENT:ARG:淫壶
			LOCAL += 5
		ELSE
			;恥じらい
			SIF TALENT:ARG:羞耻心强
				LOCAL -= 5
			;清楚
			SIF TALENT:ARG:清纯
				LOCAL -= 3
		ENDIF
	ELSEIF INSERT("Ａインサート", ARG) && SELECTCOM >= 70 && SELECTCOM <= 79
		SIF EXP:ARG:Ａ经验 + EXP:ARG:Ａ性交经验*2 <= 50 || EXP:ARG:腰技经验 <= 20
			RETURNF 0
		SIF ABL:ARG:Ａ感觉 < 3
			LOCAL -= 5
		SELECTCASE PALAM:ARG:Ａ潤 + PALAM:PLAYER:Ｐ潤
		CASE IS < 3000
			LOCAL -= 5
		CASE IS < 10000
			LOCAL -= 2
		ENDSELECT
		IF TALENT:ARG:尻穴狂
			LOCAL += 5
		ELSE
			;恥じらい
			SIF TALENT:ARG:羞耻心强
				LOCAL -= 5
			;清楚
			SIF TALENT:ARG:清纯
				LOCAL -= 3
		ENDIF
	ELSEIF INSERT("尿道インサート", ARG) && SELECTCOM >= 83 && SELECTCOM <= 88
		SIF EXP:ARG:尿道经验 + EXP:ARG:尿道性交经验*2 <= 50 || EXP:ARG:腰技经验 <= 20 || PENIS(ARG)
			RETURNF 0
		SIF TALENT:ARG:尿道性感 == 0
			LOCAL -= 5

		IF TALENT:ARG:尿道狂
			LOCAL += 5
		ELSE
			;恥じらい
			SIF TALENT:ARG:羞耻心强
				LOCAL -= 5
			;清楚
			SIF TALENT:ARG:清纯
				LOCAL -= 3
		ENDIF
	ELSE
		RETURNF 0
	ENDIF
	;強気
	IF TALENT:ARG:强硬
		LOCAL += 2
		;PLAYERがあまりイってない
		SIF EXP_GAIN(6, ARG) < 5
			LOCAL += 1
	ENDIF
	;好色
	SIF TALENT:ARG:好色
		LOCAL += 1
	;淫乱
	SIF TALENT:ARG:淫乱
		LOCAL += 2
	;腰使い
	SIF ABL:ARG:腰技 || EXP:ARG:腰技经验 >= 100
		LOCAL += 1
	SIF COND("発情期", ARG)
		LOCAL += 2
	SIF LOCAL >= RAND:3 + 3
		RETURNF 1
CASE "挿入局所責め"
	SIF INSERT("ワイルドスウィング") || COND("前Ｔ調教者") != PLAYER || INSERT("Ｖインサート") == 0
		RETURNF ARG

	SELECTCASE PREVCOM
	;連続では起きない
	CASE 66 TO 69
		RETURNF ARG
	CASE ARG
		SIF INSERT("Ｖ対応コマンド") != ARG
			RETURNF ARG
	CASEELSE
		RETURNF ARG
	ENDSELECT

	;一時的に封印
	SIF COND("３Ｐ") || PREVCOM:2 >= 0
		RETURNF ARG
	IF TEQUIP:押し倒し
		SIF ABL:技巧 + ABL:腰技 <= 2 || ABL:Ｖ感觉 < 3 || EXP:Ｖ性交经验 < 20 || EXP:腰技经验 < 20
			RETURNF ARG
	ELSE
		SIF ABL:PLAYER:技巧 + ABL:PLAYER:腰技 <= 2
			RETURNF ARG
	ENDIF
	;ミニマムだとＧスポのみ
	SIF SIZE("ペニス", PLAYER) == 0 && TALENT:妖精 == 0
		RETURNF 66
	;たまにはＧスポ選択
	SIF RAND:5 == 0
		RETURNF 66

	;体位によってどっちになりやすいかがある
	SELECTCASE ARG
	CASE 61
		LOCAL = 80
	CASE 62
		LOCAL = 90
	CASE 63
		LOCAL = 70
	CASE 64
		LOCAL = 30
	CASEELSE
		LOCAL = 60
	ENDSELECT
	;Ｖ感覚が高く欲情しているほど子宮口へ行きやすい
	LOCAL += (MIN(GET_PALAMLV(PALAM:欲情), 5) + ABL:Ｖ感觉) * RAND:11
	;ハードスキンだとハードピストンしやすい
	SIF STATE("ハードスキン", PLAYER)
		LOCAL += 20
	;ペニスが大きめだと子宮口へ行きやすい
	SIF SIZE("ペニス", PLAYER) > MAX(ABL:Ｖ扩张, 1)
		LOCAL += 10
	;子宮性感が無い相手だとＧスポを攻めるのが無難
	SIF TALENT:子宫性感 == 0 && TALENT:史莱姆 == 0
		LOCAL -= 50
	;LOCALの値は0〜100
	SELECTCASE LOCAL
	CASE IS >= 100
		RETURNF 67
	CASEELSE
		RETURNF 66
	ENDSELECT
;意図せず子宮に入ってしまった
CASE "うっかり子宮挿入"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 67 || PREVCOM != 67
		RETURNF 0
	;子宮口が緩んでいない
	SIF COND("子宮口弛緩度") == 0
		RETURNF 0
	SIF COND("前Ｔ調教者") != PLAYER || INSERT("Ｖインサート") == 0 || EXP_GAIN(68)
		RETURNF 0
	RETURNF 1
CASE "双頭バイブプレイ"
	SIF TALENT:PLAYER:男性 || COND("双頭バイブ") == 0
		RETURNF 0
	RETURNF 1
CASE "お掃除フェラ"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF CHECK_COM("恥垢お掃除フェラ", ARG)
		RETURNF 1
	SELECTCASE ARG
	CASE 4
		SIF COND("お掃除フェラフラグ", PLAYER)
			RETURNF 1
		SIF PENIS(TARGET) == 0
			RETURNF 0
		;押し倒し状態は今のところなし
		SIF TEQUIP:押し倒し
			RETURNF 0
		;尿のお掃除orセックスのお掃除
		SIF PREVPLAY("挿入中") && CHECK_STAIN(STAIN:Ｐ, "Ｖ", "Ａ")
			RETURNF 1
		SIF PREVPLAY("尿道インサート") && PREVEX("尿道内射精")
			RETURNF 1
		SIF PREVPLAY("放尿") && CHECK_STAIN(STAIN:Ｐ, "尿")
			RETURNF 1
	CASE 91
		SIF COND("お掃除フェラフラグ")
			RETURNF 1
		SIF PENIS(PLAYER) == 0
			RETURNF 0
		SIF MIN(ABL:技巧, ABL:侍奉精神) < 3
			RETURNF 0
		;尿のお掃除orセックスのお掃除
		SIF PREVPLAY("挿入中", PLAYER) && CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｖ", "Ａ")
			RETURNF 1
		SIF PREVPLAY("尿道挿入中", PLAYER) && PREVEX("尿道内射精")
			RETURNF 1
		SIF PREVPLAY("放尿", PLAYER) && CHECK_STAIN(STAIN:PLAYER:Ｐ, "尿")
			RETURNF 1
	ENDSELECT
	RETURNF LOCAL
CASE "恥垢お掃除フェラ"
	SIF ARG == 0
		ARG = SELECTCOM
	SELECTCASE ARG
	CASE 4
		SIF COND("恥垢お掃除フェラフラグ", PLAYER)
			RETURNF 1
		SIF PENIS(TARGET) == 0 || COND("真性包茎")
			RETURNF 0
		;押し倒し状態は今のところなし
		SIF TEQUIP:押し倒し
			RETURNF 0
		;前回お掃除フェラ実行の時に続けてやっちゃう感じで
		SIF PREVPLAY("お掃除フェラ", PLAYER) == 0 || CHECK_STAIN(STAIN:Ｐ, "Ｐ恥垢") == 0
			RETURNF 0
		;お掃除フェラよりは難易度高い
		SIF TALENT:PLAYER:无视污臭 || TALENT:PLAYER:性变态
			RETURNF 1
		IF PLAYER == MASTER
			SIF COND("性癖：においフェチ", PLAYER) && TALENT:偏爱 + TALENT:相思相爱
				RETURNF 1
		ELSE
			SIF ABL:PLAYER:侍奉精神 + TALENT:PLAYER:献身 + TALENT:PLAYER:污臭钝感 + TALENT:PLAYER:弱气味 - TALENT:PLAYER:污臭敏感 >= 5
				RETURNF 1
		ENDIF
	CASE 91
		SIF COND("恥垢お掃除フェラフラグ")
			RETURNF 1
		SIF PENIS(PLAYER) == 0 || COND("真性包茎", PLAYER)
			RETURNF 0
		SIF MIN(ABL:技巧, ABL:侍奉精神) < 3
			RETURNF 0
		;前回お掃除フェラ実行の時に続けてやっちゃう感じで
		SIF PREVPLAY("お掃除フェラ") == 0 || CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｐ恥垢") == 0
			RETURNF 0
		;お掃除フェラよりは難易度高い
		SIF PLAYER == MASTER && TALENT:恋慕
			RETURNF 1
		SIF ABL:侍奉精神 + TALENT:献身 + TALENT:污臭钝感 + TALENT:弱气味 - TALENT:污臭敏感 >= 5 || TALENT:性变态 || TALENT:无视污臭
			RETURNF 1
	ENDSELECT
CASE "手コキフェラ"
	SIF COND("前Ｔ調教者") != PLAYER || TEQUIP:Ｗ押し倒し
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	SIF (ARG == 90 && PREVCOM == 91) || (ARG == 90 && PREVCOM == 140) || (ARG == 91 && PREVCOM == 90)
		RETURNF ARG
CASE "パイズリフェラ"
	SIF COND("前Ｔ調教者") != PLAYER || TEQUIP:Ｗ押し倒し
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	SIF (ARG == 91 && PREVCOM == 81) || (ARG == 91 && PREVCOM == 92) || (ARG == 92 && PREVCOM == 91)
		RETURNF ARG
CASE "Ａ責め手コキ"
	SIF COND("前Ｔ調教者") != PLAYER || TEQUIP:Ｗ押し倒し || TEQUIP:シックスナイン
		RETURNF 0
	SIF ABL:侍奉精神 + TALENT:献身 < 3 && ABL:施虐倾向 < 3
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	SIF (ARG == 90 || ARG == 97) && PREVCOM == 96
		RETURNF ARG
	SIF ARG == 96 && (PREVCOM == 97 || PREVCOM == 90)
		RETURNF ARG
CASE "ぱふぱふ手コキ"
	SIF !TALENT:巨乳 || COND("前Ｔ調教者") != PLAYER || TEQUIP:Ｗ押し倒し || TEQUIP:シックスナイン
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM

	SELECTCASE ARG
	CASE 90, 97
		SIF PREVCOM == 104
			RETURNF ARG
		SIF PREVCOM == 5 && PREVPLAY("母乳飲み", PLAYER)
			RETURNF ARG
	CASE 104
		SIF PREVCOM == 90 || PREVCOM == 97
			RETURNF ARG
	ENDSELECT

CASE "足コキ：踏みつけ"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 95
		RETURNF 0
	SIF GETBIT(TEQUIP:足コキ, 0)
		RETURNF 1
CASE "足コキ：股間見せ"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 95
		RETURNF 0
	SIF GETBIT(TEQUIP:足コキ, 1)
		RETURNF 1
CASE "足コキ：片足コキ"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 95
		RETURNF 0
	SIF GETBIT(TEQUIP:足コキ, 2) == 0
		RETURNF 1
CASE "足コキ：両足コキ"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 95
		RETURNF 0
	SIF GETBIT(TEQUIP:足コキ, 2)
		RETURNF 1

CASE "メルティキッス"
	;蕩唇
	SIF TALENT:荡唇
		RETURNF 1
	LOCAL = RAND:(BASE:精力 + 1)
	;ストゥーナには相性が悪い
	SIF NO:TARGET == 34
		LOCAL *= 5
	;メローナが蕩唇
	SIF TALENT:ARG:荡唇
		LOCAL /= 4
	;Ｍ敏感
	SIF TALENT:Ｍ敏感
		LOCAL /= 2
	;Ｍ鈍感
	SIF TALENT:Ｍ钝感
		LOCAL *= 2
	SIF RAND:(BASE:ARG:精力 + 1) >= LOCAL
		RETURNF 1
CASE "口内放尿"
	SIF COND("前Ｔ調教者") != PLAYER || TEQUIP:押し倒し || COND("口の使用") == 0
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG != 138
		RETURNF 0
	IF PENIS(PLAYER)
		SIF PREVEX("口内射精")
			RETURNF 1
	ELSE
		SIF PREVCOM == 98
			RETURNF 1
	ENDIF
CASE "子宮内放尿"
	SIF COND("前Ｔ調教者") != PLAYER || PENIS(PLAYER) == 0 || TEQUIP:押し倒し || PREVCOM != 68
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	;インサート中＆前回中出し
	SIF ARG == 138 && INSERT("Ｖインサート") && INSERT("インサート相手") == PLAYER && PREVEX("Ｖ中出し")
		RETURNF 1
CASE "膣内放尿"
	SIF COND("前Ｔ調教者") != PLAYER || PENIS(PLAYER) == 0 || TEQUIP:押し倒し
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	;インサート中＆前回中出し
	SIF ARG == 138 && INSERT("Ｖインサート") && INSERT("インサート相手") == PLAYER && PREVEX("Ｖ中出し")
		RETURNF 1
CASE "温泉浣腸"
	SIF COND("前Ｔ調教者") != PLAYER || PENIS(PLAYER) == 0 || TEQUIP:押し倒し
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	;インサート中＆前回中出し
	SIF ARG == 138 && INSERT("Ａインサート") && INSERT("インサート相手") == PLAYER && PREVEX("Ａ中出し")
		RETURNF 1
CASE "乳内放尿"
	SIF COND("前Ｔ調教者") != PLAYER || PENIS(PLAYER) == 0 || TEQUIP:押し倒し
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	;ニプルファック＆前回中出し
	SIF ARG == 138 && PREVCOM == 82 && PREVEX("乳内射精")
		RETURNF 1
CASE "膀胱内放尿"
	SIF COND("前Ｔ調教者") != PLAYER || PENIS(PLAYER) == 0 || TEQUIP:押し倒し
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	;尿道インサート中＆前回中出し
	SIF ARG == 138 && INSERT("尿道インサート") && INSERT("インサート相手") == PLAYER && PREVEX("尿道内射精")
		RETURNF 1
CASE "強制放尿"
	SIF TEQUIP:押し倒し
		RETURNF 0
	;お風呂場以外で従順３未満
	SIF TEQUIP:お風呂プレイ == 0 && ABL:服从 <= 2
		RETURNF 1
	SIF MAX(ABL:服从 - 1, ABL:欲望 - 1, ABL:露出癖, ABL:受虐倾向) < 3
		RETURNF 1
CASE "二本フィスト"
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG >= 141 && ARG <= 142 && GETBIT(TEQUIP:PLAYER:特殊行動, 1)
		RETURNF 1
;以下３Ｐ協力コマンド
CASE "Ｖ二本挿入"
	SIF PLAYER:1 <= 0 || STATE("撃沈", PLAYER:1)
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG == 60 && INSERT("Ｖインサート", TARGET, PLAYER:1) && ABL:Ｖ扩张 >= SIZE("Ｗペニス")
		RETURNF 1
CASE "Ａ二本挿入"
	SIF PLAYER:1 <= 0 || STATE("撃沈", PLAYER:1)
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG == 71 && INSERT("Ａインサート", TARGET, PLAYER:1) && ABL:Ａ扩张 >= SIZE("Ｗペニス")
		RETURNF 1
CASE "二穴挿入"
	SIF PLAYER:1 <= 0 || STATE("撃沈", PLAYER:1)
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	IF ARG == 60 && INSERT("Ａインサート", TARGET, PLAYER:1)
		RETURNF 1
	ELSEIF ARG == 71 && INSERT("Ｖインサート", TARGET, PLAYER:1)
		RETURNF 1
	ENDIF
CASE "左右ニプルファック"
	SIF PLAYER:1 <= 0 || STATE("撃沈", PLAYER:1)
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	SIF ARG == 82 && PREVCOM:2 == 82
		RETURNF 1
CASE "Ｗ奉仕"
	SIF PLAYER:1 <= 0 || STATE("撃沈", PLAYER:1)
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	SELECTCASE ARG
	CASE 90
		SIF (GROUPMATCH(ARG, SELECTCOM:1, PREVCOM:1) || PREVCOM:1 == 97) && PENIS(PLAYER)
			RETURNF 1
	CASE 91
		SIF GROUPMATCH(ARG, SELECTCOM:1, PREVCOM:1) || PREVCOM:1 == 140
			RETURNF 1
	CASE 92 TO 94, 104
		SIF GROUPMATCH(ARG, SELECTCOM:1, PREVCOM:1)
			RETURNF 1
	CASE 95
		SIF GROUPMATCH(ARG, SELECTCOM:1, PREVCOM:1) && PENIS(PLAYER)
			RETURNF 1
	CASE 97
		SIF GROUPMATCH(ARG, SELECTCOM:1, PREVCOM:1) && CHECK_CLO("ぱんつコキ", TARGET:1)
			RETURNF 1
	ENDSELECT
;広い意味だと何でもＯＫっぽいので、あえて奉仕コマンド限定の方がいいかも
CASE "３Ｐ奉仕"
	SIF PLAYER:1 <= 0 || STATE("撃沈", PLAYER:1)
		RETURNF 0
	SIF ARG == 0
		ARG = SELECTCOM
	SELECTCASE ARG
	;対面座位はダメ
	CASE 63, 73
	;インサート系
	CASE 60 TO 79, 83 TO 88
		;前回が手コキ、フェラ、パイズリ、アナル奉仕、ぱんつコキ、クンニさせる、イラマチオのどれかでないと駄目
		SIF GROUPMATCH(PREVCOM:2, 90, 91, 92, 96, 97, 98, 140) == 0
			RETURNF 0

		SELECTCASE PREVCOM:2
		CASE 90, 91, 96 TO 98, 140
			RETURNF PREVCOM:2
		CASE 92
			SELECTCASE ARG
			CASE 64, 65, 74, 75
				RETURNF PREVCOM:2
			ENDSELECT
		ENDSELECT
	;特殊インサート系。今のところニプルファックのみ
	CASE 82
		;前回が、ニプルファック、手コキ、フェラ、ぱんつコキでないと駄目
		SIF GROUPMATCH(PREVCOM:2, 82, 90, 91, 97) == 0
			RETURNF 0
		RETURNF PREVCOM:2
	;奉仕系。手コキ、フェラ、パイズリ、アナル奉仕、ぱんつコキ、クンニさせる、イラマチオ
	CASE 90 TO 92, 96 TO 98, 140
		;挿入されながら奉仕
		SIF INSERT("インサート", TARGET, PLAYER:1)
			RETURNF 1

		SELECTCASE ARG
		;手コキ
		CASE 90
			;二本手コキorフェラ＆手コキorニプルファック＆手コキ
			SIF GROUPMATCH(PREVCOM:2, 90, 97)
				RETURNF 90
			SIF GROUPMATCH(PREVCOM:2, 91, 140)
				RETURNF 91
			SIF PREVCOM:2 == 82
				RETURNF 82
		;フェラ
		CASE 91
			;二本フェラorフェラ＆手コキorニプルファック＆フェラ
			SIF GROUPMATCH(PREVCOM:2, 91, 140)
				RETURNF 91
			SIF GROUPMATCH(PREVCOM:2, 90, 97)
				RETURNF 90
			SIF PREVCOM:2 == 82
				RETURNF 82
		;ぱんつコキ
		CASE 97
			;二本手コキorフェラ＆手コキorニプルファック＆ぱんつコキ
			SIF PREVCOM:2 == 90
				RETURNF 90
			SIF GROUPMATCH(PREVCOM:2, 91, 140)
				RETURNF 91
			SIF PREVCOM:2 == 82
				RETURNF 82
		ENDSELECT
	ENDSELECT
ENDSELECT
RETURNF 0

@PRINT_TRAIN_NAME(ARG)
STR:2 = %TRAIN_NAME(ARG)%
PRINTFORML %STR:2%

;-------------------------------------------------
;変化する要素を加味したTRAINNAME（コマンド中の表示）
;-------------------------------------------------
@TRAIN_NAME(ARG)
#FUNCTIONS
;３Ｐの時
IF CHECK_COM("３Ｐ奉仕") && COND("３Ｐ")
	LOCALS = %TRAINNAME:(SELECTCOM:2)% ＆ %TRAINNAME:ARG%
	SELECTCASE ARG
	CASE 82
		SIF CHECK_COM("左右ニプルファック", ARG)
			LOCALS = 左右ニプルファック
	CASE 90
		SIF CHECK_COM("３Ｐ奉仕", ARG) == 90
			LOCALS = 二本手コキ
	CASE 91
		SIF CHECK_COM("３Ｐ奉仕", ARG) == 91
			LOCALS = 二本フェラ
	ENDSELECT
	RETURNF LOCALS
ENDIF

LOCALS = %TRAIN_NAME_SELECT(ARG)%
SELECTCASE ARG
CASE 0
	IF CHECK_COM("ローションマッサージ", ARG)
		LOCALS = ローション尻愛撫
	ELSEIF CHECK_COM("フラート", ARG)
		LOCALS = フラート
	ENDIF
CASE 1
	IF CHECK_COM("恥垢お掃除クンニ", ARG)
		LOCALS = 恥垢お掃除クンニ
	ELSEIF CHECK_COM("お掃除クンニ", ARG)
		LOCALS = お掃除クンニ
	ELSEIF TEQUIP:押し倒し
		IF CHECK_CLO("ブルマ露出")
			LOCALS = ブルマ顔面騎乗
		ELSE
			LOCALS = 顔面騎乗
		ENDIF
	ELSEIF TEQUIP:羞恥プレイ
		LOCALS = 岩清水
	ELSE
		LOCALS = クンニ
	ENDIF
CASE 60 TO 69
	IF ARG == 60
		IF CHECK_COM("Ｖ二本挿入")
			LOCALS = Ｖ二本挿入
		ELSEIF CHECK_COM("二穴挿入")
			LOCALS = 二穴挿入
		ENDIF
	ELSEIF ARG == 64 && TEQUIP:羞恥プレイ
		LOCALS = 乱れ牡丹
	ELSEIF ARG == 65 && INSERT("ワイルドスウィング")
		LOCALS = ワイルドスウィング
	ELSEIF ARG == 66
		LOCALS = %TRAINNAME:ARG%
		SIF INSERT("騎乗位")
			LOCALS = 騎乗Ｇスポ責め
	ELSEIF ARG == 67
		LOCALS = %TRAINNAME:ARG%
		;屈曲位で連続実行
		IF PREVCOM == 67 && INSERT("屈曲位")
			LOCALS = 種付けプレス
		ELSEIF PENIS(PLAYER) && STATE("ハードスキン", PLAYER) && TEQUIP:押し倒し == 0
			LOCALS = ハードピストン
		ELSEIF INSERT("騎乗位")
			LOCALS = 騎乗子宮口責め
		ENDIF
	ENDIF
CASE 70 TO 79
	IF ARG == 75 && INSERT("ワイルドスウィング")
		LOCALS = Ａワイルドスウィング
	ELSEIF ARG == PREVCOM && PENIS(PLAYER) && COND("前Ｔ調教者") == PLAYER && STATE("ハードスキン", PLAYER) && INSERT("Ａインサート")
		LOCALS = ハードアナルピストン
	ENDIF
	IF ARG == 71
		IF CHECK_COM("Ａ二本挿入")
			LOCALS = Ａ二本挿入
		ELSEIF CHECK_COM("二穴挿入")
			LOCALS = 二穴挿入
		ENDIF
	ENDIF
CASE 83 TO 88
	SIF INSERT("ワイルドスウィング")
		LOCALS = ワイルドスウィング
CASE 90
	IF LOCALS != TRAINNAME:ARG
	ELSEIF EQUIP:手袋 && !CHECK_GLOVE("指露出") && PENIS(PLAYER)
		LOCALS = 手袋コキ
	ELSEIF TALENT:哈比
		LOCALS = 羽コキ
	ENDIF
CASE 97
	IF LOCALS != TRAINNAME:ARG
	ELSEIF CHECK_OUTER("ブルマ")
		LOCALS = ブルマコキ
	ELSEIF CHECK_PANTIES("水着")
		LOCALS = 比基尼コキ
	ELSE
		LOCALS = ぱんつコキ
	ENDIF
CASE 173
	IF TEQUIP:新妻プレイ
		LOCALS = 新妻プレイ終了
	ELSEIF CHECK_CLO("水着姿")
		LOCALS = 新妻プレイ(水着エプロン)
	ELSEIF CHECK_CLO("ブルマ露出")
		LOCALS = 新妻プレイ(ブルマエプロン)
	ELSEIF CHECK_CLO("下着姿")
		LOCALS = 新妻プレイ(下着エプロン)
	ELSEIF CHECK_CLO("ハダカ")
		LOCALS = 新妻プレイ(ハダカエプロン)
	ELSE
		LOCALS = 新妻プレイ
	ENDIF
ENDSELECT
RETURNF LOCALS


;-------------------------------------------------
;変化する要素を加味したTRAINNAME（コマンド選択するときの表示）
;-------------------------------------------------
@TRAIN_NAME_SELECT(ARG)
#FUNCTIONS
LOCALS = %TRAIN_NAME_STANDARD(ARG)%
;特殊な名前
SELECTCASE ARG
CASE 0
	SIF CHECK_COM("ローションマッサージ", ARG)
		LOCALS = ローション尻愛撫
CASE 1
	IF CHECK_COM("恥垢お掃除クンニ", ARG)
		LOCALS = 恥垢お掃除クンニ
	ELSEIF CHECK_COM("お掃除クンニ", ARG)
		LOCALS = お掃除クンニ
	ENDIF
	SIF TEQUIP:押し倒し
		LOCALS = 顔面騎乗
CASE 2
	SIF TEQUIP:アナルバイブ && TEQUIP:触手 == 0
		LOCALS = Ａバイブ弄り
CASE 3
	IF GETBIT(CHECK_ONANIE(ARG), 14)
		LOCALS = オナホ%LOCALS%
	ELSEIF GETBIT(CHECK_ONANIE(ARG), 10)
		LOCALS = シャワー%LOCALS%
	ELSEIF GETBIT(CHECK_ONANIE(ARG), 13)
		LOCALS = 二穴バイブ%LOCALS%
	ELSEIF GETBIT(CHECK_ONANIE(ARG), 11)
		LOCALS = バイブ%LOCALS%
	ELSEIF GETBIT(CHECK_ONANIE(ARG), 12)
		LOCALS = Ａバイブ%LOCALS%
	ELSEIF TEQUIP:尿道ブジー
		LOCALS = 尿道%LOCALS%
	ELSEIF TEQUIP:尿道バイブ
		LOCALS = 尿道%LOCALS%
	ELSE
		;IF GETBIT(CHECK_ONANIE(ARG), 0)
		;	IF TALENT:男性
		;		LOCALS = Ｐ%LOCALS%
		;	ELSE
		;		LOCALS = Ｃ%LOCALS%
		;	ENDIF
		;ELSEIF GETBIT(CHECK_ONANIE(ARG), 1)
		;	LOCALS = Ｖ%LOCALS%
		;ELSEIF GETBIT(CHECK_ONANIE(ARG), 2)
		;	LOCALS = Ａ%LOCALS%
		;ELSEIF GETBIT(CHECK_ONANIE(ARG), 3)
		;	LOCALS = Ｂ%LOCALS%
		;ENDIF
	ENDIF

	IF CHECK_COM("インサート自慰")
		IF INSERT("Ｖインサート")
			LOCALS = %CONDS("体位")%%LOCALS%
		ELSEIF INSERT("Ａインサート")
			LOCALS = Ａ%CONDS("体位")%%LOCALS%
		ELSEIF INSERT("尿道インサート")
			LOCALS = 尿道%CONDS("体位")%%LOCALS%
		ENDIF
	ENDIF
	SIF STRLENS(LOCALS) > 14
		RETURNF LOCALS

	IF TEQUIP:ビデオカメラ
		LOCALS = 公開%LOCALS%
	ELSEIF TEQUIP:野外プレイ
		LOCALS = 野外%LOCALS%
	ENDIF
CASE 4
	IF CHECK_COM("恥垢お掃除フェラ", ARG)
		LOCALS = 恥垢お掃除フェラ
	ELSEIF CHECK_COM("お掃除フェラ", ARG)
		LOCALS = お掃除フェラする
	ENDIF
CASE 5
	IF CHECK_COM("乳首舐めに変化", ARG)
		IF CHECK_COM("母乳飲み可能", ARG)
			IF CHECK_COM("胸揉み", ARG) && CHECK_COM("乳首舐め", ARG)
				LOCALS = 胸愛撫＆母乳飲み
			ELSE
				LOCALS = 母乳飲み
			ENDIF
		ELSEIF CHECK_COM("胸揉み", ARG) && CHECK_COM("乳首舐め", ARG)
			LOCALS = 胸愛撫＆乳首舐め
		ELSE
			LOCALS = 乳首舐め
		ENDIF
	ENDIF
CASE 6
	SIF NUM("ターン数") == 0
		LOCALS = 温柔的吻
CASE 7
	IF CHECK_COM("ふきふき", ARG)
		LOCALS = ふきふき
	ELSEIF CHECK_COM("陰核愛撫に変化", ARG)
		LOCALS = 陰核愛撫
	ENDIF
CASE 8
	IF CHECK_COM("Ｇスポ責め", ARG)
		LOCALS = Ｇスポット刺激
	ELSEIF TEQUIP:バイブ
		LOCALS = バイブ弄り
	ENDIF
CASE 9
	SIF TEQUIP:押し倒し
		LOCALS = Ａ顔面騎乗
CASE 10
	SIF INSERT("尿道インサート", TARGET, PLAYER)
		LOCALS = オナホプレイ
CASE 11
	SIF TEQUIP:押し倒し
		LOCALS = 胸の谷間を犯す
CASE 12
	SIF PENIS(TARGET) == 0
		LOCALS = 用脚玩弄股间
CASE 13
	IF PREVCOM == 13
		IF COND("両穴くぱぁ")
			LOCALS = 両穴観察
		ELSE
			LOCALS = アソコ観察
		ENDIF
	ELSEIF PREVCOM == 14 && CHECK_COM("両穴くぱぁ")
		LOCALS = 両穴くぱぁさせる
	ENDIF
CASE 14
	IF PREVCOM == 14
		IF COND("両穴くぱぁ")
			LOCALS = 両穴観察
		ELSE
			LOCALS = アナル観察
		ENDIF
	ELSEIF PREVCOM == 13 && CHECK_COM("両穴くぱぁ")
		LOCALS = 両穴くぱぁさせる
	ENDIF
CASE 15
	SIF TEQUIP:尿道バイブ
		LOCALS = バイブ弄り
CASE 31
	SIF TEQUIP:バイブ
		LOCALS = バイブを抜く
CASE 33
	IF COND("アナルパール")
		LOCALS = アナルパールを抜く
	ELSEIF TEQUIP:アナルバイブ
		LOCALS = Ａバイブを抜く
	ENDIF
CASE 34
	SIF TEQUIP:クリキャップ
		LOCALS = クリキャップを外す
CASE 35
	SIF TEQUIP:ニプルキャップ
		LOCALS = ニプルキャップを外す
CASE 36
	SIF TEQUIP:搾乳機
		LOCALS = 搾乳機を外す
CASE 37
	SIF TEQUIP:オナホール
		LOCALS = オナホールを外す
CASE 38
	SIF TEQUIP:シャワー
		LOCALS = シャワーを止める
CASE 39
	SIF TEQUIP:ローター挿入
		LOCALS = ローターを引き抜く
CASE 60 TO 79
	IF TEQUIP:押し倒し == 0 && ARG >= 60 && ARG <= 65
		SELECTCASE CHECK_COM("挿入局所責め", ARG)
		CASE 66, 67
			LOCALS = 挿入局所責め
		ENDSELECT
	ENDIF
	IF CHECK_COM("３Ｐ奉仕", ARG)
		LOCALS = %TRAINNAME:ARG%(３Ｐ)
	ELSEIF (ARG == 60 || ARG == 71) && COND("３Ｐ")
		IF CHECK_COM("Ｖ二本挿入", ARG)
			LOCALS = Ｖ二本挿入(３Ｐ)
		ELSEIF CHECK_COM("Ａ二本挿入", ARG)
			LOCALS = Ａ二本挿入(３Ｐ)
		ELSEIF CHECK_COM("二穴挿入", ARG)
			LOCALS = 二穴挿入(３Ｐ)
		ENDIF
	ELSEIF ARG == 67
		;屈曲位で連続実行
		SIF PREVCOM == 67 && INSERT("屈曲位")
			LOCALS = 種付けプレス
	ELSEIF ARG == 68
		;騎乗位
		SIF INSERT("騎乗位")
			LOCALS = 子宮挿入騎乗位
	ENDIF
CASE 80
	SIF CHECK_COM("双頭バイブプレイ")
		LOCALS = 双頭バイブプレイ
CASE 82
	IF CHECK_COM("３Ｐ奉仕", ARG)
		IF CHECK_COM("左右ニプルファック", ARG)
			LOCALS = 左右ニプルファック
		ELSE
			LOCALS = %LOCALS%(３Ｐ)
		ENDIF
	ELSEIF TEQUIP:押し倒し
		LOCALS = ニプルオナホ
	ENDIF
CASE 83
	SIF TEQUIP:押し倒し
		LOCALS = 尿道騎乗位
CASE 90
	IF CHECK_COM("Ｗ奉仕", ARG)
		LOCALS = Ｗ手コキ
	ELSEIF CHECK_COM("３Ｐ奉仕", ARG)
		IF CHECK_COM("３Ｐ奉仕", ARG) == 90
			LOCALS = 二本手コキ(３Ｐ)
		ELSE
			LOCALS = %LOCALS%(３Ｐ)
		ENDIF
	ELSEIF CHECK_COM("手コキフェラ", ARG)
		LOCALS = 手コキフェラ
	ELSEIF CHECK_COM("Ａ責め手コキ", ARG)
		LOCALS = Ａ責め手コキ
	ELSEIF CHECK_COM("ぱふぱふ手コキ", ARG)
		IF CHECK_COM("母乳飲み可能", ARG)
			LOCALS = 授乳手コキ
		ELSE
			LOCALS = ぱふぱふ手コキ
		ENDIF
	ENDIF
CASE 91
	IF CHECK_COM("Ｗ奉仕", ARG)
		IF CHECK_COM("お掃除フェラ", ARG)
			LOCALS = Ｗお掃除フェラ
		ELSE
			LOCALS = Ｗフェラチオ
		ENDIF
	ELSEIF CHECK_COM("３Ｐ奉仕", ARG)
		IF CHECK_COM("３Ｐ奉仕", ARG) == 91
			LOCALS = 二本フェラ
		ELSE
			LOCALS = %LOCALS%(３Ｐ)
		ENDIF
	ELSEIF CHECK_COM("手コキフェラ", ARG)
		LOCALS = 手コキフェラ
	ELSEIF CHECK_COM("パイズリフェラ", ARG)
		LOCALS = パイズリフェラ
	ELSEIF CHECK_COM("恥垢お掃除フェラ", ARG)
		LOCALS = 恥垢お掃除フェラ
	ELSEIF CHECK_COM("お掃除フェラ", ARG)
		LOCALS = お掃除フェラ
	ENDIF
CASE 92
	IF CHECK_COM("Ｗ奉仕", ARG)
		LOCALS = Ｗパイズリ
	ELSEIF CHECK_COM("３Ｐ奉仕", ARG)
		SIF INSERT("インサート", TARGET, PLAYER:1)
			LOCALS = %LOCALS%(３Ｐ)
	ELSEIF CHECK_COM("パイズリフェラ", ARG)
		LOCALS = パイズリフェラ
	;ELSEIF TEQUIP:お風呂プレイ
	;	LOCALS = 潜望鏡
	ENDIF
CASE 93
	SIF CHECK_COM("Ｗ奉仕", ARG)
		LOCALS = Ｗ素股
CASE 94
	SIF CHECK_COM("Ｗ奉仕", ARG)
		LOCALS = Ｗ泡踊り
CASE 95
	SIF CHECK_COM("Ｗ奉仕", ARG)
		LOCALS = Ｗ足コキ
CASE 96
	IF CHECK_COM("Ａ責め手コキ", ARG)
		IF PREVCOM == 97
			LOCALS = Ａ責めぱんつコキ
		ELSE
			LOCALS = Ａ責め手コキ
		ENDIF
	ELSEIF CHECK_COM("３Ｐ奉仕", ARG)
		SIF INSERT("インサート", TARGET, PLAYER:1)
			LOCALS = %LOCALS%(３Ｐ)
	ELSEIF TEQUIP:押し倒し
		LOCALS = アナル責め
	ENDIF
CASE 97
	IF CHECK_COM("Ｗ奉仕", ARG)
		LOCALS = Ｗぱんつコキ
	ELSEIF CHECK_COM("３Ｐ奉仕", ARG)
		LOCALS = %LOCALS%(３Ｐ)
	ELSEIF CHECK_COM("Ａ責め手コキ", ARG)
		LOCALS = Ａ責めぱんつコキ
	ELSEIF CHECK_COM("ぱふぱふ手コキ", ARG)
		IF CHECK_COM("母乳飲み可能", ARG)
			LOCALS = 授乳ぱんつコキ
		ELSE
			LOCALS = ぱふぱふぱんつコキ
		ENDIF
	ENDIF
CASE 98
	IF CHECK_COM("３Ｐ奉仕", ARG)
		SIF INSERT("インサート", TARGET, PLAYER:1)
			LOCALS = %LOCALS%(３Ｐ)
	ELSEIF CHECK_COM("恥垢お掃除クンニ", ARG)
		LOCALS = 恥垢お掃除クンニ
	ELSEIF CHECK_COM("お掃除クンニ", ARG)
		LOCALS = お掃除クンニさせる
	ELSEIF TEQUIP:押し倒し
		LOCALS = アソコ舐め
	ENDIF
CASE 99
	IF TEQUIP:シックスナイン
		LOCALS = シックスナイン終了
	;相互お掃除プレイ
	ELSEIF CHECK_COM("お掃除フェラ", 91) + CHECK_COM("お掃除クンニ", 98) && CHECK_COM("お掃除フェラ", 4) + CHECK_COM("お掃除クンニ", 1)
		LOCALS = 相互お掃除６９
	ENDIF
CASE 104
	IF CHECK_COM("Ｗ奉仕", ARG)
		LOCALS = Ｗぱふぱふ
	ELSEIF CHECK_COM("ぱふぱふ手コキ", 104)
		SIF CHECK_COM("母乳飲み可能", ARG)
			LOCALS = 授乳
		IF PREVCOM == 97
			LOCALS = %LOCALS%ぱんつコキ
		ELSE
			LOCALS = %LOCALS%手コキ
		ENDIF
	ELSEIF CHECK_COM("母乳飲み可能", ARG)
		LOCALS = 授乳%LOCALS%
	ENDIF
CASE 120
	SIF TEQUIP:押し倒し
		LOCALS = 逆レイプ
CASE 126
	SIF TEQUIP:押し倒し
		LOCALS = 逆アナル
CASE 133
	SIF TEQUIP:アイマスク
		LOCALS = 目隠しを外す
CASE 134, 206
	IF TEQUIP:拘束
		LOCALS = 拘束を解く
	ELSEIF TEQUIP:触手
		LOCALS = 触手緊縛
	ELSE
		LOCALS = 縄・拘束
	ENDIF
CASE 135
	SIF TEQUIP:口枷
		LOCALS = 口枷を外す
CASE 136
	IF COND("アナル栓")
		IF COND("液体浣腸")
			LOCALS = 排泄
		ELSE
			LOCALS = 腸内ガス排泄
		ENDIF
	ELSEIF COND("液体浣腸")
		LOCALS = 浣腸液追加
	ENDIF
CASE 137
	SIF TEQUIP:拡張バルーン
		LOCALS = 拡張バルーン終了
CASE 138
	IF CHECK_COM("口内放尿", ARG)
		LOCALS = 口内放尿
	ELSEIF CHECK_COM("膣内放尿", ARG)
		LOCALS = 膣内放尿
		SIF CHECK_COM("子宮内放尿", ARG)
			LOCALS = 子宮内放尿
	ELSEIF CHECK_COM("温泉浣腸", ARG)
		LOCALS = 温泉浣腸
	ELSEIF CHECK_COM("乳内放尿", ARG)
		LOCALS = 乳内放尿
	ELSEIF CHECK_COM("膀胱内放尿", ARG)
		LOCALS = 膀胱内放尿
	ENDIF
CASE 139
	IF INSERT("インサート") || INSERT("挿入中") || TEQUIP:拘束 || TEQUIP:アイマスク

	ELSEIF TEQUIP:お風呂プレイ
		LOCALS = 湯船に浸かる
	ENDIF
CASE 140
	IF CHECK_COM("３Ｐ奉仕", ARG)
		SIF INSERT("インサート", TARGET, PLAYER:1)
			LOCALS = %LOCALS%(３Ｐ)
	ENDIF
CASE 144
	IF TEQUIP:押し倒し
		LOCALS = 聖水プレイ
	ELSEIF CHECK_COM("強制放尿", ARG)
		LOCALS = 強制放尿
	ENDIF
CASE 145
	SIF TEQUIP:押し倒し
		LOCALS = 逆剃毛
CASE 149
	SIF TEQUIP:カテーテル
		LOCALS = カテーテルを抜く
CASE 151
	SIF TEQUIP:尿道ブジー
		LOCALS = プジーを抜く
CASE 152
	SIF TEQUIP:尿道バイブ
		LOCALS = バイブを抜く
CASE 150
	SIF TEQUIP:ディープスロート
		LOCALS = ディープスロート終了
CASE 170
	SIF TEQUIP:ビデオカメラ
		LOCALS = 撮影終了
CASE 171
	SIF TEQUIP:野外プレイ
		LOCALS = 野外プレイ終了
CASE 172
	SIF TEQUIP:お風呂プレイ
		LOCALS = 風呂から出る
CASE 173
	SIF TEQUIP:新妻プレイ
		LOCALS = 新妻プレイ終了
CASE 174
	SIF TEQUIP:館内露出プレイ
		LOCALS = 部屋に戻る
CASE 175
	IF TEQUIP:押し倒し
		IF EQUIP:パンツ && EQUIP:ボディスーツ + EQUIP:アウター == 0 && CHECK_CLO("パンツ露出")
			LOCALS = 下着を脱ぐ
		ELSE
			LOCALS = ハダカになる
		ENDIF
	ELSEIF TEQUIP:拘束 || TEQUIP:館内露出プレイ || TEQUIP:野外プレイ
		IF EQUIP:パンツ && EQUIP:ボディスーツ + EQUIP:アウター == 0 && CHECK_CLO("パンツ露出")
			LOCALS = 下着を脱がす
		ELSE
			LOCALS = ハダカにする
		ENDIF
	ENDIF
CASE 179
	SIF TEQUIP:羞恥プレイ
		LOCALS = 羞恥プレイ終了
CASE 182
	SIF TEQUIP:PLAYER:押し倒し
		LOCALS = 押し倒し終了

CASE 200
	SIF TEQUIP:触手
		LOCALS = 触手送還
CASE 201
	SIF TEQUIP:バイブ
		LOCALS = 触手挿入終了
CASE 202
	SIF TEQUIP:アナルバイブ
		LOCALS = アナル触手終了
CASE 203
	SIF TEQUIP:クリキャップ
		LOCALS = 触手クリ責め終了
CASE 204
	SIF TEQUIP:ニプルキャップ
		LOCALS = 触手乳首責め終了
CASE 205
	SIF TEQUIP:搾乳機
		LOCALS = 触手搾乳終了
CASE 207
	SIF TEQUIP:浣腸
		LOCALS = 排泄
CASE 208
	SIF TEQUIP:触手口辱
		LOCALS = 触手口辱終了
CASE 209
	SIF TEQUIP:オナホール
		LOCALS = 触手ペニス責め終了

CASE 243
	;TARGETが唱える際はＷ押し倒しコマンド
	IF GETBIT(MAGIC_USER(ARG, "コマンド"), 2)
		LOCALS = %TALENT_NAME(ARG, TALENT:ARG)%押し倒し
	;MASTERとASSIが両方使える
	ELSEIF GETBIT(MAGIC_USER(ARG, "コマンド"), 0) && GETBIT(MAGIC_USER(ARG, "コマンド"), 1)
		;名称が違う
		IF TALENT_NAME(ARG, TALENT:MASTER:ARG) != TALENT_NAME(ARG, TALENT:ASSI:ARG)
			LOCALS = %TALENT_NAME(ARG, TALENT:MASTER:ARG)%・%TALENT_NAME(ARG, TALENT:ASSI:ARG)%
		ELSE
			LOCALS = %TALENT_NAME(ARG, TALENT:MASTER:ARG)%
		ENDIF
	;使用者がASSIのみ
	ELSEIF GETBIT(MAGIC_USER(ARG, "コマンド"), 0)
		LOCALS = %TALENT_NAME(ARG, TALENT:ASSI:ARG)%
	;使用者がMASTERのみ
	ELSEIF GETBIT(MAGIC_USER(ARG, "コマンド"), 1)
		LOCALS = %TALENT_NAME(ARG, TALENT:MASTER:ARG)%
	ENDIF

CASE 260
	SIF CHECK_COM("ローションマッサージ", ARG)
		LOCALS = ローション尻愛撫

CASE 280
	SIF TEQUIP:PLAYER:拘束 || STATE("束缚术", PLAYER)
		LOCALS = ふりほどく

;自慰する
CASE 282
	SELECTCASE PREVCOM
	CASE 3
		LOCALS = 相互自慰
	CASE 1, 4, 9, 13, 14
	CASEELSE
		SIF TEQUIP:PLAYER:自慰する == 3
			LOCALS = 相互自慰
	ENDSELECT

CASE 301
	SIF TEQUIP:高潮管理
		LOCALS = 手加減を止める
CASE 302
	SIF TEQUIP:演奏中
		LOCALS = 演奏中断
CASE 304
	SIF TEQUIP:押し倒し
		LOCALS = 逆ドラ姦
CASE 305
	SIF TEQUIP:押し倒し
		LOCALS = 逆Ａドラ姦
CASE 306
	SIF STATE("ハードスキン", PLAYER)
		LOCALS = ハードスキン解除
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;おもに押し倒しなど場面変化を考慮した標準のTRAINNAME
;（ここで設定したものにコマンド名が変わっても、コマンドの色が変化しない）
;-------------------------------------------------
@TRAIN_NAME_STANDARD(ARG)
#FUNCTIONS
LOCALS = %TRAINNAME:ARG%
SELECTCASE ARG
CASE 1
	SIF TEQUIP:押し倒し
		LOCALS = 顔面騎乗
CASE 9
	SIF TEQUIP:押し倒し
		LOCALS = Ａ顔面騎乗
CASE 11
	SIF TEQUIP:押し倒し
		LOCALS = 胸の谷間を犯す
CASE 12
	SIF PENIS(TARGET) == 0
		LOCALS = 用脚玩弄股间
CASE 82
	SIF TEQUIP:押し倒し
		LOCALS = ニプルオナホ
CASE 96
	SIF TEQUIP:押し倒し
		LOCALS = アナル責め
CASE 98
	SIF TEQUIP:押し倒し
		LOCALS = アソコ舐め
CASE 120
	SIF TEQUIP:押し倒し
		LOCALS = 逆レイプ
CASE 126
	SIF TEQUIP:押し倒し
		LOCALS = 逆アナル
CASE 144
	SIF TEQUIP:押し倒し
		LOCALS = 聖水プレイ
CASE 145
	SIF TEQUIP:押し倒し
		LOCALS = 逆剃毛

;160番台,助手による汎用コマンド
CASE 160
	LOCALS = %TEXTS("助手の名称")%を犯す
CASE 161
	LOCALS = %TEXTS("助手の名称")%を犯させる
CASE 162
	LOCALS = %TEXTS("助手の名称")%フェラ
CASE 163
	LOCALS = %TEXTS("助手の名称")%にキス

CASE 175
	IF TEQUIP:押し倒し
		IF EQUIP:パンツ && EQUIP:ボディスーツ + EQUIP:アウター == 0 && CHECK_CLO("パンツ露出")
			LOCALS = 下着を脱ぐ
		ELSE
			LOCALS = ハダカになる
		ENDIF
	ELSEIF TEQUIP:拘束 && TEQUIP:押し倒し == 0
		IF EQUIP:パンツ && EQUIP:ボディスーツ + EQUIP:アウター == 0 && CHECK_CLO("パンツ露出")
			LOCALS = 下着を脱がす
		ELSE
			LOCALS = ハダカにする
		ENDIF
	ENDIF
CASE 280
	SIF TEQUIP:PLAYER:拘束 || STATE("束缚术", PLAYER)
		LOCALS = ふりほどく
CASE 304
	SIF TEQUIP:押し倒し
		LOCALS = 逆ドラ姦
CASE 305
	SIF TEQUIP:押し倒し
		LOCALS = 逆Ａドラ姦
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;調教のジャンル判定
;-------------------------------------------------
@TRAIN_GENRE(ARGS, ARG)
#FUNCTION
SELECTCASE ARGS
CASE "Ｖ調教"
	SELECTCASE ARG
	CASE 8, 31, 39, 60 TO 69, 177, 201, 304, 307
		RETURNF 1
	ENDSELECT
CASE "Ａ調教"
	SELECTCASE ARG
	CASE 2, 9, 33, 70 TO 79, 136, 138, 142, 143, 202, 207, 305, 308
		RETURNF 1
	ENDSELECT
CASE "Ｐ調教"
	SELECTCASE ARG
	CASE 4, 10 TO 12, 37, 120 TO 129, 209
		RETURNF 1
	ENDSELECT
CASE "道具調教"
	SELECTCASE ARG
	CASE 30 TO 59
		RETURNF 1
	ENDSELECT
CASE "ＳＭ調教"
	SELECTCASE ARG
	CASE 130 TO 159, 206, 207
		RETURNF 1
	ENDSELECT
ENDSELECT

;-------------------------------------------------
;物の大きさを判定する関数
;-------------------------------------------------
@SIZE(ARGS, ARG)
#FUNCTION
VARSET LOCAL
SELECTCASE ARGS
CASE "指の直径"
	SIF ARG == 0
		ARG = TARGET
	;薬指の一番太い部分の値の平均値を使う
	;身長を(50+13x)cmとすると指は(7+x)mmぐらいか
	;妖精だと7〜8mm、小柄な女性で13mm、長身な女性でも16〜17mmぐらい。オトコだとそれぞれ+2mm
	LOCAL = MAX(BASE:ARG:身長 - 50, 0)/13 + 7

	SIF TALENT:ARG:男性
		LOCAL += 2
CASE "基本ペニスサイズ"
	SIF ARG == 0
		ARG = PLAYER
	IF CONFIG("ペニス詳細設定")
		IF GETBIT(TALENT:ARG:阴茎, 5)
			RETURNF 5
		ELSEIF GETBIT(TALENT:ARG:阴茎, 4)
			RETURNF 4
		ELSEIF GETBIT(TALENT:ARG:阴茎, 3)
			RETURNF 3
		ELSEIF GETBIT(TALENT:ARG:阴茎, 2)
			RETURNF 2
		ELSEIF GETBIT(TALENT:ARG:阴茎, 1)
			RETURNF 0
		ELSEIF GETBIT(TALENT:ARG:阴茎, 0)
			RETURNF 1
		ENDIF
	ELSE
		RETURNF 1 + (TALENT:ARG:龙之血 > 0) - (TALENT:ARG:梦魔 + TALENT:ARG:上位梦魔 + TALENT:ARG:魔王残片 == 0 && AGE(ARG) == 1)
	ENDIF
CASE "ペニスの直径"
	SIF ARG == 0
		ARG = PLAYER

	IF TALENT:ARG:妖精
		LOCAL = -7
	ELSEIF STATE("小根术", ARG)
		LOCAL = 0
	;オンナはペニバン
	ELSEIF TALENT:ARG:女性
		LOCAL = 7
	ELSE
		LOCAL = SIZE("基本ペニスサイズ", ARG)*7
		;ウルズグン
		LOCAL += STATE("精强术", ARG)*3
		IF STATE("大根术", ARG)
			LOCAL += 7
		ELSEIF STATE("小根术", ARG)
			LOCAL -= 7
		ENDIF
	ENDIF

	RETURNF 27 + LOCAL + BASE:ARG:識別番号%6
CASE "ペニス"
	SIF ARG == 0
		ARG = PLAYER
	;妖精orミニマム
	SIF TALENT:ARG:妖精 || STATE("小根术", ARG)
		RETURNF 0
	;オンナはペニバン
	SIF TALENT:ARG:女性
		RETURNF 1
	LOCAL = SIZE("基本ペニスサイズ", ARG)
	IF TALENT:ARG:普通人
		SELECTCASE RAND:100
		CASE IS >= 95
			LOCAL = 3
		CASE IS >= 85
			LOCAL = 2
		CASE IS >= 10
			LOCAL = 1
		CASE 0
			LOCAL = 0
		ENDSELECT
	ENDIF
	;ウルズグン
	SIF LOCAL <= 2 && STATE("精强术", ARG) >= 3
		LOCAL += 1
	SIF STATE("大根术", ARG)
		LOCAL += 1
CASE "Ｗペニス"
	IF PLAYER:1
		RETURNF 1 + SIZE("ペニス", MASTER) + SIZE("ペニス", PLAYER:1)
	ELSE
		RETURNF 1 + SIZE("ペニス", MASTER)
	ENDIF
CASE "バイブ"
	SIF ARG == 0
		ARG = TARGET
	SIF TEQUIP:ARG:バイブ == 0
		RETURNF 0
	;妖精用は大きさ-1
	SELECTCASE GETBITS(TEQUIP:ARG:バイブ, 0, 9)
	CASE 1p0
		LOCAL = 0
	CASE 1p1
		LOCAL = 1
	CASE 1p2
		LOCAL = 2
	CASE 1p3
		LOCAL = 3
	CASE 1p4
		LOCAL = 4
	CASE 1p5
		LOCAL = 5
	ENDSELECT
	;妖精用バイブ
	SIF TALENT:ARG:妖精
		LOCAL = MAX(LOCAL - 1, 0)
CASE "Ａバイブ"
	SIF ARG == 0
		ARG = TARGET
	SIF TEQUIP:ARG:アナルバイブ == 0
		RETURNF 0
	;妖精用は大きさ-1
	SELECTCASE GETBITS(TEQUIP:ARG:アナルバイブ, 0, 9)
	CASE 1p0
		LOCAL = 0
	CASE 1p1
		LOCAL = 1
	CASE 1p2
		LOCAL = 2
	CASE 1p3
		LOCAL = 3
	CASE 1p4
		LOCAL = 4
	CASE 1p5
		LOCAL = 5
	ENDSELECT
	;妖精用バイブ
	SIF TALENT:ARG:妖精
		LOCAL = MAX(LOCAL - 1, 0)
CASE "尿道バイブ"
	SIF ARG == 0
		ARG = TARGET
	SIF TEQUIP:ARG:尿道バイブ == 0
		RETURNF 0
	;妖精用は大きさ-1
	SELECTCASE GETBITS(TEQUIP:ARG:尿道バイブ, 0, 9)
	CASE 1p0
		LOCAL = 0
	CASE 1p1
		LOCAL = 1
	CASE 1p2
		LOCAL = 2
	CASE 1p3
		LOCAL = 3
	CASE 1p4
		LOCAL = 4
	CASE 1p5
		LOCAL = 5
	ENDSELECT
	;妖精用バイブ
	SIF TALENT:ARG:妖精
		LOCAL = MAX(LOCAL - 1, 0)
CASE "フィスト"
	SIF ARG == 0
		ARG = PLAYER
	;これはSELECTCOM等で変動する。最小で１、最大で５となる
	IF SELECTCOM == 143
		RETURNF SIZE("Ｗフィスト", ARG)
	ELSEIF CHECK_COM("二本フィスト")
		RETURNF SIZE("二本フィスト", ARG)
	ELSE
		RETURNF SIZE("片手フィスト", ARG)
	ENDIF
CASE "Ｗフィスト"
	SIF ARG == 0
		ARG = PLAYER
	RETURNF SIZE("片手フィスト", ARG) + 1
CASE "片手フィスト"
	SIF ARG == 0
		ARG = PLAYER
	LOCAL = 3 + TALENT:ARG:男性 - TALENT:ARG:妖精
	SIF TALENT:ARG:娇小体型 || TALENT:ARG:伪娘
		LOCAL -= 1
CASE "二本フィスト"
	SIF ARG == 0
		ARG = PLAYER
	LOCAL = SIZE("片手フィスト", ARG)
	RETURNF MAX(LOCAL*2 - 1, LOCAL + 2)
CASE "ドラゴン"
	RETURNF 5
ENDSELECT
RETURNF LOCAL

;-------------------------------------------------
;物の堅さを判定する関数
;-------------------------------------------------
@HARD(ARGS, ARG)
#FUNCTION

VARSET LOCAL

SELECTCASE ARGS
CASE "ペニス"
	SIF ARG == 0
		ARG = PLAYER
	;ペニバン
	SIF PENIS(ARG) == 0
		RETURNF 3

	LOCAL = 1

	;ハードスキンやウルズグン
	IF STATE("ハードスキン", ARG) && STATE("精强术", ARG) >= 3
		LOCAL += 3
	ELSEIF STATE("ハードスキン", ARG)
		LOCAL += 2
	ELSEIF STATE("精强术", ARG)
		LOCAL += 1
	ENDIF
	SIF TALENT:ARG:龙之血
		LOCAL += 2
CASE "バイブ"
	SIF ARG == 0
		ARG = TARGET
	SIF TEQUIP:ARG:バイブ == 0
		RETURNF 0
	LOCAL = 3
	;触手
	SIF TEQUIP:ARG:触手
		LOCAL = 2
CASE "Ａバイブ"
	SIF ARG == 0
		ARG = TARGET
	SIF TEQUIP:ARG:アナルバイブ == 0
		RETURNF 0
	IF COND("アナルパール", ARG)
		LOCAL = 1
	ELSE
		SELECTCASE GETBITS(TEQUIP:ARG:バイブ, 0, 9)
		CASE 1p0
			LOCAL = 2
		CASEELSE
			LOCAL = 3
		ENDSELECT
	ENDIF
	;触手
	SIF TEQUIP:ARG:触手
		LOCAL = 2
CASE "尿道バイブ"
	SIF ARG == 0
		ARG = TARGET
	SIF TEQUIP:ARG:尿道バイブ == 0
		RETURNF 0

		SELECTCASE GETBITS(TEQUIP:ARG:バイブ, 0, 9)
		CASE 1p0
			LOCAL = 2
		CASEELSE
			LOCAL = 3
		ENDSELECT

CASE "フィスト"
	SIF ARG == 0
		ARG = PLAYER
	LOCAL = 3
	;オトコ
	SIF TALENT:ARG:男性
		LOCAL += 1
	;ハードスキン
	SIF STATE("ハードスキン", ARG)
		LOCAL += 1
	;スライム
	SIF TALENT:ARG:史莱姆
		LOCAL -= 2
CASE "ドラゴン"
	RETURNF 4
ENDSELECT
RETURNF LOCAL

;-------------------------------------------------
;膣を使える(中に入れることが出来る)か判定
;TEQUIPについては、ARG:1で例外を指定できる
;-------------------------------------------------
@USE_V(ARG, ARG:1)
#FUNCTION
;オトコor検閲orＶ露出不可
SIF TALENT:ARG:男性 || COND("運命検閲：アソコ", ARG) || !CHECK_CLO("Ｖ露出可")
	RETURNF 0
;バイブ挿入中はダメ
SIF TEQUIP:ARG:バイブ && ARG:1 != 11
	RETURNF 0
;バルーン使用中は不可
SIF TEQUIP:ARG:拡張バルーン == 1 && ARG:1 != 48
	RETURNF 0
RETURNF 1

;-------------------------------------------------
;アナルを使える(中に入れることが出来る)か判定
;TEQUIPについては、ARG:1で例外を指定できる
;-------------------------------------------------
@USE_A(ARG, ARG:1)
#FUNCTION
;検閲orＡ露出不可
SIF COND("運命検閲：アナル", ARG) || !CHECK_CLO("Ａ露出可")
	RETURNF 0
;アナルバイブ挿入中はダメ
SIF TEQUIP:ARG:アナルバイブ && ARG:1 != 13
	RETURNF 0
;アナルプラグ使用中はダメ
SIF COND("アナルプラグ", ARG) && ARG:1 != 46
	RETURNF 0
;バルーン使用中は不可
SIF TEQUIP:ARG:拡張バルーン == 2 && ARG:1 != 48
	RETURNF 0
RETURNF 1

;-------------------------------------------------
;ARGのARGS状態をEQUIP:ステート変化〜9で管理する
;-------------------------------------------------
@STATE(ARGS, ARG)
#FUNCTION
SIF ARG < 0
	RETURNF 0
SIF ARG == 0 && TARGET
	ARG = TARGET
SELECTCASE ARGS
CASE "小根术"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 0)+TALENT:ARG:小根术常态化
CASE "高敏术"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 1)+TALENT:ARG:高敏术常态化
CASE "倍化术"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 2)
CASE "フィーブル"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 3)
CASE "ドランク"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 4)
CASE "悪酔い"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 5)
CASE "恍惚"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 6)
CASE "撃沈"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 7)
CASE "ふにゃふにゃ"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 8)
CASE "ハードスキン"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 9)
CASE "具現"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 10)
CASE "束缚术"
	RETURNF GETBITS(EQUIP:ARG:ステート変化, 11, 14)
CASE "バインド：手"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 11)
CASE "バインド：脚"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 12)
CASE "バインド：口"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 13)
CASE "バインド：Ｐ"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 14)
CASE "大根术"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 15)+TALENT:ARG:大根术常态化
CASE "快感术"
	RETURNF GETBIT(EQUIP:ARG:ステート変化, 16)

CASE "精强术"
	RETURNF MIN(EQUIP:ARG:精强术+TALENT:ARG:精强术常态化, 3)
CASE "魅强术"
	RETURNF EQUIP:ARG:魅强术
CASE "防强术"
	RETURNF EQUIP:ARG:防强术

CASE "グン"
	RETURNF (EQUIP:ARG:精强术&&(TALENT:ARG:精强术常态化 == 0) )+EQUIP:ARG:魅强术+EQUIP:ARG:防强术
CASE "バッド"
	LOCAL = GETBIT(EQUIP:ARG:ステート変化, 0) && (TALENT:ARG:小根术常态化 == 0)
	LOCAL += GETBIT(EQUIP:ARG:ステート変化, 1) && (TALENT:ARG:高敏术常态化 == 0)
	LOCAL += GETBITS(EQUIP:ARG:ステート変化, 3, 6, 8) + GETBITS(EQUIP:ARG:ステート変化, 11, 14, 16)
	RETURNF LOCAL
ENDSELECT
RETURNF 0

@ADD_STATE, ARG, ARGS
SIF ARG < 0
	RETURN 0
SIF ARG == 0 && TARGET
	ARG = TARGET
SELECTCASE ARGS
CASE "小根术"
	SETBIT EQUIP:ARG:ステート変化, 0
CASE "高敏术"
	SETBIT EQUIP:ARG:ステート変化, 1
CASE "倍化术"
	SETBIT EQUIP:ARG:ステート変化, 2
CASE "フィーブル"
	SETBIT EQUIP:ARG:ステート変化, 3
CASE "ドランク"
	SETBIT EQUIP:ARG:ステート変化, 4
CASE "悪酔い"
	SETBIT EQUIP:ARG:ステート変化, 5
CASE "恍惚"
	SETBIT EQUIP:ARG:ステート変化, 6
	TFLAG:99 = 0
CASE "撃沈"
	SETBIT EQUIP:ARG:ステート変化, 7
CASE "ふにゃふにゃ"
	SETBIT EQUIP:ARG:ステート変化, 8
CASE "ハードスキン"
	SETBIT EQUIP:ARG:ステート変化, 9
CASE "具現"
	SETBIT EQUIP:ARG:ステート変化, 10
	TALENT:ARG:女性 = 0
	TALENT:ARG:扶她 = 2
CASE "バインド：手"
	SETBIT EQUIP:ARG:ステート変化, 11
	SETBIT TEQUIP:ARG:拘束, 20
	SETBIT TEQUIP:ARG:拘束, 30
	SIF ARG == PLAYER
		TEQUIP:押し倒し = 1
CASE "バインド：脚"
	SETBIT EQUIP:ARG:ステート変化, 12
	SETBIT TEQUIP:ARG:拘束, 21
	SETBIT TEQUIP:ARG:拘束, 31
	SIF ARG == PLAYER
		TEQUIP:押し倒し = 1
CASE "バインド：口"
	SETBIT EQUIP:ARG:ステート変化, 13
	SIF ARG == PLAYER
		TEQUIP:押し倒し = 1
CASE "バインド：Ｐ"
	SETBIT EQUIP:ARG:ステート変化, 14
	SIF ARG == PLAYER
		TEQUIP:押し倒し = 1
CASE "大根术"
	SETBIT EQUIP:ARG:ステート変化, 15
CASE "快感术"
	SETBIT EQUIP:ARG:ステート変化, 16

CASE "媚薬"
	TEQUIP:ARG:媚薬 += 100
	SIF TALENT:ARG:药物耐性
		TEQUIP:ARG:媚薬 /= 4

;グンは一回でMAXになるように変更
CASE "精强术"
	;EQUIP:ARG:精强术 = MIN(EQUIP:ARG:精强术 + 1, 3)
	EQUIP:ARG:精强术 = 3
CASE "魅强术"
	;EQUIP:ARG:魅强术 = MIN(EQUIP:ARG:魅强术 + 1, 3)
	EQUIP:ARG:魅强术 = 3
CASE "防强术"
	;EQUIP:ARG:防强术 = MIN(EQUIP:ARG:防强术 + 1, 3)
	EQUIP:ARG:防强术 = 3
ENDSELECT

@REMOVE_STATE, ARG, ARGS
SIF ARG < 0
	RETURN 0
SIF ARG == 0 && TARGET
	ARG = TARGET
SELECTCASE ARGS
CASE "小根术"
	CLEARBIT EQUIP:ARG:ステート変化, 0
CASE "高敏术"
	CLEARBIT EQUIP:ARG:ステート変化, 1
CASE "倍化术"
	CLEARBIT EQUIP:ARG:ステート変化, 2
CASE "フィーブル"
	CLEARBIT EQUIP:ARG:ステート変化, 3
CASE "ドランク"
	CLEARBIT EQUIP:ARG:ステート変化, 4
CASE "悪酔い"
	CLEARBIT EQUIP:ARG:ステート変化, 5
CASE "恍惚"
	CLEARBIT EQUIP:ARG:ステート変化, 6
	TFLAG:99 = 0
CASE "撃沈"
	CLEARBIT EQUIP:ARG:ステート変化, 7
CASE "ふにゃふにゃ"
	CLEARBIT EQUIP:ARG:ステート変化, 8
CASE "ハードスキン"
	CLEARBIT EQUIP:ARG:ステート変化, 9
CASE "具現"
	CLEARBIT EQUIP:ARG:ステート変化, 10
	TALENT:ARG:女性 = 1
	TALENT:ARG:扶她 = 0
CASE "束缚术"
	CLEARBIT EQUIP:ARG:ステート変化, 11
	CLEARBIT EQUIP:ARG:ステート変化, 12
	CLEARBIT EQUIP:ARG:ステート変化, 13
	CLEARBIT EQUIP:ARG:ステート変化, 14
	SIF GETBIT(TEQUIP:ARG:拘束, 30)
		CLEARBIT TEQUIP:ARG:拘束, 20
	SIF GETBIT(TEQUIP:ARG:拘束, 31)
		CLEARBIT TEQUIP:ARG:拘束, 21
	CLEARBIT TEQUIP:ARG:拘束, 30
	CLEARBIT TEQUIP:ARG:拘束, 31
CASE "バインド：手"
	CLEARBIT EQUIP:ARG:ステート変化, 11
	SIF GETBIT(TEQUIP:ARG:拘束, 30)
		CLEARBIT TEQUIP:ARG:拘束, 20
	CLEARBIT TEQUIP:ARG:拘束, 30
CASE "バインド：脚"
	CLEARBIT EQUIP:ARG:ステート変化, 12
	SIF GETBIT(TEQUIP:ARG:拘束, 31)
		CLEARBIT TEQUIP:ARG:拘束, 21
	CLEARBIT TEQUIP:ARG:拘束, 31
CASE "バインド：口"
	CLEARBIT EQUIP:ARG:ステート変化, 13
CASE "バインド：Ｐ"
	CLEARBIT EQUIP:ARG:ステート変化, 14
CASE "大根术"
	CLEARBIT EQUIP:ARG:ステート変化, 15
CASE "快感术"
	CLEARBIT EQUIP:ARG:ステート変化, 16

CASE "精强术"
	EQUIP:ARG:精强术 = 0
CASE "魅强术"
	EQUIP:ARG:魅强术 = 0
CASE "防强术"
	EQUIP:ARG:防强术 = 0
CASE "グン"
	EQUIP:ARG:精强术 = 0
	EQUIP:ARG:魅强术 = 0
	EQUIP:ARG:防强术 = 0

CASE "バッド"
	CLEARBIT EQUIP:ARG:ステート変化, 0
	CLEARBIT EQUIP:ARG:ステート変化, 1
	CLEARBIT EQUIP:ARG:ステート変化, 3
	CLEARBIT EQUIP:ARG:ステート変化, 4
	CLEARBIT EQUIP:ARG:ステート変化, 5
	CLEARBIT EQUIP:ARG:ステート変化, 6
	CLEARBIT EQUIP:ARG:ステート変化, 8
	CLEARBIT EQUIP:ARG:ステート変化, 11
	CLEARBIT EQUIP:ARG:ステート変化, 12
	CLEARBIT EQUIP:ARG:ステート変化, 13
	CLEARBIT EQUIP:ARG:ステート変化, 14
	CLEARBIT EQUIP:ARG:ステート変化, 16
	SIF GETBIT(TEQUIP:ARG:拘束, 30)
		CLEARBIT TEQUIP:ARG:拘束, 20
	SIF GETBIT(TEQUIP:ARG:拘束, 31)
		CLEARBIT TEQUIP:ARG:拘束, 21
	CLEARBIT TEQUIP:ARG:拘束, 30
	CLEARBIT TEQUIP:ARG:拘束, 31
ENDSELECT

;-------------------------------------------------
;PALAMの上昇予定値
;-------------------------------------------------
@UP_PALAM(ARG, ARG:1)
#FUNCTION
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET
LOCAL = CUP:(ARG:1):ARG + PALAM:(ARG:1):ARG
RETURNF LOCAL

;-------------------------------------------------
;エラーのでないPALAMLV。間違えやすそうな気もする。
;-------------------------------------------------
@PALAM_LV(ARG)
#FUNCTION
SELECTCASE ARG
CASE IS <= 0
	RETURNF 0
;PALAMLVの値は、順に1:100, 2:500, 3:3000, 4:1万, 5:3万, 6:6万, 7:10万, 8:15万, 9:25万
CASE 1 TO 9
	LOCAL = PALAMLV:ARG
CASE 10
	LOCAL = 500000
CASE 11
	LOCAL = 1000000
CASE 12
	LOCAL = 2500000
CASE 13
	LOCAL = 5000000
CASE 14
	LOCAL = 10000000
CASE 15
	LOCAL = 50000000
CASEELSE
	LOCAL = 100000000
ENDSELECT

RETURNF LOCAL

;-------------------------------------------------
;ARGのPALAMLVを返す
;-------------------------------------------------
@GET_PALAMLV(ARG)
#FUNCTION
SELECTCASE ARG
CASE IS <= 0
	RETURNF 0
CASE IS < 250000
	LOCAL = GETPALAMLV(ARG, 8)
CASE IS < 500000
	LOCAL = 9
CASE IS < 1000000
	LOCAL = 10
CASE IS < 2500000
	LOCAL = 11
CASE IS < 5000000
	LOCAL = 12
CASE IS < 10000000
	LOCAL = 13
CASE IS < 50000000
	LOCAL = 14
CASE IS < 100000000
	LOCAL = 15
CASEELSE
	LOCAL = 16
ENDSELECT

RETURNF LOCAL

;-------------------------------------------------
;ARGの数値をPALAM_LVを用いてARG = PALAM_LV(n) + (PALAM_LV(n+1) - PALAM_LV(n) ) * X で表し、
;nにARG:1少ない値を代入した値を返させる。ARG:1を省略したらARG:1は1
;例)ARG = 3512, ARG:1 = 1 なら ARG = PALAM_LV(3) + (PALAM_LV(4) - PALAM_LV(3) ) * 0.073なので、
;　 ARGをPALAM_LV(2) + (PALAM_LV(3) - PALAM_LV(2) ) * 0.073 = 682にする
;おもに、素質の影響で得るPALAMを少なくさせたい時に使う
;-------------------------------------------------
@DOWN_PALAMLV(ARG, ARG:1)
#FUNCTION
#DIM LCOUNT
#DIM MEMO_PALAMLV
#DIM MEMO_DIF, 2
#DIM MEMO_PER

;ARGが100未満なら０
SIF GET_PALAMLV(ARG) <= 0
	RETURNF 0
SIF ARG:1 <= 0
	ARG:1 = 1

;ARGのPALAMLVを求める
MEMO_PALAMLV = GET_PALAMLV(ARG)
MEMO_DIF = PALAM_LV(MEMO_PALAMLV + 1) - PALAM_LV(MEMO_PALAMLV)
MEMO_DIF:1 = ARG - PALAM_LV(MEMO_PALAMLV)

FOR LCOUNT, 0, 101
	SELECTCASE MEMO_DIF * LCOUNT / 100
	CASE IS >= MEMO_DIF:1
		MEMO_PER = LCOUNT
		BREAK
	ENDSELECT
NEXT

RETURNF PALAM_LV(MEMO_PALAMLV - ARG:1) + (PALAM_LV(MEMO_PALAMLV + 1 - ARG:1) - PALAM_LV(MEMO_PALAMLV - ARG:1) ) * MEMO_PER / 100

;-------------------------------------------------
;@DOWN_PALAMLV(ARG)の逆方向版
;-------------------------------------------------
@UP_PALAMLV(ARG, ARG:1)
#FUNCTION
#DIM LCOUNT
#DIM MEMO_PALAMLV
#DIM MEMO_DIF, 2
#DIM MEMO_PER

;ARGが100未満の時に０を100にするか、マイルドに５倍にしておくか…
;試しに０を100にしてみる
;SIF ARG <= 100
;	RETURNF ARG * 5
SIF ARG:1 <= 0
	ARG:1 = 1

;ARGのPALAMLVを求める
MEMO_PALAMLV = GET_PALAMLV(ARG)
MEMO_DIF = PALAM_LV(MEMO_PALAMLV + 1) - PALAM_LV(MEMO_PALAMLV)
MEMO_DIF:1 = ARG - PALAM_LV(MEMO_PALAMLV)

FOR LCOUNT, 0, 101
	SELECTCASE MEMO_DIF * LCOUNT / 100
	CASE IS >= MEMO_DIF:1
		MEMO_PER = LCOUNT
		BREAK
	ENDSELECT
NEXT

RETURNF PALAM_LV(MEMO_PALAMLV + ARG:1) + (PALAM_LV(MEMO_PALAMLV + 1 + ARG:1) - PALAM_LV(MEMO_PALAMLV + ARG:1) ) * MEMO_PER / 100


;-------------------------------------------------
;EXPの段階でよく用いる値をLVにしてみた。EXPLVとは別物なので注意！
;-------------------------------------------------
@EXP_LV(ARG)
#FUNCTION
SELECTCASE ARG
CASE IS <= 0
	RETURNF 0
CASE 1
	LOCAL = 5
CASE 2
	LOCAL = 20
CASE 3
	LOCAL = 50
CASE 4
	LOCAL = 150
CASE 5
	LOCAL = 300
CASE 6
	LOCAL = 600
CASE 7
	LOCAL = 1000
CASEELSE
	LOCAL = 1000 + (ARG - 7)*500
ENDSELECT

RETURNF LOCAL

@GET_EXP_LV(ARG)
#FUNCTION
SELECTCASE ARG
CASE IS < 5
	RETURNF 0
CASE IS < 20
	RETURNF 1
CASE IS < 50
	RETURNF 2
CASE IS < 150
	RETURNF 3
CASE IS < 300
	RETURNF 4
CASE IS < 600
	RETURNF 5
CASE IS < 1000
	RETURNF 6
CASEELSE
	LOCAL = (ARG - 1000)/500 + 7
ENDSELECT

RETURNF LOCAL


;-------------------------------------------------
;今回の調教でARG:1が得たARG経験
;@EVENTTURNEND内で初期化される
;-------------------------------------------------
@EXP_GAIN(ARG, ARG:1)
#FUNCTION
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET
LOCAL = EXP:(ARG:1):ARG - TCVAR:(ARG:1):(ARG + 200)
RETURNF LOCAL

@RESET_EXP_GAIN, ARG
FOR LOCAL, 0, 100
	TCVAR:ARG:(LOCAL + 200) = EXP:ARG:LOCAL
NEXT


@EQ_EXP_GAIN, ARG, ARG:1
TCVAR:(ARG:1):(ARG + 200) = EXP:(ARG:1):ARG

;-------------------------------------------------
;キャラARG:1が得たARGS経験の値ARGを、加算しつつ表示させるための関数。ARGはTARGETならば省略可能
;例）CALL PRINT_EXP_ADD, "羞恥快楽経験", 10, PLAYERだと、
;EXP:PLAYER:羞耻快乐经验 += 10
;PRINTFORML 羞恥快楽経験＋{10}（%CALLNAME:PLAYER%）
;に等しい。
;-------------------------------------------------
@PRINTL_EXP_ADD, ARGS, ARG, ARG:1
SIF ARG <= 0 || ARG:1 < 0
	RETURN 0
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET

EXP:(ARG:1):ARGS += ARG
PRINTFORML %ARGS%＋{ARG}\@ ARG:1 != TARGET ? （%CALLNAME:(ARG:1)%） # \@

;-------------------------------------------------
;PRINTL_EXP_ADDのウェイト有り版
;-------------------------------------------------
@PRINTW_EXP_ADD, ARGS, ARG, ARG:1
SIF ARG <= 0 || ARG:1 < 0
	RETURN 0
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET

EXP:(ARG:1):ARGS += ARG
PRINTFORMW %ARGS%＋{ARG}\@ ARG:1 != TARGET ? （%CALLNAME:(ARG:1)%） # \@

;-------------------------------------------------
;FIRSTEXPは今までの調教でARG経験を得たことがあるかどうかの判定等に用いる
;-------------------------------------------------
@FIRSTEXP(ARG, ARG:1)
#FUNCTION
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET
LOCAL = MAX(CFLAG:(ARG:1):(800 + ARG), EXP_GAIN(ARG, ARG:1) )
RETURNF LOCAL

;-------------------------------------------------
;FIRSTEXPのフラグをセットする
;タイミングは@EVENTTURNEND_FLAGで調教終了後の一回のみ見る（調教以外で得た経験は無視する）
;-------------------------------------------------
@SET_FIRSTEXP, ARG
SIF EXP:ARG:调教次数 == 0
	RETURN 0

FOR LOCAL, 0, 100
	CFLAG:ARG:(800 + LOCAL) += EXP:ARG:LOCAL - TCVAR:ARG:(LOCAL+200)
NEXT

;-------------------------------------------------
;PREVUP(ARG, ARG:1)は前回ターンでARG:1が得たUP
;-------------------------------------------------
@PREVUP(ARG, ARG:1)
#FUNCTION
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET
LOCAL = TCVAR:(ARG:1):(40 + ARG)
RETURNF LOCAL

@SET_PREVUP, ARG
FOR LOCAL, 0, 16
	TCVAR:ARG:(40 + LOCAL) = CUP:ARG:LOCAL
NEXT

;-------------------------------------------------
;PREVEX(ARGS, ARG)は前回ターンでARG:1が得たNOWEX:ARGS
;-------------------------------------------------
@PREVEX(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET

RETURNF TCVAR:ARG:(GETNUM(EX, ARGS) + 60)


@SET_PREVEX, ARG
FOR LOCAL, 0, 40
	TCVAR:ARG:(60 + LOCAL) = NOWEX:ARG:LOCAL
NEXT

;-------------------------------------------------
;SEXUAL_PLAYは、好色なキャラに補正がかかるプレイ
;ABNORMAL_PLAYは倒錯的なキャラに補正がかかるプレイ
;ORAL_PLAYはお口を使ったプレイ
;-------------------------------------------------
@SEXUAL_PLAY()
#FUNCTION
SELECTCASE SELECTCOM
CASE 0 TO 14, 30 TO 32, 60 TO 129, 260, 261
	RETURNF 1
ENDSELECT
RETURNF 0

@ABNORMAL_PLAY()
#FUNCTION
SELECTCASE SELECTCOM
CASE 2, 9, 14, 33, 70 TO 79, 96, 130 TO 159, 170, 171, 174, 179, 260 TO 279, 303 TO 305, 505
	RETURNF 1
ENDSELECT
RETURNF 0

@ORAL_PLAY()
#FUNCTION
SELECTCASE SELECTCOM
CASE 6, 91, 96, 98, 99, 140, 208, 311
	RETURNF 1
ENDSELECT
RETURNF 0

;-------------------------------------------------
;FIRSTCOMは今までに選択したコマンドの記録
;今までの調教でARGを選んだかどうか判定
;とりあえず、Train番号640番までとする
;-------------------------------------------------
@FIRSTCOM(ARG, ARG:1)
#FUNCTION
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET
SIF GETBIT(CFLAG:(ARG:1):(710 + ARG/64), ARG%64)
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;FIRSTCOMの状態ごとの記録版
;ARGSに入る文字列は基本的には全部全角です。間違えやすいので注意
;-------------------------------------------------
@FIRSTCOM_EX(ARGS, ARG, ARG:1)
#FUNCTION
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET
SELECTCASE ARGS
CASE "屈服刻印ＬＶ１", "屈服刻印ＬＶ1", "屈服刻印LV1", "屈服刻印LV１", "屈服刻印Lv1", "屈服刻印Lv１"
	SIF GETBIT(CFLAG:(ARG:1):(720 + ARG/64), ARG%64)
		RETURNF 1
CASE "屈服刻印ＬＶ２", "屈服刻印ＬＶ2", "屈服刻印LV2", "屈服刻印LV２", "屈服刻印Lv2", "屈服刻印Lv２"
	SIF GETBIT(CFLAG:(ARG:1):(730 + ARG/64), ARG%64)
		RETURNF 1
CASE "屈服刻印ＬＶ３", "屈服刻印ＬＶ3", "屈服刻印LV3", "屈服刻印LV３", "屈服刻印Lv3", "屈服刻印Lv３"
	SIF GETBIT(CFLAG:(ARG:1):(740 + ARG/64), ARG%64)
		RETURNF 1
CASE "恋慕"
	SIF GETBIT(CFLAG:(ARG:1):(750 + ARG/64), ARG%64)
		RETURNF 1
CASE "淫乱"
	SIF GETBIT(CFLAG:(ARG:1):(760 + ARG/64), ARG%64)
		RETURNF 1
CASE "相思相愛"
	SIF GETBIT(CFLAG:(ARG:1):(770 + ARG/64), ARG%64)
		RETURNF 1
CASE "反発刻印ＬＶ３", "反発刻印ＬＶ3", "反発刻印LV3", "反発刻印LV３", "反発刻印Lv3", "反発刻印Lv３"
	SIF GETBIT(CFLAG:(ARG:1):(780 + ARG/64), ARG%64)
		RETURNF 1
ENDSELECT
RETURNF 0

;-------------------------------------------------
;FIRSTCOMS_EX(ARGS, ARGS:1, ARG)はSELECTCOMでなくコマンド群(ARGS:1部分)対応のFIRSTCOM_EX
;ARGSに入る文字列は基本的には全部全角です。間違えやすいので注意
;-------------------------------------------------
@FIRSTCOMS_EX(ARGS, ARGS:1, ARG)
#FUNCTION
#DIM LCOUNT
SIF ARG == 0 && TARGET
	ARG = TARGET
SELECTCASE ARGS:1
CASE "Ｖ挿入"
	;Ｖインサートを選択済みなら1を返す
	SIF FIRSTCOM_EX(ARGS, 177, ARG)
		RETURNF 1
	FOR LCOUNT, 60, 70
		SIF FIRSTCOM_EX(ARGS, LCOUNT, ARG)
			RETURNF 1
	NEXT
CASE "Ａ挿入"
	;Ａインサートを選択済みなら1を返す
	FOR LCOUNT, 70, 80
		SIF FIRSTCOM_EX(ARGS, LCOUNT, ARG)
			RETURNF 1
	NEXT
CASE "尿道挿入"
	;尿道インサートを選択済みなら1を返す
	SIF FIRSTCOM_EX(ARGS, 83, ARG)
		RETURNF 1
CASE "Ｖ逆挿入"
	FOR LCOUNT, 120, 125
		SIF FIRSTCOM_EX(ARGS, LCOUNT, ARG)
			RETURNF 1
	NEXT
CASE "Ａ逆挿入"
	FOR LCOUNT, 125, 130
		SIF FIRSTCOM_EX(ARGS, LCOUNT, ARG)
			RETURNF 1
	NEXT
ENDSELECT
RETURNF 0


;-------------------------------------------------
;CHOSECOMは今回選択したコマンドの記録
;今回の調教でARGを選んだかどうか判定
;とりあえず、Train番号640番までとする
;-------------------------------------------------
@CHOSECOM(ARG, ARG:1)
#FUNCTION
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET
SIF GETBIT(TCVAR:(ARG:1):(30 + ARG/64), ARG%64)
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;FIRST_EVは今までに経験したイベントの記録
;-------------------------------------------------
@FIRST_EV(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
LOCAL = NUM_EV(ARGS)
SIF GETBIT(CFLAG:ARG:(700 + LOCAL/64), LOCAL%64)
	RETURNF 1
RETURNF 0

@SET_CHOSECOM, ARG, ARG:1
SIF ARG < 0
	RETURN 0
;基本的にはSELECTCOMだが、Ｗ押し倒しのとき対応
IF ARG:1 >= 0
	LOCAL = ARG:1
	;TCVAR:30〜39に記録し、CFLAG:710〜719に@FIRSTCOMの変数もセット
	SETBIT TCVAR:ARG:(30 + LOCAL/64), LOCAL%64
	SETBIT CFLAG:ARG:(710 + LOCAL/64), LOCAL%64
	;CFLAG:720〜に@FIRSTCOM_EXの変数もセット
	SIF MARK:ARG:屈服刻印 >= 1
		SETBIT CFLAG:ARG:(720 + LOCAL/64), LOCAL%64
	SIF MARK:ARG:屈服刻印 >= 2
		SETBIT CFLAG:ARG:(730 + LOCAL/64), LOCAL%64
	SIF MARK:ARG:屈服刻印 >= 3
		SETBIT CFLAG:ARG:(740 + LOCAL/64), LOCAL%64
	SIF TALENT:ARG:恋慕
		SETBIT CFLAG:ARG:(750 + LOCAL/64), LOCAL%64
	SIF TALENT:ARG:淫乱
		SETBIT CFLAG:ARG:(760 + LOCAL/64), LOCAL%64
	SIF TALENT:ARG:相思相爱
		SETBIT CFLAG:ARG:(770 + LOCAL/64), LOCAL%64
	SIF MARK:ARG:排斥刻印 == 3
		SETBIT CFLAG:ARG:(780 + LOCAL/64), LOCAL%64
ENDIF
;ARGが助手ならば、今回のコマンドも記録する
IF ARG == ASSI
	PREVCOM:1 = SELECTCOM:1
	PREVCOM:2 = SELECTCOM:2
	PREVCOM:3 = SELECTCOM:3
ENDIF


@RESET_CHOSECOM, ARG
VARSET TCVAR:ARG:0, 0, 30, 40
VARSET TCVAR:ARG:0, 0, 140, 150

;-------------------------------------------------
;FIRSTCOMS(ARGS)は今までに選択したコマンド群の記録
;CHOSECOMS(ARGS)は今回選択したコマンド群の記録
;-------------------------------------------------
@FIRSTCOMS(ARGS, ARG)
#FUNCTION
#DIM LCOUNT
SIF ARG == 0 && TARGET
	ARG = TARGET
SELECTCASE ARGS
CASE "挿入"
	;ＶorＡor尿道インサートを選択済みなら1を返す
	SIF FIRSTCOM(83, ARG)
		RETURNF 1
	SIF FIRSTCOM(177, ARG)
		RETURNF 1
	FOR LCOUNT, 60, 80
		SIF FIRSTCOM(LCOUNT, ARG)
			RETURNF 1
	NEXT
CASE "Ｖ挿入"
	;Ｖインサートを選択済みなら1を返す
	SIF FIRSTCOM(177, ARG)
		RETURNF 1
	FOR LCOUNT, 60, 70
		SIF FIRSTCOM(LCOUNT, ARG)
			RETURNF 1
	NEXT
CASE "Ａ挿入"
	;Ａインサートを選択済みなら1を返す
	FOR LCOUNT, 70, 80
		SIF FIRSTCOM(LCOUNT, ARG)
			RETURNF 1
	NEXT
CASE "尿道挿入"
	SIF FIRSTCOM(83, ARG)
		RETURNF 1
CASE "Ｖ逆挿入"
	FOR LCOUNT, 120, 125
		SIF FIRSTCOM(LCOUNT, ARG)
			RETURNF 1
	NEXT
CASE "Ａ逆挿入"
	FOR LCOUNT, 125, 130
		SIF FIRSTCOM(LCOUNT, ARG)
			RETURNF 1
	NEXT
ENDSELECT
RETURNF 0

@CHOSECOMS(ARGS, ARG)
#FUNCTION
#DIM LCOUNT
SIF ARG == 0 && TARGET
	ARG = TARGET
SELECTCASE ARGS
CASE "挿入"
	;ＶorＡor尿道インサートを選択済みなら1を返す
	SIF CHOSECOM(83, ARG)
		RETURNF 1
	SIF CHOSECOM(177, ARG)
		RETURNF 1
	FOR LCOUNT, 60, 80
		SIF CHOSECOM(LCOUNT, ARG)
			RETURNF 1
	NEXT
CASE "Ｖ挿入"
	;Ｖインサートを選択済みなら1を返す
	SIF CHOSECOM(177, ARG)
		RETURNF 1
	FOR LCOUNT, 60, 70
		SIF CHOSECOM(LCOUNT, ARG)
			RETURNF 1
	NEXT
CASE "Ａ挿入"
	;Ａインサートを選択済みなら1を返す
	FOR LCOUNT, 70, 80
		SIF CHOSECOM(LCOUNT, ARG)
			RETURNF 1
	NEXT
CASE "尿道挿入"
	;尿道インサートを選択済みなら1を返す
	SIF CHOSECOM(83, ARG)
		RETURNF 1
CASE "Ｖ逆挿入"
	FOR LCOUNT, 120, 125
		SIF CHOSECOM(LCOUNT, ARG)
			RETURNF 1
	NEXT
CASE "Ａ逆挿入"
	FOR LCOUNT, 125, 130
		SIF CHOSECOM(LCOUNT, ARG)
			RETURNF 1
	NEXT
ENDSELECT
RETURNF 0

;-------------------------------------------------
;CHOSE_EVは今回経験したイベントの記録
;特に、このターンに得たものについては2を返す
;-------------------------------------------------
@CHOSE_EV(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
LOCAL = NUM_EV(ARGS)
SIF GETBIT(TCVAR:ARG:(10 + LOCAL/64), LOCAL%64)
	RETURNF 2
SIF GETBIT(TCVAR:ARG:(140 + LOCAL/64), LOCAL%64)
	RETURNF 1
RETURNF 0

@SET_EV, ARG, ARGS
LOCAL = NUM_EV(ARGS)
SETBIT TCVAR:ARG:(10 + LOCAL/64), LOCAL%64
SETBIT TCVAR:ARG:(140 + LOCAL/64), LOCAL%64
SETBIT CFLAG:ARG:(700 + LOCAL/64), LOCAL%64

;-------------------------------------------------
;特殊なイベントの番号とイベント名の対応
;調教中などのイベント番号管理もここで行う(イベント番号0は設定禁止)
;記録するために用意されているFLAGのビット数の問題があるので、とりあえず640番まで。
;-------------------------------------------------
@NUM_EV(ARGS)
#FUNCTION
SELECTCASE ARGS
CASE "調教後自慰"
	RETURNF 1
CASE "調教後レズプレイ"
	RETURNF 2
CASE "調教後セックス"
	RETURNF 3
CASE "調教後Ａセックス"
	RETURNF 4
CASE "調教後ラルス自慰"
	RETURNF 9

CASE "夜這い騎乗位"
	RETURNF 20
CASE "夜這いＡ騎乗位"
	RETURNF 21
CASE "夜這いキス"
	RETURNF 22
CASE "添い寝"
	RETURNF 23
CASE "夜這い複数"
	RETURNF 24
CASE "夜這い騎乗位口上操作"
	RETURNF 31

CASE "朝フェラ"
	RETURNF 40
CASE "朝パイズリ"
	RETURNF 41
CASE "朝騎乗位"
	RETURNF 42
CASE "朝の新妻プレイ"
	RETURNF 43
CASE "朝キス"
	RETURNF 44
CASE "朝の母乳搾り"
	RETURNF 45
CASE "朝足コキ"
	RETURNF 46
CASE "朝クンニ"
	RETURNF 47
CASE "朝逆正常位"
	RETURNF 48

CASE "朝ＳＭ"
	RETURNF 60
CASE "朝拘束騎乗位"
	RETURNF 61
CASE "朝拘束Ａ騎乗位"
	RETURNF 62
CASE "朝拘束逆レイプ"
	RETURNF 63
CASE "朝拘束逆アナル"
	RETURNF 64

CASE "休憩：お風呂"
	RETURNF 70
CASE "休憩：添い寝"
	RETURNF 71
CASE "休憩：お茶"
	RETURNF 72
CASE "休憩：マッサージ"
	RETURNF 73
CASE "休憩：耳かき"
	RETURNF 74
CASE "休憩：エッチ"
	RETURNF 75

CASE "お掃除フェラ"
	RETURNF 80
CASE "お掃除クンニ"
	RETURNF 81

CASE "自発脱ぎ"
	RETURNF 83

CASE "妊娠発覚"
	RETURNF 85
CASE "出産"
	RETURNF 86
CASE "子離れ"
	RETURNF 87

CASE "Ｖバイブ追加口上"
	RETURNF 90
CASE "Ａバイブ追加口上"
	RETURNF 91

CASE "プラグ無し浣腸イベント"
	RETURNF 93

;ペニスを抜いた時の対応
CASE "中出し後くぱぁ"
	RETURNF 95
CASE "中出し後恥じらい"
	RETURNF 96
CASE "中出し後精液掻き出し"
	RETURNF 97
CASE "中出し後恍惚"
	RETURNF 98

CASE "館内露出プレイ出現"
	RETURNF 100
CASE "館内露出プレイ見物"
	RETURNF 101
CASE "館内露出プレイ見物人増加"
	RETURNF 102

;ドラ姦についてはSELECTCOM==141or142内で使用
CASE "ドラ姦口上開始"
	RETURNF 105
CASE "ドラ姦口上終了"
	RETURNF 106

CASE "避孕药"
	RETURNF 107
CASE "紧急避孕药"
	RETURNF 108
CASE "避孕护符"
	RETURNF 109
CASE "誓约卷轴"
	RETURNF 110
CASE "誓约指环"
	RETURNF 111

CASE "売却した"
	RETURNF 113

CASE "一晩解放の動向"
	RETURNF 115
CASE "一晩開放スキンシップ"
	RETURNF 116

CASE "ビデオ保存"
	RETURNF 120
CASE "ビデオ送付"
	RETURNF 121
CASE "ビデオ売却"
	RETURNF 122
CASE "ビデオ売却失敗"
	RETURNF 123

;以下は調教中などのイベント経験など
CASE "処女喪失"
	RETURNF 130
CASE "Ａ処女喪失"
	RETURNF 131
CASE "Ｖ潤LV3突破"
	RETURNF 132
CASE "欲情LV3突破"
	RETURNF 133
CASE "恥情LV3突破"
	RETURNF 134
CASE "恐怖LV3突破"
	RETURNF 135

CASE "Ｃ絶頂"
	RETURNF 140
CASE "Ｖ絶頂"
	RETURNF 141
CASE "Ａ絶頂"
	RETURNF 142
CASE "Ｂ絶頂"
	RETURNF 143
CASE "Ｍ絶頂"
	RETURNF 144
CASE "尿道絶頂"
	RETURNF 145

CASE "射精した"
	RETURNF 147
CASE "噴乳した"
	RETURNF 148
CASE "絶頂放尿"
	RETURNF 149

CASE "Ｃ焦らし"
	RETURNF 150
CASE "Ｖ焦らし"
	RETURNF 151
CASE "Ａ焦らし"
	RETURNF 152
CASE "Ｂ焦らし"
	RETURNF 153
CASE "Ｍ焦らし"
	RETURNF 154
CASE "尿道焦らし"
	RETURNF 155

CASE "射精焦らし"
	RETURNF 157

CASE "Ｃ絶頂20回以上"
	RETURNF 160
CASE "Ｖ絶頂20回以上"
	RETURNF 161
CASE "Ａ絶頂20回以上"
	RETURNF 162
CASE "Ｂ絶頂20回以上"
	RETURNF 163
CASE "Ｍ絶頂20回以上"
	RETURNF 164
CASE "尿道絶頂20回以上"
	RETURNF 165
CASE "苦痛刻印Lv3取得"
	RETURNF 166
CASE "快楽刻印Lv3取得"
	RETURNF 167
CASE "屈服刻印Lv3取得"
	RETURNF 168
CASE "反発刻印Lv3取得"
	RETURNF 169

CASE "絶頂：口"
	RETURNF 170
CASE "絶頂：手"
	RETURNF 171
CASE "絶頂：Ｖ中出し"
	RETURNF 172
CASE "絶頂：Ａ中出し"
	RETURNF 173
CASE "絶頂：胸"
	RETURNF 174
CASE "絶頂：脚"
	RETURNF 175
CASE "絶頂：素股"
	RETURNF 176
CASE "絶頂：キス"
	RETURNF 177
CASE "絶頂：逆クンニ"
	RETURNF 178
CASE "絶頂：逆レイプ"
	RETURNF 179
CASE "絶頂：逆アナル"
	RETURNF 180
CASE "絶頂：逆ドラ姦"
	RETURNF 181
CASE "絶頂：逆Ａドラ姦"
	RETURNF 182
CASE "絶頂：顔面騎乗"
	RETURNF 183
CASE "絶頂：アナル奉仕"
	RETURNF 184
CASE "絶頂：ＶＡ舐め"
	RETURNF 185
CASE "絶頂：ぱふぱふ"
	RETURNF 186
CASE "絶頂：助手"
	RETURNF 187
CASE "絶頂：ぶっかけ"
	RETURNF 188
CASE "絶頂：魔法瓶"
	RETURNF 189
CASE "絶頂：調教者がオンナ"
	RETURNF 190

CASE "エナジードレイン"
	RETURNF 200
CASE "レベルドレイン"
	RETURNF 201

CASE "挿入前暴発"
	RETURNF 205

CASE "一晩開放：ストリップ劇場"
	RETURNF 300
CASE "一晩開放：グローリーホール屋"
	RETURNF 301
CASE "一晩開放：酒場"
	RETURNF 302
CASE "一晩開放：宿屋"
	RETURNF 303
CASE "一晩開放：集会所"
	RETURNF 304
CASE "一晩開放：秘密の遊び場"
	RETURNF 305

CASE "一晩開放：冒険者相手"
	RETURNF 320
CASE "一晩開放：青年相手"
	RETURNF 321
CASE "一晩開放：少年相手"
	RETURNF 322
CASE "一晩開放：女性相手"
	RETURNF 323
CASE "一晩開放：少女相手"
	RETURNF 324

CASE "喫茶店"
	RETURNF 340
ENDSELECT
RETURNF 0

;-------------------------------------------------
;@CEVENTはキャラクターごとの各種イベントの記録を参照する関数。経験済みなら１を、未経験なら０を返す
;セットする時には@SET_CEVENTを用い、消去する際には@DEL_CEVENTを使う
;記録にはCSTR:10を使い、CSTR:10 = イベント１/イベント２/ と"/"を用いて区切る
;特殊なイベント群を作る際には、頭に"イベント名："とするのを推奨
;また、記述と操作の都合により、全角の"："と"＾"、半角の"/"の使用は禁止
;-------------------------------------------------
@CEVENT(ARGS, ARG)
#FUNCTION
#DIM LCOUNT
SIF ARG == 0 && TARGET
	ARG = TARGET

ARGS = %ARGS%/

;既にイベント経験済みかを調べる
RETURNF STRCOUNT(CSTR:ARG:10, ARGS)


@SET_CEVENT, ARGS, ARG
#DIM LCOUNT

SIF ARG == 0 && TARGET
	ARG = TARGET

ARGS = %ARGS%/

;経験済みなら追加せずに終了
SIF STRCOUNT(CSTR:ARG:10, ARGS)
	RETURN 1

;経験していないなら追加
CSTR:ARG:10 = %CSTR:ARG:10%%ARGS%


@DEL_CEVENT, ARGS, ARG
#DIM LCOUNT

SIF ARG == 0 && TARGET
	ARG = TARGET

VARSET LOCALS
;イベントごとに分解する
SPLIT CSTR:ARG:10, "/", LOCALS

;一旦CSTR:10を初期化
CSTR:ARG:10 =

FOR LCOUNT, 0, 100
	SIF LOCALS:LCOUNT == ""
		BREAK
	;経験済みだった
	SIF ARGS == LOCALS:LCOUNT
		CONTINUE

	;経験していないなら戻す
	CSTR:ARG:10 = %CSTR:ARG:10%%LOCALS:LCOUNT%/
NEXT

;-------------------------------------------------
;@CEVENTS(ARGS, ARG)はキャラARGが経験したイベント ARGS = イベント名：内容 の"内容"部分を文字列で返す関数
;当然のことながら複数記録されてるイベント群に使うには適していない
;-------------------------------------------------
@CEVENTS(ARGS, ARG)
#FUNCTIONS
#DIM LCOUNT

SIF ARG == 0 && TARGET
	ARG = TARGET

ARGS = %ARGS%：

VARSET LOCALS
;イベントごとに分解する
SPLIT CSTR:ARG:10, "/", LOCALS

FOR LCOUNT, 0, 100
	SIF LOCALS:LCOUNT == ""
		BREAK
	;イベント群ARGSでないならCONTINUE
	SIF STRCOUNT(LOCALS:LCOUNT, ARGS) == 0
		CONTINUE

	;イベント名：内容 となっているので"内容"部分を抜き出す
	LOCALS:LCOUNT = %SUBSTRING(LOCALS:LCOUNT, STRLENS(ARGS), -1)%

	;特殊な処理があるならここで
	IF STRCOUNT(LOCALS:LCOUNT, "CALLNAME_PLAYER")
		LOCALS:LCOUNT = %CALLNAME:PLAYER%%SUBSTRING(LOCALS:LCOUNT, STRLENS("CALLNAME_PLAYER"), -1)%
	ELSEIF STRCOUNT(LOCALS:LCOUNT, "CALLNAME_TARGET")
		LOCALS:LCOUNT = %CALLNAME:TARGET%%SUBSTRING(LOCALS:LCOUNT, STRLENS("CALLNAME_TARGET"), -1)%
	ELSEIF STRCOUNT(LOCALS:LCOUNT, "CALLNAME_MASTER")
		LOCALS:LCOUNT = %CALLNAME:MASTER%%SUBSTRING(LOCALS:LCOUNT, STRLENS("CALLNAME_MASTER"), -1)%
	ELSEIF ASSI && STRCOUNT(LOCALS:LCOUNT, "CALLNAME_ASSI")
		LOCALS:LCOUNT = %CALLNAME:ASSI%%SUBSTRING(LOCALS:LCOUNT, STRLENS("CALLNAME_ASSI"), -1)%
	ENDIF

	RETURNF LOCALS:LCOUNT
NEXT

LOCALS =
RETURNF LOCALS

;-------------------------------------------------
;イベント群ARGSを全て消去するのに使う
;例えば、ARGS = 助手 とすると、"助手："で始まる全てのイベント記録を取り除く
;-------------------------------------------------
@DEL_CEVENT_GROUP, ARGS, ARG
#DIM LCOUNT
#DIMS MEMOS

SIF ARG == 0 && TARGET
	ARG = TARGET

ARGS = %ARGS%：

VARSET LOCALS
;イベントごとに分解する
SPLIT CSTR:ARG:10, "/", LOCALS

;一旦CSTR:10を初期化
CSTR:ARG:10 =

FOR LCOUNT, 0, 100
	SIF LOCALS:LCOUNT == ""
		BREAK

	;イベント群ARGSならCONTINUEさせて、文字列を組み込まない
	;（LOCALS:LCOUNTの先頭からARGSの長さ分だけ抜き出したものがARGSならLOCALS:LCOUNTはイベント群ARGS）
	SIF SUBSTRING(LOCALS:LCOUNT, 0, STRLENS(ARGS) ) == ARGS
		CONTINUE

	;イベント群ARGSでないなら戻す
	CSTR:ARG:10 = %CSTR:ARG:10%%LOCALS:LCOUNT%/
NEXT

;-------------------------------------------------
;@DEL_CEVENT_GROUPのCSTR汎用版
;CSTR:(ARG:1)に影響する
;-------------------------------------------------
@DEL_CSTR_GROUP, ARGS, ARG, ARG:1
#DIM LCOUNT

SIF ARG == 0 && TARGET
	ARG = TARGET

ARGS = %ARGS%：

VARSET LOCALS
;イベントごとに分解する
SPLIT CSTR:ARG:(ARG:1), "/", LOCALS

;一旦CSTR:(ARG:1)を初期化
CSTR:ARG:(ARG:1) =

FOR LCOUNT, 0, 100
	SIF LOCALS:LCOUNT == ""
		BREAK

	;イベント群ARGSならCONTINUEさせて、文字列を組み込まない
	;（LOCALS:LCOUNTの先頭からARGSの長さ分だけ抜き出したものがARGSならLOCALS:LCOUNTはイベント群ARGS）
	SIF SUBSTRING(LOCALS:LCOUNT, 0, STRLENS(ARGS) ) == ARGS
		CONTINUE

	;イベント群ARGSでないなら戻す
	CSTR:ARG:(ARG:1) = %CSTR:ARG:(ARG:1)%%LOCALS:LCOUNT%/
NEXT


;-------------------------------------------------
;@DEL_CEVENT_GROUPの特殊版
;ARGSに応じて複雑な処理をさせたい場合にはこっち
;-------------------------------------------------
@DEL_CEVENT_GROUP_EX, ARGS, ARG
#DIM LCOUNT

SIF ARG == 0 && TARGET
	ARG = TARGET

ARGS = %ARGS%：

VARSET LOCALS
;イベントごとに分解する
SPLIT CSTR:ARG:10, "/", LOCALS

;一旦CSTR:10を初期化
CSTR:ARG:10 =

FOR LCOUNT, 0, 100
	SIF LOCALS:LCOUNT == ""
		BREAK

	;イベント群ARGSに応じた種類ならCONTINUEさせて、文字列を組み込まない
	;記述が楽だから使ってるけど若干例外が発生する処理です実は。でもまぁ私が見ている限りは問題起きないはず
	SELECTCASE ARGS
	CASE "人称全部："
		SIF STRCOUNTS(LOCALS:LCOUNT, "一人称：", "一人称かな：", "一人称主人：", "一人称主人かな：", "一人称助手：", "一人称助手かな：")
			CONTINUE
		SIF STRCOUNTS(LOCALS:LCOUNT, "二人称：", "二人称かな：", "二人称主人：", "二人称主人かな：", "二人称助手：", "二人称助手かな：")
			CONTINUE
	CASE "一人称全部："
		SIF STRCOUNTS(LOCALS:LCOUNT, "一人称：", "一人称かな：", "一人称主人：", "一人称主人かな：", "一人称助手：", "一人称助手かな：")
			CONTINUE
	CASE "一人称："
		SIF STRCOUNTS(LOCALS:LCOUNT, "一人称：", "一人称かな：")
			CONTINUE
	CASE "一人称主人："
		SIF STRCOUNTS(LOCALS:LCOUNT, "一人称主人：", "一人称主人かな：")
			CONTINUE
	CASE "一人称助手："
		SIF STRCOUNTS(LOCALS:LCOUNT, "一人称助手：", "一人称助手かな：")
			CONTINUE
	CASE "二人称全部："
		SIF STRCOUNTS(LOCALS:LCOUNT, "二人称：", "二人称かな：", "二人称主人：", "二人称主人かな：", "二人称助手：", "二人称助手かな：")
			CONTINUE
	CASE "二人称："
		SIF STRCOUNTS(LOCALS:LCOUNT, "二人称：", "二人称かな：")
			CONTINUE
	CASE "二人称主人："
		SIF STRCOUNTS(LOCALS:LCOUNT, "二人称主人：", "二人称主人かな：")
			CONTINUE
	CASE "二人称助手："
		SIF STRCOUNTS(LOCALS:LCOUNT, "二人称助手：", "二人称助手かな：")
			CONTINUE
	CASEELSE
		;その他の場合には@DEL_CEVENT_GROUPと同じ挙動をする
		SIF SUBSTRING(LOCALS:LCOUNT, 0, STRLENS(ARGS) ) == ARGS
			CONTINUE
	ENDSELECT

	;問題ない部分は戻す
	CSTR:ARG:10 = %CSTR:ARG:10%%LOCALS:LCOUNT%/
NEXT


;-------------------------------------------------
;PREVPLAYは前のターンのインサート状況など
;-------------------------------------------------
@PREVPLAY(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET

SELECTCASE ARGS
CASE "インサート"
	SIF GETBIT(TCVAR:ARG:151, 0) || GETBIT(TCVAR:ARG:151, 1) || GETBIT(TCVAR:ARG:151, 8)
		RETURNF 1
CASE "Ｖインサート"
	SIF GETBIT(TCVAR:ARG:151, 0)
		RETURNF 1
CASE "Ａインサート"
	SIF GETBIT(TCVAR:ARG:151, 1)
		RETURNF 1
CASE "尿道インサート"
	SIF GETBIT(TCVAR:ARG:151, 8)
		RETURNF 1
CASE "正常位"
	SIF GETBIT(TCVAR:ARG:151, 2)
		RETURNF 1
CASE "Ｖ正常位"
	SIF GETBIT(TCVAR:ARG:151, 2) && GETBIT(TCVAR:ARG:151, 0)
		RETURNF 1
CASE "Ａ正常位"
	SIF GETBIT(TCVAR:ARG:151, 2) && GETBIT(TCVAR:ARG:151, 1)
		RETURNF 1
CASE "尿道正常位"
	SIF GETBIT(TCVAR:ARG:151, 2) && GETBIT(TCVAR:ARG:151, 8)
		RETURNF 1
CASE "後背位"
	SIF GETBIT(TCVAR:ARG:151, 3)
		RETURNF 1
CASE "Ｖ後背位"
	SIF GETBIT(TCVAR:ARG:151, 3) && GETBIT(TCVAR:ARG:151, 0)
		RETURNF 1
CASE "Ａ後背位"
	SIF GETBIT(TCVAR:ARG:151, 3) && GETBIT(TCVAR:ARG:151, 1)
		RETURNF 1
CASE "尿道後背位"
	SIF GETBIT(TCVAR:ARG:151, 3) && GETBIT(TCVAR:ARG:151, 8)
		RETURNF 1
CASE "屈曲位"
	SIF GETBIT(TCVAR:ARG:151, 4)
		RETURNF 1
CASE "Ｖ屈曲位"
	SIF GETBIT(TCVAR:ARG:151, 4) && GETBIT(TCVAR:ARG:151, 0)
		RETURNF 1
CASE "Ａ屈曲位"
	SIF GETBIT(TCVAR:ARG:151, 4) && GETBIT(TCVAR:ARG:151, 1)
		RETURNF 1
CASE "尿道屈曲位"
	SIF GETBIT(TCVAR:ARG:151, 4) && GETBIT(TCVAR:ARG:151, 8)
		RETURNF 1
CASE "対面座位"
	SIF GETBIT(TCVAR:ARG:151, 5)
		RETURNF 1
CASE "Ｖ対面座位"
	SIF GETBIT(TCVAR:ARG:151, 5) && GETBIT(TCVAR:ARG:151, 0)
		RETURNF 1
CASE "Ａ対面座位"
	SIF GETBIT(TCVAR:ARG:151, 5) && GETBIT(TCVAR:ARG:151, 1)
		RETURNF 1
CASE "尿道対面座位"
	SIF GETBIT(TCVAR:ARG:151, 5) && GETBIT(TCVAR:ARG:151, 8)
		RETURNF 1
CASE "背面座位"
	SIF GETBIT(TCVAR:ARG:151, 6)
		RETURNF 1
CASE "Ｖ背面座位"
	SIF GETBIT(TCVAR:ARG:151, 6) && GETBIT(TCVAR:ARG:151, 0)
		RETURNF 1
CASE "Ａ背面座位"
	SIF GETBIT(TCVAR:ARG:151, 6) && GETBIT(TCVAR:ARG:151, 1)
		RETURNF 1
CASE "尿道背面座位"
	SIF GETBIT(TCVAR:ARG:151, 6) && GETBIT(TCVAR:ARG:151, 8)
		RETURNF 1
CASE "騎乗位"
	SIF GETBIT(TCVAR:ARG:151, 7)
		RETURNF 1
CASE "Ｖ騎乗位"
	SIF GETBIT(TCVAR:ARG:151, 7) && GETBIT(TCVAR:ARG:151, 0)
		RETURNF 1
CASE "Ａ騎乗位"
	SIF GETBIT(TCVAR:ARG:151, 7) && GETBIT(TCVAR:ARG:151, 1)
		RETURNF 1
CASE "尿道騎乗位"
	SIF GETBIT(TCVAR:ARG:151, 7) && GETBIT(TCVAR:ARG:151, 8)
		RETURNF 1

CASE "挿入中"
	SIF GETBIT(TCVAR:ARG:151, 10) || GETBIT(TCVAR:ARG:151, 11) || GETBIT(TCVAR:ARG:151, 12)
		RETURNF 1
CASE "Ｖ挿入中"
	SIF GETBIT(TCVAR:ARG:151, 10)
		RETURNF 1
CASE "Ａ挿入中"
	SIF GETBIT(TCVAR:ARG:151, 11)
		RETURNF 1
CASE "尿道挿入中"
	SIF GETBIT(TCVAR:ARG:151, 12)
		RETURNF 1

CASE "Ｇスポ責め"
	SIF GETBIT(TCVAR:ARG:151, 15)
		RETURNF 1

CASE "絶頂"
	SIF GETBIT(TCVAR:ARG:151, 20)
		RETURNF 1
CASE "放尿"
	SIF GETBIT(TCVAR:ARG:151, 21)
		RETURNF 1
CASE "母乳飲み"
	SIF GETBIT(TCVAR:ARG:151, 22)
		RETURNF 1

CASE "飲尿"
	SIF GETBIT(TCVAR:ARG:151, 30)
		RETURNF 1
CASE "膣内浴尿"
	SIF GETBIT(TCVAR:ARG:151, 31)
		RETURNF 1
CASE "子宮内浴尿"
	SIF GETBIT(TCVAR:ARG:151, 32)
		RETURNF 1
CASE "温泉浣腸"
	SIF GETBIT(TCVAR:ARG:151, 33)
		RETURNF 1
CASE "乳内浴尿"
	SIF GETBIT(TCVAR:ARG:151, 34)
		RETURNF 1
CASE "膀胱内浴尿"
	SIF GETBIT(TCVAR:ARG:151, 35)
		RETURNF 1

CASE "お掃除フェラ"
	SIF GETBIT(TCVAR:ARG:151, 40)
		RETURNF 1
CASE "Ａお掃除フェラ"
	SIF GETBIT(TCVAR:ARG:151, 41)
		RETURNF 1
CASE "尿お掃除フェラ"
	SIF GETBIT(TCVAR:ARG:151, 42)
		RETURNF 1
CASE "恥垢お掃除フェラ"
	SIF GETBIT(TCVAR:ARG:151, 43)
		RETURNF 1

CASE "お掃除クンニ"
	SIF GETBIT(TCVAR:ARG:151, 45)
		RETURNF 1
CASE "尿お掃除クンニ"
	SIF GETBIT(TCVAR:ARG:151, 46)
		RETURNF 1
CASE "恥垢お掃除クンニ"
	SIF GETBIT(TCVAR:ARG:151, 47)
		RETURNF 1

CASE "お掃除Ａ舐め"
	SIF GETBIT(TCVAR:ARG:151, 50)
		RETURNF 1

ENDSELECT
RETURNF 0


@SET_PREVPLAY, ARG
LOCAL = 0
IF INSERT("インサート", ARG)
	SIF INSERT("Ｖインサート", ARG)
		SETBIT LOCAL, 0
	SIF INSERT("Ａインサート", ARG)
		SETBIT LOCAL, 1
	SIF INSERT("正常位", ARG)
		SETBIT LOCAL, 2
	SIF INSERT("後背位", ARG)
		SETBIT LOCAL, 3
	SIF INSERT("屈曲位", ARG)
		SETBIT LOCAL, 4
	SIF INSERT("対面座位", ARG)
		SETBIT LOCAL, 5
	SIF INSERT("背面座位", ARG)
		SETBIT LOCAL, 6
	SIF INSERT("騎乗位", ARG)
		SETBIT LOCAL, 7

	SIF INSERT("尿道インサート", ARG)
		SETBIT LOCAL, 8
ENDIF

SIF INSERT("Ｖ挿入中", ARG)
	SETBIT LOCAL, 10
SIF INSERT("Ａ挿入中", ARG)
	SETBIT LOCAL, 11
SIF INSERT("尿道挿入中", ARG)
	SETBIT LOCAL, 12

SELECTCASE ARG
CASE PLAYER
	SELECTCASE SELECTCOM
	CASE 60 TO 69, 177
		SETBIT LOCAL, 10
	CASE 70 TO 79
		SETBIT LOCAL, 11
	CASE 83 TO 88
		SETBIT LOCAL, 12
	CASE 120 TO 124
		SETBIT LOCAL, 0
	CASE 125 TO 129
		SETBIT LOCAL, 1
	ENDSELECT

	SELECTCASE SELECTCOM
	CASE 120, 125
		SETBIT LOCAL, 2
	CASE 121, 126
		SETBIT LOCAL, 3
	CASE 122, 127
		SETBIT LOCAL, 7
	ENDSELECT
CASE TARGET
	SELECTCASE SELECTCOM
	CASE 60 TO 69, 177
		SETBIT LOCAL, 0
	CASE 70 TO 79
		SETBIT LOCAL, 1
	CASE 83 TO 88
		SETBIT LOCAL, 8
	CASE 120 TO 124
		SETBIT LOCAL, 10
	CASE 125 TO 129
		SETBIT LOCAL, 11
	ENDSELECT

	SELECTCASE SELECTCOM
	CASE 60, 70
		SETBIT LOCAL, 2
	CASE 61, 71
		SETBIT LOCAL, 3
	CASE 62, 72
		SETBIT LOCAL, 4
	CASE 63, 73
		SETBIT LOCAL, 5
	CASE 64, 74
		SETBIT LOCAL, 6
	CASE 65, 75
		SETBIT LOCAL, 7
	ENDSELECT

	;Gスポ刺激
	SIF CHECK_COM("Ｇスポ責め") || SELECTCOM == 66
		SETBIT LOCAL, 15
ENDSELECT

SIF COND("絶頂", ARG)
	SETBIT LOCAL, 20
SIF NOWEX:ARG:放尿 || NOWEX:ARG:絶頂お漏らし
	SETBIT LOCAL, 21
SIF COND("母乳飲みフラグ", ARG)
	SETBIT LOCAL, 22

SIF COND("飲尿フラグ", ARG)
	SETBIT LOCAL, 30
SIF COND("膣内浴尿フラグ", ARG)
	SETBIT LOCAL, 31
SIF COND("子宮内浴尿フラグ", ARG)
	SETBIT LOCAL, 32
SIF COND("温泉浣腸フラグ", ARG)
	SETBIT LOCAL, 33
SIF COND("乳内浴尿フラグ", ARG)
	SETBIT LOCAL, 34
SIF COND("膀胱内浴尿フラグ", ARG)
	SETBIT LOCAL, 35

SIF COND("お掃除フェラフラグ", ARG)
	SETBIT LOCAL, 40
;お掃除フェラフラグも立っているので、Ａお掃除フェラを行った場合には40bitもＯＮになる
SIF COND("Ａお掃除フェラフラグ", ARG)
	SETBIT LOCAL, 41
SIF COND("尿お掃除フェラフラグ", ARG)
	SETBIT LOCAL, 42
SIF COND("恥垢お掃除フェラフラグ", ARG)
	SETBIT LOCAL, 43

SIF COND("お掃除クンニフラグ", ARG)
	SETBIT LOCAL, 45
SIF COND("尿お掃除クンニフラグ", ARG)
	SETBIT LOCAL, 46
SIF COND("恥垢お掃除クンニフラグ", ARG)
	SETBIT LOCAL, 47

SIF COND("お掃除Ａ舐めフラグ", ARG)
	SETBIT LOCAL, 50


TCVAR:ARG:151 = LOCAL
