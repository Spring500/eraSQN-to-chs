;調教後性交渉追加版(2008/04/17 byα)
;夜、厳しい条件をクリアすると…？
@SELF_CHECK
SIF COND("同衾イベント") || COND("同衾イベント", MASTER)
	RETURN 0

;調教後イベントの記録初期化
CVARSET CFLAG, 92, 0

;念のため、PLAYERをMASTERに直す
PLAYER = MASTER

;MASTERとの絡み
SIF ABL:欲望 && RAND:2
	CALL SELF_CHECK_PLAY, MASTER
;自慰
SIF ABL:欲望 && ABL:自慰上瘾 && RAND:4 == 0
	CALL SELF_CHECK_PLAY, TARGET

;助手との絡み
IF ASSI && ABL:欲望 && RAND:2
	;PLAYERをASSIにする
	PLAYER = ASSI
	CALL SELF_CHECK_PLAY, ASSI
ENDIF

;PLAYERをMASTERに直す
PLAYER = MASTER


@SELF_CHECK_PLAY, ARG
VARSET LOCAL
;イベントの決定(ARG使うのはここだけ)
CALL PLAY_SELF, ARG
LOCALS = %STR:100%
SIF LOCALS == ""
	RETURN 0

;基本回数は精力依存
LOCAL = BASE:精力/20 + LIMIT(RAND:(BASE:精力/10 + 1), 1, BASE:精力/15)

SELECTCASE LOCALS
CASE "調教後セックス"
	;V感覚、淫壷によるボーナス
	LOCAL += MAX(ABL:Ｖ感觉/3, 0) + TALENT:淫壶

	IF LOCAL > 0
		DRAWLINE

		CALL SIMULATE_TRAIN_FIRST

		IF PLAYER == MASTER
			IF BASE:PLAYER:STOCK
				PRINTFORML %CALLNAME:PLAYER%と%CALLNAME:TARGET%はまだ冷めやらぬ昂奮を抑えきれず、
				PRINTFORML 汗だくになってベッドの上でまぐわった…
				CALL SIMULATE_TRAIN_COM, 60, LOCAL
			ELSEIF BASE:PLAYER:精力 * 2 >= MAXBASE:PLAYER:精力
				PRINTFORML 興奮冷めやらぬ%CALLNAME:TARGET%は、
				PRINTFORML %CALLNAME:PLAYER%を押し倒して貪るように己の欲望をぶつけてきた…
				TEQUIP:推到 = 1
				CALL SIMULATE_TRAIN_COM, 65, LOCAL
			ELSE
				PRINTFORML 火照った身体をもてあましている%CALLNAME:TARGET%は、
				PRINTFORML 疲れきった%CALLNAME:PLAYER%に跨って限界を超えて精を絞ってきた…
				TEQUIP:推到 = 1
				CALL SIMULATE_TRAIN_COM, 65, LOCAL
			ENDIF

			CALL SELF_KOJO, LOCALS

			PRINTL
			IF ABL:服从 + ABL:欲望 + ABL:Ｖ感觉 >= 13 && COND("欲求不满")
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%のモノを舌で舐め清めながら、
				PRINTFORMW 上目遣いに名残惜しげに見つめてくる…
				PRINTFORMW しかしその視線を振り解き、%CALLNAME:PLAYER%は部屋を後にした…
				CALL SIMULATE_TRAIN_COM, 91, 1
			ELSEIF TALENT:偏爱 + TALENT:相思相爱 && RAND:2
				IF TALENT:PLAYER:无视污臭 && ABL:施虐倾向 >= 3 && RAND:5 == 0
					PRINTFORMW %CALLNAME:PLAYER%は満足した様子の%CALLNAME:TARGET%のアソコを舌で綺麗に舐め清めて、部屋を後にした…
					CALL SIMULATE_TRAIN_COM, 1, LOCAL
				ELSE
					PRINTFORMW %CALLNAME:PLAYER%は満足した様子の%CALLNAME:TARGET%の身体を綺麗に拭き清めて、部屋を後にした…
				ENDIF
			ELSE
				PRINTFORMW %CALLNAME:PLAYER%は満足した様子の%CALLNAME:TARGET%にペニスを綺麗にさせて、部屋を後にした…
				CALL SIMULATE_TRAIN_COM, 91, 1
			ENDIF
		ELSE
			PRINTFORML 調教後%CALLNAME:TARGET%と%CALLNAME:ASSI%は、溢れる欲望に突き動かされて肌を重ねたようだ…
			PRINTFORML この時ばかりは%CALLNAME:MASTER%の事も二人の脳裏から消え、
			PRINTFORM 獣のように飽きることなく
			IF TIME == 0
				PRINTFORM 日が暮れるまで
			ELSE
				PRINTFORM 一晩中
			ENDIF
			PRINTFORML 互いに快楽を貪りあった…
			CALL SIMULATE_TRAIN_COM, 61, LOCAL
			CALL SELF_KOJO, LOCALS
		ENDIF

		;経験や珠などの獲得量計算
		CALL SIMULATE_TRAIN_CALC
		;結果発表
		CALL SIMULATE_TRAIN_SHOW

		CALL CALC, "欲求不満解消", TARGET, LOCAL
	ENDIF
CASE "調教後Ａセックス"
	;A感覚、尻穴狂いによるボーナス
	LOCAL += MAX(ABL:Ａ感觉/3, 0) + TALENT:尻穴狂

	IF LOCAL > 0
		DRAWLINE

		CALL SIMULATE_TRAIN_FIRST

		IF PLAYER == MASTER
			IF BASE:PLAYER:STOCK
				PRINTFORML %CALLNAME:PLAYER%と%CALLNAME:TARGET%はまだ冷めやらぬ昂奮を抑えきれず、
				PRINTFORML お尻の穴で何度もベッドの上でまぐわった…
				CALL SIMULATE_TRAIN_COM, 71, LOCAL
			ELSEIF BASE:PLAYER:精力 * 2 >= MAXBASE:PLAYER:精力
				PRINTFORML アナルが疼いて興奮冷めやらぬ%CALLNAME:TARGET%は、
				PRINTFORML %CALLNAME:PLAYER%を押し倒して貪るように己の欲望をぶつけてきた…
				TEQUIP:推到 = 1
				CALL SIMULATE_TRAIN_COM, 75, LOCAL
			ELSE
				PRINTFORML 火照った身体をもてあましている%CALLNAME:TARGET%は、
				PRINTFORML 疲れきった%CALLNAME:PLAYER%に跨ってアナルで精を絞ってきた…
				TEQUIP:推到 = 1
				CALL SIMULATE_TRAIN_COM, 75, LOCAL
			ENDIF
			CALL SELF_KOJO, LOCALS

			IF ABL:服从 + ABL:欲望 + ABL:百合倾向 >= 13 && COND("欲求不满")
				PRINTL
				PRINTFORMW %CALLNAME:TARGET%は名残惜しげに袖を引っ張る
				PRINTFORMW しかしその手を振り解き、%CALLNAME:PLAYER%は部屋を後にした…
			ENDIF
		ELSE
			PRINTFORML 調教後%CALLNAME:TARGET%と%CALLNAME:ASSI%は、火照った身体をもてあまして肌を重ねたようだ…
			PRINTFORML 溢れる欲望に突き動かされ、この時ばかりは%CALLNAME:MASTER%の事も二人の脳裏から消え
			PRINTFORM 獣のように飽きることなく
			IF TIME == 0
				PRINTFORM 日が暮れるまで
			ELSE
				PRINTFORM 一晩中
			ENDIF
			PRINTFORML %CALLNAME:TARGET%の肛門で快楽を貪りあった…
			CALL SIMULATE_TRAIN_COM, 71, LOCAL
			CALL SELF_KOJO, LOCALS
		ENDIF

		;経験や珠などの獲得量計算
		CALL SIMULATE_TRAIN_CALC
		;結果発表
		CALL SIMULATE_TRAIN_SHOW

		CALL CALC, "欲求不満解消", TARGET, LOCAL
	ENDIF
CASE "調教後レズプレイ"
	;C感覚、淫核によるボーナス
	LOCAL += MAX(ABL:Ｃ感觉/3, 0) + TALENT:淫核

	IF LOCAL > 0
		DRAWLINE

		CALL SIMULATE_TRAIN_FIRST

		IF PLAYER == MASTER
			IF BASE:PLAYER:STOCK
				PRINTFORML %CALLNAME:PLAYER%と%CALLNAME:TARGET%はまだ冷めやらぬ昂奮を抑えきれず、
				PRINTFORML お互いの火照った身体を慰めあい何度もイッてしまった…
				CALL SIMULATE_TRAIN_COM, 1, RAND:LOCAL + 1
				CALL SIMULATE_TRAIN_COM, 98, RAND:LOCAL + 1
			ELSE
				PRINTFORML 興奮冷めやらぬ%CALLNAME:TARGET%は、
				SIF BASE:PLAYER:精力 * 2 < MAXBASE:PLAYER:精力
					PRINT 疲れきった
				PRINTFORML %CALLNAME:PLAYER%を押し倒して貪るように秘所を擦りあわせてきた…
				CALL SIMULATE_TRAIN_COM, 98, RAND:LOCAL + 1
			ENDIF

			CALL SIMULATE_TRAIN_COM, 80, LOCAL
			CALL SELF_KOJO, LOCALS
			IF ABL:服从 + ABL:欲望 + ABL:百合倾向 >= 13 && COND("欲求不满")
				PRINTL
				PRINTFORMW %CALLNAME:TARGET%は名残惜しげに袖を引っ張る
				PRINTFORMW しかしその手を振り解き、%CALLNAME:PLAYER%は部屋を後にした…
			ENDIF
		ELSE
			PRINTFORML 調教後%CALLNAME:TARGET%と%CALLNAME:ASSI%は、情欲のままに肌を重ねたようだ…
			PRINTFORML 溢れる欲望に突き動かされ、この時ばかりは%CALLNAME:MASTER%の事も二人の脳裏から消え
			PRINTFORM 獣のように飽きることなく
			IF TIME == 0
				PRINTFORM 日が暮れるまで
			ELSE
				PRINTFORM 一晩中
			ENDIF

			CALL SIMULATE_TRAIN_COM, 1, RAND:LOCAL + 1
			CALL SIMULATE_TRAIN_COM, 98, RAND:LOCAL + 1
			CALL SIMULATE_TRAIN_COM, 80, LOCAL/2 + 1

			IF TALENT:扶她 && TALENT:PLAYER:扶她
				PRINTFORML 互いの秘所を擦りつけあい、ペニスをしゃぶりあって快楽を貪った…
				CALL SIMULATE_TRAIN_COM, 91, LOCAL/2 + 1
			ELSE
				PRINTFORML 互いの秘所を擦りつけあって、快楽を貪った…
			ENDIF
			CALL SELF_KOJO, LOCALS

			SWAP PLAYER, TARGET
			CALL SIMULATE_TRAIN_COM, 80, LOCAL/2 + 1
			CALL SIMULATE_TRAIN_COM, 91, LOCAL/2 + 1
			SWAP PLAYER, TARGET
		ENDIF

		;経験や珠などの獲得量計算
		CALL SIMULATE_TRAIN_CALC
		;結果発表
		CALL SIMULATE_TRAIN_SHOW

		CALL CALC, "欲求不満解消", TARGET, LOCAL
	ENDIF
CASE "調教後自慰"
	LOCAL += RAND:(ABL:自慰上瘾 + 1)
	IF LOCAL > 0
		DRAWLINE
		PRINTFORM %CALLNAME:PLAYER%が出て行った後、%CALLNAME:TARGET%は
		SIF TALENT:恋慕
			PRINTFORML %CALLNAME:PLAYER%の事を想いながら、

		;フィストフラグ
		A = 0
		B = 0

		IF GETBIT(CHECK_ONANIE(SELECTCOM), 0) && PENIS(TARGET)
			PRINT ペニスを扱いて
		ELSEIF GETBIT(CHECK_ONANIE(SELECTCOM), 1)
			IF RAND:2 == 0
				PRINT アソコ
			ELSE
				PRINT 秘所
			ENDIF
			IF EXP:Ｖ拳交经验 >= 5 && TALENT:处女 == 0 && ABL:Ｖ扩张 >= 4 && RAND:3 == 0
				IF RAND:2
					PRINT を自らの拳で掻き回して
				ELSE
					PRINT に自らの拳を出し入れして
				ENDIF
				A = 2
				B = 10
				LOCAL = MAX(1, LOCAL/2)
			ELSEIF RAND:3 == 0
				PRINT を自らの指で弄って
			ELSEIF RAND:2 == 0
				PRINT を自らの指で掻き回して
			ELSE
				PRINT に自らの指を出し入れして
			ENDIF
		ELSEIF GETBIT(CHECK_ONANIE(SELECTCOM), 2)
			IF RAND:2 == 0
				PRINT アナル
			ELSE
				PRINT お尻の穴
			ENDIF
			IF EXP:Ａ拳交经验 >= 5 && ABL:Ａ扩张 >= 4 && RAND:3 == 0
				IF RAND:2
					PRINT を自らの拳で掻き回して
				ELSE
					PRINT に自らの拳を出し入れして
				ENDIF
				A = 2
				B = 10
				LOCAL = MAX(1, LOCAL/2)
			ELSEIF RAND:3 == 0
				PRINT を自らの指で弄って
			ELSEIF RAND:2 == 0
				PRINT を自らの指で掻き回して
			ELSE
				PRINT に自らの指を出し入れして
			ENDIF
		ELSE
			IF RAND:2 == 0
				PRINT クリトリス
			ELSE
				PRINT 秘所
			ENDIF
			PRINTFORM を自らの%NAMES("指")%で
			IF RAND:3 == 0
				PRINT 擦りながら
			ELSEIF RAND:2 == 0
				PRINT 弄りながら
			ELSE
				PRINT 捏ね回しながら
			ENDIF
		ENDIF
		PRINTFORML {LOCAL}回自慰をしたようだ。

		CALL SELF_KOJO, LOCALS

		PRINTFORML %EXPNAME:10%＋{LOCAL}
		EXP:自慰经验 += LOCAL

		PRINTFORML %EXPNAME:2%＋{LOCAL}
		EXP:绝顶经验 += LOCAL
		IF TALENT:女性 == 0
			PRINTFORML %EXPNAME:3%＋{LOCAL}
			EXP:射精经验 += LOCAL
		ENDIF

		IF GETBIT(CHECK_ONANIE(SELECTCOM), 1)
			PRINTFORML %EXPNAME:0%＋{LOCAL+B}
			EXP:Ｖ经验 += LOCAL+B
			SIF A
				PRINTFORML %EXPNAME:53%＋{LOCAL * A}
			EXP:Ｖ扩张经验 += LOCAL * A
			PRINTFORML %PALAMNAME:1%の珠＋{(LOCAL+B) * 200}
			JUEL:快Ｖ += (LOCAL+B) * 200
		ELSEIF GETBIT(CHECK_ONANIE(SELECTCOM), 2)
			PRINTFORML %EXPNAME:1%＋{LOCAL+B}
			EXP:Ａ经验 += LOCAL+B
			SIF A
				PRINTFORML %EXPNAME:57%＋{LOCAL * A}
			EXP:Ａ扩张经验 += LOCAL * A
			PRINTFORML %PALAMNAME:2%の珠＋{(LOCAL+B) * 200}
			JUEL:快Ａ += (LOCAL+B) * 200
		ELSE
			PRINTFORML %PALAMNAME:0%の珠＋{LOCAL * 500}
			JUEL:快Ｃ += LOCAL * 500
		ENDIF

		PRINTFORML %PALAMNAME:4%の珠＋{LOCAL * 100}
		JUEL:恭顺 += LOCAL * 100
		PRINTFORMW %PALAMNAME:5%の珠＋{(LOCAL+B) * 250}
		JUEL:欲情 += (LOCAL+B) * 250

		BASE:体力 = MAX(BASE:体力 - (LOCAL+B)*50, 1)
		BASE:气力 = MAX(BASE:气力 - (LOCAL+B)*30, 0)

		IF ABL:服从 + ABL:欲望 + ABL:自慰上瘾 >= 10 && TIME == 0
			PRINTFORML そのことを%CALLNAME:TARGET%は報告してきた。
			PRINTFORML %PALAMNAME:8%の珠＋{(LOCAL+B) * 200}
			JUEL:耻情 += (LOCAL+B) * 200
		ENDIF

		SIF ABL:服从 == 5 || ABL:欲望 == 5
			PRINTFORMW しかし何度自分を慰めてみても、%CALLNAME:PLAYER%への欲望は満たされなかった…
	ENDIF
ENDSELECT

;調教後イベントの記録
IF LOCALS != "" && LOCAL
	CFLAG:PLAYER:92 = NUM_EV(LOCALS)
	CFLAG:92 = NUM_EV(LOCALS)
	DRAWLINE
ENDIF

@PLAY_SELF, ARG
#DIM LCOUNT
STR:100 =
;恋慕、淫乱による調教後性交渉チェック
SIF BASE:PLAYER:精力 == 0 || TALENT:淫乱 + TALENT:恋慕 == 0
	RETURN 0
;体力を使い果たした
SIF BASE:体力 < 500 || BASE:精力 <= 0
	RETURN 0
;性欲大満足
SIF BASE:精液欲 >= MAXBASE:精液欲*2
	RETURN 0

;助手とのプレイが発生するか
IF ARG == ASSI && ABL:服从 >= 3 && ABL:欲望 >= 3
	;相性がよいことが条件
	SIF RELATION:(NO:ASSI) < 120 || RELATION:ASSI:(NO:TARGET) < 120
		RETURN 0
ENDIF

VARSET LOCALS

IF ARG == PLAYER
	;Ｖセックス
	CALL COM_ABLE65
	SIF RESULT && PENIS(PLAYER) && ABL:Ｖ感觉 >= 3 && TALENT:处女 + TALENT:PLAYER:童贞 == 0
		LOCALS:0 = 調教後セックス
	;Ａセックス
	CALL COM_ABLE75
	SIF RESULT && PENIS(PLAYER) && EXP:PLAYER:Ａ插入经验 && COND("ベテラン：Ａセックス")
		LOCALS:1 = 調教後Ａセックス
	;レズプレイ
	CALL COM_ABLE80
	SIF RESULT && ABL:百合倾向 >= 3
		LOCALS:2 = 調教後レズプレイ
ELSEIF ARG == TARGET
	;自慰
	SIF ABL:自慰上瘾 >= 3 && COND("身嗜み：自慰禁止") == 0 && PLAYER == MASTER
		LOCALS:0 = 調教後自慰
ENDIF

LOCAL = 1
FOR LCOUNT, 0, 10
	SIF LOCALS:LCOUNT == ""
		CONTINUE

	SIF RAND:LOCAL == 0
		STR:100 = %LOCALS:LCOUNT%
	LOCAL += 1
NEXT

RETURN 0
