;-------------------------------------------------
;朝に起こるイベント
;-------------------------------------------------
@EVENT_NEWDAY
#DIM LCOUNT
#DIM MEMO_EXP, 100

;変数クリア
VARSET LOCAL
;TAGETの経験増加の判定で使う
FOR LCOUNT, 0, 100
	MEMO_EXP:LCOUNT = EXP:LCOUNT
NEXT

;朝イベントの記録初期化
CVARSET CFLAG, 90, 0

;条件によってイベントの種類が増える
PLAYER = MASTER

;最初にサドっ気イベントの判定を行う(発生確率が低い)
IF RAND:5 == 0
	CALL BIND_AT_MORNING
	IF RESULT == 1
		BEGIN TURNEND
		RETURN 1
	ENDIF
ENDIF
;朝イベントの決定
CALL PLAY_AT_MORNING
LOCALS = %STR:100%
SIF LOCALS == ""
	RETURN 0

;基本回数は精力依存
LOCAL = BASE:精力/20 + LIMIT(RAND:(BASE:精力/10 + 1), 1, BASE:精力/15)

DRAWLINE
SELECTCASE LOCALS
CASE "朝逆正常位"
	SIF TALENT:オトコ
		LOCAL += 1
	LOCAL = MAX(LOCAL + ABL:欲望/2, 1)

	CALL SIMULATE_TRAIN_FIRST
	CALL SIMULATE_TRAIN_COM, 120, LOCAL
	SIF EXP:射精経験 == MEMO_EXP:3
		CALL SIMULATE_TRAIN_COM, 120, LOCAL

	PRINTFORMW %CALLNAME:MASTER%は下腹部\@ RAND:2 ? を襲う衝撃 # から広がる快感 \@に気がついて目を覚ました
	PRINTFORMW どうやら、%CALLNAME:TARGET%が寝ている%CALLNAME:MASTER%を犯して楽しんでいる最中のようだ…
	IF TARGET == ASSI
		IF EXP:射精経験 > MEMO_EXP:3 && RAND:2
			PRINTFORMW すっかり目が覚めた%CALLNAME:MASTER%は腰を突き出して%CALLNAME:TARGET%にしっかり奉仕するように言いつけると、膣奥を叩く精液の感触を楽しみながら今日のスケジュールを報告させました…
		ELSE
			PRINTFORMW 主人が目を覚ましたことを確認すると、%CALLNAME:TARGET%は緩々と腰を動かして%CALLNAME:MASTER%に嬌声をあげさせながら今日のスケジュールを報告しはじめた…
		ENDIF
	;ＭＡＳＴＥＲ主導権な展開はまだできてない
	;ELSEIF RAND:2
	;	PRINTFORML %CALLNAME:MASTER%が下半身の違和感に目を覚ますと、下着を剥ぎ取りいきり立ったペニスを突き入れようとする%CALLNAME:TARGET%と目があった
	;	PRINTFORML しどろもどろになって言い訳するAへあなたは妖艶に笑いかけながら、足を開いてAを誘った…
	ELSE
		IF EXP:射精経験 > MEMO_EXP:3 && RAND:3 == 0
			PRINTFORM %CALLNAME:MASTER%が起きるまで射精を我慢していたらしい%CALLNAME:TARGET%は、
		ELSEIF RAND:2
			PRINTFORM 膣の締まりの変化で%CALLNAME:MASTER%の目覚めを感じ取った%CALLNAME:TARGET%は、
		ELSE
			PRINTFORM 寝込みを襲ったことについて嗜める%CALLNAME:MASTER%の口をキスで塞いだ%CALLNAME:TARGET%は、
			CALL SIMULATE_TRAIN_COM, 6, 1 + RAND:2
		ENDIF
		IF EXP:射精経験 > MEMO_EXP:3 && RAND:3 == 0
			PRINTFORMW 腰を突き出すと共に大量の精液を\@ COND("危険日", MASTER) ? 危険日の子宮に流し込み # 気持ちよさそうに中出しし \@ながら朝の挨拶をしてきた…
		ELSEIF ABL:腰使い >= 3 && RAND:3 == 0
			PRINTFORMW 巧みに腰を動かしてお互いの快感を高めながら朝の挨拶をしてきた…
		ELSEIF ABL:サドっ気 >= 3 && RAND:2
			PRINTFORMW 寝起きで\@ RAND:2 ? 抵抗が弱い # 動きの鈍い \@のをいい事に\@ (EXP:射精経験 > MEMO_EXP:3 && RAND:2) ? たっぷりと種付け # 更に激しく犯 \@しながら朝の挨拶をしてきた…
			CALL SIMULATE_TRAIN_COM, 120, 2
		ELSE
			PRINTFORMW 叩きつけるように腰を振って気持ちよさそうな顔を見せながら朝の挨拶をしてきた…
		ENDIF
	ENDIF

	CALL SELF_KOJO, LOCALS

	;経験や珠などの獲得量計算
	CALL SIMULATE_TRAIN_CALC
	;結果発表
	CALL SIMULATE_TRAIN_SHOW
	CALL CALC, "欲求不満解消", TARGET, LOCAL
CASE "朝クンニ"
	SIF TALENT:オトコ
		LOCAL += 1
	LOCAL = MAX(LOCAL + (ABL:奉仕精神 + ABL:レズっ気)/3, 1)
	PRINTFORML 朝、目を覚ますと股間が生暖かい…
	PRINTFORM もしや、と思い勢いよく布団をめくってみると、
	IF ABL:レズっ気 >= 3 && ABL:欲望 >= 5 && RAND:3 == 0
		PRINTFORM 上気した様子で
	ELSEIF RAND:3 == 0
		PRINTFORM 股間に顔を埋めて
	ELSEIF RAND:2
		PRINTFORM 股を押し開きながら
	ELSE
		PRINTFORM 脚の間に身体を割り込ませて
	ENDIF
	PRINTFORMW アソコを舐めている%CALLNAME:TARGET%と目が合った
	PRINTFORM %CALLNAME:TARGET%はぺろっと舌を出して笑顔を浮かべ、
	IF ABL:奉仕精神 >= 3 && RAND:2
		PRINTFORML 陰唇を綺麗にお掃除しながら朝の挨拶をしてきた…
		CALL SELF_KOJO, LOCALS
		CALL RESET_STAIN_EX, "Ｖ", MASTER
	ELSE
		PRINTFORML 朝の挨拶をしてきた…
		CALL SELF_KOJO, LOCALS
	ENDIF
	CALL SIMULATE_TRAIN, 98, LOCAL
	IF ABL:レズっ気 >= 3 && COND("身嗜み：ノーパン", MASTER) == 0 && RAND:2
		PRINTFORML %CALLNAME:TARGET%が部屋を出て行った後に鏡の前に立つと、無防備な下半身が目に入る
		PRINTFORML …どうやら、%CALLNAME:TARGET%にパンツを持っていかれたようだ……
	ENDIF
	CALL CALC, "欲求不満解消", TARGET, 1
CASE "朝足コキ"
	LOCAL = MAX(LOCAL + (ABL:サドっ気 + ABL:足使い)/3, 1)

	PRINTFORML 朝、%CALLNAME:TARGET%\@ PENIS(MASTER) ? の足コキで # に足でアソコを責められて \@目を覚ました
	
	CALL SELF_KOJO, LOCALS
	CALL SIMULATE_TRAIN, 95, LOCAL
	
	PRINTFORM %CALLNAME:TARGET%は
	IF TALENT:意地悪 || TALENT:小悪魔
		IF EXP:精液経験 > MEMO_EXP:20
			PRINTFORM 足に付着した精液を指で拭ってペロリとひと舐めしつつ、
		ELSEIF PENIS(MASTER) == 0
			PRINTFORM 足に付着した愛液を指で拭ってペロリとひと舐めしつつ、
		ENDIF
	ENDIF
	PRINTFORML 優越感に浸ったような顔で朝の挨拶をしてきた

	CALL CALC, "欲求不満解消", TARGET, 1
CASE "朝の母乳搾り"
	LOCAL = MAX(LOCAL + MIN(GET_EXP_LV(EXP:噴乳経験), 5), 1)
	;巨乳
	IF TALENT:巨乳
		PRINTFORML 朝、台所に来ると%CALLNAME:TARGET%がその大きな胸から母乳を搾り出していた
		;妊娠または出産経験
		IF TALENT:妊娠 || TALENT:出産経験
			IF RAND:2 == 0
				PRINTFORML %CALLNAME:TARGET%はぱんぱんに張り詰めた胸から、恍惚の表情で母乳を噴き出している…
			ELSE
				PRINTFORML %CALLNAME:TARGET%は慈愛に満ちた表情で、勢いよく噴き出る母乳を器に搾り出している…
			ENDIF
		ENDIF
	ELSE
		PRINTFORML 朝、台所に来ると%CALLNAME:TARGET%が自らの母乳を器に搾り出していた
	ENDIF

	CALL SELF_KOJO, LOCALS
	;巨乳の場合、2倍の量が搾れ、半分を売りに出します
	IF TALENT:巨乳
		EXP:噴乳経験 += LOCAL * 2
		PRINTFORML %EXPNAME:54%＋{LOCAL*2}
		IF TALENT:献身 || TALENT:好色
			IF RAND:2 == 0
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%を見つけると、にこりと微笑み手招きしてきた…
				PRINTFORML 近付くと、%CALLNAME:TARGET%は%CALLNAME:MASTER%の頭を抱え、未だ母乳の滴る乳首を%CALLNAME:MASTER%の口に押し込み嬉しそうに搾り始めた…
			ELSE
				PRINTFORML %CALLNAME:TARGET%はジョッキ一杯の濃厚な母乳を笑顔で差し出した
			ENDIF
		ENDIF
		PRINTFORML %PALAMNAME:4%の珠＋{LOCAL*60}
		PRINTFORML %PALAMNAME:7%の珠＋{LOCAL*20}
		PRINTFORMW %PALAMNAME:14%の珠＋{LOCAL*50}
		JUEL:欲情 += LOCAL*60
		JUEL:習得 += LOCAL*20
		JUEL:快Ｂ += LOCAL*50
		PRINTFORML %CALLNAME:MASTER%は%CALLNAME:TARGET%が搾り出した{LOCAL*100*(TALENT:巨乳 + 1)}ccの母乳の半分を瓶詰めし、売りに出した
		;出した人のプロフィールが見れる安心品質
		PRINTFORML %CALLNAME:TARGET%の母乳1番搾りは%MONEY_C(LOCAL*250*(TALENT:巨乳 + 1) )%で売れた
		PRINTFORML 所持金+%MONEY_C(LOCAL*250*(TALENT:巨乳 + 1) )%
		MONEY += LOCAL*250*(TALENT:巨乳 + 1)
	ELSE
		EXP:噴乳経験 += LOCAL
		PRINTFORML %EXPNAME:54%＋{LOCAL}
		PRINTFORML %CALLNAME:TARGET%は搾りたての母乳をコップに入れて手渡してくれた
		PRINTFORML %PALAMNAME:4%の珠＋{LOCAL*60}
		PRINTFORML %PALAMNAME:7%の珠＋{LOCAL*20}
		PRINTFORML %PALAMNAME:14%の珠＋{LOCAL*50}
		JUEL:欲情 += LOCAL*60
		JUEL:習得 += LOCAL*20
		JUEL:快Ｂ += LOCAL*50
	ENDIF
CASE "朝の新妻プレイ"
	LOCAL = MAX(LOCAL + (ABL:奉仕精神 + ABL:露出癖)/3, 1)
	PRINTFORML 朝起きると、キッチンからいい匂いが漂ってきた
	PRINTFORML そこに行くと%CALLNAME:TARGET%が裸エプロンで朝食を作っていた…
	PRINTFORML ―――衝動的に後ろから襲い掛かった

	CALL SELF_KOJO, LOCALS
	CALL SIMULATE_TRAIN, 61, LOCAL

	PRINTFORML 思わず朝食前に%CALLNAME:TARGET%を{LOCAL}回ほどいただいてしまった…
	SIF TALENT:恥じらい
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%の行為に文句を言ったりしているが、明らかに嬉しそうだ…
	CALL CALC, "欲求不満解消", TARGET, LOCAL
CASE "朝パイズリ"
	LOCAL = MAX(LOCAL + (ABL:奉仕精神 + ABL:精液中毒)/3, 1)
		PRINTFORML 朝、目が覚めると同時に下腹部に違和感があるのに気づいた
	;貧乳の場合
	IF TALENT:貧乳
		PRINTFORML 気づかれないように目をやると、%CALLNAME:TARGET%が胸でペニスを弄っていた
		PRINTFORML どうやらパイズリの練習をしているらしい……
	;巨乳の場合
	ELSEIF TALENT:巨乳
		PRINTFORML 目をやると%CALLNAME:TARGET%が大きな胸でペニスを挟み、擦り上げている
	;それ以外
	ELSE
		PRINTFORML 目をやると%CALLNAME:TARGET%が胸でペニスを擦り上げ、先端から溢れる液体を吸っている
	ENDIF

	CALL SELF_KOJO, LOCALS
	CALL SIMULATE_TRAIN, 92, LOCAL
	IF EXP:精液経験 > MEMO_EXP:20
		PRINTFORML %CALLNAME:TARGET%は精液の貼りついた顔で朝の挨拶をしてきた
	ELSE
		PRINTFORML %CALLNAME:TARGET%は股間に顔を埋めたままの姿勢で朝の挨拶をしてきた
	ENDIF
	CALL CALC, "欲求不満解消", TARGET, 1
CASE "朝騎乗位"
	LOCAL = MAX(LOCAL + (ABL:Ｖ感覚 + ABL:精液中毒)/3, 1)

	PRINTFORML 朝起きると腰の辺りに重みを感じた
	PRINTFORML 目をやると%CALLNAME:TARGET%が上に跨って喘いでいる……
	CALL SELF_KOJO, LOCALS
	IF TARGET == ASSI
		IF RAND:2
			PRINTFORMW %CALLNAME:MASTER%が目を覚ましたことを確認すると、%CALLNAME:TARGET%は緩々と腰を動かし、嬌声を挿みながら、今日のスケジュールを報告した…
		ELSE
			PRINTFORMW すっかり目が覚めた%CALLNAME:MASTER%は、%CALLNAME:TARGET%の腰を掴んで突き上げながら、今日のスケジュールを喘ぎ声まじりで報告させた…
		ENDIF
		LOCAL += 1
	ENDIF
	CALL SIMULATE_TRAIN, 65, LOCAL
	IF EXP:MASTER:射精経験 > MEMO_EXP:3
		IF TARGET != ASSI
			PRINTFORML %CALLNAME:TARGET%は膣内で射精を受け止めながら、朝の挨拶をしてきた
		ELSE
			PRINTFORML 騎乗位で%CALLNAME:TARGET%の膣内に夢精しながら目を覚ましました
		ENDIF
	ELSE
		PRINTFORML 騎乗位で目を覚ましました
	ENDIF
	CALL CALC, "欲求不満解消", TARGET, LOCAL
CASE "朝キス"
	LOCAL = LIMIT(LOCAL/2 + (ABL:従順 + ABL:奉仕精神)/5, 1, 1 + TALENT:相思相愛 + TALENT:蕩唇)

	;奉仕レベルが高いと、先に起きている確率が高い
	IF ABL:奉仕精神 >= 3 && RANDF(2)
		PRINTFORML 朝、%CALLNAME:TARGET%の\@ LOCAL >= 3 ? 濃厚な # \@口付けで目を覚ました
	ELSE
		IF AGE(TARGET) == 1 && RAND:2
			PRINTFORML 朝、%CALLNAME:MASTER%は突然の揺れと身体にかかる重みで目を覚ました
			PRINTFORMW どうやら、%CALLNAME:TARGET%が胸に跨ってユサユサとしていたようだ…
			PRINTFORMW %CALLNAME:MASTER%の目覚めに気付いた%CALLNAME:TARGET%は、顔を近づけておはようのキスをしてきた
		ELSEIF TALENT:巨乳 && RAND:2
			PRINTFORML 朝、目を覚ますと枕が妙に柔らかくて気持ち良い……
			IF TALENT:恥薄い && RAND:3 == 0
				PRINT 全裸で
			ELSEIF RAND:2
				PRINT 下着姿で
			ENDIF
			PRINTFORMW ベッドに潜り込んだ%CALLNAME:TARGET%が、%CALLNAME:MASTER%の頭を胸に抱き抱えているようだ
			PRINTFORMW %CALLNAME:MASTER%の目覚めに気付いた%CALLNAME:TARGET%は、顔を近づけておはようのキスをしてきた
		ELSE
			PRINTFORML 朝、目を覚ますと身体が重い……
			PRINTFORMW いつの間にかベッドに潜り込んだ%CALLNAME:TARGET%が、%CALLNAME:MASTER%を抱き枕にしているようだ
			IF TALENT:相思相愛 && RAND:2
				PRINTFORML %CALLNAME:MASTER%が愛おしさに耐え切れずに眠っている%CALLNAME:TARGET%に口付けをすると、
				PRINTFORML %CALLNAME:TARGET%は夢見心地のままで%CALLNAME:MASTER%の頬を捕まえて、深い深いキスを返してきた…
			ELSE
				PRINTFORMW %CALLNAME:MASTER%の目覚めに気付いた%CALLNAME:TARGET%は、顔を近づけておはようのキスをしてきた
			ENDIF
		ENDIF
	ENDIF

	CALL SELF_KOJO, LOCALS

	CALL SIMULATE_TRAIN, 6, LOCAL

	IF ABL:奉仕精神 >= 3 && RANDF(2)
		PRINTFORM %CALLNAME:TARGET%は
		IF RAND:4 && TALENT:恥じらい && TALENT:素直 + TALENT:相思相愛 == 0
			PRINTFORM 顔を朱に染めながら
		ELSE
			PRINTFORM %CALLNAME:MASTER%に抱きつ\@ TALENT:好色 ? いて、股間を撫でさすり # き \@ながら、
		ENDIF
		PRINTFORML 朝の挨拶をしてきた
	ENDIF
	CALL CALC, "欲求不満解消", TARGET, 1
CASE "朝フェラ"
	LOCAL = MAX(LOCAL + (ABL:奉仕精神 + ABL:精液中毒)/3, 1)

	PRINTFORML 朝、目が覚めると妙に下腹部が気持ち良い…
	CALL COM_ABLE99
	IF RESULT && COND("脚の使用") && ABL:欲望 >= 3 && ABL:奉仕精神 >= 3 && CLO_ORI(TARGET,"アウター") == 0 && RAND:2
		PRINTFORM 視線をそちらに向けると…大股開きの体勢で身体に跨る%CALLNAME:TARGET%の
		IF (TALENT:淫乱 && ABL:露出癖 >= 5 && RAND:2) || CLO_ORI(TARGET,"スーツ","下着") == 0 || COND("身嗜み：ノーパン")
			IF RAND:3 == 0
				PRINT 今にも愛液が垂れそうなアソコ
			ELSEIF RAND:2
				PRINT すっかり発情したアソコ
			ELSE
				PRINT 剥き出しのアソコ
			ENDIF
		ELSEIF CLO_ORI(TARGET,"スーツ")
			IF (ABL:欲望 >= 4 || TALENT:濡れやすい) && RAND:2
				PRINTFORM 染みの浮いたパンツ
			ELSEIF AGE(TARGET) >= 3
				PRINTFORM 股間に食い込むパンツ
			ELSE
				PRINTFORM 可愛らしいパンツ
			ENDIF
		ELSE
			IF (ABL:欲望 >= 4 || TALENT:濡れやすい) && RAND:2
				PRINTFORM 染みの浮いた
			ELSEIF AGE(TARGET) >= 3
				PRINTFORM 股間に食い込む
			ELSE
				PRINTFORM 可愛らしい
			ENDIF
			IF EQUIP:パンツ
				PRINTFORM %PANTIES(TARGET, 1)%
			ELSE
				PRINTFORM パンツ
			ENDIF
		ENDIF
		PRINTL が目に入った
		PRINTFORML どうやら、%CALLNAME:TARGET%が寝ている%CALLNAME:MASTER%の顔に股間を擦りつけながらフェラチオをしていたようだ…
		SELECTCOM = 99
	ELSE
		PRINTFORML どうやら、%CALLNAME:TARGET%が寝ている%CALLNAME:MASTER%にフェラチオをしていたようだ…
		SELECTCOM = 91
	ENDIF

	;シックスナインかで口上分岐させるなら、SELECTCOM == 99でシックスナインの時となる
	CALL SELF_KOJO, LOCALS

	CALL SIMULATE_TRAIN, SELECTCOM, LOCAL

	;お掃除フェラ
	IF ABL:奉仕精神 + TALENT:献身 - (COND("包茎", MASTER) > 0) >= 3
		;ＭＡＳＴＥＲが包茎
		IF COND("包茎", MASTER) && ABL:奉仕精神 + TALENT:献身 >= 4 && RAND:3
			PRINTFORM %CALLNAME:TARGET%は%CALLNAME:MASTER%の
			IF COND("真性包茎", MASTER)
				PRINTFORML 亀頭を包む皮の中に舌を差し入れ、内部に溜まった恥垢を舌で掻き出しながら朝の挨拶をしてきた
			ELSE
				PRINTFORML 包茎ペニスの皮をぐっと根元の方に寄せて亀頭を剥き出しにし、寝ている間に溜まった恥垢を舌でお掃除しながら朝の挨拶をしてきた
			ENDIF
		ELSE
			IF EXP:精液経験 > MEMO_EXP:20
				PRINTFORML %CALLNAME:TARGET%は精液を\@ ABL:精液中毒 >= 3 ? 美味しそう # 零さず \@に飲み込むと、口でお掃除しながら朝の挨拶をしてきた
			ELSE
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%のペニスを口でお掃除しながら朝の挨拶をしてきた
			ENDIF
		ENDIF
		PRINTFORML お掃除フェラ経験＋１
		EXP:お掃除フェラ経験 += 1
	ELSEIF (TALENT:好色 || ABL:奉仕精神 >= 5) && COND("脚の使用") && CLO_ORI(TARGET,"下着") && COND("身嗜み：ノーパン") == 0 && RAND:3 == 0
		PRINTFORM %CALLNAME:TARGET%は体温の残るパンツで%CALLNAME:MASTER%のペニスを拭き清め
		IF RAND:2
			PRINTFORML ながら朝の挨拶をしてきた
		ELSE
			PRINTFORML て、目の前で汚れたそれをはきながら朝の挨拶をしてきた
		ENDIF
	ELSEIF SELECTCOM == 99 && ABL:サドっ気 && RAND:2
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%の顔にふにゅっと股間を下ろして、肩越しに朝の挨拶をしてきた
	ELSEIF EXP:精液経験 > MEMO_EXP:20 && RAND:3
		IF ABL:サドっ気 >= 3 && RAND:2
			PRINTFORML %CALLNAME:TARGET%は精液を味わいながら飲み込むと、唇をぺろりと舐めて朝の挨拶をしてきた
		ELSE
			PRINTFORML %CALLNAME:TARGET%は精液の貼りついた顔で朝の挨拶をしてきた
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:TARGET%は股間に顔を埋め
		IF COND("指の使用") && RAND:4 == 0
			PRINTFORM て指のぬめりを舐めとりながら、笑顔で
		ELSEIF RAND:3 == 0
			PRINTFORM て、ペニスを舐めしゃぶりながら
		ELSEIF RAND:2
			PRINTFORM て口をもごもごさせながら、くぐもった声で
		ELSE
			PRINTFORM たままの姿勢で
		ENDIF
		PRINTFORML 朝の挨拶をしてきた
	ENDIF
	CALL CALC, "欲求不満解消", TARGET, 1
ENDSELECT

;朝イベントの記録
IF LOCALS != "" && LOCAL
	CFLAG:PLAYER:90 = NUM_EV(LOCALS)
	CFLAG:90 = NUM_EV(LOCALS)
	WAIT
ENDIF


@PLAY_AT_MORNING
#DIM LCOUNT

STR:100 = 
;反発刻印が3あるとダメ
SIF MARK:反発刻印 >= 3
	RETURN 0

VARSET LOCALS

CALL COM_ABLE6
SIF RESULT && TALENT:恋慕 && ABL:従順 >= 3 && CHECK_COM("キス")
	LOCALS:0 = 朝キス
SIF TALENT:恋慕 && TALENT:母乳体質 && ABL:従順 >= 3 && ABL:奉仕精神 >= 3 && EXP:噴乳経験 >= 20
	LOCALS:1 = 朝の母乳搾り
CALL COM_ABLE95
SIF RESULT && ABL:サドっ気 >= 3
	LOCALS:2 = 朝足コキ
CALL COM_ABLE98
SIF RESULT && (ABL:レズっ気 >= 3 || TALENT:オトコ) && TALENT:MASTER:オトコ == 0
	LOCALS:3 = 朝クンニ
CALL COM_ABLE120
SIF RESULT && PENIS(TARGET) && TALENT:童貞 == 0 && EXP:Ｖ挿入経験 >= 20 && ABL:欲望 >= 3 && TALENT:MASTER:処女 == 0 && TALENT:MASTER:妊娠 == 0
	LOCALS:4 = 朝逆正常位

CALL COM_ABLE91
SIF RESULT && PENIS(MASTER) && ABL:欲望 >= 2 && ABL:奉仕精神 >= 2 && ABL:精液中毒 >= 1
	LOCALS:5 = 朝フェラ
CALL COM_ABLE65
SIF RESULT && PENIS(MASTER) && ABL:欲望 >= 4 && ABL:Ｖ感覚 >= 3 && TALENT:処女 + TALENT:オトコ + TALENT:PLAYER:童貞 == 0
	LOCALS:6 = 朝騎乗位
CALL COM_ABLE92
SIF RESULT && PENIS(MASTER) && ABL:欲望 >= 4 && ABL:奉仕精神 >= 4 && TALENT:オトコ == 0
	LOCALS:7 = 朝パイズリ
CALL COM_ABLE61
SIF RESULT && PENIS(MASTER) && ITEM:エプロン && TALENT:恋慕 && TALENT:処女 + TALENT:オトコ + TALENT:PLAYER:童貞 == 0
	LOCALS:8 = 朝の新妻プレイ

LOCAL = 1
FOR LCOUNT, 0, 10
	SIF LOCALS:LCOUNT == ""
		CONTINUE
	SIF RAND:LOCAL == 0
		STR:100 = %LOCALS:LCOUNT%

	LOCAL += 1
NEXT

RETURN 0

;-------------------------------------------------
;拘束されて弄ばれるイベント
;欲求不満イベントに変更してみる
;-------------------------------------------------
@BIND_AT_MORNING
#DIM LCOUNT
VARSET LOCAL
VARSET LOCALS
;そもそもこの種類のイベントが起きるかのチェック
CALL CHECK_BIND_AT_MORNING, TARGET, ""
LOCALS = %STR:100%
;TARGETがプレイの条件を満たしているか？
SIF LOCALS == ""
	RETURN 0

;複数キャラを使うイベント用の対象フラグリセット
VARSET TARGET, 0, 1, 4

;仲間を呼ぶｗ
FOR LOCAL, 2, CHARANUM
	SIF LOCAL == TARGET || LOCAL == MASTER
		CONTINUE

	CALL CHECK_BIND_AT_MORNING, LOCAL, LOCALS
	LOCALS:9 = %STR:100%

	;選択されているプレイを許容できるなら欲求不満値を記録して、参加候補入り（ＭＡＸ二人）
	IF LOCALS:9 == LOCALS
		LOCAL:99 += 1
		IF TARGET:1 == 0
			TARGET:1 = LOCAL
		ELSEIF TARGET:2 == 0
			TARGET:2 = LOCAL
		ELSEIF RAND:(LOCAL:99) <= 1
			TARGET:(RAND:2 + 1) = LOCAL
		ENDIF
	ENDIF
NEXT

LOCALS:1 = %CALLNAME:TARGET%
LOCALS:2 = %CALLNAME:TARGET%
LOCAL = ABL:欲望 + RAND:(ABL:サドっ気 + 5) + TALENT:淫乱*2 + TALENT:恋慕

IF TARGET:1 > 0
	LOCALS:1 = %CALLNAME:TARGET%と%CALLNAME:(TARGET:1)%
	LOCALS:2 = %CALLNAME:TARGET%達
	LOCALS:3 = 二人
	LOCAL:1 = ABL:(TARGET:1):欲望 + RAND:(ABL:(TARGET:1):サドっ気 + 1) + TALENT:(TARGET:1):淫乱*2 + TALENT:(TARGET:1):恋慕
ENDIF
IF TARGET:2 > 0
	LOCALS:1 = %CALLNAME:TARGET%と%CALLNAME:(TARGET:1)%と%CALLNAME:(TARGET:2)%
	LOCALS:2 = %CALLNAME:TARGET%達
	LOCALS:3 = ３人
	LOCAL:2 = ABL:(TARGET:2):欲望 + RAND:(ABL:(TARGET:2):サドっ気 + 1) + TALENT:(TARGET:2):淫乱*2 + TALENT:(TARGET:2):恋慕
ENDIF

LOCAL:3 = 1 + (TARGET:1 > 0) + (TARGET:2 > 0)
DRAWLINE

CVARSET TEQUIP, 30, 1
LOCAL:50 = ABL:MASTER:技巧
ABL:MASTER:技巧 = 2

CALL SIMULATE_TRAIN_FIRST, LOCAL:3

SELECTCASE LOCALS
CASE "朝ＳＭ"
	PRINTFORMW 朝、目が覚めると身体が動かない……
	PRINTFORMW 何時の間にか裸でベッドに縛り付けられている
	PRINTFORMW こちらを見下ろしている%LOCALS:1%が、嗜虐と情欲に濁った眼差しで挨拶してきた……
	CALL SELF_KOJO, LOCALS
	PRINTL 
	PRINTFORMW そのまま夜になるまで%LOCALS:2%に弄ばれた……
	FOR LCOUNT, 0, LOCAL:3
		SWAP TARGET, TARGET:LCOUNT
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 1, MAX(LOCAL:LCOUNT/(2 + RAND:4), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 6, MAX(LOCAL:LCOUNT/(3 + RAND:4), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 9, MAX(LOCAL:LCOUNT/(2 + RAND:4), 1)
		IF RAND:2
			CALL COM_ABLE65
			SIF RESULT && TALENT:処女 + TALENT:MASTER:童貞 == 0 && ABL:従順+ABL:欲望+ABL:Ｖ感覚+ABL:奉仕精神 > 14 && PENIS(MASTER)
				CALL SIMULATE_TRAIN_COM, 65, MAX(LOCAL:LCOUNT/(5 + RAND:5), 1 + RAND:3)
		ENDIF
		IF RAND:3 == 0
			CALL COM_ABLE75
			SIF RESULT && TALENT:Ａ処女 == 0 && EXP:PLAYER:Ａ挿入経験 && ABL:Ａ感覚 >= MAX(3, ABL:Ｖ感覚) && ABL:従順+ABL:欲望+ABL:Ａ感覚+ABL:奉仕精神 > 14 && PENIS(MASTER)
				CALL SIMULATE_TRAIN_COM, 75, MAX(LOCAL:LCOUNT/(5 + RAND:5), 1 + RAND:3)
		ENDIF
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 90, LOCAL:LCOUNT/(3 + RAND:3)
		CALL SIMULATE_TRAIN_COM, 91, LOCAL:LCOUNT/(4 + RAND:3)
		SIF RAND:2 && TALENT:巨乳
			CALL SIMULATE_TRAIN_COM, 92, MAX(LOCAL:LCOUNT/(4 + RAND:3), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 95, LOCAL:LCOUNT/(3 + RAND:3)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 96, LOCAL:LCOUNT/(4 + RAND:3)
		SWAP TARGET, TARGET:LCOUNT
	NEXT
CASE "朝拘束騎乗位"
	PRINTFORMW 朝、目が覚めると身体が重い……\@ TARGET:1 > 0 ? だけでなく、呼吸も苦しい… # \@
	IF TARGET:1
		PRINTFORM …状況の把握に時間がかかったが、どうやら%CALLNAME:(TARGET:1)%が顔に
		SIF TARGET:2
			PRINTFORM 、%CALLNAME:(TARGET:2)%が腕に、
		PRINTFORMW 股間を夢中で擦りつけているようだ
		PRINTFORMW その上、%CALLNAME:TARGET%が騎乗位で楽しんでいるらしく、股間は色々な液体で凄い有様だ…
		PRINTL 
		PRINTFORMW 目が覚めた%CALLNAME:MASTER%に気づいた%LOCALS:3%の嗜虐と情欲に濁った眼差しを見るに、当分解放してはもらえなさそうだ…
	ELSE
		PRINTFORMW どうやら、何時の間にか裸でベッドに縛り付けられているようだ…
		PRINTFORMW 重さのかかっている場所を見遣ると、腰に跨っている%CALLNAME:TARGET%と目が合ってしまった
		PRINTFORMW 結構な時間楽しんだ後の様で、その結合部は泡立った液体がブクブクと垂れ、ドロドロになっている…
		PRINTL 
		PRINTFORMW %CALLNAME:TARGET%の嗜虐と情欲に濁った眼差しを見るに、当分解放してはもらえなさそうだ…
	ENDIF

	CALL SELF_KOJO, LOCALS
	PRINTL 
	PRINTFORMW そのまま夜になるまで%LOCALS:2%に絞られ続け、弄ばれた……
	FOR LCOUNT, 0, LOCAL:3
		SWAP TARGET, TARGET:LCOUNT
		CALL SIMULATE_TRAIN_COM, 1, LOCAL:LCOUNT/(2 + RAND:4)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 6, LOCAL:LCOUNT/(4 + RAND:4)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 9, LOCAL:LCOUNT/(2 + RAND:4)
		CALL SIMULATE_TRAIN_COM, 65, LOCAL:LCOUNT
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 91, LOCAL:LCOUNT/(3 + RAND:3)
		SIF RAND:2 && TALENT:巨乳
			CALL SIMULATE_TRAIN_COM, 92, LOCAL:LCOUNT/(4 + RAND:3)
		SIF RAND:4 == 0
			CALL SIMULATE_TRAIN_COM, 95, LOCAL:LCOUNT/(5 + RAND:3)
		SIF RAND:5 == 0
			CALL SIMULATE_TRAIN_COM, 96, LOCAL:LCOUNT/(5 + RAND:3)
		SWAP TARGET, TARGET:LCOUNT
	NEXT
CASE "朝拘束Ａ騎乗位"
	PRINTFORMW 朝、目が覚めると身体が重い……\@ TARGET:1 > 0 ? だけでなく、呼吸も苦しい… # \@
	IF TARGET:1
		PRINTFORM …状況の把握に時間がかかったが、どうやら%CALLNAME:(TARGET:1)%が顔に
		SIF TARGET:2
			PRINTFORM 、%CALLNAME:(TARGET:2)%が腕に、
		PRINTFORMW 股間を夢中で擦りつけているようだ
		PRINTFORMW その上、%CALLNAME:TARGET%がお尻の穴で楽しんでいるらしく、股間は色々な液体で凄い有様だ…
		PRINTL 
		PRINTFORMW 目が覚めた%CALLNAME:MASTER%に気づいた%LOCALS:3%の嗜虐と情欲に濁った眼差しを見るに、当分解放してはもらえなさそうだ…
	ELSE
		PRINTFORMW どうやら、何時の間にか裸でベッドに縛り付けられているようだ…
		PRINTFORMW 重さのかかっている場所を見遣ると、腰に跨ってアナルにペニスを咥え込んでいる%CALLNAME:TARGET%と目が合ってしまった
		PRINTFORMW 結構な時間楽しんだ後の様で、その結合部は泡立った液体がブクブクと垂れ、ドロドロになっている…
		PRINTL 
		PRINTFORMW %CALLNAME:TARGET%の嗜虐と情欲に濁った眼差しを見るに、当分解放してはもらえなさそうだ…
	ENDIF

	CALL SELF_KOJO, LOCALS
	PRINTL 
	PRINTFORMW そのまま夜になるまで%LOCALS:2%に絞られ続け、弄ばれた……
	FOR LCOUNT, 0, LOCAL:3
		SWAP TARGET, TARGET:LCOUNT
		CALL SIMULATE_TRAIN_COM, 1, MAX(LOCAL:LCOUNT/(2 + RAND:4), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 6, LOCAL:LCOUNT/(4 + RAND:4)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 9, LOCAL:LCOUNT/(2 + RAND:4)
		CALL SIMULATE_TRAIN_COM, 75, LOCAL:LCOUNT
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 91, LOCAL:LCOUNT/(3 + RAND:3)
		SIF RAND:2 && TALENT:巨乳
			CALL SIMULATE_TRAIN_COM, 92, LOCAL:LCOUNT/(4 + RAND:3)
		SIF RAND:4 == 0
			CALL SIMULATE_TRAIN_COM, 95, LOCAL:LCOUNT/(5 + RAND:3)
		SIF RAND:5 == 0
			CALL SIMULATE_TRAIN_COM, 96, LOCAL:LCOUNT/(5 + RAND:3)
		SWAP TARGET, TARGET:LCOUNT
	NEXT
;今のところこの下は無し
CASE "朝拘束逆レイプ"
	PRINTFORMW 朝、目が覚めると身体が動かない……
	PRINTFORMW 何時の間にか裸でベッドに縛り付けられている
	PRINTFORMW こちらを見下ろしている%LOCALS:1%が、嗜虐と情欲に濁った眼差しで挨拶してきた……
	CALL SELF_KOJO, LOCALS
	PRINTFORMW そのまま夜になるまで%LOCALS:2%に弄ばれた……
CASE "朝拘束逆アナル"
	PRINTFORMW 朝、目が覚めると身体が動かない……
	PRINTFORMW 何時の間にか裸でベッドに縛り付けられている
	PRINTFORMW こちらを見下ろしている%LOCALS:1%が、嗜虐と情欲に濁った眼差しで挨拶してきた……
	CALL SELF_KOJO, LOCALS
	PRINTFORMW そのまま夜になるまで%LOCALS:2%に弄ばれた……
ENDSELECT

CALL SIMULATE_TRAIN_CALC
CALL SIMULATE_TRAIN_SHOW

WAIT

;朝の拘束イベントの記録
CFLAG:MASTER:90 = NUM_EV(LOCALS)
CFLAG:90 = NUM_EV(LOCALS)
CALL CALC, "欲求不満解消", TARGET, LOCAL + LOCAL:1 + LOCAL:2
IF TARGET:1 > 0
	CFLAG:(TARGET:1):90 = NUM_EV(LOCALS)
	CALL CALC, "欲求不満解消", TARGET:1, LOCAL + LOCAL:1 + LOCAL:2
	;仲良くなる
	CALL CHANGE_RELATION, 5, TARGET, TARGET:1, TARGET:2
ENDIF
IF TARGET:2 > 0
	CFLAG:(TARGET:2):90 = NUM_EV(LOCALS)
	CALL CALC, "欲求不満解消", TARGET:2, LOCAL + LOCAL:1 + LOCAL:2
ENDIF

CVARSET TEQUIP, 30, 0

ABL:MASTER:技巧 = LOCAL:50
RETURN 1


@CHECK_BIND_AT_MORNING, ARG, ARGS
#DIM LCOUNT

STR:100 = 
;欲求不満度が低いor欲望が3LV未満、好感度が500未満、反発刻印あり
SIF COND("欲求不満", ARG) == 0 || ABL:ARG:欲望 < 3 || CFLAG:ARG:2 < 500 || MARK:ARG:反発刻印 > 0
	RETURN 0

VARSET LOCALS

;サドorサドっ気5LVならＳＭコース
IF TALENT:ARG:サド || ABL:ARG:サドっ気 >= 5
	IF ARGS == "朝ＳＭ"
		STR:100 = 朝ＳＭ
		RETURN 0
	ENDIF
	LOCALS:0 = 朝ＳＭ
ENDIF

CALL COM_ABLE65
IF RESULT && PENIS(MASTER) && TALENT:ARG:処女 + TALENT:MASTER:童貞 == 0 && ABL:ARG:Ｖ感覚 >= 3 && EXP:ARG:Ｖ性交経験 >= 10 && EXP:ARG:腰使い経験 >= 20
	IF ARGS == "朝拘束騎乗位"
		STR:100 = 朝拘束騎乗位
		RETURN 0
	ENDIF
	LOCALS:1 = 朝拘束騎乗位
ENDIF

CALL COM_ABLE75
IF RESULT && PENIS(MASTER) && EXP:MASTER:Ａ挿入経験 && COND("ベテラン：Ａセックス")
	IF ARGS == "朝拘束Ａ騎乗位"
		STR:100 = 朝拘束Ａ騎乗位
		RETURN 0
	ENDIF
	LOCALS:2 = 朝拘束Ａ騎乗位
ENDIF

;欲求不満
SIF COND("欲求不満") && (LOCALS:1 == "朝拘束騎乗位" || LOCALS:2 == "朝拘束Ａ騎乗位")
	LOCALS:0 = 

LOCAL = 1
FOR LCOUNT, 0, 10
	SIF LOCALS:LCOUNT == ""
		CONTINUE
	SIF RAND:LOCAL == 0
		STR:100 = %LOCALS:LCOUNT%

	LOCAL += 1
NEXT

;今のところここまで
RETURN 0

