@COM_ARCANA, ARG
;アルカナの詠唱

CALL PRINT_TRAIN_NAME(ARG)

CALL MAGIC_CASTER_SELECT, ARG

SIF REFUSE_CHECK()
	RETURN 0

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

CALL KOJO_MESSAGE_COM
;Ｗ押し倒し等での口上処理
CALL KOJO_MESSAGE_COM_MULTI

CALL SETFLAG, "今回調教コマンド"

;-------------------------------------------------
;アルカナのイベント一括処理
;ARG=アルカナ番号、ARG:1=詠唱者、ARG:2=対象
;調教コマンドで呼ばれた際には@MAGIC_CASTER_SELECTを経るのでARG:1とARG:2は自動的に決定される(TCVAR:22とTCVAR:23から逆算)が、他の場所で呼ぶ際にはARG:1とARG:2は必ず指定すること
;テキスト出力にはPRINT_STRを用いる
;-------------------------------------------------
@TRAIN_MESSAGE_ARCANA, ARG, ARG:1, ARG:2
#DIM LCOUNT
;詠唱者
#DIM CASTER
;対象
#DIM MAGIC_TARGET
;ペニスサイズの変化をみる
#DIM MEMO_PSIZE
;出力するテキスト
#DIMS MESSAGE
#DIMS MESSAGE_RESIST

;アルカナ詠唱者フラグ（ARGはアルカナ番号）
IF ARG:1 > 0
	TCVAR:(ARG:1):23 = ARG
ELSE
	ARG:1 = FINDCHARA(TCVAR:23, ARG)
ENDIF
;アルカナ対象フラグ（ARGはアルカナ番号）
IF ARG:2 > 0
	TCVAR:(ARG:2):22 = ARG
ELSE
	ARG:2 = FINDCHARA(TCVAR:22, ARG)
ENDIF

;括弧を多用するのを避けるために代入
CASTER = ARG:1
MAGIC_TARGET = ARG:2


MESSAGE =
MESSAGE_RESIST =

SELECTCASE ARG
;アルカナ
CASE 230 TO 259
	MESSAGE = %MESSAGE%＜%TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)%＞_L_
CASE 312
	MESSAGE = %MESSAGE%＜具現＞_
ENDSELECT

SELECTCASE ARG
;バインド
CASE 240
	SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
	CASE "トゥレイルルート"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%は地面に根を這わせた！_L_%CALLNAME:MAGIC_TARGET%のカラダが根に捉えられる！_W_
	CASE "シッポ巻きつき"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%は尻尾を%CALLNAME:MAGIC_TARGET%の全身に巻きつけてきた！_W_
	CASE "クィーンズボンデージ"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%は%CALLNAME:MAGIC_TARGET%にムチを絡ませた！_W_
	CASE "ワインディングウェッブ"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%は%CALLNAME:MAGIC_TARGET%に糸を絡み付けてきた！_W_
	CASE "パワーバインド"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%は力ずくで%CALLNAME:MAGIC_TARGET%を取り押さえた！_L_%CALLNAME:MAGIC_TARGET%の体が紐で拘束されていく！_W_
	CASE "ペトラアイズ"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%は妖しい視線で%CALLNAME:MAGIC_TARGET%を睨みつけた！_W_
	CASE "バインドコード"
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%は無数のコードを放った！_L_%CALLNAME:MAGIC_TARGET%の体が拘束されていく！_W_
	CASE "ワンダートリップ"
		MESSAGE = %MESSAGE%「吹・ｖ黴・ゆ・ｯり等啓ｍ・凍・麾槙・・A金-黴・痘u・hｕ操÷逐枕装・br>哩循魔…！」_W_
		MESSAGE = %MESSAGE%不思議な声で%CALLNAME:CASTER%はおたけびをあげた！_L_目の前の景色が歪んでいく！_W_
	CASEELSE
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%は%TRAINNAME:ARG%を唱えた…_W_
	ENDSELECT

	SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
	CASE "束缚术", "ペトラアイズ"
		MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%には効かなかった！
	CASEELSE
		MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%は素早く振り払った！
	ENDSELECT
;ヴィジョン
CASE 243
	;TARGETが使った際にはＷ押し倒しへ
	SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
	CASE "分身"
		MESSAGE = %MESSAGE%「ふっ……」_L_%CALLNAME:CASTER%は分身した！_W_
		SIF CASTER == TARGET
			MESSAGE = %MESSAGE%二人がかりで%CALLNAME:MAGIC_TARGET%を翻弄しようとしてくる！_W_
		MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%は素早く振り払った！
	CASE "分裂"
		MESSAGE = %MESSAGE%「え～い、ぷるるん分身の術～～っ！」_L_なんと%CALLNAME:CASTER%は分裂した！_W_
		SIF CASTER == TARGET
			MESSAGE = %MESSAGE%二体の%CALLNAME:CASTER%がニコニコ笑顔で%CALLNAME:MAGIC_TARGET%に抱きついてくる！_W_
		MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%は素早く振り払った！
	CASEELSE
		MESSAGE = %MESSAGE%%CALLNAME:CASTER%は%TRAINNAME:ARG%を唱えた…_W_
		SIF CASTER == TARGET
			MESSAGE = %MESSAGE%作り出された虚像と共に%CALLNAME:CASTER%が襲い掛かってくる！_W_
		MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%には効かなかった！
	ENDSELECT

;アルカナ
CASE 230 TO 259
	MESSAGE = %MESSAGE%%CALLNAME:CASTER%は%TRAINNAME:ARG%を唱えた…_W_
	MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%には効かなかった！
CASEELSE
	MESSAGE = %MESSAGE%%CALLNAME:CASTER%は%TRAINNAME:ARG%の能力を使った…_W_
	MESSAGE_RESIST = %CALLNAME:MAGIC_TARGET%には効かなかった！
ENDSELECT

IF CONDS("カウンター", CASTER) == "アルカナ"
	SELECTCASE ARG
	CASE 230, 231, 240, 243
		CALL PRINT_STR, MESSAGE
		MESSAGE =
		PRINTFORML 抵抗を試みますか？
		PRINTFORML [ 0] はい
		PRINTFORML [ 1] いいえ
		IF CONFIG("制限時間撤廃")
			CALL INPUT_SELECT, 2
			IF RESULT == 0
				PRINTFORML ＜レジスト成功！＞
				PRINTFORMW %MESSAGE_RESIST%
				MESSAGE_RESIST = レジスト
			ENDIF
		ELSE
			TINPUT MAX(4000 + 200*(ABL:MAGIC_TARGET:ＬＶ - ABL:CASTER:ＬＶ), 2000), 1, 1
			IF RESULT == 0 && LIMIT(70 + 3*(ABL:MAGIC_TARGET:ＬＶ - ABL:CASTER:ＬＶ), 50, 95) > RAND:100
				PRINTFORML ＜レジスト成功！＞
				PRINTFORMW %MESSAGE_RESIST%
				MESSAGE_RESIST = レジスト
			ELSEIF RESULT == 0
				PRINTFORML ＜レジスト失敗＞
			ENDIF
		ENDIF
	ENDSELECT
ENDIF

IF MESSAGE_RESIST != "レジスト"
	SELECTCASE ARG
	CASE 230
		IF STATE("大根术", MAGIC_TARGET)
			IF TALENT:MAGIC_TARGET:大根术常态化 == 0
				MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%のペニスの大きさが元に戻った！_W_
				CALL REMOVE_STATE, MAGIC_TARGET, "大根术"
			ENDIF
		ELSE
			MESSAGE = %MESSAGE%なんと%CALLNAME:MAGIC_TARGET%のペニスが小さくなってしまった！_W_
			CALL ADD_STATE, MAGIC_TARGET, "小根术"
		ENDIF
	CASE 231
		MESSAGE = %MESSAGE%なんと%CALLNAME:MAGIC_TARGET%の身体は敏感になってしまった！_W_
		CALL ADD_STATE, MAGIC_TARGET, "高敏术"
	CASE 232
		;ペニスサイズの記憶
		MEMO_PSIZE = SIZE("ペニス", MAGIC_TARGET)
		CALL ADD_STATE, MAGIC_TARGET, "精强术"
		IF PENIS(MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の精力が極限まで強められ、硬度を増したペニスが天を衝く！_W_
			;ペニスもでかくなった！
			SIF SIZE("ペニス", MAGIC_TARGET) > MEMO_PSIZE
				MESSAGE = %MESSAGE%その大きさは先程までとは比べ物にならない！！_W_
		ELSE
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の精力が極限まで強められる！_W_
		ENDIF
	CASE 233
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の魅力が高まる！_W_
		CALL ADD_STATE, MAGIC_TARGET, "魅强术"
	CASE 234
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の精神力が極限まで強められる！_W_
		CALL ADD_STATE, MAGIC_TARGET, "防强术"
	CASE 235
		SIF BASE:MASTER:STOCK < MAXBASE:MASTER:STOCK
			MESSAGE = %MESSAGE%%CALLNAME:MASTER%のストックが１つ回復した！_W_

		SELECTCASE MAXBASE:MASTER:精力 - BASE:MASTER:精力
		CASE IS >= 20
			MESSAGE = %MESSAGE%%CALLNAME:MASTER%の精力が20回復した！_W_
		CASE IS != 0
			MESSAGE = %MESSAGE%%CALLNAME:MASTER%の精力が全快した！_W_
		ENDSELECT
		CALL CALC, "精力回復", 20, MASTER
		BASE:MASTER:STOCK = MIN(BASE:MASTER:STOCK + 1, MAXBASE:MASTER:STOCK)
	CASE 236
		SELECTCASE MAXBASE:体力 - BASE:体力
		;回復するまでもない
		CASE IS <= 0
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%には効果がなかった！_W_
		CASE IS <= 300
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の体力が全回復した！_W_
			CALL CALC, "体力回復", 300, MAGIC_TARGET
		CASEELSE
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の体力が300回復した！_W_
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の体力+300 ({BASE:MAGIC_TARGET:体力} → {BASE:MAGIC_TARGET:体力 + 300} /{MAXBASE:MAGIC_TARGET:体力})_W_
		ENDSELECT
		CALL CALC, "体力回復", 300, MAGIC_TARGET
	CASE 237
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は連続行動ができるようになった！_W_
		CALL ADD_STATE, MAGIC_TARGET, "倍化术"
	CASE 238
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の高められた能力が元に戻った！_W_
		CALL REMOVE_STATE, MAGIC_TARGET, "グン"
	CASE 239
		SIF STATE("束缚术", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の体の拘束が解けた！_W_
		SIF TALENT:MAGIC_TARGET:小根术常态化 == 0 && STATE("小根术", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%のペニスが元のサイズに戻った！_W_
		SIF TALENT:MAGIC_TARGET:高敏术常态化 == 0 && STATE("高敏术", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%の感じやすくなった神経が鎮まった！_W_
		SIF STATE("快感术", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は幻覚から解放された！_W_
		IF STATE("恍惚", MAGIC_TARGET) || STATE("ドランク", MAGIC_TARGET) || STATE("悪酔い", MAGIC_TARGET) || STATE("ふにゃふにゃ", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は我を取り戻した！_W_
			;MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は押し倒され状態から復帰した！_W_
		ENDIF
		SIF STATE("フィーブル", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は本来の実力を取り戻した！_W_
		CALL REMOVE_STATE, MAGIC_TARGET, "バッド"
	CASE 240
		IF CONDS("カウンター", CASTER) == "アルカナ"
			SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
			CASE "ワンダートリップ"
				MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は体の自由が利かなくなってしまった！_W_
				CALL ADD_STATE, MAGIC_TARGET, "バインド：手"
				CALL ADD_STATE, MAGIC_TARGET, "バインド：脚"
			CASEELSE
				IF COND("指の使用", MAGIC_TARGET) && STATE("バインド：手", MAGIC_TARGET) == 0
					MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は両手を封じられた！_W_
					CALL ADD_STATE, MAGIC_TARGET, "バインド：手"
				ENDIF
				IF COND("脚の使用", MAGIC_TARGET) && STATE("バインド：脚", MAGIC_TARGET) == 0
					MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は両足を封じられた！_W_
					CALL ADD_STATE, MAGIC_TARGET, "バインド：脚"
				ENDIF
				IF ABL:CASTER:施虐倾向 && ABL:CASTER:欲望 >= 3 && PENIS(MAGIC_TARGET) && STATE("バインド：Ｐ", MAGIC_TARGET) == 0 && TEQUIP:MAGIC_TARGET:31 == 0
					MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%はペニスを封じられた！_W_

					SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
					CASE "束缚术"
						MESSAGE = %MESSAGE%見えない締め付けが射精するのをせき止める！_W_
					CASE "ペトラアイズ"
						MESSAGE = %MESSAGE%見えない力が射精するのをせき止める！_W_
						MESSAGE = %MESSAGE%加えて、%CALLNAME:MAGIC_TARGET%のペニスは石のように硬くされてしまった！_W_
						CALL ADD_STATE, MAGIC_TARGET, "ハードスキン"
					CASEELSE
						MESSAGE = %MESSAGE%締め付けのせいで、射精する事ができない！_W_
					ENDSELECT

					CALL ADD_STATE, MAGIC_TARGET, "バインド：Ｐ"
				ENDIF
			ENDSELECT
		ENDIF
	CASE 241
		MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%のあらゆる能力が高まった！_W_
		CALL ADD_STATE, MAGIC_TARGET, "精强术"
		CALL ADD_STATE, MAGIC_TARGET, "魅强术"
		CALL ADD_STATE, MAGIC_TARGET, "防强术"
	CASE 242
		IF STATE("小根术", MAGIC_TARGET)
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%のペニスの大きさが元に戻った！_W_
			CALL REMOVE_STATE, MAGIC_TARGET, "小根术"
		ELSE
			MESSAGE = %MESSAGE%なんと%CALLNAME:MAGIC_TARGET%のペニスが大きくなってしまった！_W_
			CALL ADD_STATE, MAGIC_TARGET, "大根术"
		ENDIF
	CASE 243
		;TARGETが使った際にはＷ押し倒しへ
		IF CASTER == TARGET
			MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は%CALLNAME:CASTER%達に押し倒されてしまった！_W_

			;本来のヴィジョンは終了
			CALL REMOVE_STATE, MAGIC_TARGET, "快感术"

			;虚像作成
			ADDCOPYCHARA TARGET
			TARGET:1 = CHARANUM - 1
			;虚像は後で消える
			CALL SET_CEVENT, "消滅予定", TARGET:1
			CALLNAME:(TARGET:1) = %CALLNAME:(TARGET:1)%Ｂ
			NAME:(TARGET:1) = %NAME:(TARGET:1)%Ｂ

			SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
			CASE "分身"
				CALL SET_CEVENT, "分身", TARGET:1
			CASE "分裂"
				CALL SET_CEVENT, "分裂", TARGET:1
			CASEELSE

			ENDSELECT

			CALL SETFLAG, "Ｗ推到"
		ELSE
			SELECTCASE TALENT_NAME(ARG, TALENT:CASTER:ARG, CASTER)
			CASE "分身"
				MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は%CALLNAME:CASTER%の分身に襲われた！_W_
			CASE "分裂"
				MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は%CALLNAME:CASTER%の分裂体に襲われた！_W_
			CASEELSE
				MESSAGE = %MESSAGE%%CALLNAME:MAGIC_TARGET%は夢魔の幻覚に襲われた！_W_
			ENDSELECT

			CALL ADD_STATE, MAGIC_TARGET, "快感术"
		ENDIF
	CASE 312
		IF STATE("具現", MAGIC_TARGET)
			CALL REMOVE_STATE, MAGIC_TARGET, "具現"
			MESSAGE = %MESSAGE%具現の能力で生やした%CALLNAME:MAGIC_TARGET%のペニスが消滅した…_W_
			CALL SETFLAG, "具現化ＯＦＦフラグ", MAGIC_TARGET
		ELSE
			CALL ADD_STATE, MAGIC_TARGET, "具現"
			MESSAGE = %MESSAGE%なんと%CALLNAME:MAGIC_TARGET%の股間から
			SELECTCASE SIZE("ペニス", MAGIC_TARGET)
			CASE IS >= 3
				MESSAGE = %MESSAGE%凶悪な大きさの
			CASE 2
				MESSAGE = %MESSAGE%ほれぼれする大きさの
			ENDSELECT
			MESSAGE = %MESSAGE%ペニスが凄い勢いで生えてきた！_W_
			CALL EXP50_CHECK, MAGIC_TARGET
			CALL SETFLAG, "具現化ＯＮフラグ", MAGIC_TARGET
		ENDIF
	ENDSELECT
ENDIF

BASE:CASTER:精力 -= MAGIC_EP(ARG, CASTER)
TCVAR:CASTER:109 += MAGIC_EP(ARG, CASTER)
MESSAGE = %MESSAGE%%CALLNAME:CASTER%の精力-{MAGIC_EP(ARG, CASTER)}   ({BASE:CASTER:精力}/{MAXBASE:CASTER:精力})_W_

CALL PRINT_STR, MESSAGE

;-------------------------------------------------
;アルカナや特殊能力を管理する
;-------------------------------------------------
@MAGIC_CASTER_SELECT, ARG
SELECTCASE MAGIC_USER(ARG, "コマンド")
;唱えられる人は一人だけ
CASE 1, 2, 4
	IF ASSI && USE_MAGIC(ARG, "コマンド", ASSI)
		LOCAL = ASSI
	ELSEIF USE_MAGIC(ARG, "コマンド", MASTER)
		LOCAL = MASTER
	ELSEIF USE_MAGIC(ARG, "コマンド")
		LOCAL = TARGET
	ENDIF

	TCVAR:LOCAL:23 = ARG

	;唱える人がTARGET＆押し倒しでない時には止められる様にしてみる
	IF LOCAL == TARGET && TEQUIP:LOCAL:推到 == 0
		PRINTFORML %CALLNAME:LOCAL% 精力({BASE:LOCAL:精力}/{MAXBASE:LOCAL:精力})が%TALENT_NAME(ARG, TALENT:LOCAL:ARG)% 消費精力:{MAGIC_EP(ARG, LOCAL)}を\@ ARG >= 230 && ARG <= 259 ? 唱え # 使い \@ます。
		PRINTFORML よろしいですか？
		PRINTFORML  [ 0] はい
		PRINTFORML  [ 1] やっぱやめる
		CALL INPUT_SELECT, 2
		IF RESULT == 1
			TFLAG:14 = 1
			RETURN 0
		ENDIF
	ENDIF
CASEELSE
	PRINTFORML 誰が%TRAINNAME:ARG% %TEXTS("アルカナ解説", ARG)% を\@ ARG >= 230 && ARG <= 259 ? 唱え # 使い \@ますか？
	PRINTL

	IF ASSI && USE_MAGIC(ARG, "コマンド", ASSI)
		LOCALS = %CALLNAME:ASSI%(助手)
		PRINTFORML  [ 0] %TEXT_LJ(LOCALS, 22)% 精力({BASE:ASSI:精力}/{MAXBASE:ASSI:精力})　消費精力:{MAGIC_EP(ARG, ASSI)}
	ENDIF
	IF USE_MAGIC(ARG, "コマンド", MASTER)
		LOCALS = %CALLNAME:MASTER%(主人)
		PRINTFORML  [ 1] %TEXT_LJ(LOCALS, 22)% 精力({BASE:MASTER:精力}/{MAXBASE:MASTER:精力})　消費精力:{MAGIC_EP(ARG, MASTER)}
	ENDIF
	SIF USE_MAGIC(ARG, "コマンド")
		PRINTFORML  [ 2] %TEXT_LJ(CALLNAME:TARGET, 22)% 精力({BASE:精力}/{MAXBASE:精力})　消費精力:{MAGIC_EP(ARG, TARGET)}
	PRINTL [100] 戻る

	CALL INPUT_SELECT, 3, 100
	IF RESULT == 100
		TFLAG:14 = 1
		RETURN 0
	ELSEIF RESULT == 0 && ASSI && USE_MAGIC(ARG, "コマンド", ASSI)
		TCVAR:ASSI:23 = ARG
	ELSEIF RESULT == 1 && USE_MAGIC(ARG, "コマンド", MASTER)
		TCVAR:MASTER:23 = ARG
	ELSEIF RESULT == 2 && USE_MAGIC(ARG, "コマンド")
		TCVAR:23 = ARG
	ELSE
		RESTART
	ENDIF
ENDSELECT

CALL MAGIC_TARGET_SELECT, ARG


;-------------------------------------------------
;誰に使用するかを選択
;使い手はTCVAR:23 == ARG となっている
;-------------------------------------------------
@MAGIC_TARGET_SELECT, ARG
SELECTCASE TRAINNAME:ARG
;MASTERのみ
CASE "STOCK"
	TCVAR:MASTER:22 = ARG
;TARGETのみ
CASE "回复术", "解强化术"
	TCVAR:22 = ARG
;使い手により変化
CASE "快感术"
	;TARGETが使う時
	IF TCVAR:23 == ARG
		TCVAR:PLAYER:22 = ARG
	ELSE
		TCVAR:22 = ARG
	ENDIF
CASE "小根术"
	VARSET LOCAL

	;ペニス有り＆エンラジがかかっていない＆常にエンラジでない ことが条件
	SIF ASSI && PENIS(ASSI) && STATE("小根术", ASSI) == 0 && TALENT:ASSI:大根术常态化 == 0
		SETBIT LOCAL, 0
	SIF PENIS(MASTER) && STATE("小根术", MASTER) == 0 && TALENT:MASTER:大根术常态化 == 0
		SETBIT LOCAL, 1
	SIF PENIS(TARGET) && STATE("小根术", TARGET) == 0 && TALENT:大根术常态化 == 0
		SETBIT LOCAL, 2

	PRINTL 誰を対象としますか？
	IF GETBIT(LOCAL, 0)
		LOCALS = %CALLNAME:ASSI%(助手)
		PRINTFORML  [ 0] %TEXT_LJ(LOCALS, 22)% 現在の大きさ：%CONDS("ペニスサイズ", ASSI)% \@ STATE("大根术", ASSI) ? エンラジ影響中 # \@
	ENDIF
	IF GETBIT(LOCAL, 1)
		LOCALS = %CALLNAME:MASTER%(主人)
		PRINTFORML  [ 1] %TEXT_LJ(LOCALS, 22)% 現在の大きさ：%CONDS("ペニスサイズ", MASTER)% \@ STATE("大根术", MASTER) ? エンラジ影響中 # \@
	ENDIF
	SIF GETBIT(LOCAL, 2)
		PRINTFORML  [ 2] %TEXT_LJ(CALLNAME:TARGET, 22)% 現在の大きさ：%CONDS("ペニスサイズ")% \@ STATE("大根术", TARGET) ? エンラジ影響中 # \@
	PRINTL [100] 戻る
	CALL INPUT_SELECT, 3, 100
	IF RESULT == 100
		TFLAG:14 = 1
		RETURN 0
	ELSEIF RESULT == 0 && GETBIT(LOCAL, 0)
		TCVAR:ASSI:22 = ARG
	ELSEIF RESULT == 1 && GETBIT(LOCAL, 1)
		TCVAR:MASTER:22 = ARG
	ELSEIF RESULT == 2 && GETBIT(LOCAL, 2)
		TCVAR:22 = ARG
	ELSE
		RESTART
	ENDIF
CASE "大根术"
	VARSET LOCAL

	SIF ASSI && PENIS(ASSI) && STATE("大根术", ASSI) == 0 && TALENT:ASSI:小根术常态化 == 0
		SETBIT LOCAL, 0
	SIF PENIS(MASTER) && STATE("大根术", MASTER) == 0 && TALENT:MASTER:小根术常态化 == 0
		SETBIT LOCAL, 1
	SIF PENIS(TARGET) && STATE("大根术", TARGET) == 0 && TALENT:小根术常态化 == 0
		SETBIT LOCAL, 2

	PRINTL 誰を対象としますか？
	IF GETBIT(LOCAL, 0)
		LOCALS = %CALLNAME:ASSI%(助手)
		PRINTFORML  [ 0] %TEXT_LJ(LOCALS, 22)% 現在の大きさ：%CONDS("ペニスサイズ", ASSI)% \@ STATE("小根术", ASSI) ? ミニマム影響中 # \@
	ENDIF
	IF GETBIT(LOCAL, 1)
		LOCALS = %CALLNAME:MASTER%(主人)
		PRINTFORML  [ 1] %TEXT_LJ(LOCALS, 22)% 現在の大きさ：%CONDS("ペニスサイズ", MASTER)% \@ STATE("小根术", MASTER) ? ミニマム影響中 # \@
	ENDIF
	SIF GETBIT(LOCAL, 2)
		PRINTFORML  [ 2] %TEXT_LJ(CALLNAME:TARGET, 22)% 現在の大きさ：%CONDS("ペニスサイズ")% \@ STATE("小根术", TARGET) ? ミニマム影響中 # \@
	PRINTL [100] 戻る
	CALL INPUT_SELECT, 3, 100
	IF RESULT == 100
		TFLAG:14 = 1
		RETURN 0
	ELSEIF RESULT == 0 && GETBIT(LOCAL, 0)
		TCVAR:ASSI:22 = ARG
	ELSEIF RESULT == 1 && GETBIT(LOCAL, 1)
		TCVAR:MASTER:22 = ARG
	ELSEIF RESULT == 2 && GETBIT(LOCAL, 2)
		TCVAR:22 = ARG
	ELSE
		RESTART
	ENDIF

CASE "具現"
	VARSET LOCAL
	SIF ASSI && (TALENT:ASSI:女性 || STATE("具現", ASSI) )
		SETBIT LOCAL, 0
	SIF TALENT:MASTER:女性 || STATE("具現", MASTER)
		SETBIT LOCAL, 1
	SIF TALENT:女性 || STATE("具現", TARGET)
		SETBIT LOCAL, 2
	PRINTL 誰を対象としますか？
	IF GETBIT(LOCAL, 0)
		LOCALS = %CALLNAME:ASSI%(助手)
		PRINTFORML  [ 0] %TEXT_LJ(LOCALS, 22)% \@ TALENT:ASSI:女性 ? オンナ # 具現中 \@
	ENDIF
	IF GETBIT(LOCAL, 1)
		LOCALS = %CALLNAME:MASTER%(主人)
		PRINTFORML  [ 1] %TEXT_LJ(LOCALS, 22)% \@ TALENT:MASTER:女性 ? オンナ # 具現中 \@
	ENDIF
	SIF GETBIT(LOCAL, 2)
		PRINTFORML  [ 2] %TEXT_LJ(CALLNAME:TARGET, 22)% \@ TALENT:女性 ? オンナ # 具現中 \@
	PRINTL [100] 戻る
	CALL INPUT_SELECT, 3, 100
	IF RESULT == 100
		TFLAG:14 = 1
		RETURN 0
	ELSEIF RESULT == 0 && GETBIT(LOCAL, 0)
		TCVAR:ASSI:22 = ARG
	ELSEIF RESULT == 1 && GETBIT(LOCAL, 1)
		TCVAR:MASTER:22 = ARG
	ELSEIF RESULT == 2 && GETBIT(LOCAL, 2)
		TCVAR:22 = ARG
	ELSE
		RESTART
	ENDIF
;それ以外はPLAYER
CASEELSE
	TCVAR:PLAYER:22 = ARG
ENDSELECT


;-------------------------------------------------
;アルカナのコマンド番号をARGで指定して、それを誰が使えるのかをチェックする
;0bit ASSI, 1bit MASTER, 2bit TARGET
;ARGSは判定する時の状況で、コマンド選択、カウンター時で区別する
;-------------------------------------------------
@MAGIC_USER(ARG, ARGS)
#FUNCTION
#DIM LCOUNT
VARSET LOCAL

;誰が唱えられるかをチェック
;0bitでASSI詠唱可能、1bit でMASTER詠唱可能、2bitでTARGET詠唱可能
FOR LCOUNT, 1, CHARANUM
	SELECTCASE LCOUNT
	CASE MASTER, TARGET, ASSI
	CASEELSE
		CONTINUE
	ENDSELECT
	SIF USE_MAGIC(ARG, ARGS, LCOUNT) == 0
		CONTINUE

	SELECTCASE LCOUNT
	CASE ASSI
		SETBIT LOCAL, 0
	CASE MASTER
		SETBIT LOCAL, 1
	CASE TARGET
		SETBIT LOCAL, 2
	ENDSELECT
NEXT

RETURNF LOCAL

;-------------------------------------------------
;キャラ番号ARG:1がアルカナのコマンド番号ARGを使えるのかをチェックする
;ARGSは判定する時の状況で、コマンド選択、カウンター時で区別する
;-------------------------------------------------
@USE_MAGIC(ARG, ARGS, ARG:1)
#FUNCTION
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET
SIF TEQUIP:(ARG:1):眼罩 || TEQUIP:(ARG:1):束缚 || TEQUIP:(ARG:1):口塞 || TEQUIP:(ARG:1):演奏中
	RETURNF 0
;口か目が制限されてるとダメ
SIF COND("口の使用", ARG:1) == 0 || COND("視力", ARG:1) == 0
	RETURNF 0
;消費精力
SIF BASE:(ARG:1):精力 <= MAGIC_EP(ARG, ARG:1)
	RETURNF 0

SELECTCASE ARG
CASE 312
	SIF TALENT:(ARG:1):具现 == 0
		RETURNF 0
CASEELSE
	SIF TALENT:(ARG:1):ARG == 0 && TALENT:(ARG:1):魔法使 != 2
		RETURNF 0
ENDSELECT

SELECTCASE ARG:1
;使用者が主人や助手
CASE ASSI, MASTER
	RETURNF 1
;調教相手が使用する場合
CASEELSE
	;反発刻印が1以下かつ従順３ＬＶor屈服刻印２ＬＶ
	SELECTCASE ARGS
	CASE "コマンド"
		SELECTCASE TRAINNAME:ARG
		CASE "快感术"
			SIF TEQUIP:(ARG:1):Ｗ推到
				RETURNF 0
			RETURNF 1
		CASE "魅强术", "防强术", "倍化术", "全强术"
			RETURNF 0
		CASE "大根术"
			IF COND("ベテラン：Ｖセックス", ARG:1) && SIZE("ペニス", PLAYER) <= ABL:(ARG:1):Ｖ扩张
			ELSEIF COND("ベテラン：Ａセックス", ARG:1) && SIZE("ペニス", PLAYER) <= ABL:(ARG:1):Ａ扩张
			ELSE
				RETURNF 0
			ENDIF
		CASE "解弱化术"
			SIF TEQUIP:(ARG:1):推到 && MARK:(ARG:1):屈服刻印 <= 2
				RETURNF 0
		ENDSELECT
		SIF MARK:(ARG:1):排斥刻印 >= 2
			RETURNF 0
		SIF ABL:(ARG:1):服从 < 3 && MARK:(ARG:1):屈服刻印 < 2
			RETURNF 0
		RETURNF 1
	CASE "カウンター"
		;精力には余裕を５程度残す
		SIF BASE:(ARG:1):精力 < 5 + MAGIC_EP(ARG, ARG:1)
			RETURNF 0
		SELECTCASE TRAINNAME:ARG
		CASE "小根术"
			SIF SIZE("ペニス", PLAYER) >= MAX(2, ABL:(ARG:1):Ｖ扩张) && TALENT:(ARG:1):好色 == 0 && ABL:(ARG:1):服从 < 3
				RETURNF 1
		CASE "高敏术"
			SIF STATE("高敏术", PLAYER) == 0
				RETURNF 1
		CASE "精强术"
			SIF STATE("精强术", ARG:1) < 3
				RETURNF 1
		CASE "魅强术"
			SIF STATE("魅强术", ARG:1) < 3
				RETURNF 1
		CASE "防强术"
			SIF STATE("防强术", ARG:1) < 3
				RETURNF 1
		CASE "回复术"
			SIF BASE:(ARG:1):体力 < MIN(1500, MAXBASE:(ARG:1):体力 - 300)
				RETURNF 1
		CASE "倍化术"
			SIF STATE("倍化术", ARG:1) == 0
				RETURNF 1
		CASE "解强化术"
			SIF STATE("グン", PLAYER) && TALENT:(ARG:1):好色 == 0 && ABL:(ARG:1):服从 < 3 && MARK:(ARG:1):排斥刻印
				RETURNF 1
		CASE "束缚术"
			SIF STATE("束缚术", PLAYER) == 0
				RETURNF 1
		CASE "全强术"
			SIF STATE("精强术", ARG:1) < 3
				RETURNF 1
			SIF STATE("魅强术", ARG:1) < 3
				RETURNF 1
			SIF STATE("防强术", ARG:1) < 3
				RETURNF 1
		CASE "大根术"
			SIF COND("ベテラン：Ｖセックス", ARG:1) && SIZE("ペニス", PLAYER) < ABL:(ARG:1):Ｖ扩张
				RETURNF 1
			SIF COND("ベテラン：Ａセックス", ARG:1) && SIZE("ペニス", PLAYER) < ABL:(ARG:1):Ａ扩张
				RETURNF 1
		CASE "快感术"
			SIF TEQUIP:(ARG:1):Ｗ推到
				RETURNF 0
			RETURNF 1
		ENDSELECT
	ENDSELECT
ENDSELECT
RETURNF 0

;---------------------------------------------------------------------
;ARG:1がARG番のアルカナを唱えるのに必要な精力
;ARG:1が省略されている場合には基本消費量を返すが、ARG:1 > 0ならばARG:1が魔法使いかどうかを見て消費量を減らす
;---------------------------------------------------------------------
@MAGIC_EP(ARG, ARG:1)
#FUNCTION
VARSET LOCAL
SELECTCASE ARG
CASE 230
	LOCAL = 2
CASE 231
	LOCAL = 4
CASE 232
	LOCAL = 3
CASE 233
	LOCAL = 4
CASE 234
	LOCAL = 3
CASE 235
	LOCAL = 30
CASE 236
	LOCAL = 8
CASE 237
	LOCAL = 4
CASE 238
	LOCAL = 3
CASE 239
	LOCAL = 7
CASE 240
	LOCAL = 8
CASE 241
	LOCAL = 9
CASE 242
	LOCAL = 4
CASE 243
	LOCAL = 8
CASE 312
	LOCAL = 8
ENDSELECT

SIF ARG:1 <= 0
	RETURNF LOCAL

;魔法使いだとアルカナ使用時の疲労減少
SELECTCASE ARG
CASE 230 TO 259
	SELECTCASE TALENT:(ARG:1):魔法使
	CASE IS >= 2
		LOCAL -= MIN((LOCAL+1)/2, 10)
	CASE 1
		LOCAL = MAX(MULTIPLY(LOCAL, 75), 1)
	ENDSELECT
ENDSELECT

RETURNF LOCAL


@LEARNING_MAGIC, ARG, ARGS
#DIM LCOUNT

SELECTCASE ARGS
CASE "個人授業"
	PRINTL
	PRINTFORML どれを教えてもらいますか？
CASEELSE
	PRINTL
	PRINTFORML どれを覚えさせますか？ - 習得の珠×{JUEL:ARG:习得}/{NUM("宝珠：アルカナ習得", ARG)}
ENDSELECT

VARSET LOCAL
FOR LCOUNT, 230, 230 + NUM("存在するアルカナ")
	SIF TALENT:ARG:LCOUNT || TALENTNAME:LCOUNT == ""
		CONTINUE
	SELECTCASE LCOUNT
	CASE 235, 240
		SIF ARG == MASTER
			CONTINUE
	ENDSELECT
	PRINTFORML  [{LCOUNT - 230, 2}] %TEXT_LJ(TALENTNAME:LCOUNT, 14)% %TEXTS("アルカナ解説", LCOUNT)%
	LOCAL:LCOUNT = 1
NEXT

PRINTFORML [100] 覚えない

$INPUT_LOOP
INPUT

LOCAL = RESULT + 230

IF RESULT == 100
	RETURN 0
ELSEIF RESULT >= 0 && RESULT < NUM("存在するアルカナ") && LOCAL:LOCAL

ELSE
	GOTO INPUT_LOOP
ENDIF

SELECTCASE ARGS
CASE "個人授業"
	TALENT:ARG:LOCAL = 1
CASEELSE
	PRINTFORMW %NAME:ARG%は%TALENTNAME:LOCAL%を覚えた。
	JUEL:ARG:习得 -= NUM("宝珠：アルカナ習得", ARG)
	TALENT:ARG:LOCAL = 1
	SIF COND("アルカナ習得可能", ARG)
		RESTART
ENDSELECT

RETURN LOCAL

@COM230
CALL COM_ARCANA, 230
RETURN 0

@TRAIN_MESSAGE_COM230
CALL TRAIN_MESSAGE_ARCANA, 230

@COM231
CALL COM_ARCANA, 231
RETURN 0

@TRAIN_MESSAGE_COM231
CALL TRAIN_MESSAGE_ARCANA, 231

@COM232
CALL COM_ARCANA, 232
RETURN 0

@TRAIN_MESSAGE_COM232
CALL TRAIN_MESSAGE_ARCANA, 232

@COM233
CALL COM_ARCANA, 233
RETURN 0

@TRAIN_MESSAGE_COM233
CALL TRAIN_MESSAGE_ARCANA, 233

@COM234
CALL COM_ARCANA, 234
RETURN 0

@TRAIN_MESSAGE_COM234
CALL TRAIN_MESSAGE_ARCANA, 234

@COM235
CALL COM_ARCANA, 235
RETURN 0

@TRAIN_MESSAGE_COM235
CALL TRAIN_MESSAGE_ARCANA, 235

@COM236
CALL COM_ARCANA, 236
RETURN 0

@TRAIN_MESSAGE_COM236
CALL TRAIN_MESSAGE_ARCANA, 236

@COM237
CALL COM_ARCANA, 237
RETURN 0

@TRAIN_MESSAGE_COM237
CALL TRAIN_MESSAGE_ARCANA, 237

@COM238
CALL COM_ARCANA, 238
RETURN 0

@TRAIN_MESSAGE_COM238
CALL TRAIN_MESSAGE_ARCANA, 238

@COM239
CALL COM_ARCANA, 239
RETURN 0

@TRAIN_MESSAGE_COM239
CALL TRAIN_MESSAGE_ARCANA, 239

@COM240
CALL COM_ARCANA, 240
RETURN 0

@TRAIN_MESSAGE_COM240
CALL TRAIN_MESSAGE_ARCANA, 240

@COM241
CALL COM_ARCANA, 241
RETURN 0

@TRAIN_MESSAGE_COM241
CALL TRAIN_MESSAGE_ARCANA, 241

@COM242
CALL COM_ARCANA, 242
RETURN 0

@TRAIN_MESSAGE_COM242
CALL TRAIN_MESSAGE_ARCANA, 242

@COM243
CALL COM_ARCANA, 243
RETURN 0

@TRAIN_MESSAGE_COM243
CALL TRAIN_MESSAGE_ARCANA, 243

@EQUIP_COM243
SIF STATE("快感术")
	CALL SOURCE_COM0, 80
IF ASSI && STATE("快感术", ASSI) && PLAYER != ASSI
	SWAP TARGET, ASSI
	CALL SOURCE_COM0, 80
	SWAP TARGET, ASSI
ENDIF
IF STATE("快感术", PLAYER)
	BASE:PLAYER:绝顶 += MAXBASE:PLAYER:绝顶/5
	TCVAR:PLAYER:101 += MAXBASE:PLAYER:绝顶/5
ENDIF
RETURN 1

@EQUIP_COM243_2
LOCALS =
SIF STATE("快感术")
	LOCALS = %CALLNAME:TARGET%
SIF STATE("快感术", PLAYER)
	LOCALS = %LOCALS%\@ STATE("快感术") ? と # \@%CALLNAME:PLAYER%
SIF ASSI && STATE("快感术", ASSI) && PLAYER != ASSI
	LOCALS = %LOCALS%\@ STATE("快感术") + STATE("快感术", PLAYER) ? と # \@%CALLNAME:ASSI%

SIF LOCALS == ""
	RETURN 0

PRINTL ＜ヴィジョン＞
IF STATE("快感术", PLAYER)
	PRINTFORML %LOCALS%は幻覚に襲われている！
ELSE
	PRINTFORML %LOCALS%は幻覚に襲われて快感に喘いでいる！
ENDIF

RETURN 1
