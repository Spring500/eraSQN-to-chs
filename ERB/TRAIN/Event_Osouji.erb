;--------------------------------------------------------
;お掃除クンニ
;--------------------------------------------------------
@OSOUJI_V
PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%のアソコを押し広げ、
IF ABL:百合倾向 >= 3 && RAND:2 == 0
	PRINTFORMW 膣口を啜り上げてしっかり中まで綺麗にした…
ELSE
	PRINTFORMW 舌で丁寧に汚れを舐めとった…
ENDIF

;経験や汚れなどの処理は全部ここで行う
CALL SETFLAG, "お掃除クンニ", TARGET

PRINTFORML クンニ経験＋１
PRINTFORML お掃除クンニ経験＋１
PRINTL 

;CALL SELF_KOJO, "お掃除クンニ"

;--------------------------------------------------------
;お掃除フェラ
;--------------------------------------------------------
@OSOUJI_F
CALL CHECK_BASEPLAYER2
LOCAL = (ABL:侍奉精神 * 5) + ABL:技巧

;アナル汚れ
IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ａ")
	LOCAL -= 10
	;A感覚
	SIF ABL:Ａ感觉 >= 3
		LOCAL += 2
	SIF ABL:Ａ感觉 >= 5
		LOCAL += 5
	;尻穴狂い
	SIF TALENT:尻穴狂
		LOCAL += 10
	;汚臭鈍感
	SIF TALENT:污臭钝感
		LOCAL += 5
	;汚臭敏感
	SIF TALENT:污臭敏感
		LOCAL -= 5
ENDIF
;素直
SIF TALENT:坦率
	LOCAL += 5
;プライド高い
SIF TALENT:高傲
	LOCAL -= 10
;プライド低い
SIF TALENT:自卑
	LOCAL += 5
;献身的
SIF TALENT:献身
	LOCAL += 10
;淫乱
SIF TALENT:淫乱
	LOCAL += 5
;男嫌い
SIF TALENT:讨厌男性
	LOCAL -= 10
;恋慕
SIF TALENT:恋慕
	LOCAL += 10
;反発刻印
IF MARK:排斥刻印 == 1
	LOCAL -= 5
ELSEIF MARK:排斥刻印 == 2
	LOCAL -= 15
ELSEIF MARK:排斥刻印 == 3
	LOCAL = 30
ENDIF

;焦らし中はこんなことする余裕はありません
SIF (NOWEX:Ｍ绝顶边缘 && CUP:快Ｃ) || (NOWEX:Ｃ绝顶边缘 + NOWEX:濒临尿道绝顶 && CUP:快Ｃ) || (NOWEX:Ｖ绝顶边缘 && CUP:快Ｖ) || (NOWEX:Ａ绝顶边缘 && CUP:快Ａ) || (NOWEX:Ｂ绝顶边缘 && CUP:快Ｂ)
	LOCAL -= 20
;ラブトラップ、アイマスク、縄・拘束、ボールギャグではしません
SIF TEQUIP:爱情圈套？ || TEQUIP:眼罩 || TEQUIP:束缚 || TEQUIP:口塞 || LOCAL < 0
	RETURN 0

SIF LOCAL > RAND:100
	CALL OSOUJI


@OSOUJI

PRINTL 
;お掃除前口上
TFLAG:47 = 0
CALL SELF_KOJO, "お掃除フェラ"

IF RAND:3 == 0 || SHOOT("中出し") == 0
	PRINTFORMW %CALLNAME:TARGET%は射精したばかりの%CALLNAME:PLAYER%のペニスを丁寧に舐めあげ、くわえ込んでちゅぷちゅぷと隅々までぬぐい始めた……
ELSEIF RAND:2 == 0
	IF INSERT("Ｖインサート")
		PRINT 割れ目から抜き出されたどろどろのペニスを、
	ELSEIF INSERT("Ａインサート")
		PRINT アナルから抜き出されたどろどろのペニスを、
	ELSEIF INSERT("尿道インサート")
		IF PENIS(TARGET)
			PRINT 自らのペニスから抜き出されたどろどろのソレを、
		ELSE
			PRINT 尿道から抜き出されたどろどろのペニスを、
		ENDIF
	ENDIF
	PRINTFORMW %CALLNAME:TARGET%は愛おしげに口に含んだ……
ELSE
	IF INSERT("Ｖインサート")
		PRINT 割れ目から抜き出された精液まみれのペニスを見ると、
	ELSEIF INSERT("Ａインサート")
		PRINT アナルから抜き出された精液まみれのペニスを見ると、
	ELSEIF INSERT("尿道インサート")
		IF PENIS(TARGET)
			PRINT 自らのペニスから抜き出された精液まみれのソレを見ると、
		ELSE
			PRINT 尿道から抜き出された精液まみれのペニスを見ると、
		ENDIF
	ENDIF
	PRINTFORMW %CALLNAME:TARGET%は舌なめずりしてじわじわと唇を近づけてきた……
ENDIF

;お掃除中口上
TFLAG:47 = 1
CALL SELF_KOJO, "お掃除フェラ"

IF RAND:3 == 0
	PRINTFORMW %CALLNAME:TARGET%は亀頭をぺちょぺちょと舐め、裏を舌先でつうっとなぞり、中にまだ残った精液を優しく何度も吸い上げていく。
	PRINTFORMW 優しい快感が下半身を包むが、%CALLNAME:TARGET%の口内はあんまりに柔らかく熱すぎた。
	PRINTFORMW %CALLNAME:PLAYER%の奥底から、音を立てそうな勢いで精液が充填されてくるのがわかる……
ELSEIF RAND:2 == 0
	PRINTFORMW 様子を細かく見ながら丁寧にペニスをしゃぶり続けられ、%CALLNAME:PLAYER%の下半身に甘い痺れが広がっていく……
ELSE
	PRINTFORMW ペニスの内部に一滴も精液を残すまいと言わんばかりの凄まじいバキュームに、まだ敏感すぎる%CALLNAME:PLAYER%のペニスは翻弄される……
ENDIF

;射精ゲージ
LOCAL = (ABL:技巧 + (ABL:侍奉精神 + 1)/2 + ABL:舌技) * 2000 * (RAND:5 + (ABL:侍奉精神 + 1)/2 + 1)

SIF ABL:PLAYER:技巧
	LOCAL /= ABL:PLAYER:技巧
;LVの差による補正
LOCAL = MULTIPLY(LOCAL, LIMIT(5*(ABL:ＬＶ - ABL:PLAYER:ＬＶ), -30, 100) + 100)

SIF COND("口に弱い", PLAYER)
	TIMES LOCAL, 2.00
BASE:PLAYER:绝顶 += LOCAL

;射精ゲージ増加の一時保存
TCVAR:PLAYER:101 += LOCAL

PRINTL 

;ペニスを抜く処理
CALL RESET_INSERT, TARGET, PLAYER

;お掃除で射精しなかった
IF BASE:PLAYER:绝顶 < MAXBASE:PLAYER:绝顶
	;お掃除で射精しなかった
	TFLAG:47 = 2
	CALL SELF_KOJO, "お掃除フェラ"

	IF RAND:2 == 0
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%をそっと見つめると、丁寧に舌を使い始めた。
		PRINTFORMW うっとりとする、適度な快感が気だるい%CALLNAME:PLAYER%の体を包んでいく……
	ELSE
		PRINTFORMW %CALLNAME:TARGET%の口がゆっくりと離れると、むずむずと物足りない感じが這い上がってきた……
	ENDIF
	PRINTL 

;お掃除で射精しそう？
ELSE
	;お掃除で射精しそう？
	TFLAG:47 = 3
	CALL SELF_KOJO, "お掃除フェラ"

	IF RAND:3 == 0
		PRINTFORMW すっかり萎えていたはずのペニスがもうぱんぱんに張り詰め、腰が震えるほどの快感が背筋をぞくぞくと駆け上っていく。
	ELSEIF RAND:2 == 0
		PRINTFORMW %CALLNAME:TARGET%は、またじわじわとあなたのペニスに口を近づけてきた……
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は、真剣な顔で再びペニスを口に含んで責め立てはじめた。
		PRINTFORMW 射精したばかりのペニスを丁寧にしゃぶられる、度を越した強烈な快感に弄ばれる%CALLNAME:PLAYER%に気づくこともなく……
	ENDIF
	PRINTL 

	;射精した
	TFLAG:47 = 4
	CALL SELF_KOJO, "お掃除フェラ"

	IF RAND:3 == 0
		PRINTFORMW 強引に搾りだされた精液がペニスを通った瞬間、最初の射精以上の快感が全身を弾けさせた……
	ELSEIF RAND:2 == 0
		PRINTFORMW %CALLNAME:TARGET%が強烈に亀頭を吸いたてた瞬間、再び精液が爆発した。
		PRINTFORMW 視界が白くなるような快感が%CALLNAME:PLAYER%を押し流す……
	ELSE
		PRINTFORMW 何度か息で吹かれ、不意打ちぎみに喉の奥まで一気にくわえ込まれて、ひとたまりもなく%CALLNAME:PLAYER%は絶頂に跳ねた……
	ENDIF
	PRINTL 

	CALL BOUHATSU, "お掃除フェラ"

	;お掃除で射精後
	TFLAG:47 = 5
	CALL SELF_KOJO, "お掃除フェラ"

	IF NO:TARGET == 20
		PRINTFORMW 今度こそ「邪気」を最後まで清めてしまおうと尿道の中まで吸いあげ続ける%CALLNAME:TARGET%に、%CALLNAME:PLAYER%は抵抗する力もなく身を任せるしかなかった……
	ELSE
		PRINTFORMW 噴きあがる精液を顔に浴びながら、%CALLNAME:TARGET%は幸せそうに笑っていた……
	ENDIF
	PRINTL 
	;ダウン判定
	CALL COUNTER_DOWN
ENDIF

;経験や汚れなどの処理は全部ここで行う
CALL SETFLAG, "お掃除フェラ", TARGET

PRINTFORML フェラ経験＋１
PRINTFORML お掃除フェラ経験＋１
PRINTL 

RETURN 1

;-------------------------------------------------
;※暴発判定
;-------------------------------------------------
@BOUHATSU, ARGS
CALL CHECK_BASEPLAYER2
;精力が既にないなら終了
SIF BASE:PLAYER:精力 < 0
	RETURN 0

;絶頂許可
TCVAR:PLAYER:2 = 1

;射精ＬＶの計算
LOCAL = CALCF("绝顶", PLAYER)

;射精してないなら終了
SIF LOCAL == 0
	RETURN 0

;女はMAX3
;SIF PENIS(PLAYER) == 0 && LOCAL >= 4
;	LOCAL = 3

PRINTL 

;クィーンメイブ専用焦らしモード
IF CFLAG:459 > 0 && NO:TARGET == 17
	LOCAL = MIN(LOCAL, BASE:PLAYER:精力 - 1)
	CFLAG:459 = 0
ENDIF

;ペニスあり
IF PENIS(PLAYER)
	;挿入前に暴発
	IF COND("挿入前暴発フラグ", PLAYER)
		CALL SELF_KOJO, "挿入前暴発"
		PRINTFORMW …はずだったのだが
		PRINTFORMW 興奮のあまり、挿入する前に%CALLNAME:TARGET%の股間に精液を撒き散らしてしまった…
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%は堪えきれず射精してしまった……
	ENDIF
	
	NOWEX:PLAYER:射精 = LOCAL
	EX:PLAYER:射精 += NOWEX:PLAYER:射精
	EXP:PLAYER:射精经验 += NOWEX:PLAYER:射精
	EXP:精液经验 += LOCAL * 2
	IF LOCAL >= 3
		PRINTFORM 超大量
	ELSEIF LOCAL == 2
		PRINTFORM 大量
	ENDIF
	PRINTL 射精
	PRINTFORML 精液経験＋{LOCAL * 2}

	;PLAYERのペニスに精液汚れを追加
	CALL ADD_STAIN, PLAYER, "Ｐ", "精液"
	;精液を出したところに精液汚れを追加
	IF COND("挿入前暴発フラグ", PLAYER)
		CALL ADD_STAIN, TARGET, "Ｖ", "精液"
	;口に汚れを追加
	ELSEIF SELECTCOM == 91 || SELECTCOM == 99 || SELECTCOM == 140 || ARGS == "お掃除フェラ"
		;夢魔の口⇔調教者のＰの汚れが移動
		CALL EQ_STAIN, TARGET, PLAYER, 0, 2
		CALL SETFLAG, "精飲経験", LOCAL
	;胸に汚れを追加
	ELSEIF SELECTCOM == 81 || SELECTCOM == 92
		CALL ADD_STAIN, TARGET, "Ｂ", "精液"
	;Ｖに汚れを追加
	ELSEIF (SELECTCOM >= 60 && SELECTCOM <= 69) || INSERT("Ｖインサート")
		CALL SETFLAG, "膣内精液経験", LOCAL
	;Ａに汚れを追加
	ELSEIF (SELECTCOM >= 70 && SELECTCOM <= 79) || INSERT("Ａインサート")
		CALL SETFLAG, "肛内精液経験", LOCAL
	;ＶかＰに汚れを追加
	ELSEIF SELECTCOM == 83 || INSERT("尿道インサート")
		CALL SETFLAG, "膀胱精液経験", LOCAL
	;手に汚れを追加
	ELSEIF SELECTCOM == 90 || SELECTCOM == 97
		CALL ADD_STAIN, TARGET, "手", "精液"
	ENDIF
ELSE
	PRINTFORMW %CALLNAME:PLAYER%は堪えきれずイってしまった……
	EXP:PLAYER:绝顶经验 += LOCAL
	IF LOCAL >= 3
		PRINTFORM 超絶
	ELSEIF LOCAL == 2
		PRINTFORM 強
	ENDIF
	PRINTFORML 絶頂
	PRINTFORML 絶頂経験＋{LOCAL}
ENDIF

;暴発回数
NOWEX:爆发 = LOCAL
EX:爆发 += LOCAL

;イかせた回数
EXP:高潮次数 += 1

;堪えたフラグを初期化
EX:PLAYER:忍住了没去 = 0

CALL GET_EXPERIENCE, LOCAL, TARGET
IF TEQUIP:Ｗ推到
	EXP:(TARGET:1):高潮次数 += 1
	CALL GET_EXPERIENCE, LOCAL, TARGET:1
ENDIF

;精力減少
LOCAL:1 = MAX(LOCAL * 2, 1)

TCVAR:PLAYER:109 += LOCAL:1
BASE:PLAYER:精力 -= LOCAL:1
PRINTFORMW 精力-{LOCAL:1}

;射精ゲージ対応
BASE:PLAYER:绝顶 = MAXBASE:PLAYER:绝顶 * (1 + BASE:STOCK) * BASE:精力 * (100 + STATE("精强术", PLAYER) * 20)
BASE:PLAYER:绝顶 /= (2 + BASE:STOCK) * MAXBASE:精力 * LOCAL:1 * 100

BASE:PLAYER:绝顶 = LIMIT(BASE:PLAYER:绝顶, 0, MAXBASE:PLAYER:绝顶 - 1)

BASE:PLAYER:快感容许值（男） = BASE:PLAYER:绝顶

PRINTL 

RETURN 1
