;-------------------------------------------------
;カウンター初期設定
;イベントフラグ		TFLAG:90
;恍惚確率		TFLAG:91
;弱点効果		TFLAG:92
;押し倒し確率		TFLAG:93
;アルカナカウンター確率	TFLAG:94
;反撃せず		CFLAG:9
;連続回数		TFLAG:95
;カウンターの威力	TFLAG:96
;精力ゲージ		BASE:ASSI:精力　BASE:MASTER:精力
;ストック		BASE:MASTER:ストック
;-------------------------------------------------
@SET_COUNTER_BASE
#DIM LCOUNT
#DIM MEMO_STOCK

;MASTER以外の精力限界値セット
FOR LCOUNT, 1, CHARANUM
	MAXBASE:LCOUNT:精力 = MAXBASE:LCOUNT:体力/300 + TALENT:LCOUNT:淫乱*3 + TALENT:LCOUNT:竜の血*5 + TALENT:LCOUNT:上位夢魔*5 + ABL:LCOUNT:欲望*2 + ABL:LCOUNT:技巧 + ABL:LCOUNT:ＬＶ
NEXT

;MASTERの精力限界値リセット
MAXBASE:MASTER:精力 = 10 + ABL:MASTER:技巧*4 + TALENT:MASTER:竜の血*5 + TALENT:MASTER:上位夢魔*5 + ABL:MASTER:ＬＶ
SIF CONFIG("ＥＡＳＹ")
	MAXBASE:MASTER:精力 += 5

;アクセサリ効果（精力）
MAXBASE:MASTER:精力 = MULTIPLY(MAXBASE:MASTER:精力, 100 + TALENT:MASTER:精力強化)

;ストックが増えた時には増える
MEMO_STOCK = MAXBASE:MASTER:ストック

;ストック
SELECTCASE FLAG:255
CASE IS >= 21
	MAXBASE:MASTER:ストック = 3
CASE IS >= 14
	MAXBASE:MASTER:ストック = 2
CASE IS >= 7
	MAXBASE:MASTER:ストック = 1
CASEELSE
	MAXBASE:MASTER:ストック = 0
ENDSELECT

;アクセサリ効果（ストック）
SIF TALENT:MASTER:ストック増加
	MAXBASE:MASTER:ストック += 1

BASE:MASTER:ストック = MIN(BASE:MASTER:ストック + MAXBASE:MASTER:ストック - MEMO_STOCK, MAXBASE:MASTER:ストック)


;レベルリミッター使用時にはなんと！常に全快！！…まぁ禁断の知識の産物だしな
;IF CONFIG("レベルリミッター")
;	BASE:MASTER:精力 = MAXBASE:MASTER:精力
;	BASE:MASTER:ストック = MAXBASE:MASTER:ストック
;ENDIF

;精力の回復処理
FOR LCOUNT, 1, CHARANUM
	SIF BASE:LCOUNT:精力 == 0 || BASE:LCOUNT:精力 > MAXBASE:LCOUNT:精力 || CONFIG("ＥＡＳＹ")
		BASE:LCOUNT:精力 = MAXBASE:LCOUNT:精力
NEXT


@SET_COUNTER_ABL
#DIM LCOUNT
;ジャンルごとに何をやるかとその確率を決定
CALL CALC_COUNTER_ABL, TARGET

;同じコマンドを連続で行う確率は低くする
FOR LCOUNT, 0, 11
	SIF ABL:(LCOUNT + 40) && I:LCOUNT == PREVCOM
		ABL:(LCOUNT + 40) = MAX(ABL:(LCOUNT + 40)/2, 1)
NEXT
;-------------------------------------------------
;インサートしている時のフェラなど、不可能な行動はここで処理
;-------------------------------------------------
;インサートでイかされた後は、ご奉仕かキス率が高い
IF ( (INSERT("Ｖインサート") || PREVCOM == 65 || PREVCOM == 66 || PREVCOM == 67) && PREVEX("Ｖ絶頂") ) || ( (INSERT("Ａインサート") || PREVCOM == 75) && PREVEX("Ａ絶頂") ) || ( (INSERT("尿道インサート") || PREVCOM == 83 || PREVCOM == 84 || PREVCOM == 85 || PREVCOM == 86 || PREVCOM == 87 || PREVCOM == 88) && PREVEX("尿道絶頂") )
	ABL:手 /= 4
	ABL:口 *= 4
	SIF I:2 != 104
		ABL:胸 /= 4
	ABL:足 /= 4
	;サドっ気あるならお掃除強要
	IF ABL:欲望 + ABL:サドっ気 + TALENT:サド >= ABL:奉仕精神 * 2 && RAND:2 == 0
		ABL:顔面騎乗 *= 3
	ELSE
		ABL:顔面騎乗 /= 5
	ENDIF
	ABL:キス *= 2
	ABL:お尻責め /= 5
	ABL:レイプ /= 5
	ABL:素股 /= 4
	ABL:道具 = 0
;インサートの後はインサートor挿入れたままできること
ELSEIF INSERT("インサート") && ABL:騎乗位 + ABL:キス && RAND:4
	ABL:手 = 0
	ABL:口 = 0
	SIF I:2 != 104
		ABL:胸 = 0
	ABL:足 = 0
	ABL:顔面騎乗 = 0
	ABL:お尻責め = 0
	ABL:レイプ = 0
	ABL:素股 = 0
	ABL:道具 = 0
;レイプの後は連続レイプor使った穴にお礼
ELSEIF (PREVCOM == 120 || PREVCOM == 126) && ABL:レイプ && RAND:3
	ABL:手 = 0
	SIF TALENT:恋慕 == 0 && ABL:奉仕精神 < ABL:サドっ気
		ABL:口 = 0
	ABL:胸 = 0
	ABL:足 = 0
	ABL:顔面騎乗 = 0
	ABL:騎乗位 = 0
	ABL:キス = 0
	SIF PREVCOM != 84 && ABL:奉仕精神 < ABL:サドっ気
		ABL:お尻責め = 0
	ABL:素股 = 0
	ABL:道具 = 0
;ペニスに奉仕した後は奉仕orインサート率が高い
ELSEIF PENIS(PLAYER) && PREVCOM >= 90 && PREVCOM <= 119 && ABL:手 + ABL:口 + ABL:胸 + ABL:お尻責め + ABL:素股 && RAND:4
	ABL:足 = 0
	ABL:騎乗位 *= 2
	ABL:キス = 0
	ABL:レイプ = 0
	ABL:道具 = 0
;潤滑が少ない時には、顔面騎乗やフェラチオ率が高く、インサート率が低い
ELSEIF MAX(PALAM:Ｖ潤, PALAM:Ａ潤, PALAM:PLAYER:Ｐ潤) < 500 && ABL:顔面騎乗
	SIF I:1 == 91
		ABL:口 *= 2
	ABL:顔面騎乗 *= 2
	ABL:騎乗位 /= 5
	ABL:素股 /= 5
ENDIF

SELECTCOM = SET_COUNTERCOM(TARGET)

CALL SETFLAG, "ワイルドスウィング", TARGET

@SET_COUNTERCOM(ARG)
#FUNCTION
#DIM LCOUNT
;確率の母体決定
LOCAL = ABL:ARG:手+ABL:ARG:口+ABL:ARG:胸+ABL:ARG:足+ABL:ARG:顔面騎乗+ABL:ARG:騎乗位+ABL:ARG:キス+ABL:ARG:お尻責め+ABL:ARG:レイプ+ABL:ARG:素股+ABL:ARG:道具
IF LOCAL == 0
	IF ARG == TARGET
		IF TALENT:オトコ
			ABL:顔面騎乗 = 1
			RETURNF 9
		ELSE
			ABL:顔面騎乗 = 1
			RETURNF 1
		ENDIF
	ELSE
		RETURNF -1
	ENDIF
ENDIF

;PRINTFORML 確率母体 = {LOCAL}
;FOR LCOUNT, 40, 51
;	PRINTFORML %TEXT_RJ(ABLNAME:LCOUNT, 10)%率 ≒ {ABL:ARG:LCOUNT*100/LOCAL}％
;NEXT

LOCAL = 1 + RAND:LOCAL
SELECTCASE LOCAL
;手
CASE IS <= ABL:ARG:手
	RETURNF I:0
;口
CASE IS <= ABL:ARG:手+ABL:ARG:口
	RETURNF I:1
;胸
CASE IS <= ABL:ARG:手+ABL:ARG:口+ABL:ARG:胸
	RETURNF I:2
;足
CASE IS <= ABL:ARG:手+ABL:ARG:口+ABL:ARG:胸+ABL:ARG:足
	RETURNF I:3
;顔面騎乗
CASE IS <= ABL:ARG:手+ABL:ARG:口+ABL:ARG:胸+ABL:ARG:足+ABL:ARG:顔面騎乗
	RETURNF I:4
;騎乗位
CASE IS <= ABL:ARG:手+ABL:ARG:口+ABL:ARG:胸+ABL:ARG:足+ABL:ARG:顔面騎乗+ABL:ARG:騎乗位
	RETURNF I:5
;キス
CASE IS <= ABL:ARG:手+ABL:ARG:口+ABL:ARG:胸+ABL:ARG:足+ABL:ARG:顔面騎乗+ABL:ARG:騎乗位+ABL:ARG:キス
	RETURNF I:6
;お尻
CASE IS <= ABL:ARG:手+ABL:ARG:口+ABL:ARG:胸+ABL:ARG:足+ABL:ARG:顔面騎乗+ABL:ARG:騎乗位+ABL:ARG:キス+ABL:ARG:お尻責め
	RETURNF I:7
;レイプ
CASE IS <= ABL:ARG:手+ABL:ARG:口+ABL:ARG:胸+ABL:ARG:足+ABL:ARG:顔面騎乗+ABL:ARG:騎乗位+ABL:ARG:キス+ABL:ARG:お尻責め+ABL:ARG:レイプ
	RETURNF I:8
;素股
CASE IS <= ABL:ARG:手+ABL:ARG:口+ABL:ARG:胸+ABL:ARG:足+ABL:ARG:顔面騎乗+ABL:ARG:騎乗位+ABL:ARG:キス+ABL:ARG:お尻責め+ABL:ARG:レイプ+ABL:ARG:素股
	RETURNF I:9
CASEELSE
	RETURNF I:10
ENDSELECT


@CALC_COUNTER_ABL, ARG
#DIM LCOUNT
LOCAL:100 = TARGET
TARGET = ARG
;COMABLEで実行条件を判定するためのFLAGをセット
FLAG:10 = 1

;奉仕系の種別では、対応する得意技能の意味が強め
VARSET I

SELECTCASE ABL:奉仕精神 - ABL:欲望 - TEQUIP:媚薬/100
CASE IS > 0
	LOCAL:1 = (ABL:奉仕精神 - ABL:欲望 - TEQUIP:媚薬/100)*2
CASE IS < 0
	LOCAL:2 = (TEQUIP:媚薬/100 + ABL:欲望 - ABL:奉仕精神)*2
ENDSELECT

;ABL:手,手
IF COND("調教側")
	CALL COUNTER_ABL, 0, 5, 7, 8, 141
ELSE
	CALL COUNTER_ABL, 0, 90, 97, 105
ENDIF
IF ABL:手
	ABL:手 += TALENT:献身 + LOCAL:1 + ABL:指使い
	SIF COND("手に弱い", PLAYER)
		ABL:手 += 5
ENDIF

;ABL:口,口
IF COND("調教側")
	CALL COUNTER_ABL, 1, 1
ELSE
	CALL COUNTER_ABL, 1, 91, 98, 150
ENDIF
IF ABL:口
	ABL:口 += TALENT:献身 + LOCAL:1 + ABL:舌使い
	SIF COND("口に弱い", PLAYER)
		ABL:口 += 5
ENDIF

;ABL:胸,胸
IF COND("調教側")
ELSE
	CALL COUNTER_ABL, 2, 82, 92, 104
ENDIF
IF ABL:胸
	ABL:胸 += TALENT:献身 + TALENT:巨乳*3 + LOCAL:1 + ABL:魔乳
	SIF COND("胸に弱い", PLAYER)
		ABL:胸 += 5
ENDIF

;ABL:足,足
IF COND("調教側")
ELSE
	IF MAX(ABL:サドっ気 + TALENT:サド, ABL:奉仕精神) + ABL:PLAYER:マゾっ気 >= 2
		CALL COUNTER_ABL, 3, 95
	ELSE
		ABL:足 = 0
	ENDIF
ENDIF
IF ABL:足
	ABL:足 += TALENT:高慢 + TALENT:サド + LOCAL:1 + ABL:足使い
	SIF COND("脚に弱い", PLAYER)
		ABL:足 += 5
ENDIF

;ABL:顔面騎乗,顔面騎乗
IF COND("調教側")
	CALL COUNTER_ABL, 4, 98
ELSE
	;聖水イベントは前提条件が厳しくレアなので確率優遇
	CALL COM_ABLE144
	IF RESULT
		CALL COUNTER_ABL, 4, 144
	ELSEIF ABL:Ａ感覚 >= 3 || TALENT:オトコ
		CALL COUNTER_ABL, 4, 1, 9, 144
	ELSE
		CALL COUNTER_ABL, 4, 1, 144
	ENDIF
ENDIF
IF ABL:顔面騎乗
	ABL:顔面騎乗 += TALENT:好色 + TALENT:淫乱 + LOCAL:2 + ABL:腰使い
	SELECTCASE I:4
	CASE 1
		ABL:顔面騎乗 += TALENT:淫壷
		SIF COND("♀に弱い", PLAYER)
			ABL:顔面騎乗 += 5
	CASE 9
		ABL:顔面騎乗 += TALENT:尻穴狂い
		SIF COND("お尻に弱い", PLAYER)
			ABL:顔面騎乗 += 5
	CASE 144
		;比較的レアイベントなので確率高め
		ABL:顔面騎乗 += ABL:サドっ気*10 + TALENT:サド*50
		SIF TALENT:倒錯的
			ABL:顔面騎乗 += 50
		SELECTCASE BASE:尿意
		CASE IS >= 20000
			ABL:顔面騎乗 += 100
		CASE IS >= 15000
			ABL:顔面騎乗 += 50
		ENDSELECT
	ENDSELECT
ENDIF

;ABL:騎乗位,騎乗位
IF COND("調教側")
ELSE
	;基本はＶ騎乗位だが、Ａと尿道でも出来るかで分岐
	IF CHECK("Ａインサート") && CHECK("尿道インサート")
		;淫壷と尻穴狂いの有無で確率にすごい偏りが出る
		IF TALENT:淫壷 && TALENT:尻穴狂い == 0 && TALENT:尿道狂い == 0 && RAND:10
			CALL COUNTER_ABL, 5, 65, 66, 67, 68
		ELSEIF TALENT:淫壷 == 0 && TALENT:尻穴狂い && TALENT:尿道狂い == 0 && RAND:10
			CALL COUNTER_ABL, 5, 75
		ELSEIF TALENT:淫壷 == 0 && TALENT:尻穴狂い == 0 && TALENT:尿道狂い && RAND:10
			CALL COUNTER_ABL, 5, 88
		ELSE
			CALL COUNTER_ABL, 5, 65, 66, 67, 68, 75, 88
		ENDIF
	ELSEIF CHECK("尿道インサート")
		;淫壷と尿道狂いの有無で確率にすごい偏りが出る
		IF TALENT:淫壷 && TALENT:尿道狂い == 0 && RAND:10
			CALL COUNTER_ABL, 5, 65, 66, 67, 68
		ELSEIF TALENT:淫壷 == 0 && TALENT:尿道狂い && RAND:10
			CALL COUNTER_ABL, 5, 88
		ELSE
			CALL COUNTER_ABL, 5, 65, 66, 67, 68, 88
		ENDIF
	ELSEIF CHECK("Ａインサート")
		;淫壷と尻穴狂いの有無で確率にすごい偏りが出る
		IF TALENT:淫壷 && TALENT:尻穴狂い == 0 && RAND:10
			CALL COUNTER_ABL, 5, 65, 66, 67, 68
		ELSEIF TALENT:淫壷 == 0 && TALENT:尻穴狂い && RAND:10
			CALL COUNTER_ABL, 5, 75
		ELSE
			CALL COUNTER_ABL, 5, 65, 66, 67, 68, 75
		ENDIF
	ELSE
		CALL COUNTER_ABL, 5, 65, 66, 67, 68
	ENDIF
ENDIF
IF ABL:騎乗位
	;アナル騎乗位や尿道騎乗位はしてこないかも
	IF I:5 >= 70 && I:5 <= 79 && TALENT:尻穴狂い == 0 && ABL:Ａ感覚 < 5
		CALL COM_ABLE65
		IF RESULT
			SIF COND("発情期") || EXP:Ａ性交経験 < MIN(50, EXP:Ｖ性交経験)
				I:5 = 65
		ENDIF
	ELSEIF I:5 == 88 && TALENT:尿道狂い == 0
		CALL COM_ABLE65
		IF RESULT
			SIF COND("発情期") || EXP:尿道性交経験 < MIN(50, EXP:Ｖ性交経験)
				I:5 = 65
		ENDIF
	ENDIF

	ABL:騎乗位 += TALENT:好色 + TALENT:淫乱 + LOCAL:2 + ABL:腰使い
	SIF MARK:淫紋 && TALENT:オトコ == 0
		ABL:騎乗位 += (MARK:淫紋 + 1)/2

	SELECTCASE I:5
	CASE 65 TO 69
		SIF TARGET == ASSI
			I:5 = 65
		ABL:騎乗位 += ABL:Ｖ感覚 + TALENT:淫壷
		SIF INSERT("Ｖインサート")
			ABL:騎乗位 += 5
		SIF COND("♀に弱い", PLAYER)
			ABL:騎乗位 += 5
	CASE 75
		ABL:騎乗位 += ABL:Ａ感覚 + TALENT:尻穴狂い
		SIF INSERT("Ａインサート")
			ABL:騎乗位 += 5
		SIF COND("お尻に弱い", PLAYER)
			ABL:騎乗位 += 5
	CASE 88
		ABL:騎乗位 += TALENT:尿道狂い*3
		SIF INSERT("尿道インサート")
			ABL:騎乗位 += 5
	ENDSELECT
ENDIF

;ABL:キス,キス
IF COND("調教側")
	CALL COUNTER_ABL, 6, 6
ELSE
	CALL COUNTER_ABL, 6, 6
ENDIF
IF ABL:キス
	ABL:キス += TALENT:恋慕 + TALENT:蕩唇 + LOCAL:2 + ABL:舌使い
	SIF COND("唇に弱い", PLAYER)
		ABL:キス += 5
ENDIF

;ABL:お尻責め,お尻責め
IF COND("調教側")
	CALL COUNTER_ABL, 7, 2, 142
ELSE
	CALL COUNTER_ABL, 7, 96
ENDIF
IF ABL:お尻責め && MAX(ABL:サドっ気 + TALENT:サド, ABL:奉仕精神) + ABL:PLAYER:マゾっ気 >= 6
	ABL:お尻責め += ABL:サドっ気/2 + TALENT:サド + ABL:PLAYER:マゾっ気/2 + LOCAL:1
	;アナル調教知識
	SIF GETBIT(TALENT:調教知識, 3)
		ABL:お尻責め += 5
ELSE
	ABL:お尻責め = 0
	I:7 = 0
ENDIF

;ABL:レイプ,TARGETのペニス使用or逆レイプ
IF COND("調教側")
	CALL COUNTER_ABL, 8, 60, 61, 64, 65, 71, 75, 140
ELSE
	CALL COUNTER_ABL, 8, 4, 120, 126
ENDIF
IF ABL:レイプ
	ABL:レイプ += LOCAL:2 + ABL:腰使い
	IF TALENT:オトコ && TALENT:PLAYER:オトコ
		ABL:レイプ += ABL:ＢＬっ気
	ELSEIF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
		ABL:レイプ += ABL:レズっ気 + TALENT:両刀
	ENDIF
	SIF PENIS(TARGET)
		ABL:レイプ += ABL:欲望/2
	;オトコ
	SIF TALENT:オトコ
		ABL:レイプ += 5
ENDIF

;ABL:素股,素股
IF COND("調教側")
ELSE
	CALL COUNTER_ABL, 9, 80, 93, 94
ENDIF
SIF ABL:素股
	ABL:素股 += TALENT:献身 + LOCAL:1 + ABL:腰使い/2

;ABL:道具
IF COND("調教側")
ELSE
	CALL COUNTER_ABL, 10, 304, 305
ENDIF
SIF ABL:道具
	ABL:道具 += LOCAL:2 + TALENT:サド*5

;COMABLEで実行条件を判定するためのFLAGを解除
FLAG:10 = 0
TARGET = LOCAL:100


@COUNTER_ABL, ARG, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9
#DIM LCOUNT
ABL:(40 + ARG) = 0
LOCAL = 1
FOR LCOUNT, 1, 10
	TRYCALLFORM COM_ABLE{ARG:LCOUNT}
	IF RESULT
		ABL:(40 + ARG) += 1
		SIF RAND:LOCAL == 0 || I:ARG == 0
			I:ARG = ARG:LCOUNT
		LOCAL += 1
	ENDIF
	SIF ARG:(LCOUNT + 1) == 0
		BREAK
NEXT


;ここで一括管理
@COUNTER_SYS_1
#DIM LCOUNT
;カウンターのフラグ消去
VARSET TFLAG, 0, 90, 98

;Ｗ押し倒しや３Ｐ、ふにゃふにゃ、焦らしの時にはカウンターは無し
SIF TEQUIP:Ｗ押し倒し || SELECTCOM:1 >= 0 || SELECTCOM:2 >= 0 || STATE("ふにゃふにゃ", TARGET) || TEQUIP:PLAYER:てかげん || COND("排泄：限界")
	RETURN 0
;カウンターしてこないコマンド群
SELECTCASE SELECTCOM
CASE 13, 24, 133 TO 135, 138, 141 TO 149, 151, 170 TO 199, 260 TO 279
	RETURN 0
ENDSELECT

$COUNTER_AGAIN
;-------------------------------------------------
;反撃確率算出　カウンターが起き得るコマンドにはTFLAG:90を割り振る
;-------------------------------------------------
;アルカナカウンター確率算出	TFLAG:94
;カウンターで使ってくるアルカナひとつに付き20
;-------------------------------------------------
VARSET LOCAL
FOR LCOUNT, 230, 230 + NUM("存在するアルカナ")
	;絶頂してると駄目
	SIF NOWEX:絶頂数合計
		BREAK
	SIF USE_MAGIC(LCOUNT, "カウンター") == 0
		CONTINUE

	TFLAG:94 += 20

	LOCAL += 1
	SIF RAND:LOCAL == 0
		TFLAG:97 = LCOUNT
NEXT

;-------------------------------------------------
;反撃判定　TFLAG:90
;-------------------------------------------------
;反撃確定
IF COUNTER_TRY()
	;アルカナカウンター
	IF TFLAG:90 == 9
		;PRINTL ＜アルカナ詠唱＞
		;誰にかけるかで分岐
		SELECTCASE CONDS("カウンター：アルカナ")
		CASE "ミニマム", "センシブ", "イーサグン", "バインド", "エンラジ", "ヴィジョン"
			CALL TRAIN_MESSAGE_ARCANA, TFLAG:97, TARGET, PLAYER
		CASEELSE
			CALL TRAIN_MESSAGE_ARCANA, TFLAG:97, TARGET, TARGET
		ENDSELECT

		PRINTL 
		CALL COUNTER_KOJO, "アルカナ"
	ELSE
		PRINTL 
		CALL COUNTER_POWER
		SELECTCASE CONDS("カウンター：強度")
		CASE "失敗"
			;連続カウンターしているなら、わざわざしなかったときの台詞は喋らない
			SIF TFLAG:95 == 0
				CALL COUNTER_KOJO, CONDS("カウンター")
			TFLAG:90 = 0
			TFLAG:95 = 0
		CASEELSE
			;IF TFLAG:95
			;	PRINTL ＜連続カウンター＞
			;ELSE
				PRINTL ＜カウンター＞
			;ENDIF
			TFLAG:95 += 1
			CALL COUNTER_KOJO, CONDS("カウンター")
		ENDSELECT
	ENDIF
;反撃せず
ELSE
	TFLAG:90 = 0
	TFLAG:95 = 0
	;カウンターをしないときの口上
	SIF RAND:4 == 0 && TFLAG:95 == 0
		CALL COUNTER_KOJO, CONDS("カウンター")
ENDIF
;SIF TFLAG:95
;	GOTO COUNTER_AGAIN

;-------------------------------------------------
;撃沈していないなら、恍惚チェック
;-------------------------------------------------
@COUNTER_SYS_2
;-------------------------------------------------
;恍惚状態からの復帰判定
;-------------------------------------------------
IF STATE("恍惚", PLAYER)
	CALL COUNTER_REVIVAL
	;恍惚から復帰（恍惚になったターンは復活しません）
	IF STATE("恍惚", PLAYER) == 0
		TFLAG:90 = 14
		CALL COUNTER_KOJO, "恍惚から復帰"
	ENDIF
ELSE
	CALL COUNTER_FASCINATION
ENDIF
;-------------------------------------------------
;撃沈判定
;精力0時に助手は行動不可・主人は復活判定
;-------------------------------------------------
CALL COUNTER_DOWN

RETURN 0

@CHECK_OSHITAOSHI, ARG
;ARG msの時間の猶予で押し倒しの選択肢表示＆TEQUIP:押し倒し等の処理を行う
LOCAL = MULTIPLY(ARG, 100 - LIMIT(ABL:ＬＶ - ABL:PLAYER:ＬＶ, -10, 5)*5)
PRINTFORML  [0] 抵抗する！
PRINTFORML  [1] 身を委ねる

IF CONFIG("制限時間撤廃")
	CALL INPUT_SELECT, 2
ELSE
	TINPUT LOCAL, 100, 1
ENDIF

IF RESULT
	TEQUIP:押し倒し = 1
	;インサート中なら騎乗位へ
	SIF INSERT("インサート")
		TEQUIP:体位 = CLEARBITS(TEQUIP:体位, 0, 9) + 1p5
ENDIF

@COUNTER_TRY()
#FUNCTION
;反撃確率を算出してLOCALに代入
LOCAL = 20
;連続カウンター
LOCAL -= TFLAG:95*50
;臆病
LOCAL -= TALENT:臆病*10
;強気
LOCAL += TALENT:強気*20
;無関心
LOCAL -= TALENT:無関心*3
;好奇心
LOCAL += TALENT:好奇心*3
;保守的
LOCAL -= TALENT:保守的*3
;一線越えない
LOCAL -= TALENT:一線越えない*10
;目立ちたがり
LOCAL += TALENT:目立ちたがり*3
;貞操観念
LOCAL -= TALENT:貞操観念*3
;貞操無頓着
LOCAL += TALENT:貞操無頓着*3
;抑圧
LOCAL -= TALENT:抑圧*10
;開放
LOCAL += TALENT:解放*10
;抵抗
LOCAL -= TALENT:抵抗*20
;恥じらい
LOCAL -= TALENT:恥じらい*5
;恥薄い
LOCAL += TALENT:恥薄い*3
;献身的
LOCAL += TALENT:献身*5
;好色
LOCAL += TALENT:好色*10
;淫乱
LOCAL += TALENT:淫乱*15
;男嫌い
SIF TALENT:男嫌い && TALENT:PLAYER:オトコ
	LOCAL -= 20
;サド
LOCAL += TALENT:サド*20
;恋慕
LOCAL += TALENT:恋慕*20
;妄信
LOCAL += TALENT:妄信*10
;小悪魔
LOCAL += TALENT:小悪魔*10
;マゾ
LOCAL -= TALENT:マゾ*20
;ドランク
SIF STATE("ドランク", TARGET) || STATE("悪酔い", TARGET)
LOCAL -= 10
;魔導人形
LOCAL += TALENT:魔導人形*10
;上位夢魔
LOCAL += TALENT:上位夢魔*10
;欲望
LOCAL += ABL:欲望*2
;奉仕精神
LOCAL += ABL:奉仕精神*2
;マゾっ気
LOCAL -= ABL:マゾっ気*4
;サドっ気
LOCAL += ABL:サドっ気*4
;欲情
LOCAL += GET_PALAMLV(PALAM:欲情) * 3
;恐怖
LOCAL -= POWER(GET_PALAMLV(PALAM:恐怖), 2)
;不快
LOCAL -= GET_PALAMLV(PALAM:反感) * 2
;絶頂
LOCAL -= NOWEX:絶頂数合計 * 10
;連続絶頂中
LOCAL -= PALAM:連続絶頂COMBO数 * 5
;インサート時には積極的
SIF INSERT("Ｖインサート") && NOWEX:Ｖ絶頂 == 0 && ABL:Ｖ感覚 + TALENT:Ｖ敏感 >= 3
	LOCAL += 20
SIF INSERT("Ａインサート") && NOWEX:Ａ絶頂 == 0 && ABL:Ａ感覚 + TALENT:Ａ敏感 >= 3
	LOCAL += 20
SIF INSERT("尿道インサート") && NOWEX:尿道絶頂 == 0 && TALENT:尿道狂い
	LOCAL += 20
;秘湯の入浴剤+恥じらい
SIF TEQUIP:MASTER:入浴剤 == 2 && TALENT:恥じらい
	LOCAL += 10
;ミミックの１T目はボディミミック！
SIF NUM("ターン数") == 0 && NO:TARGET == 33 && CFLAG:8 < 4
	LOCAL += 100
;強絶頂なら反撃してこない
SIF MAX(NOWEX:Ｃ絶頂, NOWEX:Ｖ絶頂, NOWEX:Ａ絶頂, NOWEX:Ｂ絶頂, NOWEX:Ｍ絶頂, NOWEX:尿道絶頂) >= 2
	LOCAL = 0
;拘束、浣腸、触手時には反撃しない（暫定）
SIF TEQUIP:拘束 || TEQUIP:浣腸 || TEQUIP:触手 || TEQUIP:尿道ブジー
	LOCAL = 0
;排泄中
SIF NOWEX:放尿 || NOWEX:排泄 || NOWEX:射精
	LOCAL = 0
;恍惚or反撃してこないフラグがONor二人で押し倒し中
SIF STATE("恍惚", PLAYER) || CFLAG:9 || TEQUIP:Ｗ押し倒し
	LOCAL = 0
;インサート時に外に出させるなら、さらにはしてこない
SIF SHOOT("外") && BASE:PLAYER:絶頂 >= MAXBASE:PLAYER:絶頂 && INSERT("インサート")
	LOCAL = 0
;ペニバンでインサートされた時には一方的
SIF INSERT("Ｖインサート") && PENIS(PLAYER) == 0
	LOCAL = 0
;疲れている
SIF BASE:体力 - DOWNBASE:体力 <= 500 || BASE:気力 - DOWNBASE:気力 <= 0
	LOCAL = 0
;大怪我
SIF TALENT:Ｖ損傷 || TALENT:Ａ損傷
	LOCAL = 0
;ノンケ
SIF TALENT:オトコ && TALENT:PLAYER:オトコ && ABL:ＢＬっ気 == 0
	LOCAL = 0
SIF TALENT:オンナ && TALENT:PLAYER:オンナ && ABL:レズっ気 == 0
	LOCAL = 0
;やる気が無かったり様子見だったり
SELECTCASE CONDS("機嫌")
CASE "様子見", "やる気なし"
	LOCAL = 0
ENDSELECT

LOCAL:1 = SELECTCOM
SELECTCASE SELECTCOM
;何もしないコマンドの時には反撃率が高い
CASE 139
	IF LOCAL > 0 && COND("ながらオナニーフラグ") == 0
		LOCAL += 50
		LOCAL:1 = PREVCOM
	ELSE
		LOCAL = 0
	ENDIF
;尿道プレイの時には反撃なし
CASE 15, 83 TO 88, 151
	LOCAL = 0
ENDSELECT

;マイナスまたは100％を超えていたら修正
LOCAL = LIMIT(LOCAL, 0, 100)

;何もしないコマンド選択の時には前回コマンドを参照
SELECTCASE LOCAL:1
;Ｖインサート
CASE 60 TO 69
	TFLAG:90 = 1
;アナルインサート
CASE 70 TO 79
	TFLAG:90 = 2
;愛撫とアイテム
CASE 0 TO 2, 4 TO 59
	TFLAG:90 = 3
;奉仕
CASE 90 TO 119
	TFLAG:90 = 4
;SM
CASE 130 TO 132, 136 TO 138
	TFLAG:90 = 5
ENDSELECT

IF INSERT("Ｖインサート")
	TFLAG:90 = 1
ELSEIF INSERT("Ａインサート")
	TFLAG:90 = 2
ENDIF

;アルカナカウンターは絶頂してると駄目
IF RAND:(LIMIT(COND("アルカナカウンター"), 1, 70) ) > RAND:100
	TFLAG:90 = 9
	SIF LOCAL > 0 && COND("アルカナカウンター") >= 40
		LOCAL += 20
ENDIF

;押し倒し時はアルカナのみ
SIF TEQUIP:押し倒し && TFLAG:90 != 9
	LOCAL = 0

SIF CONFIG("内部データの表示")
	PRINTFORML 反撃確率：{LOCAL}％
SIF LOCAL > RAND:100 && TFLAG:90
	RETURNF LOCAL

RETURNF 0

;汎用文
@COUNTER_TEXT, ARGS
;カウンターしないフラグ
SIF CFLAG:9
	RETURN 0
SELECTCASE ARGS
CASE "カウンター無し"
	;ペニバンでインサートされた時には一方的
	IF (SELECTCOM >= 60 && SELECTCOM <= 79) && PENIS(PLAYER) == 0
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の激しい抽送に翻弄されている……
	ELSEIF NOWEX:絶頂数合計
		PRINTFORMW %CALLNAME:TARGET%は恍惚としている…
	ELSEIF TEQUIP:拘束
		IF GETBIT(TEQUIP:拘束, 40)
			PRINTFORMW %CALLNAME:TARGET%は縄に拘束されて動けない……
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は拘束されて動けない……
		ENDIF
	ELSEIF TEQUIP:触手
		PRINTFORMW %CALLNAME:TARGET%は蠢く触手に翻弄されている……
	ELSEIF TEQUIP:押し倒し
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の上で笑みを浮かべている……
	ELSE
		PRINTL 
		PRINTFORMW %CALLNAME:TARGET%は様子をうかがっている……
	ENDIF
CASE "Ｖインサート"
	SELECTCASE CONDS("カウンター：強度")
	CASE "必殺"
		PRINTFORMW %CALLNAME:TARGET%は膣壁を巧みに操り%CALLNAME:PLAYER%のペニスを締め上げてきた！
	CASE "強"
		PRINTFORMW %CALLNAME:TARGET%は膣壁を操り%CALLNAME:PLAYER%のペニスをしごいてきた……
	CASE "中"
		PRINTFORMW %CALLNAME:TARGET%は膣壁を操り%CALLNAME:PLAYER%のペニスをしごいてきた……
	CASE "弱"
		PRINTFORMW %CALLNAME:TARGET%は膣壁を操り%CALLNAME:PLAYER%のペニスをしごいてきた……
	CASEELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の激しい抽送に翻弄されている……
	ENDSELECT
CASE "Ａインサート"
	SELECTCASE CONDS("カウンター：強度")
	CASE "必殺"
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の敏感な所を一気に責めたててきた！
	CASE "強"
		PRINTFORMW %CALLNAME:TARGET%は隙を見て反撃してきた……
	CASE "中"
		PRINTFORMW %CALLNAME:TARGET%は隙を見て反撃してきた……
	CASE "弱"
		PRINTFORMW %CALLNAME:TARGET%は隙を見て反撃してきた……
	CASEELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の責めに翻弄されている……
	ENDSELECT
CASE "愛撫"
	SELECTCASE CONDS("カウンター：強度")
	CASE "必殺"
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の敏感な所を一気に責めたててきた！
	CASE "強"
		PRINTFORMW %CALLNAME:TARGET%は隙を見て反撃してきた……
	CASE "中"
		PRINTFORMW %CALLNAME:TARGET%は隙を見て反撃してきた……
	CASE "弱"
		PRINTFORMW %CALLNAME:TARGET%は隙を見て反撃してきた……
	CASEELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の責めに翻弄されている……
	ENDSELECT
CASE "奉仕"
	IF PENIS(PLAYER) == 0 || RAND:2
		SELECTCASE CONDS("カウンター：強度")
		CASE "必殺"
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の敏感な所を一気に責めたててきた！
		CASE "強"
			PRINTFORMW %CALLNAME:TARGET%は休む間を与えず%CALLNAME:PLAYER%に快感を与え続けている……
		CASE "中"
			PRINTFORMW %CALLNAME:TARGET%は休む間を与えず%CALLNAME:PLAYER%に快感を与え続けている……
		CASE "弱"
			PRINTFORMW %CALLNAME:TARGET%は休む間を与えず%CALLNAME:PLAYER%に快感を与え続けている……
		CASEELSE
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の体に夢中になっている……
		ENDSELECT
	ELSE
		SELECTCASE CONDS("カウンター：強度")
		CASE "必殺"
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%のペニスを激しくしごき上げてきた！
		CASE "強"
			PRINTFORMW %CALLNAME:TARGET%は休む間を与えず%CALLNAME:PLAYER%のペニスをしごき続けている……
		CASE "中"
			PRINTFORMW %CALLNAME:TARGET%は休む間を与えず%CALLNAME:PLAYER%のペニスをしごき続けている……
		CASE "弱"
			PRINTFORMW %CALLNAME:TARGET%は休む間を与えず%CALLNAME:PLAYER%のペニスをしごき続けている……
		CASEELSE
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%のペニスに夢中になっている……
		ENDSELECT
	ENDIF
CASE "ＳＭ"
	SELECTCASE CONDS("カウンター：強度")
	CASE "必殺"
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の敏感な所を一気に責めたててきた！
	CASE "強"
		PRINTFORMW %CALLNAME:TARGET%は隙を見て反撃してきた……
	CASE "中"
		PRINTFORMW %CALLNAME:TARGET%は隙を見て反撃してきた……
	CASE "弱"
		PRINTFORMW %CALLNAME:TARGET%は隙を見て反撃してきた……
	CASEELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の責めに翻弄されている……
	ENDSELECT
ENDSELECT

@COUNTER_POWER
;必殺確率を算出
SIF CONFIG("内部データの表示")
	PRINTFORML 必殺確率：{LIMIT(ABL:技巧*2-ABL:PLAYER:技巧+TALENT:上位夢魔*2, 0, 100)}％
IF LIMIT(ABL:技巧*2-ABL:PLAYER:技巧+TALENT:上位夢魔*2, 0, 100) > RAND:100
	LOCAL = 20000
ELSE
	;反撃強度を算出してCに代入
	;技巧等から反撃の強さを決定しています。
	LOCAL = LIMIT(RAND:10000 + LIMIT(ABL:技巧 - ABL:PLAYER:技巧, -10, 10)*(300 + RAND:201), 100, 10000)
	LOCAL = MULTIPLY(LOCAL, 100+ABL:ＬＶ*10+ABL:奉仕精神*5+TALENT:上位夢魔*20-ABL:PLAYER:ＬＶ*10)
ENDIF

;女同士の場合TARGETの両刀・レズっ気・レズ中毒で増減
SIF TALENT:PLAYER:オトコ == 0 && TALENT:オトコ == 0
	LOCAL = MULTIPLY(LOCAL, 75+TALENT:両刀*25+ABL:レズっ気*3)

;アクセサリ効果（精神力）
LOCAL = DIVIDE(LOCAL, 100 + TALENT:PLAYER:精神力強化*2)
;エオルグン
SIF STATE("エオルグン", PLAYER)
	TIMES LOCAL, 0.80
;ドランク
IF STATE("悪酔い")
	TIMES LOCAL, 0.40
ELSEIF STATE("ドランク")
	TIMES LOCAL, 0.80
ENDIF

SELECTCASE CONDS("カウンター")
CASE "Ｖインサート", "Ａインサート"
	LOCAL = MULTIPLY(LOCAL, 100 + STATE("ウルズグン")*15)
ENDSELECT

;何もしない場合には相手有利
SIF SELECTCOM == 139
	LOCAL += 500

LOCAL = MULTIPLY(LOCAL, 100 + STATE("シギルグン")*10)


;テスト用
SIF CONFIG("内部データの表示")
	PRINTFORML 反撃強度：{LOCAL}
;カウンターの威力によって、性行動(SOURCE:性行動)、達成(SOURCE:達成感)のソースが上昇
SELECTCASE LOCAL
;失敗
CASE IS < 500
	SOURCE:性行動 += LOCAL
	SOURCE:達成感 += LOCAL
	TFLAG:96 = 0
;弱
CASE 500 TO 2999
	SOURCE:性行動 += LOCAL/2 + 250
	SOURCE:達成感 += LOCAL/2 + 250
	TFLAG:96 = 1
;中　MAX3000
CASE 3000 TO 7999
	SOURCE:性行動 += LOCAL/4 + 1000
	SOURCE:達成感 += LOCAL/4 + 1000
	TFLAG:96 = 2
;強　MAX4500
CASE 8000 TO 19999
	SOURCE:性行動 += LOCAL/8 + 2000
	SOURCE:達成感 += LOCAL/8 + 2000
	TFLAG:96 = 3
;必殺
CASEELSE
	SOURCE:性行動 += LOCAL/16 + 3250
	SOURCE:達成感 += LOCAL/16 + 3250
	TFLAG:96 = 4
ENDSELECT

BASE:PLAYER:絶頂 += LOCAL

;射精ゲージ増加の一時保存
TCVAR:PLAYER:101 += LOCAL

RETURN LOCAL


@COUNTER_FASCINATION
;恍惚、押し倒し判定
SIF TEQUIP:ラブトラップ || TEQUIP:拘束 || TEQUIP:触手 || TEQUIP:尿道ブジー || COND("３Ｐ")
	RETURN 0
SIF STATE("撃沈", PLAYER) || STATE("恍惚", PLAYER)
	RETURN 0

SELECTCASE SELECTCOM
CASE 280, 304, 305
	RETURN 0
ENDSELECT

;恍惚確率を算出する
TFLAG:91 = TCVAR:PLAYER:1*3 + (TALENT:魅惑 + TALENT:謎の魅力 + TALENT:魅力 + ABL:技巧)*2 - ABL:PLAYER:技巧*4 + TALENT:上位夢魔*3 + COND("カウンターの強度")*2

;弱点の処理
SIF TFLAG:90
	TFLAG:92 = CALCF("弱点突かれ", SELECTCOM)
;二人で押し倒し
IF TEQUIP:Ｗ押し倒し
	TFLAG:91 += TALENT:ASSI:魅惑+TALENT:ASSI:謎の魅力+TALENT:ASSI:魅力+ABL:ASSI:技巧+ABL:ASSI:奉仕精神+TALENT:ASSI:上位夢魔 + 2
	SIF TFLAG:90
		TFLAG:92 += CALCF("弱点突かれ", SELECTCOM:1)
ENDIF
TFLAG:91 += TFLAG:92

;エオルグン
SIF STATE("エオルグン", PLAYER)
	TFLAG:91 -= 20
;ドランク
SIF STATE("ドランク", PLAYER)
	TFLAG:91 += 10
;アクセサリ効果（精神力）
TFLAG:91 = DIVIDE(TFLAG:91, 100 + TALENT:PLAYER:精神力強化*2)

;弱点優遇措置
TFLAG:91 = LIMIT(TFLAG:91, TFLAG:92, 100)
;5％未満なら切捨て
SIF TFLAG:91 < 5
	TFLAG:91 = 0

;テスト用
SIF CONFIG("内部データの表示")
	PRINTFORML 恍惚確率：{TFLAG:91}％

;恍惚、押し倒し判定
;カウンターされているなら、恍惚になったりする
IF COND("恍惚確率") > RAND:100 && TFLAG:90
	PRINTL 
	PRINTFORML %CALLNAME:PLAYER%は恍惚となってしまった……
	TEQUIP:押し倒し = 1
	CALL ADD_STATE, PLAYER, "恍惚"
	FORCEWAIT
	;てかげん中なら、解除
	IF TEQUIP:てかげん
		TEQUIP:てかげん = 0
		PRINTFORMW 主導権を%CALLNAME:TARGET%に奪われ、%CALLNAME:PLAYER%はこれ以上てかげんを続けることが出来ない…！
	ENDIF
	IF BASE:気力 > 0
		TFLAG:90 = 10
		CALL COUNTER_KOJO, "恍惚"
	ENDIF
	PRINTL 
ENDIF

;以下、押し倒し状態でないなら押し倒し判定
SIF TEQUIP:押し倒し
	RETURN 0
;やってこない状態
SIF TEQUIP:アイマスク || TEQUIP:浣腸 || TEQUIP:触手 || TEQUIP:尿道ブジー || STATE("ふにゃふにゃ", TARGET) || STATE("恍惚", PLAYER) || CFLAG:9
	RETURN 0
;大怪我
SIF TALENT:Ｖ損傷 || TALENT:Ａ損傷
	RETURN 0
;性癖の問題
SIF TALENT:PLAYER:オトコ && TALENT:オトコ && ABL:ＢＬっ気 == 0
	RETURN 0
SIF TALENT:PLAYER:オンナ && TALENT:オンナ && ABL:レズっ気 == 0
	RETURN 0
;体位によっては無理
SELECTCASE CONDS("体位")
CASE "正常位", "屈曲位", "後背位"
	RETURN 0
ENDSELECT

TFLAG:93 = ABL:欲望*5 + BASE:欲求不満*2 + (ABL:サドっ気 - ABL:マゾっ気)*3 + COND("恍惚確率") - COND("絶頂", PLAYER)*5 - NOWEX:絶頂数合計*20
SIF TALENT:強気
	TFLAG:93 += 30
SIF TALENT:淫乱
	TFLAG:93 += 20
SIF TALENT:好色
	TFLAG:93 += 15
SIF STATE("ドランク", TARGET) || STATE("悪酔い", TARGET)
	TFLAG:93 += 30
;イかされまくっても、押し倒してこなくなる
SIF BASE:性欲 >= MAXBASE:性欲
	TFLAG:93 -= 20

;アクセサリ（押し倒し率ＵＰ）
SIF TALENT:PLAYER:押し倒され率 > 0
	TFLAG:93 += TALENT:PLAYER:押し倒され率

;気力による補正(プラスマイナス補正はここまでで済ませる)
TFLAG:93 = TFLAG:93*BASE:気力/MAXBASE:気力
;欲情による補正
SELECTCASE PALAM:欲情
CASE IS < 500
	TIMES TFLAG:93 , 0.10
CASE IS < 3000
	TIMES TFLAG:93 , 0.30
CASE IS < 10000
	TIMES TFLAG:93 , 0.50
CASE IS > 30000
	TIMES TFLAG:93 , 1.20
ENDSELECT

;押し倒し頻度補正
SIF TCVAR:152 > 0
	TFLAG:93 /= TCVAR:152

;押し倒し確率が０以下の場合は当然押し倒さない
TFLAG:93 = MAX(TFLAG:93, 0)

;テスト用
SIF CONFIG("内部データの表示")
	PRINTFORML 押し倒し確率：{TFLAG:93}％

IF COND("押し倒し確率") > RAND:100
	;押し倒し頻度減少
	TCVAR:152 += 5 + RAND:6

	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%を押し倒してきた！
	LOCAL = LIMIT( (100 - COND("押し倒し確率") )*30, 1000, 10000)

	CALL CHECK_OSHITAOSHI, LOCAL

	IF TEQUIP:押し倒し
		TFLAG:90 = 11
		IF RAND:2 && TALENT:オンナ && TALENT:恥薄い && (TALENT:強気 || ABL:サドっ気 + ABL:PLAYER:マゾっ気 >= 5) && COND("室内") && USE_V(TARGET)
			PRINTL 
			;PRINTFORML %CALLNAME:TARGET%は飛び上がって%CALLNAME:PLAYER%の顔を太ももで挟み込むと、股間を押し付けて視界を奪いながらベッドに押し倒してきた！
			VARSET LOCAL
			IF TEQUIP:シックスナイン == 0 && PENIS(PLAYER) && COND("指の使用")
				CALL COM_ABLE99
				LOCAL:1 = RESULT
			ENDIF
			IF INSERT("合体中") == 0 && PENIS(PLAYER) && COND("ベテラン：Ｖセックス")
				CALL COM_ABLE65
				LOCAL:2 = RESULT
			ENDIF

			IF LOCAL:2 && RANDIF(2, LOCAL:1)
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%にのしかかり、当然の様にペニスを自らの膣で咥え込んできた！

				CALL SET_INSERT, TARGET, PLAYER, 65
			ELSEIF LOCAL:1 && RAND:2
				PRINTFORM %CALLNAME:TARGET%は仰向けになった%CALLNAME:PLAYER%のペニスを手で掴み、
				IF RAND:2
					PRINTFORML 顔を跨いで股間を鼻に擦りつけてきた！
				ELSE
					PRINTFORML お尻を顔に押し付けるようにして乗ってきた！
				ENDIF
				TEQUIP:シックスナイン = 90
				TEQUIP:PLAYER:シックスナイン = 1
				TEQUIP:顔面騎乗 = 1
			ELSE
				PRINTFORML %CALLNAME:TARGET%は仰向けに倒された%CALLNAME:PLAYER%の顔を膝立ちで跨ぎ、ゆっくりと腰を落として股間で鼻と口を塞いできた！
				TEQUIP:顔面騎乗 = 1
			ENDIF
		ENDIF
		CALL COUNTER_KOJO, "押し倒し"
		PRINTFORMW %CALLNAME:PLAYER%は押し倒されてしまった……！
		IF TEQUIP:てかげん
			TEQUIP:てかげん = 0
			PRINTFORMW 主導権を%CALLNAME:TARGET%に奪われ、%CALLNAME:PLAYER%はこれ以上てかげんを続けることが出来ない…！
		ENDIF
		;ムード増加
		TFLAG:33 += 5
	ELSE
		IF TALENT:恋慕 && PLAYER == MASTER
			PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%を抱きとめて、自分に任せるようにと耳元で囁いた…
			IF CFLAG:9 == 0 && MOOD_HEART() >= 3
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%に身を委ねる事にしたようだ…
				CFLAG:9 = 1
			ENDIF
		ELSE
			IF RAND:2 == 0
				PRINTFORMW しかし%CALLNAME:PLAYER%はやんわりと払いのけた！
			ELSE
				PRINTFORMW しかし%CALLNAME:PLAYER%は巧みに身をかわした！
			ENDIF
			;ムード減少
			TFLAG:33 -= 5
		ENDIF
	ENDIF
	PRINTL 
ENDIF

;基本的にはMASTERかASSIかPLAYER:1だけが対象
@COUNTER_DOWN
#DIM MEMO_LINECOUNT

;助手はコマンドを実行できなくなる
IF ASSI && STATE("撃沈", ASSI) == 0
	IF (ASSIPLAY || (SELECTCOM >= 160 && SELECTCOM <= 169) || TEQUIP:Ｗ押し倒し || SELECTCOM:1 >= 0 || SELECTCOM:2 >= 0) && (BASE:ASSI:体力 < 500 || BASE:ASSI:精力 <= 0)
		CALL ASSI_DOWN, ASSI
		TFLAG:90 = 15
		CALL COUNTER_KOJO, "助手が限界"
	ENDIF
ENDIF
;主人
IF BASE:MASTER:精力 <= 0
	PRINTFORMW %CALLNAME:MASTER%はもう体力の限界だ……
	;ストックが残っていれば復活
	IF BASE:MASTER:ストック >= 1
		IF TEQUIP:触手
			PRINTFORMW %CALLNAME:MASTER%は気力を振り絞って再び触手を操りはじめた……！
		ELSEIF STATE("恍惚", PLAYER)
			PRINTFORMW %CALLNAME:MASTER%は恍惚としながらも、気力を振り絞った……！
		ELSE
			PRINTFORMW %CALLNAME:MASTER%は気力を振り絞ってもう一度立ち上がった……！
		ENDIF
		;精力を限界まで回復
		BASE:MASTER:精力 = MAXBASE:MASTER:精力
		;ストックを減少
		BASE:MASTER:ストック -= 1
		TFLAG:90 = 12
		PRINTL 
		MEMO_LINECOUNT = LINECOUNT

		CALL COUNTER_KOJO, "ストック使用"

		SIF LINECOUNT == MEMO_LINECOUNT
			CLEARLINE 1
	;そうでなければ調教を強制終了
	ELSE
		PRINTFORMW ★★★%CALLNAME:MASTER%の体力が限界です・調教を終了します★★★
		CALL ADD_STATE, MASTER, "撃沈"
		TFLAG:90 = 13
		PRINTL 
		MEMO_LINECOUNT = LINECOUNT

		CALL COUNTER_KOJO, "強制終了"

		SIF LINECOUNT == MEMO_LINECOUNT
			CLEARLINE 1

		BEGIN AFTERTRAIN
	ENDIF
ENDIF

RETURN 0

;助手の撃沈アナウンス。３Ｐなどだと助手以外でもここに来る
@ASSI_DOWN, ARG
SIF STATE("撃沈", ARG) || (BASE:ARG:体力 >= 500 && BASE:ARG:精力 > 0)
	RETURN 0
SIF ARG == ASSI
	PRINTFORML ★%TEXTS("助手の名称")%撃沈★
PRINTFORMW %CALLNAME:ARG%はもう体力の限界だ……
PRINTFORMW これ以上%CALLNAME:ARG%を調教に参加させるのは無理なようだ……
CALL ADD_STATE, ARG, "撃沈"
CALL SETFLAG, "撃沈", ARG


@COUNTER_REVIVAL
;復帰判定
SIF STATE("恍惚", PLAYER) == 0
	RETURN 0
;復帰確率を算出して代入(母乳を飲んでいると復帰率減少)
LOCAL = 10 + ABL:PLAYER:技巧*4 - ABL:技巧*4 - EXP_GAIN(74, PLAYER)*5
SIF BASE:PLAYER:精力*2 >= MAXBASE:PLAYER:精力
	LOCAL += 10
SIF COND("絶頂", PLAYER) == 0
	LOCAL += 5
SIF ABL:PLAYER:ＬＶ > ABL:ＬＶ
	LOCAL += LIMIT(ABL:PLAYER:ＬＶ - ABL:ＬＶ, 5, 20)
;エオルグン
SIF STATE("エオルグン", PLAYER)
	LOCAL += 10
;アクセサリ効果（技巧）
SIF TALENT:PLAYER:技巧強化 > 0
	LOCAL = MULTIPLY(LOCAL, 100 + TALENT:PLAYER:技巧強化)
;１Ｔごとに復帰率が５％ＵＰ
SELECTCASE TFLAG:99
CASE 0
	LOCAL = 0
CASEELSE
	LOCAL += (TFLAG:99 - 1)*5
ENDSELECT
;マイナスまたは100％を超えていたら修正
LOCAL = LIMIT(LOCAL, 0, 100)
SIF CONFIG("内部データの表示")
	PRINTFORML 復帰確率：{LOCAL}％

IF LOCAL > RAND:100
	PRINTFORML %CALLNAME:PLAYER%は正気に返った！
	CALL REMOVE_STATE, PLAYER, "恍惚"
	;自動で押し倒しから復帰するのはオミット
	;IF LOCAL > RAND:100 && TEQUIP:Ｗ押し倒し == 0 && STATE("バインド", PLAYER) == 0
	;	PRINTFORMW %CALLNAME:PLAYER%は押し倒され状態から復帰した！
	;	TEQUIP:押し倒し = 0
	;ENDIF
ELSE
	PRINTFORMW %CALLNAME:PLAYER%は恍惚としている……
	TFLAG:99 += 1
ENDIF

;-------------------------------------------------
;特殊な技の管理
;ARGSに技名、ARGに技の強さなどを代入
;-------------------------------------------------
@COUNTER_SKILL, ARGS, ARG
#DIM LCOUNT

VARSET LOCAL

SELECTCASE ARGS
CASE "風の愛撫"
	SIF STATE("センシブ", PLAYER)
		RETURN 0
	PRINTFORML ＜%CALLNAME:TARGET%の風の愛撫！＞
	PRINTFORML %CALLNAME:TARGET%はそよ風を巻き起こした！
	PRINTFORMW 風が優しく%CALLNAME:PLAYER%の全身を愛撫する！
	IF 12 + LIMIT(ABL:ＬＶ - ABL:PLAYER:ＬＶ, -5, 5) >= RAND:20
		PRINTFORMW %CALLNAME:PLAYER%は敏感になってしまった！
		CALL ADD_STATE, PLAYER, "センシブ"
	ELSE
		PRINTFORMW だが%CALLNAME:PLAYER%には効果がなかった！
	ENDIF
CASE "ブラッドサック"
	SELECTCASE ARG
	;不発
	CASE 0
		PRINTFORML ＜%CALLNAME:TARGET%のブラッドサック！＞
		IF RAND:2 == 0 && TEQUIP:アイマスク
			IF RAND:3 == 0
				PRINTFORML しかし%CALLNAME:TARGET%が切り裂いたのは空気だけだった…
			ELSEIF RAND:2 == 0
				PRINTFORML しかし%CALLNAME:TARGET%の牙は虚空を噛んだ…
			ELSE
				PRINTFORML しかし%CALLNAME:TARGET%が噛み付いた空間には何もなかった…
			ENDIF
		ELSE
			IF RAND:3 == 0
				PRINTFORML しかし%CALLNAME:PLAYER%はギリギリの所で%CALLNAME:TARGET%の牙をかわした！
			ELSEIF RAND:2 == 0
				PRINTFORML しかし%CALLNAME:PLAYER%はひらりと避けた！
			ELSE
				PRINTFORML しかし%CALLNAME:PLAYER%は身をよじって逃げた！
			ENDIF
		ENDIF
		RETURN 0
	CASE IS < 10
		LOCALS = ！
	CASE IS < 20
		LOCALS = ！！！
	CASEELSE
		LOCALS = ！！！！！
	ENDSELECT

	PRINTFORML ＜%CALLNAME:TARGET%のブラッドサック%LOCALS%＞
	PRINTFORML %CALLNAME:PLAYER%の精力が{ARG}減少した！
	SIF BASE:体力 < MAXBASE:体力
		PRINTFORML %CALLNAME:TARGET%の体力が{LIMIT(MAXBASE:体力 - BASE:体力, 1, 100*ARG)}回復した！
	PRINTFORMW 吸血と共に名状し難い快感が%CALLNAME:PLAYER%を襲う……！
	BASE:PLAYER:精力 -= ARG
	TCVAR:PLAYER:109 += ARG
	BASE:体力 += LIMIT(MAXBASE:体力 - BASE:体力, 1, 100*ARG)

	BASE:PLAYER:絶頂 += MAXBASE:PLAYER:絶頂 * ARG / 10
	TCVAR:PLAYER:101 += MAXBASE:PLAYER:絶頂 * ARG / 10
	;絶頂フラグON
	TCVAR:PLAYER:2 = 1

;-------------------------------------------------
;口上からの呼び出し専用。
;カウンターやコマンド内派生・追加として使う場合はこちらを呼び出す。
;ふにゃふにゃを付加するのが目的なので快感等は上昇しない。上昇させる場合は呼び出し時に処理を追加すること。
;-------------------------------------------------
CASE "メルティキッス"
	;TARGETに口枷がはまっていると無理
	SIF TEQUIP:口枷
		RETURN 0
	PRINTFORML ＜%CALLNAME:TARGET%のメルティキッス！＞
	;調教者の口⇔メローナの口の汚れが移動
	CALL EQ_STAIN, PLAYER, TARGET, 0, 0

	;キス経験の増加
	EXP:70 += 1
	EXP:PLAYER:70 += 1

	;ハードスキン状態なら元に戻す。
	IF STATE("ハードスキン", PLAYER)
		CALL REMOVE_STATE, PLAYER, "ハードスキン"
		PRINTFORML %CALLNAME:PLAYER%は柔らかくなった！
		PRINTFORMW %CALLNAME:PLAYER%のハードスキンの効果が切れた！
	;PLAYERが恍惚
	ELSEIF STATE("恍惚", PLAYER)
		PRINTFORMW %CALLNAME:TARGET%は恍惚としている%CALLNAME:PLAYER%の唇を奪い、更に理性を蕩かせていく…
		BASE:PLAYER:絶頂 += 500 * ABL:PLAYER:Ｍ感覚 * ABL:技巧
	ELSEIF STATE("ふにゃふにゃ", PLAYER)
		PRINTFORML ふにゃふにゃになっていたところにキスを重ねられ、%CALLNAME:PLAYER%は恍惚となってしまった！
		CALL ADD_STATE, PLAYER, "恍惚"
		FORCEWAIT
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%にふにゃふにゃにされてしまった！（与える快感ダウン！）
		CALL ADD_STATE, PLAYER, "ふにゃふにゃ"
		;M感覚Lv3以上だと恍惚が付くかも
		IF STATE("恍惚", PLAYER) == 0 && ABL:PLAYER:Ｍ感覚 >= 3 && RAND:3 == 0
			PRINTFORML %CALLNAME:PLAYER%はさらに、%CALLNAME:TARGET%の巧みなキスに恍惚となってしまった！
			CALL ADD_STATE, PLAYER, "恍惚"
			FORCEWAIT
		ENDIF
	ENDIF

CASE "運命改竄"
	PRINTFORMW ＜%CALLNAME:TARGET%の運命改ざん！＞
	LOCAL = CUP:快Ｃ + CUP:快Ｖ + CUP:快Ａ + CUP:快Ｂ + CUP:快Ｍ
	PRINTFORM %CALLNAME:TARGET%
	SETCOLOR SETCOLOR_DUMMY()
	PRINTFORM に
	RESETCOLOR
	PRINTFORML {LOCAL}の快感を与えた
	REDRAW 0
	WAIT
	CLEARLINE 1
	PRINTFORM %CALLNAME:TARGET%
	SETCOLOR DEF_COLOR("イエロー")
	PRINT が
	RESETCOLOR
	PRINTFORML {LOCAL}の快感を与えた
	REDRAW 1
	WAIT
	BASE:MASTER:絶頂 += LOCAL
	CALL SET_SHOOT, PLAYER, "変更不可"
	CUP:快Ｃ = 0
	CUP:快Ｖ = 0
	CUP:快Ａ = 0
	CUP:快Ｂ = 0
	CUP:快Ｍ = 0
	CDOWN:0 = 0
	CDOWN:1 = 0
	CDOWN:2 = 0
	CDOWN:14 = 0
	CDOWN:15 = 0
	NOWEX:Ｃ絶頂 = 0
	NOWEX:Ｖ絶頂 = 0
	NOWEX:Ａ絶頂 = 0
	NOWEX:Ｂ絶頂 = 0
	NOWEX:Ｍ絶頂 = 0
	NOWEX:尿道絶頂 = 0

CASE "運命検閲"
	PRINTFORMW ＜%CALLNAME:TARGET%の運命検閲！＞
	TEQUIP:運命検閲 = 0
	LOCAL = RAND:6
	SELECTCASE LOCAL
	CASE 5
		PRINT 『詠唱』
	CASE 4
		PRINT 『アナル』
	CASE 3
		PRINT 『キス』
	CASE 2
		PRINT 『胸』
	CASE 1
		PRINT 『アソコ』
	CASE 0
		PRINT 『愛撫』
	ENDSELECT
	SETBIT TEQUIP:運命検閲, LOCAL

	LOCAL:1 = RAND:8
	IF LOCAL:1 != LOCAL && LOCAL:1 <= 5
		PRINT と
		SELECTCASE LOCAL:1
		CASE 5
			PRINT 『詠唱』
		CASE 4
			PRINT 『アナル』
		CASE 3
			PRINT 『キス』
		CASE 2
			PRINT 『胸』
		CASE 1
			PRINT 『アソコ』
		CASE 0
			PRINT 『愛撫』
		ENDSELECT
		SETBIT TEQUIP:運命検閲, LOCAL:1
	ENDIF
	PRINTFORMW が禁止された！
ENDSELECT
