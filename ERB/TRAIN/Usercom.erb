@SHOW_USERCOM
#DIM LCOUNT
#DIM MEMO_LINECOUNT
;存在している調教のみを数えていって何番目か
#DIM NUM_TRAIN
#DIM NEXT_LINE
#DIM NUM_COLUMN
#DIMS COM_NAMES
#DIMS COM_NAMES_HIDDEN

REDRAW 0
;主導権が無いフラグを一時的に消去
CLEARBIT FLAG:99, 11

;着せ替え部分の高速化フラグ消去
TCVAR:26 = 0

IF FLAG:238 || CONFIG("調教コンフィグモード")
	FLAG:238 = 0

	NUM_TRAIN = -1

	NEXT_LINE = 0
	NUM_COLUMN = 3 + (CONFIG("画面表示：調教コマンド４列") > 0)

	;直接選択できるコマンドは499番までだが、現状は320までで十分すぎる
	FOR LCOUNT, 0, 320
		SIF STRLENS(TRAINNAME:LCOUNT) == 0
			CONTINUE

		NUM_TRAIN += 1

		IF CONFIG("調教コンフィグモード：全コマンド表示") == 0
			TRYCALLFORM COM_ABLE{LCOUNT}
			SIF RESULT == 0
				CONTINUE
		ENDIF

		RESETCOLOR
		SIF CONFIG("画面表示：未実行コマンドの色") && FIRSTCOM(LCOUNT) == 0
			SETCOLOR DEF_COLOR("明灰色")

		IF CONFIG("調教コンフィグモード") == 0
			;コマンド表示フィルタ
			SELECTCASE DITEMTYPE:0:LCOUNT
			;非表示設定
			CASE 1
				SIF CONFIG("非表示コマンドの表示") == 0
					CONTINUE
				SETCOLOR DEF_COLOR("青")
			;禁止設定
			CASE 2
				CONTINUE
			ENDSELECT
		ELSE
		 	SELECTCASE TRAINNAME:LCOUNT
			CASE "〜予備〜"
				CONTINUE
			ENDSELECT
		ENDIF

		IF CONFIG("ＨＡＲＤ") && TEQUIP:押し倒し
			SELECTCASE LCOUNT
			CASE 178, 280, 281
				PRINTFORMC %TRAIN_NAME_SELECT(LCOUNT)%[{NUM_TRAIN, 3}]
			CASEELSE
				SETCOLOR DEF_COLOR("灰色")
				PRINTFORMC %TRAIN_NAME_SELECT(LCOUNT)%[XXX]
				RESETCOLOR
			ENDSELECT
		ELSE

			IF CONFIG("調教コンフィグモード") == 0
				;コマンド名が通常と違う場合には色を変える(押し倒し状態での基本コマンドは色を変えない)
				SIF TRAIN_NAME_SELECT(LCOUNT) != TRAIN_NAME_STANDARD(LCOUNT)
					SETCOLOR DEF_COLOR("ハートピンク")

				;プレイを終わらせられる時には色を変える
				SELECTCASE LCOUNT
				CASE 31, 201
					SIF TEQUIP:バイブ
						SETCOLOR DEF_COLOR("黄色")
				CASE 33, 202
					SIF TEQUIP:アナルバイブ
						SETCOLOR DEF_COLOR("黄色")
				CASE 34, 203
					SIF TEQUIP:クリキャップ
						SETCOLOR DEF_COLOR("黄色")
				CASE 35, 204
					SIF TEQUIP:ニプルキャップ
						SETCOLOR DEF_COLOR("黄色")
				CASE 36, 205
					SIF TEQUIP:搾乳機
						SETCOLOR DEF_COLOR("黄色")
				CASE 38
					SIF TEQUIP:シャワー
						SETCOLOR DEF_COLOR("黄色")
				CASE 39
					SIF TEQUIP:ローター挿入
						SETCOLOR DEF_COLOR("黄色")
				CASE 99
					SIF TEQUIP:シックスナイン
						SETCOLOR DEF_COLOR("黄色")
				CASE 133
					SIF TEQUIP:アイマスク
						SETCOLOR DEF_COLOR("黄色")
				CASE 134
					SIF TEQUIP:拘束
						SETCOLOR DEF_COLOR("黄色")
				CASE 135
					SIF TEQUIP:口枷
						SETCOLOR DEF_COLOR("黄色")
				CASE 136
					SIF COND("アナル栓")
						SETCOLOR DEF_COLOR("黄色")
				CASE 137
					SIF TEQUIP:拡張バルーン
						SETCOLOR DEF_COLOR("黄色")
				CASE 149
					SIF TEQUIP:カテーテル
						SETCOLOR DEF_COLOR("黄色")
				CASE 152
					SIF TEQUIP:尿道バイブ
						SETCOLOR DEF_COLOR("黄色")
				CASE 170
					SIF TEQUIP:ビデオカメラ
						SETCOLOR DEF_COLOR("黄色")
				CASE 171
					SIF TEQUIP:野外プレイ
						SETCOLOR DEF_COLOR("黄色")
				CASE 172
					SIF TEQUIP:お風呂プレイ
						SETCOLOR DEF_COLOR("黄色")
				CASE 173
					SIF TEQUIP:新妻プレイ
						SETCOLOR DEF_COLOR("黄色")
				CASE 174
					SIF TEQUIP:館内露出プレイ
						SETCOLOR DEF_COLOR("黄色")
				CASE 179
					SIF TEQUIP:羞恥プレイ
						SETCOLOR DEF_COLOR("黄色")
				CASE 182
					SIF TEQUIP:PLAYER:押し倒し
						SETCOLOR DEF_COLOR("黄色")
				CASE 200
					SIF TEQUIP:触手
						SETCOLOR DEF_COLOR("黄色")
				CASE 208
					SIF TEQUIP:触手口辱
						SETCOLOR DEF_COLOR("黄色")
				CASE 209
					SIF TEQUIP:オナホール
						SETCOLOR DEF_COLOR("黄色")
				CASE 301
					SIF TEQUIP:てかげん
						SETCOLOR DEF_COLOR("黄色")
				CASE 302
					SIF TEQUIP:演奏中
						SETCOLOR DEF_COLOR("黄色")
				CASE 306
					SIF STATE("ハードスキン", PLAYER)
						SETCOLOR DEF_COLOR("黄色")
				ENDSELECT
			ELSE
				SELECTCASE DITEMTYPE:0:LCOUNT
				;通常状態
				CASE 0
					SELECTCASE LCOUNT
					CASE 0 TO 9, 60, 61, 65, 71, 75, 80 TO 95, 98, 99, 230 TO 289
						SETCOLOR DEF_COLOR("暗黄色")
					CASEELSE
						RESETCOLOR
					ENDSELECT
				;非表示設定
				CASE 1
					SIF CONFIG("調教コンフィグモード：非表示コマンドを隠す")
						CONTINUE
					SETCOLOR DEF_COLOR("青")
				;禁止設定
				CASE 2
					SIF CONFIG("調教コンフィグモード：禁止コマンドを隠す")
						CONTINUE
					SETCOLOR DEF_COLOR("緑")
				ENDSELECT
			ENDIF

			IF GETCOLOR() != DEF_COLOR("黄色") && GETCOLOR() != DEF_COLOR("ハートピンク")
				;表示するかどうか
				SELECTCASE LCOUNT
				CASE 0 TO 29
					SIF CONFIG("愛撫系非表示") == 0
						CONTINUE
				CASE 30 TO 59
					SIF CONFIG("道具系非表示") == 0
						CONTINUE
				CASE 60 TO 69
					SIF CONFIG("Ｖセックス系非表示") == 0
						CONTINUE
				CASE 70 TO 79
					SIF CONFIG("Ａセックス系非表示") == 0
						CONTINUE
				CASE 80 TO 89
					SIF CONFIG("特殊セックス系非表示") == 0
						CONTINUE
				CASE 90 TO 119
					SIF CONFIG("奉仕系非表示") == 0
						CONTINUE
				CASE 120 TO 129
					SIF CONFIG("調教者に挿入系非表示") == 0
						CONTINUE
				CASE 130 TO 159
					SIF CONFIG("ＳＭ・ハード系非表示") == 0
						CONTINUE
				CASE 160 TO 169
					SIF CONFIG("助手系非表示") == 0
						CONTINUE
				CASE 170 TO 199
					SIF CONFIG("特殊系非表示") == 0
						CONTINUE
				CASE 200 TO 229
					SIF CONFIG("触手系非表示") == 0
						CONTINUE
				CASE 230 TO 259
					SIF CONFIG("アルカナ系非表示") == 0
						CONTINUE
				CASE 260 TO 279
					SIF CONFIG("消費アイテム系非表示") == 0
						CONTINUE
				CASEELSE
					SIF CONFIG("その他非表示") == 0
						CONTINUE
				ENDSELECT
			ENDIF

			PRINTFORMC %TRAIN_NAME_SELECT(LCOUNT)%[{NUM_TRAIN, 3}]
		ENDIF

		NEXT_LINE += 1

		SIF NEXT_LINE % NUM_COLUMN == 0
			PRINTL 
	NEXT
	SIF NEXT_LINE % NUM_COLUMN
		PRINTL 
ENDIF
RESETCOLOR

CALL SETFLAG, "助手相手の恋慕外し終了"

;-------------------------------------------------
;特殊なコマンド
;-------------------------------------------------
PRINTL 

;HARDで押し倒されているなら、主導権が無いフラグをセット
IF CONFIG("ＨＡＲＤ") && TEQUIP:押し倒し
	SETBIT FLAG:99, 11
ELSE
	CLEARBIT FLAG:99, 11
ENDIF


NEXT_LINE = 0
VARSET COM_NAMES
VARSET COM_NAMES_HIDDEN

IF CONFIG("調教コンフィグモード") || (CONFIG("項目表示フィルタ：Ｂ") == 0 && CONFIG("項目表示フィルタ：Ｃ") == 0)
	SIF CONFIG("愛撫系非表示")
		SETCOLOR DEF_COLOR("黄色")
	PRINTC 愛撫系[300]
	RESETCOLOR
	SIF CONFIG("道具系非表示")
		SETCOLOR DEF_COLOR("黄色")
	PRINTC 道具系[301]
	RESETCOLOR
	SIF CONFIG("Ｖセックス系非表示")
		SETCOLOR DEF_COLOR("黄色")
	PRINTC Ｖセックス系[302]
	RESETCOLOR
	PRINTL 

	SIF CONFIG("Ａセックス系非表示")
		SETCOLOR DEF_COLOR("黄色")
	PRINTC Ａセックス系[303]
	RESETCOLOR
	SIF CONFIG("特殊セックス系非表示")
		SETCOLOR DEF_COLOR("黄色")
	PRINTC 特殊セックス系[304]
	RESETCOLOR
	SIF CONFIG("奉仕系非表示")
		SETCOLOR DEF_COLOR("黄色")
	PRINTC 奉仕系[305]
	RESETCOLOR
	PRINTL 

	SIF CONFIG("調教者に挿入系非表示")
		SETCOLOR DEF_COLOR("黄色")
	PRINTC 調教者に挿入系[306]
	RESETCOLOR
	SIF CONFIG("ＳＭ・ハード系非表示")
		SETCOLOR DEF_COLOR("黄色")
	PRINTC ＳＭ・ハード系[307]
	RESETCOLOR
	SIF CONFIG("助手系非表示")
		SETCOLOR DEF_COLOR("黄色")
	;メイド長系とか言われても困るし助手系のまま
	PRINTFORMC 助手系[308]
	RESETCOLOR
	PRINTL 

	SIF CONFIG("特殊系非表示")
		SETCOLOR DEF_COLOR("黄色")
	PRINTC 特殊系[309]
	RESETCOLOR
	SIF CONFIG("触手系非表示")
		SETCOLOR DEF_COLOR("黄色")
	PRINTC 触手系[310]
	RESETCOLOR
	SIF CONFIG("アルカナ系非表示")
		SETCOLOR DEF_COLOR("黄色")
	PRINTC アルカナ系[311]
	RESETCOLOR
	PRINTL 

	SIF CONFIG("消費アイテム系非表示")
		SETCOLOR DEF_COLOR("黄色")
	PRINTC 消費アイテム系[312]
	RESETCOLOR
	SIF CONFIG("その他非表示")
		SETCOLOR DEF_COLOR("黄色")
	PRINTC その他[313]
	RESETCOLOR
	PRINTC 表示コマンド群の反転[320]
	PRINTL 
ENDIF

IF CONFIG("調教コンフィグモード") == 0
	CALL COM_ABLE500
	SIF RESULT == 0
		SETCOLOR DEF_COLOR("灰色")
	PRINTC アルカナ一覧[340]
	RESETCOLOR

	CALL COM_ABLE501
	SIF RESULT == 0
		SETCOLOR DEF_COLOR("灰色")
	PRINTC 消費アイテム一覧[341]
	RESETCOLOR

	IF CONFIG("非表示コマンドの表示") == 0
		PRINTC 非表示コマンドを表示[350]
	ELSE
		SETCOLOR DEF_COLOR("青")
		PRINTC 非表示コマンドを隠す[350]
		RESETCOLOR
	ENDIF
	PRINTL 

	PRINTC 能力表示[400]

	IF CONFIG("項目表示フィルタ：Ｃ") == 0
		IF TEQUIP:押し倒し
			PRINTC 汚れ表示[410]
		ELSE
			PRINTC 汚れ表示＆管理[410]
		ENDIF

		IF PLAYER == MASTER
			SIF TALENT:恋慕 == 0
				SETCOLOR DEF_COLOR("灰色")
			SIF CONFIG("ＨＡＲＤ")
					CFLAG:9 = 0
			IF CFLAG:9
				PRINTC 愛し合う[420]
			ELSE
				PRINTC 可愛がる[420]
			ENDIF
			RESETCOLOR
		ENDIF
		PRINTL 

		SIF CHECK_EXCHANGE("調教者") == 0
			SETCOLOR DEF_COLOR("灰色")
		IF ASSIPLAY
			PRINTFORMC 主人と交代[430]
		ELSE
			PRINTFORMC %TEXTS("助手の名称")%と交代[430]
		ENDIF
		RESETCOLOR

		SIF CHECK_EXCHANGE("調教対象") == 0
			SETCOLOR DEF_COLOR("灰色")
		IF PLAYER == MASTER
			IF TEQUIP:Ｗ押し倒し
				PRINTFORMC 主導側を交代する[431]
			ELSEIF TEQUIP:押し倒し
				PRINTFORMC %TEXTS("助手の名称")%と交代する[431]
			ELSE
				PRINTFORMC %TEXTS("助手の名称")%を相手する[431]
			ENDIF
		ELSE
			PRINTFORMC 立場を逆転する[431]
		ENDIF
		RESETCOLOR

		PRINTC 宝珠取得予定数表示[440]

		PRINTL 

		PRINTC 調教コンフィグモード[888]
	ENDIF

	IF CONFIG("項目表示フィルタ：Ｂ")
		PRINTC 項目表示フィルタ：Ｂ[900]
	ELSEIF CONFIG("項目表示フィルタ：Ｃ")
		PRINTC 項目表示フィルタ：Ｃ[900]
	ELSE
		PRINTC 項目表示フィルタ：Ａ[900]
	ENDIF

	SIF COND("調教終了可能") == 0
		SETCOLOR DEF_COLOR("灰色")
	PRINTC 調教終了[999]
	RESETCOLOR
ELSE
	IF CONFIG("Ｖ調教フィルタ")
		PRINTC Ｖ調教フィルタ：○[360]
	ELSE
		PRINTC Ｖ調教フィルタ：×[360]
	ENDIF
	IF CONFIG("Ａ調教フィルタ")
		PRINTC Ａ調教フィルタ：○[361]
	ELSE
		PRINTC Ａ調教フィルタ：×[361]
	ENDIF
	IF CONFIG("実行値フィルタ")
		PRINTC 実行値フィルタ：○[362]
	ELSE
		PRINTC 実行値フィルタ：×[362]
	ENDIF
	PRINTL 

	IF CONFIG("脱童貞フィルタ")
		PRINTC 脱童貞フィルタ：○[363]
	ELSE
		PRINTC 脱童貞フィルタ：×[363]
	ENDIF
	IF CONFIG("脱処女フィルタ")
		PRINTC 脱処女フィルタ：○[364]
	ELSE
		PRINTC 脱処女フィルタ：×[364]
	ENDIF
	IF CONFIG("ペニバンフィルタ")
		PRINTC ペニバンフィルタ：○[365]
	ELSE
		PRINTC ペニバンフィルタ：×[365]
	ENDIF
	PRINTL 

	SELECTCASE GETBIT(FLAG:98, 0) + GETBIT(FLAG:98, 4)*1p4 + GETBIT(FLAG:98, 5)*1p5
	CASE 0
		PRINTC ゲージ：モノクロＡ[370]
	CASE 1p5
		PRINTC ゲージ：モノクロＢ[370]
	CASE 1p4 + 1p5
		PRINTC ゲージ：モノクロＣ[370]
	CASE 1p0 + 1p5
		PRINTC ゲージ：カラフルＡ[370]
	CASE 1p0 + 1p4 + 1p5
		PRINTC ゲージ：カラフルＢ[370]
	ENDSELECT

	IF CONFIG("画面表示：パラメータ４列")
	 	PRINTC パラメータ：４列[371]
	ELSE
	 	PRINTC パラメータ：３列[371]
	ENDIF
	IF CONFIG("画面表示：調教コマンド４列")
		PRINTC コマンド表示：４列[372]
	ELSE
		PRINTC コマンド表示：３列[372]
	ENDIF
	PRINTL 

	IF CONFIG("画面表示：未実行コマンドの色")
		PRINTC 未実行コマンド：暗色[373]
	ELSE
		PRINTC 未実行コマンド：通常[373]
	ENDIF
	IF CONFIG("画面表示：尿意ゲージ")
		PRINTC 尿意ゲージ：表示[374]
	ELSE
		PRINTC 尿意ゲージ：隠す[374]
	ENDIF
	IF CONFIG("画面表示：排泄欲ゲージ")
		PRINTC 排泄欲ゲージ：表示[375]
	ELSE
		PRINTC 排泄欲ゲージ：隠す[375]
	ENDIF
	PRINTL 

	PRINTC 射精モード選択[380]
	PRINTL 
	PRINTC 調教に戻る[888]
	IF CONFIG("調教コンフィグモード：全コマンド表示")
		PRINTC 実行不可コマンド隠す[890]
	ELSE
		PRINTC 調教コマンドの全表示[890]
	ENDIF
	IF CONFIG("調教コンフィグモード：非表示コマンドを隠す")
		PRINTC 青緑コマンド：青隠す[891]
	ELSEIF CONFIG("調教コンフィグモード：禁止コマンドを隠す")
		PRINTC 青緑コマンド：緑隠す[891]
	ELSE
		PRINTC 青緑コマンド：全表示[891]
	ENDIF
ENDIF

PRINTL 

MEMO_LINECOUNT = LINECOUNT

SIF MAX(PREVCOM, PREVCOM:2) >= 0 && CONFIG("調教コンフィグモード") == 0
	CALL SHOW_PREVCOM

;選んだコマンド次第で軽い説明表示。原則１行
SELECTCASE TFLAG:103
CASE 360
	SIF CONFIG("Ｖ調教フィルタ")
		PRINTFORML  Ｖ調教フィルタＯＮ。処女の時に、膣を使う調教を実行不可にします
CASE 361
	SIF CONFIG("Ａ調教フィルタ")
		PRINTFORML  Ａ調教フィルタＯＮ。アナルを使う調教を実行不可にします
CASE 362
	SIF CONFIG("実行値フィルタ")
		PRINTFORML  実行値フィルタＯＮ。実行値が足りていない調教を実行不可にします
CASE 363
	SIF CONFIG("脱童貞フィルタ")
		PRINTFORML  脱童貞フィルタＯＮ。童貞を失う調教を実行不可にします
CASE 364
	SIF CONFIG("脱処女フィルタ")
		PRINTFORML  脱処女フィルタＯＮ。インサートの時に処女を奪うか確認します
CASE 365
	SIF CONFIG("ペニバンフィルタ")
		PRINTFORML  ペニバンフィルタＯＮ。ペニスバンドの使用を禁止します
CASE 372
	SIF CONFIG("画面表示：調教コマンド４列") && GETCONFIG("ウィンドウ幅") < 1000
		PRINTFORML  ４列表示の場合には、ウィンドウ幅は1000ぐらいがおすすめです
CASE 373
	IF CONFIG("画面表示：未実行コマンドの色")
		PRINTFORM  まだ選んだことが無いコマンドが
		CALL PRINT_COLORTEXT, "灰色", DEF_COLOR("明灰色")
		PRINTFORML になります
	ENDIF
CASE 890
	IF CONFIG("調教コンフィグモード：全コマンド表示")
		PRINTFORML  [{TARGET, 2}]%CALLNAME:TARGET%が実行できないコマンドも全て表示します
	ELSE
		PRINTFORML  [{TARGET, 2}]%CALLNAME:TARGET%が実行できるコマンドだけを表示します
	ENDIF
CASE 888
	IF CONFIG("調教コンフィグモード")
		PRINT  選択した調教コマンドを
		CALL PRINT_COLORTEXT, "非表示", DEF_COLOR("青")
		PRINT 、
		CALL PRINT_COLORTEXT, "禁止", DEF_COLOR("緑")
		PRINTFORM 状態に変えます（
			CALL PRINT_COLORTEXT, "この色", DEF_COLOR("暗黄色")
		PRINTFORML のものは禁止不可)
	ENDIF
CASE 891
	IF CONFIG("調教コンフィグモード：非表示コマンドを隠す")
		CALL PRINT_COLORTEXT, " 非表示", DEF_COLOR("青")
		PRINTFORML のコマンドを隠します（調教コンフィグモードに限る）
	ELSEIF CONFIG("調教コンフィグモード：禁止コマンドを隠す")
		CALL PRINT_COLORTEXT, " 禁止", DEF_COLOR("緑")
		PRINTFORML のコマンドを隠します
	ENDIF
CASE 900
	;前回コマンドがない時に限り説明出す
	IF MEMO_LINECOUNT == LINECOUNT
		IF CONFIG("項目表示フィルタ：Ｂ")
			PRINTPLAIN  [300]〜[320]
			PRINTFORML のコマンド群フィルタを隠します
		ELSEIF CONFIG("項目表示フィルタ：Ｃ")
			PRINTFORML  使用頻度の低い項目を全て隠します
		ENDIF
	ENDIF
ENDSELECT



SIF MEMO_LINECOUNT == LINECOUNT
	PRINTL 


;ARGSには"調教者"or"調教対象"が入る
@CHECK_EXCHANGE(ARGS)
#FUNCTION

SELECTCASE ARGS
;PLAYERとPLAYER:1を交代させる。（調教開始時にPLAYER = MASTER、PLAYER:1 = ASSIとなっている）
CASE "調教者"
	SIF PLAYER <= 0 || PLAYER:1 <= 0
		RETURNF 0
	SIF TEQUIP:PLAYER:押し倒し || TEQUIP:ラブトラップ || TEQUIP:押し倒し || TEQUIP:てかげん || TEQUIP:新妻プレイ || TEQUIP:演奏中 || TEQUIP:触手 || TEQUIP:ビデオカメラ
		RETURNF 0
	;撃沈していたら交代できない
	SIF STATE("撃沈", PLAYER) || STATE("撃沈", PLAYER:1)
		RETURNF 0
	RETURNF 1
;TARGETと(TARGET:1 or ASSI)間でTARGETを交代
CASE "調教対象"
	;押し倒し中は、Ｗ押し倒しでないと交代できない
	SIF TEQUIP:押し倒し && TEQUIP:Ｗ押し倒し == 0
		RETURNF 0
	SIF TEQUIP:PLAYER:押し倒し || TEQUIP:ラブトラップ || TEQUIP:拘束 || TEQUIP:ビデオカメラ || TEQUIP:演奏中 || TEQUIP:触手 || TEQUIP:ビデオカメラ
		RETURNF 0
	;TARGET:1が居る(主にＷ押し倒し中)
	IF TARGET:1
		SIF BASE:精力 <= 0 || BASE:(TARGET:1):精力 <= 0
			RETURNF 0
	 	SIF STATE("撃沈", TARGET:1) || STATE("ふにゃふにゃ", TARGET) || STATE("ふにゃふにゃ", TARGET:1)
			RETURNF 0
		RETURNF 1
	;助手は居る（交代＝助手と調教対象の立場交換）
	ELSEIF ASSI
		SIF BASE:精力 <= 0 || COND("助手可能") == 0
			RETURNF 0
	 	SIF STATE("撃沈", ASSI) || STATE("ふにゃふにゃ", TARGET)
			RETURNF 0
		RETURNF 1
	ENDIF
ENDSELECT

RETURNF 0

@SHOW_PREVCOM
PRINTFORM < 前回調教コマンド : %TEXTS("前回調教コマンド")%
SIF NUM("ターン数") == 0 && PREVCOM:2 >= 0
	PRINTFORM \@ PLAYER == MASTER ? (%TEXTS("助手の名称")%) # (主人) \@
SETCOLOR DEF_COLOR("イエロー")
PRINTFORM %STR:1%
RESETCOLOR
PRINTFORML >

@USERCOM

LOCAL = RESULT

;HARDで押し倒されているなら、主導権が無いフラグをセット
IF CONFIG("ＨＡＲＤ") && TEQUIP:押し倒し
	SETBIT FLAG:99, 11
ELSE
	CLEARBIT FLAG:99, 11
ENDIF

TFLAG:103 = LOCAL

;コマンド選択
IF LOCAL >= 0 && LOCAL <= 299
	IF CONFIG("調教コンフィグモード") == 0
		CALL NO_SELECTCOM, LOCAL

		SIF RESULT == 999
			RETURN 0

		SELECTCOM = RESULT
		TRYCALLFORM COM_ABLE{SELECTCOM}

		SIF RESULT == 0
			RETURN 0

		;主導権が無いフラグを解除
		CLEARBIT FLAG:99, 11

		REDRAW 1

		DOTRAIN SELECTCOM
	ELSE
		CALL NO_SELECTCOM, LOCAL
		LOCAL = RESULT

		SELECTCASE LOCAL
		CASE 999
			CLEARLINE LINECOUNT - TFLAG:31
			RETURN 0
		CASEELSE
			IF STRLENS(TRAINNAME:LOCAL) == 0
				CLEARLINE LINECOUNT - TFLAG:31
				RETURN 0
			ENDIF

		 	SELECTCASE TRAINNAME:LOCAL
			CASE "〜予備〜"
				CLEARLINE LINECOUNT - TFLAG:31
				RETURN 0
			ENDSELECT
		ENDSELECT

		DITEMTYPE:0:LOCAL = (DITEMTYPE:0:LOCAL + 1)%3

		SELECTCASE LOCAL
		CASE 0 TO 9, 60, 61, 65, 71, 75, 80 TO 95, 98, 99, 230 TO 289
			SIF DITEMTYPE:0:LOCAL == 2
				DITEMTYPE:0:LOCAL = 0
		ENDSELECT
	ENDIF
;調教コマンドの種類ごとの表示ＯＮＯＦＦ
ELSEIF LOCAL >= 300 && LOCAL <= 313
	INVERTBIT FLAG:210, LOCAL - 300
	CLEARLINE LINECOUNT - TFLAG:31
	RETURN 1
;表示コマンド群の反転[320]
ELSEIF LOCAL == 320
	FLAG:210 = INVERTBITS(FLAG:210, 0, 13)
	CLEARLINE LINECOUNT - TFLAG:31
	RETURN 1
;調教コンフィグモードへ[888]
ELSEIF LOCAL == 888
	TFLAG:103 = LOCAL
	INVERTBIT FLAG:210, 20
;項目表示フィルタ[900]
ELSEIF LOCAL == 900
	IF CONFIG("項目表示フィルタ：Ｂ")
		CLEARBIT FLAG:210, 30
		SETBIT FLAG:210, 31
	ELSEIF CONFIG("項目表示フィルタ：Ｃ")
		CLEARBIT FLAG:210, 31
	ELSE
		SETBIT FLAG:210, 30
	ENDIF
;調教終了[999]
ELSEIF LOCAL == 999 && COND("調教終了可能")
	REDRAW 1
	BEGIN AFTERTRAIN
ELSEIF CONFIG("調教コンフィグモード") == 0
	;アルカナ[340]
	IF LOCAL == 340
		DOTRAIN 500
	;アイテム[341]
	ELSEIF LOCAL == 341
		DOTRAIN 501
	;非表示コマンドを表示[350]
	ELSEIF LOCAL == 350
		INVERTBIT FLAG:210, 21
		CLEARLINE LINECOUNT - TFLAG:31
	;能力表示[400]
	ELSEIF LOCAL == 400
		CALL NEW_PRINT_STATE, TARGET, "通常"
	;汚れ表示[410]
	ELSEIF LOCAL == 410
		CALL STAIN_INFO
	;反撃するか選択[420]
	ELSEIF LOCAL == 420 && PLAYER == MASTER
		IF CONFIG("ＨＡＲＤ")
			PRINTFORMW 難易度がHARDだと選択できません
			RETURN 0
		ELSEIF TALENT:恋慕 == 0
			PRINTFORMW 現在の調教対象が[恋慕]の時に限り選択できます
			RETURN 0
		ELSEIF CFLAG:9 == 1
			PRINTFORMW %CALLNAME:TARGET%とお互いに愛し合うことにした。
		ELSEIF CFLAG:9 == 0
			PRINTFORMW %CALLNAME:TARGET%を思いのままに可愛がることにした。
		ENDIF
		INVERTBIT CFLAG:9, 0
	;交代[430]
	ELSEIF LOCAL == 430 && CHECK_EXCHANGE("調教者")
		CALL SETFLAG, "調教者交代", 1
		CLEARLINE LINECOUNT - TFLAG:31
	;TARGETとTARGET:1(いないならASSI)を交換[431]
	ELSEIF LOCAL == 431 && CHECK_EXCHANGE("調教対象")
		CALL SETFLAG, "対象交代"
		CLEARLINE LINECOUNT - TFLAG:31
	;宝珠取得予定数表示[440]
	ELSEIF LOCAL == 440
		CALL SHOW_WILLGET_JUEL
	ENDIF
ELSE
	TFLAG:103 = LOCAL
	;[360]〜は各種調教フィルタ
	IF LOCAL == 360
		INVERTBIT FLAG:209, 0
	ELSEIF LOCAL == 361
		INVERTBIT FLAG:209, 1
	ELSEIF LOCAL == 362
		INVERTBIT FLAG:209, 4
	ELSEIF LOCAL == 363
		INVERTBIT FLAG:209, 5
	ELSEIF LOCAL == 364
		INVERTBIT FLAG:209, 6
	ELSEIF LOCAL == 365
		INVERTBIT FLAG:209, 7
	;[370]〜は調教画面表示の設定
	ELSEIF LOCAL == 370
		SELECTCASE GETBIT(FLAG:98, 0) + GETBIT(FLAG:98, 4)*1p4 + GETBIT(FLAG:98, 5)*1p5
		CASE 0
			SETBIT FLAG:98, 5
		CASE 1p5
			SETBIT FLAG:98, 4
		CASE 1p4 + 1p5
			SETBIT FLAG:98, 0
			CLEARBIT FLAG:98, 4
		CASE 1p0 + 1p5
			SETBIT FLAG:98, 4
		CASE 1p0 + 1p4 + 1p5
			CLEARBIT FLAG:98, 0
			CLEARBIT FLAG:98, 4
			CLEARBIT FLAG:98, 5
		ENDSELECT
	ELSEIF LOCAL == 371
		INVERTBIT FLAG:98, 2
	ELSEIF LOCAL == 372
		INVERTBIT FLAG:98, 3
	ELSEIF LOCAL == 373
		INVERTBIT FLAG:98, 1
	ELSEIF LOCAL == 374
		INVERTBIT FLAG:98, 6
	ELSEIF LOCAL == 375
		INVERTBIT FLAG:98, 7
	;射精モード選択[380]
	ELSEIF LOCAL == 380
		CALL SETCONFIG, "射精箇所の変更"
	;調教コマンドの全表示[890]
	ELSEIF LOCAL == 890
		INVERTBIT FLAG:210, 45
	;青緑コマンド：全表示[891]
	ELSEIF LOCAL == 891
		IF CONFIG("調教コンフィグモード：非表示コマンドを隠す")
			CLEARBIT FLAG:210, 40
			SETBIT FLAG:210, 41
		ELSEIF CONFIG("調教コンフィグモード：禁止コマンドを隠す")
			CLEARBIT FLAG:210, 41
		ELSE
			SETBIT FLAG:210, 40
		ENDIF
	ENDIF
ENDIF

SIF CONFIG("調教コンフィグモード")
	CLEARLINE LINECOUNT - TFLAG:31
