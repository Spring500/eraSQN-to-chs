@EVENTEND
#PRI
REDRAW 1
;二人で押し倒しの時には、きちんと二回ここに来るのに注意

;PLAYERをMASTERに直す
SIF PLAYER != MASTER
	CALL SETFLAG, "調教者交代", 1
;消滅予定キャラはどかす(フラグや分岐の問題で居た方が便利なのかもしれないので残してみる)
SIF CEVENT("消滅予定")
	CALL SETFLAG, "対象交代"

;恋慕を外した時に戻し忘れているなら、ここで復帰
CALL SETFLAG, "助手相手の恋慕外し終了"

CALL CHECK_SATISFACTION, TARGET
CALL CHECK_SATISFACTION, ASSI

CALL SETFLAG, "調教終了", TARGET
CALL SETFLAG, "調教終了", MASTER
SIF ASSI
	CALL SETFLAG, "調教終了", ASSI


@EVENTEND
#LATER
;調教中フラグOFF
FLAG:99 = 0
IF FLAG:22 != 2
	;相手が上位夢魔の場合には攻略刻印（屈服刻印取得に要するパラメータ量を減らす）を得る
	;満足＋攻略刻印が一定値以下
	IF COND("性欲大満足") && MARK:攻略刻印 < 4
		IF TALENT:上位梦魔 && MARK:屈服刻印 < 3
			DRAWLINE
			PRINTFORMW ☆★☆イベント達成・屈服刻印習得のペナルティが軽減されます☆★☆
			DRAWLINE
		ENDIF
		MARK:攻略刻印 = 4
	ELSEIF COND("性欲満足") && MARK:攻略刻印 < 2
		IF TALENT:上位梦魔 && MARK:屈服刻印 < 3
			DRAWLINE
			PRINTFORMW ☆★☆イベント達成・屈服刻印習得のペナルティが軽減されます☆★☆
			DRAWLINE
		ENDIF
		MARK:攻略刻印 = 2
	ENDIF

	PRINTL 
	PRINTL 調教を終了しました。

	;寝取られイベント
	CALL EVENT_DRAGON_NTR, "寝取られ判定"

	;調教後行為のチェック
	CALL SELF_CHECK

	;搾乳した母乳の売却
	CALL SELL_MILK

	;調教時に録画したビデオを売却
	CALL SELL_VIDEO
	IF ASSI
		SWAP TARGET, ASSI
		CALL SELL_VIDEO
		SWAP TARGET, ASSI
	ENDIF

	;助手の調教終了時特典（狐で媚薬入手など）
	CALL AFTERTRA_BONUS_ASSI

	;消耗品のオート購入
	CALL CALC, "消耗品の自動購入"
ENDIF
CFLAG:454 = 0
CFLAG:455 = 0
CALL EVENT_AFTER_TRAIN

BEGIN ABLUP


@EVENT_AFTER_TRAIN
;気力・体力の上昇チェック
CALL EVENT_GROWTH

;何の珠を得られたか
CALL JUEL_CHECK

;射精フラグ、処女喪失フラグなどをリセット
VARSET TFLAG, 0, 0, 101

;調教経験を増やす
EXP:调教次数 += 1

;日付の記録
CFLAG:97 = DAY

@EVENT_AFTER_TRAIN_COS, ARG
#DIM LCOUNT
;汚れを初期化
CALL RESET_STAIN_EX, "全部", ARG

SELECTCASE CONFIG("調教終了時の服装")
;終了時に本人の服に着替えてもらうなら
CASE 0
	CALL COORDINATE_MUMA, ARG
;終了時にお気に入りの服に着替えてもらうなら
CASE 2
	;ここでは夜の衣装にのみ着替えるのに注意。昼の衣装については@EVENTSHOPの早朝イベント後に改めて着替える
	IF GETBIT(IS_FAVORITE_DRESS(ARG), 6)
		CALL SET_FAVORITE_DRESS, ARG, 6
	ELSEIF GETBIT(IS_FAVORITE_DRESS(ARG), 0)
		CALL SET_FAVORITE_DRESS, ARG, 0
	ENDIF
;終了時にお気に入りの服のどれかに着替えてもらうなら
CASE 3
	IF IS_FAVORITE_DRESS(ARG) == 0
		;パンツの着用禁止
		SIF COND("身嗜み：ノーパン", ARG)
			CALL SET_PANTIES, ARG
		RETURN 0
	ENDIF

	;たまには着替えないかもしれなくてもいいじゃない！
	FOR LCOUNT, 0, 100
		LOCAL = RAND:10
		SIF GETBIT(IS_FAVORITE_DRESS(ARG), LOCAL) == 0
			CONTINUE

		CALL SET_FAVORITE_DRESS, ARG, LOCAL
		BREAK
	NEXT
;終了時に薄着なら服を着てもらう
CASE 4
	SIF EQUIP:ARG:上着 + EQUIP:ARG:ボディスーツ == 0
		CALL COORDINATE_MUMA, ARG
ENDSELECT

;パンツの着用禁止
SIF COND("身嗜み：ノーパン", ARG)
	CALL SET_PANTIES, ARG

@JUEL_CHECK
#DIM LCOUNT
#DIM BONUS
#DIM MEMO_LV
#DIM MEMO_TEC
#DIM MEMO_LINECOUNT

DRAWLINE
VARSET LOCAL
BONUS = 0
;性欲などの充足ボーナス
IF COND("性欲大満足")
	PRINT 性欲大満足
	BONUS += MAX(ABL:欲望 * 4, 10)
ELSEIF COND("性欲満足")
	PRINT 性欲満足
	BONUS += MAX(ABL:欲望 * 2, 5)
ENDIF
IF COND("精液欲大満足")
	SIF BONUS
		PRINT ＆
	PRINT 精液欲大満足
	BONUS += MAX(ABL:精液上瘾 * 4, 10)
ELSEIF COND("精液欲満足")
	SIF BONUS
		PRINT ＆
	PRINT 精液欲満足
	BONUS += MAX(ABL:精液上瘾 * 2, 5)
ENDIF
IF EX:連続絶頂 >= 3
	SIF BONUS
		PRINT ＆
	PRINTFORM 連続絶頂{EX:連続絶頂}COMBO
	BONUS += LIMIT(EX:連続絶頂 - 2, 1, 100)
ENDIF

IF BONUS
	EXP:经验值 += MULTIPLY(EXP_GAIN(5), BONUS)
	PRINTL ボーナス！
	PRINTFORML 獲得する珠と経験値が{BONUS}％増しとなります
	PRINTL 
ENDIF
IF EXP_GAIN(58)
	SETCOLOR DEF_COLOR("イエロー")
	PRINTL ※媚薬の使用により、獲得する珠の量が減少しています※
	RESETCOLOR
	PRINTL 
ENDIF

VARSET J
FOR LCOUNT, 0, 16
	SELECTCASE LCOUNT
	CASE 3, 12, 13
		CONTINUE
	CASE 11
		GOTJUEL:11 = GET_JUEL(11)
		J:11 = JUEL:11 + GOTJUEL:11
	CASEELSE
		GOTJUEL:LCOUNT = MULTIPLY(GET_JUEL(LCOUNT), 100 + BONUS)
		J:LCOUNT = JUEL:LCOUNT + GOTJUEL:LCOUNT
	ENDSELECT
NEXT

;反感の珠の打ち消しチェック
CALL DENIAL_CHECK

PRINTL 調教の結果：
FOR LCOUNT, 0, 13
	;パラメータを表示する順番を並べ替える・ＭＢＣＶＡ欲恭屈恥痛恐習反不鬱・Ｖ潤Ａ潤は無し
	SELECTCASE LCOUNT
	CASE  0
		LOCAL = 0
	CASE  1
		LOCAL = 1
	CASE  2
		LOCAL = 2
	CASE  3
		LOCAL = 14
	CASE  4
		LOCAL = 15
	CASE  5
		LOCAL = 4
	CASE  6
		LOCAL = 5
	CASE  7
		LOCAL = 6
	CASE  8
		LOCAL = 7
	CASE  9
		LOCAL = 8
	CASE  10
		LOCAL = 9
	CASE  11
		LOCAL = 10
	CASE  12
		LOCAL = 11
	ENDSELECT

	PRINTFORM %PALAMNAME:LOCAL%の珠：({JUEL:LOCAL, 10} \@ GOTJUEL:LOCAL >= 0 ? + # - \@ 
	SIF GOTJUEL:LOCAL
		SETCOLOR DEF_COLOR("黄色")
	PRINTFORM {ABS(GOTJUEL:LOCAL), 10} 
		RESETCOLOR
	PRINTFORML )
	JUEL:LOCAL += GOTJUEL:LOCAL
NEXT

PRINTW 以上の珠を得ました。

PRINTL 
MEMO_LINECOUNT = LINECOUNT

FOR LCOUNT, 0, 100
	SELECTCASE LCOUNT
	CASE 4 TO 7
		CONTINUE
	ENDSELECT

	SIF EXP_GAIN(LCOUNT)
		PRINTFORML %TEXT_RJ(EXPNAME:LCOUNT, 16)%＋{EXP_GAIN(LCOUNT), 3}
NEXT

IF LINECOUNT > MEMO_LINECOUNT
	PRINTFORMW 以上の経験\@ EXP_GAIN(5) ? と、経験値を{EXP_GAIN(5)}Ｐ # を \@得ました。
ELSEIF EXP_GAIN(5)
	PRINTFORMW 経験値を{EXP_GAIN(5)}Ｐ得ました。
ELSE
	CLEARLINE 1
ENDIF

;精液のやり取り
PRINTL 

IF EX:絶対搾精量 && EX:主人への絶対射精量
	PRINTFORMW %CALLNAME:TARGET%は今回の調教で{EX:絶対搾精量}mlの精液をその身で受け止め、{EX:主人への絶対射精量}mlの精液を%CALLNAME:MASTER%に注ぎました。
ELSEIF EX:絶対搾精量
	PRINTFORMW %CALLNAME:TARGET%は今回の調教で{EX:絶対搾精量}mlの精液をその身で受け止めました。
ELSEIF EX:主人への絶対射精量
	PRINTFORMW %CALLNAME:TARGET%は今回の調教で{EX:主人への絶対射精量}mlの精液を%CALLNAME:MASTER%に注ぎました。
ELSE
	CLEARLINE 1
ENDIF

;欲求不満の解消
LOCAL = 0
SELECTCASE BASE:性欲
CASE IS >= MAXBASE:性欲 * 2
	LOCAL += 15
CASE IS >= MAXBASE:性欲
	LOCAL += 10
CASE IS >= 1
	LOCAL += 3
CASEELSE
	LOCAL += 1
ENDSELECT

SELECTCASE BASE:精液欲
CASE IS >= MAXBASE:精液欲 * 2
	LOCAL += 10
CASE IS >= MAXBASE:精液欲
	LOCAL += 5
CASE IS >= 1
	LOCAL += 1
ENDSELECT

;欲望と精液欲を同時に満足させると、欲求不満が大幅に解消される
SIF COND("性欲満足") && COND("精液欲満足")
	LOCAL += 10

;欲求不満の解消
IF BASE:欲求不満 && COND("性欲満足") && COND("精液欲満足")
	PRINTL 
	MEMO_LINECOUNT = LINECOUNT

	SETCOLOR DEF_COLOR("黄色")
	IF COND("性欲大満足") && COND("精液欲大満足")
		PRINTFORMW %CALLNAME:TARGET%の欲求不満が完全に解消されました！
	ELSEIF COND("性欲満足") && COND("精液欲満足")
		PRINTFORMW %CALLNAME:TARGET%の欲求不満が大幅に解消されました。
	ENDIF
	RESETCOLOR

	SIF LINECOUNT == MEMO_LINECOUNT
		CLEARLINE 1
ENDIF

CALL CALC, "欲求不満解消", TARGET, LOCAL

;絶頂ボーナスはここでリセット
JUEL:20 = 0
JUEL:21 = 0
JUEL:22 = 0
JUEL:23 = 0
JUEL:24 = 0

;HARDだと自動で幾つかのレベルが上がる
SIF CONFIG("ＨＡＲＤ") == 0
	RETURN 0

;上昇前の技巧とＬＶを記録
MEMO_TEC = ABL:技巧
MEMO_LV = ABL:ＬＶ

;技巧の自動レベルアップ！
FOR LCOUNT, 0, 10
	CALL ISABLUP, "技巧"
	IF RESULT && ABL:技巧 < NUM("最大レベル", 2)
		JUEL:習得 -= A
		ABL:技巧 += 1
	ELSE
		BREAK
	ENDIF
NEXT

;ＬＶの自動レベルアップ！
FOR LCOUNT, 0, NUM("最大レベル", 90)
	CALL ISABLUP, "ＬＶ"
	IF RESULT && ABL:ＬＶ < NUM("最大レベル", 90)
		EXP:经验值 -= A
		ABL:ＬＶ += 1
	ELSE
		BREAK
	ENDIF
NEXT

IF ABL:技巧 > MEMO_TEC || ABL:ＬＶ > MEMO_LV
	DRAWLINE
	PRINTFORMW 能力の自動レベルアップが発生しています…
	SIF ABL:技巧 > MEMO_TEC
		PRINTFORMW %CALLNAME:TARGET%の技巧は{ABL:技巧 - MEMO_TEC}LV上昇して{ABL:技巧}LVになりました！
	IF ABL:ＬＶ > MEMO_LV
		PRINTFORMW %CALLNAME:TARGET%のＬＶは{ABL:ＬＶ - MEMO_LV}LV上昇して{ABL:ＬＶ}LVになりました！
		;ＬＶが10以上になると上位夢魔化の可能性あり。LV14だと確実に上位夢魔になる
		IF ABL:ＬＶ >= 10 && ABL:ＬＶ + ABL:技巧/2 >= 10 + RAND:5 && TALENT:梦魔 && TALENT:上位梦魔 == 0
			PRINTFORMW %CALLNAME:TARGET%は上位夢魔となりました！
			TALENT:梦魔 = 0
			TALENT:上位梦魔 = 1
		ENDIF
		SIF ABL:ＬＶ/5 > MAX(MEMO_LV/5, 1)
			CALL GET_TALENT, ABL:ＬＶ/5 - MEMO_LV/5
	ENDIF
ENDIF

@DENIAL_CHECK
#DIM LCOUNT
SIF J:11 == 0
	RETURN 0

PRINTL 反感の珠と他の珠の打ち消しが発生しています。

VARSET LOCAL

FOR LCOUNT, 0, 100
	;欲情・恭順・屈服の珠が無いなら、恥情・苦痛・恐怖の珠を減らす
	IF J:4 + J:5 + J:6
		IF J:4 && RANDIF(1, J:5, J:6)
			LOCAL = 4
		ELSEIF J:5 && RANDIF(1, J:6)
			LOCAL = 5
		ELSE
			LOCAL = 6
		ENDIF
	ELSEIF J:8 + J:9 + J:10
		IF J:8 && RANDIF(1, J:9, J:10)
			LOCAL = 8
		ELSEIF J:9 && RANDIF(1, J:10)
			LOCAL = 9
		ELSE
			LOCAL = 10
		ENDIF
	ELSE
		BREAK
	ENDIF

	LOCAL:1 = LIMIT(J:11 / 2, 1, J:LOCAL)

	LOCAL:LOCAL += LOCAL:1
	J:LOCAL -= LOCAL:1
	GOTJUEL:LOCAL -= LOCAL:1
	J:11 -= LOCAL:1
	GOTJUEL:11 -= LOCAL:1

	SIF J:11 <= 0
		BREAK
NEXT
;減少量表示
FOR LCOUNT, 4, 11
	SIF LCOUNT == 7 || LOCAL:LCOUNT == 0
		CONTINUE
	PRINTFORML %PALAMNAME:LCOUNT%の珠×{LOCAL:LCOUNT}個減少
NEXT

PRINTL 

