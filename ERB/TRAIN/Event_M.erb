;特殊なギミックなどがないものはここに記述。もがくやドラ姦など特殊すぎるものはCOMFに記述。
;選択肢を内包するなど書くのが困難なものについては、TRAIN_MESSAGE_COM(コマンド番号)を用いて、呼び出すようにすること。
@TRAIN_MESSAGE_B
#DIM LCOUNT
#DIM MEMO_LINECOUNT

;-------------------------------------------------
;インサートなど、体位を表すフラグの管理
;-------------------------------------------------
CALL SETFLAG, "体勢戻し"


;-------------------------------------------------
;CUPの記録
;-------------------------------------------------
FOR LCOUNT, 0, 20
	LOCAL:LCOUNT = CUP:LCOUNT
NEXT
LOCAL:20 = DOWNBASE:体力
LOCAL:21 = DOWNBASE:气力
;-------------------------------------------------
;ソースの計算(予測値)
;-------------------------------------------------
CALL CALC, "調教ソースの試算"

;-------------------------------------------------
;処女チェック
;-------------------------------------------------
IF CONFIG("脱処女フィルタ")
	;選択肢の表示をチェック
	MEMO_LINECOUNT = LINECOUNT

	SELECTCASE SELECTCOM
	CASE 60 TO 69
		IF TALENT:处女 == 1 && TEQUIP:推到 == 0
			PRINTFORM %CALLNAME:TARGET%の処女を
			IF PENIS(PLAYER) && (CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ａ") || (PREVCOM >= 70 && PREVCOM <= 79) || INSERT("Ａインサート") )
				PRINTFORM アナルで汚れたペニス
			ELSE
				PRINTFORM %TRAINNAME:SELECTCOM%
			ENDIF
			PRINTFORML で奪って良いですか？
		ENDIF
	;-------------------------------------------------
	;Ａ処女の喪失チェック
	;-------------------------------------------------
	CASE 70 TO 79
		IF TALENT:Ａ处女 && TEQUIP:推到 == 0
			PRINTFORML %CALLNAME:TARGET%の\@ TALENT:Ａ处女 ? お尻の処女を奪って良いですか？ # アナルを犯しますか？ \@
		ENDIF
	;-------------------------------------------------
	;双頭バイブプレイでの処女喪失チェック
	;-------------------------------------------------
	CASE 80
		IF TEQUIP:推到 == 0 && TALENT:PLAYER:处女 && CHECK_COM("双頭バイブプレイ")
			PRINTFORML 双頭バイブプレイを行うと%CALLNAME:PLAYER%の処女は失われます
			PRINTFORML それでも実行しますか？
		ENDIF
	CASE 83 TO 88
		IF EXP:尿道性交经验 == 0
			IF PENIS(TARGET)
				PRINTFORML %CALLNAME:TARGET%のペニスの処女を%CALLNAME:PLAYER%のソレで奪っても良いですか？
			ELSE
				PRINTFORML %CALLNAME:TARGET%の尿道の処女を奪って本当に良いですか？
			ENDIF
		ENDIF
	ENDSELECT

	IF LINECOUNT > MEMO_LINECOUNT
		CALL PRINT_SELECT, "はい", "やっぱやめる"
		SIF RESULT == 1
			TFLAG:14 = 1
	ENDIF
ENDIF

;-------------------------------------------------
;コマンドの否定判定
;-------------------------------------------------
SIF REFUSE_CHECK()
	RETURN 0


;-------------------------------------------------
;サイズチェックと潤滑チェック
;サイズが大きい場合にはそっち優先
;-------------------------------------------------
VARSET LOCALS
IF TEQUIP:推到 == 0
	SELECTCASE SELECTCOM
	CASE 60 TO 69
		;大きすぎる
		IF EXP_GAIN(12) == 0 && PENIS(PLAYER) && SIZE("ペニス", PLAYER) >= ABL:Ｖ扩张 + 2
			LOCALS:0 = %CALLNAME:PLAYER%のモノは大きすぎて%CALLNAME:TARGET%の身体では受け止めきれないかもしれません
			LOCALS:2 = 本当に挿入してよいですか？
		ENDIF
	CASE 70 TO 79
		IF EXP_GAIN(13) == 0 && PENIS(PLAYER) && SIZE("ペニス", PLAYER) >= ABL:Ａ扩张 + 2
			LOCALS:0 = %CALLNAME:PLAYER%のモノは大きすぎて%CALLNAME:TARGET%のお尻の穴では受け止めきれないかもしれません
			LOCALS:2 = 本当に挿入してよいですか？
		ENDIF
	ENDSELECT

	;助手の助言
	IF ASSI && ABL:ASSI:助手 + TALENT:ASSI:聪慧 >= 3
		IF CALCF("反発刻印獲得", TARGET)
			LOCALS:1 = 「このプレイは%CALLNAME:TARGET%\@ TALENT:ASSI:魔导人偶 ? 様 # \@には早\@ CALCF("反発刻印獲得", TARGET) >= 2 ? すぎる # い \@かも」
		ELSEIF CALCF("苦痛刻印獲得", TARGET)
			LOCALS:1 = 「このプレイは\@ CALCF("苦痛刻印獲得", TARGET) >= 2 ? 相当な苦痛を伴う # ちょっと痛い \@かも」
			SELECTCASE SELECTCOM
			CASE 2, 33
				SIF PALAM:Ａ润 < 10000
					LOCALS:1 = 「%CALLNAME:TARGET%\@ TALENT:ASSI:魔导人偶 ? 様 # \@の未熟なアナルで楽しむのならローションを使った方が…」
			;Ｖ潤が必要と助言
			CASE 60 TO 79
				SELECTCASE GET_PALAMLV(PALAM:Ｖ润 + PALAM:PLAYER:Ｐ润) - MAX(SIZE("ペニス", PLAYER) - ABL:Ｖ扩张, 0)
				CASE 1

				CASE IS <= 0

				ENDSELECT
			;Ａ潤が必要と助言
			CASE 70 TO 79
				SELECTCASE GET_PALAMLV(PALAM:Ａ润 + PALAM:PLAYER:Ｐ润) - MAX(SIZE("ペニス", PLAYER) - ABL:Ａ扩张, 0)
				CASE 1
					LOCALS:1 = 「%CALLNAME:TARGET%\@ TALENT:ASSI:魔导人偶 ? 様 # \@の未熟なアナルで楽しむのなら、ローションを使うなり口でさせるなりした方が…」
				CASE IS <= 0
					LOCALS:1 = 「%CALLNAME:TARGET%\@ TALENT:ASSI:魔导人偶 ? 様 # \@の未熟なアナルで楽しむのならローションを使った方が…」
				ENDSELECT
			ENDSELECT
		ENDIF
		IF LOCALS:1 != ""
			IF LOCALS:0 != ""
				LOCALS:1 = %CALLNAME:ASSI%も%LOCALS:1%と言っています
			ELSE
				LOCALS:1 = %CALLNAME:ASSI%が%LOCALS:1%と助言してきました
			ENDIF
			LOCALS:2 = …それでも実行しますか？
		ENDIF
	ENDIF

	MEMO_LINECOUNT = LINECOUNT
	FOR LCOUNT, 0, 3
		SIF LOCALS:LCOUNT != ""
			PRINTFORML %LOCALS:LCOUNT%
	NEXT

	IF LINECOUNT > MEMO_LINECOUNT
		CALL PRINT_SELECT, "はい", "やっぱやめる"
		SIF RESULT == 1
			TFLAG:14 = 1
	ENDIF
ENDIF
;-------------------------------------------------
;コマンドの否定判定
;-------------------------------------------------
SIF REFUSE_CHECK()
	RETURN 0

;-------------------------------------------------
;コマンド実行直前の口上
;-------------------------------------------------
CALL KOJO_MESSAGE_BEFORE_COM

;-------------------------------------------------
;ボイス再生
;-------------------------------------------------
SIF CONFIG("EmueraTunes")
	TRYCALL VPLAY


;-------------------------------------------------
;コマンドの否定判定
;-------------------------------------------------
SIF REFUSE_CHECK()
	RETURN 0

;-------------------------------------------------
;調教時メッセージ
;-------------------------------------------------
SIF CONFIG("地の文ＯＦＦ") || CONFIG("口上による地の文ＯＦＦ")
	RETURN 0

PRINTL 
TRYCALLFORM TRAIN_MESSAGE_COM{SELECTCOM}

;-------------------------------------------------
;CUPの復元
;-------------------------------------------------
FOR LCOUNT, 0, 20
	CUP:LCOUNT = LOCAL:LCOUNT
NEXT
DOWNBASE:体力 = LOCAL:20
DOWNBASE:气力 = LOCAL:21

;-------------------------------------------------
;ソースの計算(確定値)
;-------------------------------------------------
CALL CALC, "調教ソースの計算"

;以下の追加地の文はどれかひとつしか表示させない
MEMO_LINECOUNT = LINECOUNT

;調教ごとの初回限定の地の文
SELECTCASE SELECTCOM
CASE 60 TO 69, 177
	CALL TEXT, "追加地の文：Ｖ挿入後"
CASE 70 TO 79
	CALL TEXT, "追加地の文：Ａ挿入後"
CASE 83 TO 88
	CALL TEXT, "追加地の文：尿道挿入後"
ENDSELECT

SIF LINECOUNT > MEMO_LINECOUNT || COND("３Ｐ")
	RETURN 0

;追加オナニー
IF SELECTCOM != PREVCOM && SELECTCOM >= 90 && SELECTCOM <= 119
	CALL PLAY_ONANIE
	CALL PLAY_ONANIE_2
;Ａ感覚LV3+絶頂あり+連続アナルセックス
ELSEIF SELECTCOM >= 70 && SELECTCOM <= 79 && SELECTCOM == PREVCOM && TEQUIP:推到 == 0 && ABL:Ａ感觉 >= 3 && EX:绝顶数合计 && INSERT("Ａインサート") && COND("前Ｔ調教者") == PLAYER && TALENT:男性 == 0
	IF RAND:3 == 0
		PRINTFORM 挿抜の激しさに歪む%CALLNAME:TARGET%のアソコからは、
	ELSEIF RAND:2
		PRINTFORM アナルから響く快楽で、%CALLNAME:TARGET%のアソコからは
	ELSE
		PRINTFORM 容赦のない突き込みに、%CALLNAME:TARGET%のアソコからは
	ENDIF
	;今回Ｖ中出しされた
	IF EXP_GAIN(17)
		PRINT 中に出された精液まじりの
	ELSEIF TALENT:处女 && RAND:3 == 0
		PRINT 処女とは思えない量の
	ELSEIF RAND:4 == 0
		PRINT 触れてもいないのに
	ELSEIF RAND:3 == 0
		PRINT だらだらと
	ELSEIF RAND:2 == 0
		PRINT 恥ずかしい程に
	ELSE
		PRINT とろとろと
	ENDIF
	PRINTFORMW 愛液が流れ出ている…
;騎乗位の追加テキスト
ELSEIF (SELECTCOM == 65 || SELECTCOM == 75 || SELECTCOM == 88) && (INSERT("インサート") == 0 || COND("前Ｔ調教者") != PLAYER)
	CALL TEXT, "追加地の文：騎乗位"
;どちらかがそろそろイきそうな状態
;コマンドごとに固有の追加台詞などはここに書く
ELSEIF SELECTCOM >= 60 && SELECTCOM <= 89 && COND("イきそう") + COND("イきそう", PLAYER)
	SIF ABL:服从 < 3 && TALENT:恋慕 == 0 && ABL:欲望 < 3
		RETURN 0

	SELECTCASE SELECTCOM
	CASE 60 TO 69
		SIF CHECK("Ｖインサート") == 0
			RETURN 0
	CASE 70 TO 79
		SIF CHECK("Ａインサート") == 0
			RETURN 0
	CASE 83 TO 88
		SIF TEQUIP:推到
			RETURN 0
		SIF CHECK("尿道インサート") == 0
			RETURN 0
	CASEELSE
		RETURN 0
	ENDSELECT

	SELECTCASE SELECTCOM
	CASE 60, 83
		PRINTFORM %CALLNAME:TARGET%は
		IF RAND:2 && COND("指の使用") && CHECK_CLO("腋露出") && (COND("性癖：腋", PLAYER) || RAND:3 == 0)
			IF RAND:2
				PRINTFORM 髪をかき上げる仕草で%TEXTS("腋：視覚")%を
			ELSE
				PRINTFORM \@ TALENT:多腕 ? 手 # 両手 \@を頭の後ろで組んで%TEXTS("腋：視覚")%を
			ENDIF
			IF RAND:2
				PRINTFORM アピールしながら、
			ELSE
				PRINTFORM 見せつけながら、
			ENDIF
			IF COND("視力") && RAND:2
				PRINTFORMW 興奮した面持ちで%CALLNAME:PLAYER%を見つめている…
			ELSE
				PRINTFORMW 息を弾ませて%CALLNAME:PLAYER%の突き込みを受け入れている…
			ENDIF
			CALL SETFLAG, "腋見せフラグ", TARGET
		ELSEIF COND("脚の使用")
			IF RAND:2
				PRINTFORM 股を大きく開いて腰を突き出し、
				IF RAND:2
					PRINTFORMW 悦んで%CALLNAME:PLAYER%を受け入れている…
				ELSE
					PRINTFORMW 貪欲に快感を貪っている…
				ENDIF
			ELSE
				IF COND("イきそう") && RAND:2
					PRINTFORMW 息を荒くしながら、半ば無意識に両脚を%CALLNAME:PLAYER%の腰に巻きつけてきている…
				ELSE
					PRINTFORMW %CALLNAME:PLAYER%の腰の後ろに脚を回し、股間を押し付けてきた…
				ENDIF
			ENDIF
		ELSE
			IF RAND:2
				PRINTFORM %CALLNAME:PLAYER%に向けて腰を突き出し、
				IF RAND:2
					PRINTFORMW 悦んで突き込みを受け入れている…
				ELSE
					PRINTFORMW 貪欲に快感を貪っている…
				ENDIF
			ELSE
				PRINTFORMW %CALLNAME:PLAYER%に身体全体を押し付けてきた…
			ENDIF
		ENDIF
	CASE 70
		PRINTFORM %CALLNAME:TARGET%は
		IF RAND:2 && COND("指の使用") && CHECK_CLO("腋露出") && (COND("性癖：腋", PLAYER) || RAND:3 == 0)
			IF RAND:2
				PRINTFORM 髪をかき上げる仕草で%TEXTS("腋：視覚")%をアピールしながら、
			ELSE
				PRINTFORM \@ TALENT:多腕 ? 手 # 両手 \@を頭の後ろで組んで%TEXTS("腋：視覚")%を見せつけながら、
			ENDIF
			IF COND("視力") && RAND:2
				PRINTFORMW 興奮した面持ちで%CALLNAME:PLAYER%を見つめている…
			ELSE
				PRINTFORMW 息を弾ませて%CALLNAME:PLAYER%の突き込みを受け入れている…
			ENDIF
			CALL SETFLAG, "腋見せフラグ", TARGET
		ELSEIF COND("脚の使用")
			PRINTFORMW Ｍ字開脚の体勢で腰を浮かせ、アナルを犯しやすい体勢をとって楽しんでいる…
		ELSE
			PRINTFORMW 腰を浮かせて、アナルを犯しやすい体勢をとって楽しんでいる…
		ENDIF
	CASE 61, 71,84
		PRINTFORMW %CALLNAME:TARGET%は快楽に身悶えながら、お尻を高く上げて%CALLNAME:PLAYER%からの激しい突き込みを楽しんでいる…
	CASE 62, 72,85
		PRINTFORM %CALLNAME:TARGET%は
		IF RAND:2 && COND("指の使用") && CHECK_CLO("腋露出") && (COND("性癖：腋", PLAYER) || RAND:3 == 0)
			IF RAND:2
				PRINTFORM 髪をかき上げる仕草で%TEXTS("腋：視覚")%を
			ELSE
				PRINTFORM \@ TALENT:多腕 ? 手 # 両手 \@を頭の後ろで組んで%TEXTS("腋：視覚")%を
			ENDIF
			IF RAND:2
				PRINTFORM アピールしながら、
			ELSE
				PRINTFORM 見せつけながら、
			ENDIF
			IF COND("視力") && RAND:2
				PRINTFORMW 興奮した面持ちで%CALLNAME:PLAYER%を見つめている…
			ELSE
				PRINTFORMW 息を弾ませて%CALLNAME:PLAYER%の突き込みを受け入れている…
			ENDIF
			CALL SETFLAG, "腋見せフラグ", TARGET
		ELSEIF RAND:2 && ABL:露出癖 >= 3 && MARK:屈服刻印 >= 2 && COND("指の使用")
			PRINTFORMW 自らの腕で脚を抱きかかえ、積極的に恥ずかしい体位を愉しんでいる…
		ELSE
			IF SELECTCOM == 72
				PRINTFORM アナルをズボズボ抉られながら、
				IF PENIS(TARGET)
					PRINTFORMW 興奮した面持ちで%CALLNAME:PLAYER%を見つめている…
				ELSE
					PRINTFORMW 涎を垂らしている自分のアソコを興奮しながら見つめている…
				ENDIF
			ELSE
				PRINTFORMW ズボズボ抉られているアソコを、興奮しながら見つめている…
			ENDIF
		ENDIF
	CASE 63, 73,86
		IF COND("指の使用")
			IF RAND:2 && CHECK_CLO("腋露出") && (COND("性癖：腋", PLAYER) || RAND:3 == 0)
				PRINTFORMW 首に手を回して抱きついてきた%CALLNAME:TARGET%の汗ばんだ%TEXTS("腋")%の%TEXTS("匂い")%が%CALLNAME:PLAYER%の鼻腔を刺激する…
			ELSE
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の首に手を回して抱きついてきた…
			ENDIF
		ENDIF
	CASE 64, 74,87
		SIF COND("指の使用")
			PRINTFORMW %CALLNAME:TARGET%は背を仰け反らせて%CALLNAME:PLAYER%にしなだれかかってきた…
	ENDSELECT
;インサート中の愛撫
ELSEIF RAND:3 == 0 && SELECTCOM >= 0 && SELECTCOM <= 19 && INSERT("インサート") && (COND("イきそう") || RAND:5 == 0)
	;フラートなら無し
	SIF TEQUIP:调情
		RETURN 0

	;ある程度の対応した部分の感度が必要
	SELECTCASE SELECTCOM
	CASE 0
		SIF MAX(ABL:Ｃ感觉, ABL:Ｂ感觉, ABL:Ｍ感觉) < 3
			RETURN 0
	CASE 1, 7
		SIF ABL:Ｃ感觉 < 3
			RETURN 0
	CASE 8
		SIF ABL:Ｖ感觉 < 3
			RETURN 0
	CASE 2, 9
		SIF ABL:Ａ感觉 < 3
			RETURN 0
	CASE 5
		SIF ABL:Ｂ感觉 < 3
			RETURN 0
	CASE 6
		SIF ABL:Ｍ感觉 < 3
			RETURN 0
	CASE 12
		SIF PENIS(TARGET) == 0 && ABL:Ｃ感觉 < 3
			RETURN 0
	CASE 15
		SIF TALENT:尿道性感 == 0
			RETURN 0
	ENDSELECT

	IF RAND:4 == 0
		PRINT 弱点を突かれた
	ELSEIF RAND:3 == 0
		PRINT 気持ちの良い場所を責められた
	ELSEIF RAND:2
		PRINT 敏感な場所を弄られた
	ELSE
		PRINT 挿入されながらの愛撫に
	ENDIF
	PRINTFORM %CALLNAME:TARGET%は
	IF RAND:3 == 0
		PRINT ゾクゾクと染みわたる快楽に全身を震わせ、
	ELSEIF RAND:2
		PRINT ビクッと震えたのちに身体を強張らせ、
	ELSE
		PRINT ピクンと肢体を跳ねさせ、
	ENDIF
	PRINTFORM %CALLNAME:PLAYER%のモノを
	IF RAND:3 == 0
		PRINTW ざわめく粘膜で意図せず舐め回してきた…
	ELSEIF RAND:2
		PRINTW 無意識に締め付けてしまう…
	ELSE
		PRINT 一際強く締め付けてくる…
	ENDIF
	CALL SETFLAG, "締め付けフラグ", TARGET
ENDIF


@TRAIN_MESSAGE_A
#DIM MEMO_LINECOUNT
SIF FLAG:6 == 0
	RETURN 0

;ウェイトの発生判定
MEMO_LINECOUNT = LINECOUNT

;絶頂
IF NOWEX:绝顶数合计
	PRINTFORM %CALLNAME:TARGET%は
	;浴尿
	IF COND("飲尿フラグ")
		PRINTFORM %CALLNAME:PLAYER%の%TEXTR("オシッコを味わい", "小便器の役目を果たし")%
	ELSEIF COND("子宮内浴尿フラグ")
		PRINTFORM %TEXTR("子宮", "大事な場所")%%TEXTR("をトイレ代わりに使われ", "に小便を浴びせられ")%
	ELSEIF COND("膣内浴尿フラグ")
		IF RAND:3 == 0
			PRINTFORM 子宮口を小便で刺激され
		ELSEIF RAND:2
			PRINTFORM 膣内で小用を足され
		ELSE
			PRINTFORM %TEXTR("膣", "大事な場所")%をトイレにされ
		ENDIF
	ELSEIF COND("温泉浣腸フラグ")
		IF RAND:3 == 0
			PRINTFORM 大腸に小便を流し込まれ
		ELSEIF RAND:2
			PRINTFORM 排泄するための場所に逆に排泄され
		ELSE
			PRINTFORM 腸内をトイレにされ
		ENDIF
	ELSEIF COND("乳内浴尿フラグ")
		IF RAND:3 == 0
			PRINTFORM 乳房の内部を小便で刺激され
		ELSEIF RAND:2
			PRINTFORM おっぱいを小便臭くされ
		ELSE
			PRINTFORM おっぱいをトイレにされ
		ENDIF
	ELSEIF COND("膀胱内浴尿フラグ")
		IF RAND:3 == 0
			PRINTFORM 尿道から逆に小便を流し込まれ
		ELSEIF RAND:2
			PRINTFORM 排泄するための場所に逆に排泄され
		ELSE
			PRINTFORM 膀胱をトイレにされ
		ENDIF
	ELSEIF NOWEX:尿道绝顶 && (NOWEX:尿道绝顶 == EX:尿道绝顶 || RAND:5 == 0)
		SELECTCASE SELECTCOM
		CASE 3
			IF TEQUIP:尿道扩张棒
				PRINTFORM 自ら尿道をプジーで攻め続け
			ENDIF
		CASE 10
			;尿道インサート中
			IF INSERT("尿道インサート", TARGET, PLAYER)
				IF RAND:3 == 0
					PRINTFORM 性処理の道具のように扱われ
				ELSEIF RAND:2
					PRINTFORM ペニスを%CALLNAME:PLAYER%のオナホにされ
				ELSE
					PRINTFORM ペニスを内外から気持ちよくされ
				ENDIF
			ELSE
				PRINTFORM ペニスを扱かれ
			ENDIF
		CASE 15
			IF TEQUIP:尿道扩张棒
				PRINTFORM 尿道をブジーで虐められ
			ELSE
				PRINTFORM 尿道を指で虐められ
			ENDIF
		CASE 83 TO 88
			IF TEQUIP:推到
				IF TALENT:尿道狂
					PRINTFORM 尿道から湧き上がる快楽で%TEXTR("頭を真っ白にし", "涎を垂らし", "夢中になって腰を振りたて")%
				ELSEIF PENIS(TARGET)
					PRINTFORM ペニスの内部で%CALLNAME:PLAYER%を犯し
				ELSE
					PRINTFORM 尿道を使ってのセックスで%CALLNAME:PLAYER%を犯し
				ENDIF
			ELSE
				IF PENIS(TARGET)
					PRINTFORM ペニスを激しい腰使いで犯され
				ELSE
					PRINTFORM 尿道を激しい腰使いで犯され
				ENDIF
			ENDIF
		CASE 151
			PRINTFORM 敏感な粘膜をブジーで虐められ
		CASEELSE
			PRINTFORM 尿道を虐められ
		ENDSELECT
		;挿入ボーナス
		SIF INSERT("尿道インサート") && PENIS(PLAYER)
			BASE:PLAYER:绝顶 += ABL:尿道名器度 * 50
	ELSEIF INSERT("インサート") && PENIS(PLAYER) && RAND:3 == 0
		IF INSERT("対面座位") && RAND:2
			PRINTFORM %CALLNAME:PLAYER%をぎゅーっと抱きしめ
		ELSE
			IF INSERT("二穴挿入")
				PRINT 両方の穴を
				LOCAL = MIN(ABL:Ｖ名器度, ABL:Ａ名器度)
			ELSEIF INSERT("Ｖインサート")
				PRINT 膣を
				LOCAL = ABL:Ｖ名器度
			ELSEIF INSERT("Ａインサート")
				PRINT アナルを
				LOCAL = ABL:Ａ名器度
			ELSEIF INSERT("尿道インサート")
				PRINT 尿道を
				LOCAL = ABL:尿道名器度
			ENDIF
			IF LOCAL >= 80 && RAND:3 == 0
				PRINT 絶妙な力加減で
			ELSEIF LOCAL >= 60 && RAND:2
				PRINT 凄い力で
			ELSEIF LOCAL >= 40
				PRINT きゅうぅっと
			ENDIF
			PRINT 締め付け
			BASE:PLAYER:绝顶 += LOCAL * 50
		ENDIF
	ELSEIF NOWEX:射精
		SELECTCASE SELECTCOM
		CASE 4
			PRINTFORM %CALLNAME:PLAYER%の口に\@ NOWEX:射精 >= 2 ? たっぷりと # \@射精し
		CASE 11
			PRINTFORM %CALLNAME:PLAYER%の胸に\@ NOWEX:射精 >= 2 ? たっぷりと # \@射精し
		CASE 12
			PRINTFORM %CALLNAME:PLAYER%の足で\@ NOWEX:射精 >= 2 ? たっぷりと # \@射精し
		CASE 37
			PRINTFORM オナホールの中に\@ NOWEX:射精 >= 2 ? たっぷりと # \@射精し
		CASEELSE
			IF INSERT("Ｖインサート", PLAYER, TARGET)
				PRINTFORM %CALLNAME:PLAYER%の膣に\@ NOWEX:射精 >= 2 ? 大量に # \@中出しし
			ELSEIF INSERT("Ａインサート", PLAYER, TARGET)
				PRINTFORM %CALLNAME:PLAYER%の腸内に精液を\@ NOWEX:射精 >= 2 ? 大量に # \@吐き出し
			ELSE
				PRINTFORM ペニスから\@ NOWEX:射精 >= 2 ? たっぷりと # \@精液を噴き出し
			ENDIF
		ENDSELECT
	ELSEIF NOWEX:喷乳 && (NOWEX:喷乳 == EX:喷乳 || RAND:5 == 0)
		PRINT 胸の先端から母乳を垂らし
	ELSEIF TALENT:男性 == 0
		;潮噴き
		IF COND("潮噴き可能") && RAND:3 == 0
			IF TEQUIP:导管
				PRINTFORM 尿道に差し込まれた管から
			ELSEIF PENIS(TARGET)
				PRINTFORM アソコから
			ELSE
				PRINTFORM %TEXTR("アソコ", "尿道口", "オシッコの穴")%から
			ENDIF
			PRINTFORM %TEXTR("透明の液体", "透明な潮", "熱い飛沫")%を
			;尿意ゲージを減らす
			IF MAX(NOWEX:Ｃ绝顶, NOWEX:Ｖ绝顶, NOWEX:尿道绝顶) + TALENT:漏尿癖 >= 5 && BASE:尿意 >= 10000 && RAND:3
				PRINT 派手に
				BASE:尿意 -= RAND:5000 + 3000
			ELSEIF MAX(NOWEX:Ｃ绝顶, NOWEX:Ｖ绝顶, NOWEX:尿道绝顶) + TALENT:漏尿癖 >= 3 && BASE:尿意 >= 6000 && RAND:3
				PRINT 勢いよく
				BASE:尿意 -= RAND:3000 + 2000
			ELSEIF RAND:2
				PRINT ピシャピシャと
				BASE:尿意 -= RAND:2000 + 1000
			ELSE
				PRINT ピュッピュッと可愛く
				BASE:尿意 -= RAND:1000 + 500
			ENDIF
			IF RAND:2
				PRINTFORM 迸らせ
			ELSE
				PRINTFORM %TEXTR("噴き出し", "噴き")%
			ENDIF
		ELSE
			IF NOWEX:绝顶数合计 >= 4 && CHECK_ECST(TARGET) >= 3 && RAND:2
				PRINT 全身を駆け巡る快感に
			ELSEIF NOWEX:绝顶数合计 >= 3 && RAND:2
				PRINT 激しい快感に
			ELSEIF NOWEX:绝顶数合计 >= 2 && RAND:2
				PRINT あまりの快感に
			ELSEIF RAND:2
				IF COND("ながらオナニーフラグ") || SELECTCOM == 3
					PRINT 快感に
				ELSE
					PRINT 与えられた快感に
				ENDIF
			ENDIF
			IF RAND:3 == 0 && COND("口の使用") && ORAL_PLAY() == 0
				IF RAND:2
					IF TALENT:强硬 && NOWEX:绝顶数合计 == 1
						PRINT 歯を食いしばり
					ELSE
						PRINT 口をだらしなく開き
					ENDIF
				ELSE
					PRINT 口の端から涎を垂らし
				ENDIF
			ELSEIF RAND:2 && COND("口の使用") && ORAL_PLAY() == 0
				IF NOWEX:绝顶数合计 >= 3 && RAND:3
					PRINT 我を忘れて絶叫し
				ELSEIF NOWEX:绝顶数合计 >= 2 && RAND:3
					IF TALENT:羞耻心强
						PRINT 叫びそうになる口を手で押さえ
					ELSE
						PRINT 叫ぶように声をあげ
					ENDIF
				ELSE
					IF RAND:2 && TALENT:好色
						PRINT 悩ましい喘ぎ声を漏らし
					ELSEIF TALENT:羞耻心强 || RAND:3 == 0
						IF NOWEX:绝顶数合计 >= 2
							PRINT 思わず嬌声をあげ
						ELSE
							PRINT 控えめな喘ぎ声をもらし
						ENDIF
					ELSE
						PRINT 大きな声をあげ
					ENDIF
				ENDIF
			ELSE
				IF RAND:3 == 0
					IF ORAL_PLAY() == 0 && RAND:2
						PRINT 激しく首を振り
					ELSE
						PRINTFORM \@ NOWEX:绝顶数合计 <= 2 ? 半ば # \@放心し
					ENDIF
				ELSEIF RAND:2 || NOWEX:Ｍ绝顶
					IF TALENT:强硬 && NOWEX:绝顶数合计 == 1
						PRINT 眉間に皺をよせ
					ELSEIF TALENT:羞耻心强 && RAND:2
						PRINT 瞳をきゅっと閉じ
					ELSE
						PRINT 瞳を潤ませ
					ENDIF
				ELSE
					IF NOWEX:Ｃ绝顶 + NOWEX:Ｖ绝顶 + NOWEX:Ａ绝顶 + NOWEX:尿道绝顶 && RAND:2 == 0
						PRINT お腹をうねらせ
					ELSE
						PRINT 背中を大きく反らし
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ELSE
		PRINT 背中を大きく反らし
	ENDIF
	PRINT ながら、
	LOCAL = 0
	IF TALENT:巨乳 && RAND:3 == 0
		IF RAND:3 == 0
			PRINT 張りのある巨乳を
		ELSEIF RAND:2
			PRINT 重そうな胸を
		ELSE
			PRINT 豊満な胸を
		ENDIF
		LOCAL = TALENT:巨乳
	ELSEIF RAND:2
		PRINT 全身を
	ELSE
		SIF TALENT:娇小体型 && TALENT:幼稚
			PRINT 小さな
		PRINT 身体を
	ENDIF

	IF LOCAL && RAND:2
		IF RAND:2
			IF INSERT("騎乗位")
				PRINT ぶるんっと
			ELSE
				PRINT ぷるぷると
			ENDIF
		ELSE
			PRINT 派手に
		ENDIF
	ELSEIF RAND:3 == 0 || U
		PRINT 小刻みに
	ELSEIF RAND:2 == 0
		PRINT ビクビクと
	ELSE
		PRINT ビクンと
	ENDIF
	IF LOCAL && RAND:2
		PRINT 揺らして
	ELSEIF RAND:3 == 0 || U
		PRINT 震わせて
	ELSEIF RAND:2 == 0
		PRINT 痙攣させて
	ELSE
		PRINT 跳ねさせて
	ENDIF
	IF RAND:2
		PRINTL 絶頂に達した
	ELSE
		PRINTL 絶頂を迎えた
	ENDIF
ENDIF
;逆レイプで射精
IF INSERT("インサート", PLAYER, TARGET) && NOWEX:射精
	PRINTFORM %CALLNAME:PLAYER%の
	IF INSERT("Ｖインサート", PLAYER, TARGET)
		PRINT 肉襞は、
	ELSEIF INSERT("Ａインサート", PLAYER, TARGET)
		PRINT アナルは、
	ELSEIF INSERT("尿道インサート", PLAYER, TARGET)
		PRINT オシッコの穴は、
	ENDIF
	SIF ABL:欲望 >= 3 && NOWEX:射精 >= 2
		PRINT 恍惚の表情を浮かべる
	PRINTFORML %CALLNAME:TARGET%から精液を搾りとっていった…
;絶頂
ELSEIF NOWEX:绝顶数合计
	LOCAL = 0
	IF SELECTCOM == 8 && TEQUIP:按摩棒 == 0
		SIF RAND:(EXP_GAIN(71)*2 + 1) == 0
			LOCAL = 1
	ELSEIF INSERT("インサート")
		SIF RAND:(EXP_GAIN(71)*3 + 6) == 0
			LOCAL = 1
	ENDIF
	;指ちゅぱ(指挿入れorインサート)
	IF COND("口の使用") && ABL:服从 >= 3 && MARK:排斥刻印 == 0 && TEQUIP:推到 == 0 && LOCAL
		;LOCAL == 0 なら口に指を入れる。それ以外なら口元に差し出す。
		LOCAL = RAND:3
		IF NOWEX:绝顶数合计 >= 4 && RAND:4 == 0
			PRINTFORM 快感で蕩けている
		ELSEIF NOWEX:绝顶数合计 >= 2 && RAND:3 == 0
			PRINTFORM 恍惚としている
		ELSEIF RAND:2
			PRINTFORM 満足そうな
		ELSE
			PRINTFORM 息を整えている
		ENDIF
		PRINTFORM %CALLNAME:TARGET%の
		IF LOCAL == 0
			PRINT 口に
		ELSE
			PRINT 口元に
		ENDIF
		IF CHECK_STAIN(STAIN:PLAYER:手, "精液")
			PRINT 精液に塗れた
		ELSEIF CHECK_STAIN(STAIN:PLAYER:手, "Ｖ")
			PRINT 愛液で汚れた
		ELSEIF STAIN:PLAYER:手
			PRINT 体液で汚れた
		ENDIF
		IF RAND:2
			PRINT 指を
		ELSE
			PRINT 人差し指を
		ENDIF
		IF LOCAL == 0
			IF RAND:2
				PRINT 咥えさせ
				LOCALS = る
			ELSE
				PRINT 押し込
				LOCALS = む
			ENDIF
		ELSE
			IF RAND:2
				PRINT 差し出
				LOCALS = す
			ELSE
				PRINT 近づけ
				LOCALS = る
			ENDIF
		ENDIF
		IF LOCAL == 0 && RAND:3 == 0
			PRINTFORM %TEXT_CONJ(LOCALS)%粘膜をくすぐると、
		ELSEIF STAIN:PLAYER:手 && ABL:侍奉精神 >= 3
			PRINTFORM %TEXT_CONJ(LOCALS)%綺麗にする様に促すと、
		ELSE
			PRINTFORM %LOCALS%と、
		ENDIF
		IF TALENT:胆怯 + TALENT:清纯 && RANDIF(2, TALENT:好色, TALENT:开朗, TALENT:羞耻心强, TALENT:献身)
			PRINT おずおずと
		ELSEIF TALENT:好色 && RANDIF(2, TALENT:开朗, TALENT:羞耻心强, TALENT:献身)
			PRINT 楽しそうに
		ELSEIF TALENT:开朗 && RANDIF(2, TALENT:羞耻心强, TALENT:献身)
			IF LOCAL == 0
				PRINT ぱくっと
				LOCAL = 1
			ELSE
				PRINT お返しにと
			ENDIF
		ELSEIF TALENT:献身 && RANDIF(2, TALENT:羞耻心强)
			PRINT 優しく
		ELSEIF TALENT:羞耻心强 && RAND:2
			PRINT 恥ずかしそうに
		ELSEIF MARK:屈服刻印 >= 2 && RAND:3 == 0
			;フェラ経験豊富
			IF LOCAL == 0 && ABL:欲望 >= 3 && INSERT("インサート") && EXP:口交经验 >= 50 && PENIS(PLAYER) && RAND:3
				PRINT 興奮した様子で
			ELSE
				PRINT 慣れた様子で
			ENDIF
		ELSEIF RAND:2
			PRINT 大人しく
		ELSE
			IF TALENT:献身
				PRINT 優しく
			ELSEIF TALENT:羞耻心强
				PRINT 恥ずかしそうに
			ELSE
				PRINT 気だるげに
			ENDIF
		ENDIF
		;指を咥える
		IF LOCAL
			PRINT 口に含んで
			IF COND("視力") && RAND:3 == 0
				PRINTFORM 、%CALLNAME:PLAYER%を
				IF MARK:屈服刻印 >= 2
					PRINT 見つめながら
				ELSE
					PRINT 窺いながら
				ENDIF
			ENDIF
		ENDIF
		;対象の口⇔調教者の指の汚れが移動
		CALL EQ_STAIN, TARGET, PLAYER, 0, 1
		;お掃除ルート
		IF STAIN:PLAYER:手 && ABL:侍奉精神 >= 3
			IF CHECK_STAIN(STAIN:PLAYER:手, "精液")
				PRINTL 付着した精液を綺麗に舐め取っていった…
				EXP:精液经验 += 1
				PRINTFORML 精液経験+1
			ELSE
				PRINTL ペロペロと舐め清めてきた…
			ENDIF
			STAIN:PLAYER:手 = 0
		ELSEIF RAND:3 == 0
			PRINTL 甘噛みしてきた…
		ELSEIF RAND:2
			PRINTL しゃぶり始めた…
		ELSE
			PRINTL 舌を這わせてきた…
		ENDIF
		;指ちゅぱ経験+1
		EXP:舔指经验 += 1
	ELSEIF NOWEX:绝顶数合计 && (NOWEX:绝顶数合计 >= 2 || BASE:性欲 >= MAXBASE:性欲)
		PRINTFORM %CALLNAME:TARGET%
		IF COND("指の使用") && TALENT:男性 == 0 && RAND:3 == 0
			IF RAND:2
				PRINT は顔を
				IF EX:绝顶数合计 >= MAXBASE:性欲 && RAND:2
					PRINT 度重なる絶頂で
					SIF TALENT:承认快感
						PRINT だらしなく
				ELSE
					PRINT 快感で
				ENDIF
				IF NOWEX:绝顶数合计 >= 3 && RAND:2
					PRINT 蕩けさせて、
				ELSE
					PRINT 緩ませて、
				ENDIF
			ELSE
				IF TALENT:恋慕 && RAND:2
					PRINT は幸せそうな表情で、
				ELSEIF NOWEX:绝顶数合计 >= 2 && RAND:2
					PRINT はうっとりした表情で、
				ELSE
					PRINT は気持ち良さそうな表情で、
				ENDIF
			ENDIF
			IF COND("口淫中")
				PRINTFORM 舌を動かす事も忘れて
			ELSEIF PENIS(TARGET) && RANDIF(3, TALENT:贫乳 == 0, NOWEX:Ｖ绝顶, NOWEX:Ｃ绝顶 +  NOWEX:尿道绝顶)
				IF ABL:精液上瘾 >= 3 && NOWEX:射精 && COND("口の使用") && INSERT("屈曲位") && RAND:2
					PRINT 大きく口を開いて自らの精液を受け止めながら
					EXP:精液经验 += 1
					STAIN:Ｍ = SET_STAIN(STAIN:Ｍ, "精液")
				ELSEIF RAND:2
					PRINT ペニスを弄りながら
				ELSE
					PRINT ペニスをゆっくりと扱きながら
				ENDIF
			ELSEIF NOWEX:Ｃ绝顶 + NOWEX:Ｖ绝顶 + NOWEX:尿道绝顶 && RANDIF(3, TALENT:贫乳 == 0, NOWEX:Ｖ绝顶)
				;顔面騎乗
				IF TEQUIP:推到 && TEQUIP:颜面骑乘
					IF ABL:施虐倾向 >= 3 && RANDIF(3, ABL:自慰上瘾 >= 3)
						IF RAND:2
							PRINTFORM %CALLNAME:PLAYER%に舌で後始末をさせながら
						ELSE
							PRINTFORM %CALLNAME:PLAYER%に舌で後戯をさせながら
						ENDIF
					ELSEIF ABL:自慰上瘾 >= 3 && RAND:3 == 0
						PRINTFORM %CALLNAME:PLAYER%の顔に気持ち良い所を擦りつけながら
					ELSEIF TALENT:好色 && RAND:3 == 0
						PRINTFORM %CALLNAME:PLAYER%にヒクつくアソコを見せ付けながら
					ELSEIF RAND:2
						PRINTFORM %CALLNAME:PLAYER%の顔の上で大股を開きながら
					ELSE
						PRINTFORM %CALLNAME:PLAYER%の顔を跨いだままで
					ENDIF
				ELSE
					IF PENIS(TARGET) && RAND:2
						PRINTFORM ペニスを
					ELSE
						PRINTFORM 股間を
					ENDIF
					IF ABL:自慰上瘾 && RAND:2
						IF TALENT:好色 + TALENT:淫乱 && RAND:2
							PRINT 弄るのを見せ付けながら
						ELSE
							PRINT 弄りながら
						ENDIF
					ELSE
						PRINT 撫でながら
					ENDIF
				ENDIF
			ELSEIF NOWEX:Ｖ绝顶 && RANDIF(3, TALENT:贫乳 == 0)
				IF RAND:2 && TALENT:恋慕
					PRINT 子宮付近を撫でながら
				ELSE
					PRINT お腹を撫でながら
				ENDIF
			ELSEIF TALENT:贫乳 == 0 && RAND:3 == 0
				PRINT 胸をやわやわと揉みながら
			ELSEIF RAND:2
				PRINT 指をしゃぶりながら
			ELSE
				PRINT 身体を撫でながら
			ENDIF
		ELSEIF RAND:2
			IF INSERT("インサート") && COND("脚の使用") && RAND:3 == 0
				IF TEQUIP:推到
					PRINTFORM は%CALLNAME:PLAYER%に跨って体を預けながら、
				ELSE
					PRINTFORM はだらしなく大股を開いたまま、
				ENDIF
			ELSEIF RAND:2
				PRINT は断続的に絶頂を繰り返しながら、
			ELSE
				PRINT はピクンピクンと身体を時折跳ねさせながら、
			ENDIF
		ELSE
			IF INSERT("インサート") && RAND:3 == 0
				IF COND("脚の使用") && RAND:2
					PRINT は緩やかに腰を動かしながら、
				ELSE
					PRINT は肉襞から粘つく液体を滴らせ、喘ぐように陰唇をヒクつかせながら、
				ENDIF
			ELSEIF NOWEX:绝顶数合计 >= 3 && RAND:3 == 0
				IF RAND:2
					PRINT は興奮冷めやらぬ様子で大きく息をつきながら、
				ELSE
					PRINT は荒い息をつきながら、
				ENDIF
			ELSEIF RAND:2
				PRINT は深い呼吸で息を整えながら、
			ELSE
				PRINT は満足げな甘い吐息をつきながら、
			ENDIF
		ENDIF
		IF RAND:3 == 0
			PRINTFORML \@ RAND:2 ? ぐったり # くたっ \@と脱力している…
		ELSE
			PRINTFORML \@ RAND:2 ? 快感 # 絶頂 \@の余韻に\@ RAND:2 ? 浸 # 酔 \@っている…
		ENDIF
	ENDIF
ENDIF

IF NOWEX:绝顶数合计 + NOWEX:射精 >= RAND:3 + 1 && STATE("バインド：Ｐ", PLAYER)
	PRINTFORML …%CALLNAME:TARGET%の絶頂により、ペニスの拘束がわずかに緩んだ！
	CALL SETFLAG, "Ｐバインド弱体フラグ", PLAYER
ENDIF

;絶頂おもらし…利尿剤が無いと、まずお漏らしはしない。お漏らし癖があると、しやすくなる。
VARSET LOCAL
LOCAL = MAX(RAND:(CALCF("お漏らし", TARGET) + 1) - 2, 0)
SIF CALCF("お漏らし", TARGET) >= 8
	LOCAL = CALCF("お漏らし", TARGET)

SIF LOCAL > 0
	PRINTL 

IF LOCAL >= 4 && ABL:欲望 >= 4 && RAND:2
	PRINTFORML %CALLNAME:TARGET%はガクガクと身体を痙攣させながら止め処なく\@ EXP_GAIN(80) ? 精液混じりの # \@尿を垂れ流した
	PRINTFORML どうやら排尿行為そのものに快楽を感じているようだ…
;アソコが露出していない
ELSEIF LOCAL >= 1 && !CHECK_CLO("Ｖ露出")
	PRINTFORM %CALLNAME:TARGET%は、あまりの快感の大きさに
	;アウター(ブルマとか)をはいている+ずらしていない+股間が露出していない
	IF CHECK_CLO("アウター露出")
		PRINTFORM %OUTER(TARGET, 1)%を穿いたまま
	;ボディスーツ+股間をずらしていない+ボトムを脱いでない+股間が露出していない
	ELSEIF CHECK_CLO("ボディスーツ露出")
		PRINTFORM %BODYS(TARGET, 1)%を着たまま
	;パンスト+破ってない
	ELSEIF CHECK_CLO("パンスト露出")
		PRINTFORM %OUTER(TARGET, 1)%を穿いたまま
	;パンツ+パンツをずらしていない+パンツを脱いでない+股間が露出していない
	ELSEIF CHECK_CLO("パンツ露出")
		PRINTFORM %PANTIES(TARGET, 1)%を穿いたまま
	ENDIF
	PRINTL お漏らしをしてしまった
	SIF !CHECK_CLO("パンスト露出")
		PRINTFORML 布地の脇から溢れる\@ EXP_GAIN(80) ? 精液混じりの # \@おしっこで、股間から下は酷い状態になっている…
ELSEIF LOCAL >= 1 && TEQUIP:颜面骑乘 && TALENT:男性 == 0
	PRINTFORM %CALLNAME:TARGET%は
	IF ABL:施虐倾向 >= 5
		IF NOWEX:绝顶数合计 >= 2 && RAND:2
			PRINT 快楽に震えながら、
		ELSEIF TEQUIP:颜面骑乘 == 1
			PRINT 嗜虐的な笑みを浮かべながら、
		ENDIF
	ELSEIF NOWEX:绝顶数合计 >= 2 && RAND:2
		PRINT 快楽のあまり、
	ENDIF
	IF TEQUIP:颜面骑乘 == 1 && ABL:施虐倾向 >= 5
		PRINTFORM %CALLNAME:PLAYER%の口に直接
	ELSE
		PRINTFORM %CALLNAME:PLAYER%の顔に乗っかった体勢のまま
	ENDIF
	IF ABL:施虐倾向 >= 5
		PRINTL 放尿し始めた…
	ELSE
		PRINTL お漏らしをしてしまった…
	ENDIF
ELSEIF LOCAL >= 3
	IF NOWEX:绝顶数合计 >= 8
		PRINT 痙攣する
	ELSEIF NOWEX:绝顶数合计 >= 4
		PRINT ぐったりとした
	ENDIF
	IF TEQUIP:尿道扩张棒
		PRINTFORML %CALLNAME:TARGET%の尿道とプジーの隙間から勢い良く尿が噴出した…
	ELSE
		PRINTFORML %CALLNAME:TARGET%の尿道口から噴水のように尿が噴き出した…
	ENDIF
ELSEIF LOCAL >= 2
	IF GETBIT(TEQUIP:束缚, 21)
		PRINT 脚を拘束された大股開きの体勢の
	ELSEIF NOWEX:绝顶数合计 >= 4
		PRINT ぐったりとした
	ENDIF
	IF TALENT:男性 || RAND:2 == 0
		PRINTFORML %CALLNAME:TARGET%の尿道口から水流が滴り、水溜りを作った…
	ELSE
		PRINTFORML %CALLNAME:TARGET%のおしっこの穴から勢い良く水流が迸り、弧を描いた…
	ENDIF
ELSEIF LOCAL >= 1
	IF RAND:3 == 0
		PRINTFORML %CALLNAME:TARGET%は快楽のあまり失禁してしまった…
	ELSEIF RAND:2
		PRINTFORML 絶頂に震える%CALLNAME:TARGET%は、尿意を抑えきれずにお漏らししてしまった…
	ELSE
		PRINTFORML 絶頂の弾みか、快楽で緩んだ小さな穴からおしっこがショロッと漏れ出した…
	ENDIF
ELSEIF RAND:3 == 0
	IF PENIS(TARGET) == 0 && CALCF("お漏らし", TARGET) >= 5 && EXP_GAIN(31) + EXP_GAIN(33) == 0
		PRINTL 
		PRINTFORML %CALLNAME:TARGET%は何かから耐えるように内腿を緊張させている。
		PRINTFORML どうやら、おしっこが漏れるのを我慢しているようだ…
	ENDIF
	LOCAL = 0
ENDIF

SIF LOCAL
	CALL EVENT_OMORASHI, LOCAL

;テキストを読むためのウエイト（表示があるときのみ）
SIF LINECOUNT > MEMO_LINECOUNT
	WAIT

IF COND("液体浣腸") + COND("精液浣腸") && COND("アナル栓") == 0
	;限界値を越えているかどうか
	IF COND("排泄：限界")
		CALL SELF_KOJO, "プラグ無し浣腸イベント"
		;アナルがふさがれていれば漏れない
		IF INSERT("Ａインサート") == 0
			PRINTFORML 
			PRINTFORM %CALLNAME:TARGET%は
			SELECTCASE SELECTCOM
			CASE 14
				PRINTFORM %CALLNAME:PLAYER%に向けてアヌスを押し広げた体勢のまま、耐え切れずに
			CASE 33
				PRINTFORM アナルを塞いでいた物を引きずり出されるのにつられて
			CASE 142, 143
				PRINTFORM アナルから腕を引き抜かれた弾みで
			CASEELSE
				IF NOWEX:绝顶数合计
					PRINTFORM 絶頂の快楽に耐え切れずに
				ELSE
					IF CUP:快Ａ >= 1000
						PRINTFORM アナルへの刺激に耐え切れずに
					ELSE
						PRINTFORM 便意を堪え切れずに
					ENDIF
				ENDIF
			ENDSELECT
			SELECTCASE BASE:排泄物
			CASE 0
				PRINTFORML 腸内の汚液を\@ COND("大量浣腸") ? 盛大にぶちまけて # 漏らして \@しまった…
			CASE IS <= 2
				IF COND("精液浣腸")
					PRINTFORM 精液で彩られた
				ELSEIF COND("温泉浣腸")
					PRINTFORM 小便混じりの
				ELSE
					PRINTFORM 薬液で柔らかくされた
				ENDIF
				PRINTFORMW 腸内の汚物を肛門から\@ COND("大量浣腸") ? 盛大に # \@撒き散らしてしまった…
			CASE IS <= 6
				PRINTFORMW 腸内に溜められていた数日分はあろうかという汚物を\@ COND("精液浣腸") ? 注ぎ込まれた精液と共に # \@凄い勢いでぶちまけてしまった…
			CASEELSE
				PRINTFORMW \@ COND("精液浣腸") ? 精液塗れにされた # \@\@ RAND:2 ? 排泄物 # 腸内の汚物 \@を盛大にぶちまけてしまった
				PRINTFORMW しばらく便秘気味だったのか、始めに軟らかめのモノが凄い勢いで飛び出てきた後に、\@ RAND:2 ? いかにも硬そうで # 一本に繋がった \@太いチョコバーのような汚物がゆっくりと時間をかけて肛門を押し広げながらひり出されている…
			ENDSELECT

			CALL SETFLAG, "排泄"
			
			;排泄した時のSOURCEを加算
			CALL SOURCE_COM136, 100

			CALL SELF_KOJO, "プラグ無し浣腸イベント"
		ENDIF
	ELSEIF COND("液体浣腸")
		PRINTFORML 
		SELECTCASE COND("液体浣腸")
		CASE 4
			IF ABL:受虐倾向 >= 5
				PRINTFORML %CALLNAME:TARGET%は苦悶と快楽で緩みきった顔で排泄感を楽しんでいる…
			ELSE
				PRINTFORML %CALLNAME:TARGET%は絶望的な量の浣腸液を何とか堪えているようだ…
			ENDIF
		CASE 3
			PRINTFORML %CALLNAME:TARGET%は大量の\@ COND("温泉浣腸") ? 小便を注がれたことによる排泄感 # 浣腸液 \@を何とか堪えているようだ…
		CASE 2
			PRINTFORML %CALLNAME:TARGET%は必死に排泄感に耐えている…
		CASE 1
			IF ABL:受虐倾向 >= 5
				PRINTFORML %CALLNAME:TARGET%は排泄感に堪えながらも時折物足りなそうな目をあなたに向ける…
			ELSE
				PRINTFORML %CALLNAME:TARGET%は漏れないよう便意を堪えている…
			ENDIF
		ENDSELECT
		CALL SELF_KOJO, "プラグ無し浣腸イベント"
	ENDIF
ENDIF


;-------------------------------------------------
;ボイス停止
;-------------------------------------------------
SIF CONFIG("EmueraTunes")
	TRYCALL VSTOP


@EVENT_OMORASHI, ARG
IF ARG >= 2
	PRINTFORML %EXPNAME:33%＋2
	EXP:漏尿经验 += 2
	BASE:尿意 = 0
ELSE
	PRINTFORML %EXPNAME:33%＋1
	EXP:漏尿经验 += 1
	BASE:尿意 = MAX(BASE:尿意 - 20000, 0)
ENDIF
TEQUIP:利尿剂 = 0

;放尿のソース獲得
CALL SOURCE_COM144, 80

;絶頂放尿
NOWEX:绝顶漏尿 = ARG
EX:绝顶漏尿 += ARG
;尿の汚れ発生
IF TALENT:男性 == 0
	CALL ADD_STAIN, TARGET, "Ｖ", "尿"
	STAIN:Ｖ = SET_STAIN(STAIN:Ｖ, "尿")
	;聖水プレイ
	SIF TEQUIP:颜面骑乘
		STAIN:PLAYER:Ｍ = SET_STAIN(STAIN:PLAYER:Ｍ, "尿")
ENDIF

;-------------------------------------------------
;ラグジャラスヒットと恍惚判定
;ARGSは今のところ"コマンド"と"精液"がある
;PLAYERかTARGETかどうかで別物だけどここで一括
;-------------------------------------------------
@LUXURIOUS_HIT, ARGS, ARG
#DIM LCOUNT
#DIM WEEKNESS

SIF ARG == 0 && TARGET
	ARG = TARGET

WEEKNESS = 0
VARSET LOCAL

SELECTCASE ARGS
CASE "コマンド"
	SELECTCASE ARG
	CASE PLAYER
		;今のところプレイヤーで判定が起きるのは主人だけ
		SIF PLAYER != MASTER
			RETURN 0

		;弱点の処理
		SELECTCASE SELECTCOM
		;手に弱い
		CASE 90
			SETBIT WEEKNESS, 0
		CASE 97
			SETBIT WEEKNESS, 0
			SIF TALENT:男性 == 0
				SETBIT WEEKNESS, 4
		;口に弱い
		CASE 91, 96, 98, 99, 140
			SETBIT WEEKNESS, 1
		;胸に弱い
		CASE 5, 81, 92, 104
			SETBIT WEEKNESS, 2
		;脚に弱い
		CASE 95
			SETBIT WEEKNESS, 3
		;♀に弱い
		CASE 1
			SIF TEQUIP:推到 || TEQUIP:颜面骑乘 || TEQUIP:６９式
				SETBIT WEEKNESS, 4
		CASE 13, 65, 99
			SETBIT WEEKNESS, 4
		;キスに弱い
		CASE 6
			SIF NUM("ターン数")
				SETBIT WEEKNESS, 5
		CASE 63, 73
			SIF CHECK_COM("キス") && TALENT:恋慕
				SETBIT WEEKNESS, 5
		;アナルに弱い
		CASE 9
			SIF TEQUIP:推到 || TEQUIP:颜面骑乘 || TEQUIP:６９式
				SETBIT WEEKNESS, 6
		CASE 14, 75
			SETBIT WEEKNESS, 6
		;♂に弱い
		CASE 4
			SIF TEQUIP:推到 || TEQUIP:颜面骑乘 || TEQUIP:６９式
				SETBIT WEEKNESS, 7
		CASE 120 TO 129
			SETBIT WEEKNESS, 7
		ENDSELECT

		;自慰中
		SELECTCASE TEQUIP:PLAYER:调教者自慰？
		;クンニ
		CASE 1
			SETBIT WEEKNESS, 4
		;自慰
		CASE 3

		;フェラする
		CASE 4
			SETBIT WEEKNESS, 7
		;アナル舐め
		CASE 9
			SETBIT WEEKNESS, 6
		;くぱぁさせる
		CASE 13
			SETBIT WEEKNESS, 4
		;Ａくぱぁさせる
		CASE 14
			SETBIT WEEKNESS, 6
		ENDSELECT

		LOCAL = TCVAR:PLAYER:101

		VARSET LOCALS
		FOR LCOUNT, 0, NUM("弱点")
			SIF GETBIT(TALENT:MASTER:弱点, LCOUNT) == 0 || GETBIT(WEEKNESS, LCOUNT) == 0
				CONTINUE
			LOCAL:1 += 1
			SIF RAND:(LOCAL:1)
				CONTINUE
			LOCALS = %NAMES("弱点", LCOUNT)%
			LOCAL += 1000 * LOCAL:1
		NEXT

		SIF LOCALS == ""
			RETURN 0

		;母乳を飲んでいると恍惚度合い上昇
		LOCAL = MULTIPLY(LOCAL, 100 + EXP_GAIN(74, PLAYER)*10)

		;性癖合致度
		FOR LCOUNT, 0, NUM("属性")
			SIF GETBIT(BASE:属性, LCOUNT) && GETBIT(TALENT:PLAYER:性癖, LCOUNT)
				LOCAL += LOCAL*20/100 + 1000
		NEXT
		;お気に入りor相思相愛
		IF PLAYER == MASTER
			IF TALENT:相思相爱
				LOCAL += LOCAL*30/100 + 2000
			ELSEIF TALENT:偏爱
				LOCAL += LOCAL*20/100 + 1000
			ENDIF
		ENDIF

		SIF LOCAL < 1000 || LOCAL + BASE:PLAYER:绝顶 < MAX(MAXBASE:PLAYER:绝顶/4, 5000) || RAND:3
			RETURN 0

		SETCOLOR DEF_COLOR("ハートピンク")

		PRINTFORML ＜ラグジャラスヒット！（%CALLNAME:PLAYER%）＞
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に夢中になってしまった！

		NOWEX:PLAYER:会心一发绝顶 = 1
		EX:PLAYER:会心一发绝顶 += 1
		BASE:PLAYER:绝顶 += LOCAL
		TCVAR:PLAYER:101 += LOCAL

		IF LOCAL >= MAX(MAXBASE:PLAYER:绝顶/3, 5000) && RAND:2 == 0
			PRINTFORML %CALLNAME:PLAYER%は恍惚となってしまった……
			TEQUIP:推到 = 1
			CALL ADD_STATE, PLAYER, "恍惚"
			FORCEWAIT
		ENDIF
		RESETCOLOR
	CASE TARGET
		;弱点等を用意してから設定する（PALAM:连续绝顶COMBO数でも判定）
		;まずは弱点を突かれたかで判定
		;GET_PALAMLV(ARG)の値は、順に1:100,2:500,3:3000,4:1万,5:3万,6:6万,7:10万,8:15万,9:25万

		;快感での汎用判定
		SIF TALENT:Ｃ敏感 || TALENT:淫核
			LOCAL = MAX(GET_PALAMLV(CUP:快Ｃ), LOCAL)
		SIF TALENT:Ｖ敏感 || TALENT:淫壶
			LOCAL = MAX(GET_PALAMLV(CUP:快Ｖ), LOCAL)
		SIF TALENT:Ａ敏感 || TALENT:尻穴狂
			LOCAL = MAX(GET_PALAMLV(CUP:快Ａ), LOCAL)
		SIF TALENT:Ｂ敏感 || TALENT:淫乳
			LOCAL = MAX(GET_PALAMLV(CUP:快Ｂ), LOCAL)
		SIF TALENT:Ｍ敏感 || TALENT:荡唇
			LOCAL = MAX(GET_PALAMLV(CUP:快Ｍ), LOCAL)


		;ラグジャラス判定
		SIF LOCAL <= RAND:10 || RAND:3
			RETURN 0

		SETCOLOR DEF_COLOR("ハートピンク")

		PRINTFORML ＜ラグジャラスヒット！＞
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の弱点を突いた！

		NOWEX:会心一发绝顶 = 1
		EX:会心一发绝顶 += 1

		SIF TALENT:Ｃ敏感 || TALENT:淫核
			TIMES CUP:快Ｃ, 2.00
		SIF TALENT:Ｖ敏感 || TALENT:淫壶
			TIMES CUP:快Ｖ, 2.00
		SIF TALENT:Ａ敏感 || TALENT:尻穴狂
			TIMES CUP:快Ａ, 2.00
		SIF TALENT:Ｂ敏感 || TALENT:淫乳
			TIMES CUP:快Ｂ, 2.00
		SIF TALENT:Ｍ敏感 || TALENT:荡唇
			TIMES CUP:快Ｍ, 2.00

		;IF LOCAL*10 + PALAM:连续绝顶COMBO数*5 > RAND:100 && RAND:2 == 0
		;	PRINTFORML %CALLNAME:TARGET%は恍惚としている……
		;	CALL ADD_STATE, TARGET, "恍惚"
		;	FORCEWAIT
		;ENDIF
		RESETCOLOR
	ENDSELECT
CASE "精液"
	;イくのをこらえた状態で無いor既に絶頂済み
	SIF NOWEX:绝顶数合计 || NOWEX:忍住了没去 == 0
		RETURN 0
	;精液に弱くないなら、精液が竜属性でない限り、ここで終了
	SIF TALENT:弱精液 == 0 && TALENT:PLAYER:龙之血 == 0
		RETURN 0

	IF SHOOT("口")
		SIF COND("精飲に弱い") == 0 && TALENT:PLAYER:龙之血 == 0
			RETURN 0
		SIF EXP_GAIN(19) && RAND:3
			RETURN 0
		TIMES CUP:快Ｍ, 2.00
	ELSEIF SHOOT("Ｖ中出し")
		SIF COND("膣内射精に弱い") == 0 && TALENT:PLAYER:龙之血 == 0
			RETURN 0
		SIF EXP_GAIN(17) && RAND:3
			RETURN 0
		TIMES CUP:快Ｖ, 2.00
	ELSEIF SHOOT("Ａ中出し")
		SIF COND("肛内射精に弱い") == 0 && TALENT:PLAYER:龙之血 == 0
			RETURN 0
		SIF EXP_GAIN(18) && RAND:3
			RETURN 0
		TIMES CUP:快Ａ, 2.00
	ELSEIF SHOOT("ニプルファック")
		SIF COND("乳内射精に弱い") == 0 && TALENT:PLAYER:龙之血 == 0
			RETURN 0
		SIF EXP_GAIN(47) && RAND:3
			RETURN 0
		TIMES CUP:快Ｂ, 2.00
	ELSEIF SHOOT("尿道中出し")
		SIF COND("尿道内射精に弱い") == 0 && TALENT:PLAYER:龙之血 == 0
			RETURN 0
		SIF EXP_GAIN(80) && RAND:3
			RETURN 0
		NOWEX:尿道绝顶 = 1
		EX:尿道绝顶 += 1

		TIMES CUP:Ｖ润, 2.00
		TIMES CUP:欲情, 2.00
		TIMES CUP:屈服, 2.00
		TIMES CUP:耻情, 2.00
	ENDIF

	NOWEX:精液绝顶 = 1
	EX:精液绝顶 += 1
	NOWEX:会心一发绝顶 = 1
	EX:会心一发绝顶 += 1
	NOWEX:忍住了没去 = 0
	EX:忍住了没去 = 0

	CALL ORGASM_ADD_A
	CALL ORGASM_ADD_C

	SETCOLOR DEF_COLOR("ハートピンク")

	PRINTFORML ＜ラグジャラスヒット！＞

	IF SHOOT("口")
		IF COND("绝顶", PLAYER) >= 2 && RAND:2
			PRINTFORM 大量に精液を飲まされた
		ELSE
			IF COND("绝顶", PLAYER) >= 2 && RAND:2
				PRINTFORM %TEXTR("大量の", "溢れんばかりの", "凄い量の")%
			ELSE
				PRINTFORM %TEXTR("勢いよく", "熱い", "濃厚な")%
			ENDIF
			IF SELECTCOM == 140
				PRINTFORM 精液%TEXTR("を食道に流し込まれた", "を喉の奥に浴びせかけられた")%
			ELSE
				PRINTFORM 精液%TEXTR("で口内を満たされた", "を口に流し込まれた", "を喉の奥に浴びせかけられた")%
			ENDIF
		ENDIF
		PRINTFORM %CALLNAME:TARGET%は、
		PRINTFORM %TEXTR("気持ちよさのあまり", "堪えきれずに", "興奮のあまり", "身体を震わせて", "鼻から熱い吐息を漏らしながら", "我慢できずに")%
		PRINTFORMW 絶頂してしまった！
	ELSEIF SHOOT("Ｖ中出し")
		IF BASE:子宫内精子量 < 100 && NOWEX:Ｖ中出 == EX:Ｖ中出 && RAND:2
			PRINTFORM 飢えた子宮に精液をご馳走された
		ELSEIF RAND:2
			IF COND("绝顶", PLAYER) >= 2 && RAND:2
				PRINTFORM %TEXTR("大量の", "溢れんばかりの", "凄い量の")%
			ELSE
				PRINTFORM %TEXTR("勢いよく", "熱い", "濃厚な")%
			ENDIF
			PRINTFORM 精液%TEXTR("で子宮を叩かれた", "を膣内に流し込まれた", "を弱点に浴びせかけられた")%
		ELSEIF COND("绝顶", PLAYER) >= 2 && RAND:2
			PRINTFORM 大量に中出しをされた
		ELSE
			PRINTFORM 大好きな膣内射精をされた
		ENDIF
		PRINTFORM %CALLNAME:TARGET%は、
		PRINTFORM %TEXTR("気持ちよさのあまり", "堪えきれずに", "興奮のあまり", "身体を震わせて", "熱い吐息を漏らしながら", "我慢できずに")%
		PRINTFORMW 絶頂してしまった！
	ELSEIF SHOOT("Ａ中出し")
		IF RAND:2
			IF COND("绝顶", PLAYER) >= 2 && RAND:2
				PRINTFORM %TEXTR("大量の", "溢れんばかりの", "凄い量の")%
			ELSE
				PRINTFORM %TEXTR("勢いよく", "熱い", "濃厚な")%
			ENDIF
			PRINTFORM 精液%TEXTR("で直腸の奥を叩かれた", "をアナルに流し込まれた", "を弱点に浴びせかけられた")%
		ELSEIF COND("绝顶", PLAYER) >= 2 && RAND:2
			PRINTFORM 大量にアナルに中出しをされた
		ELSE
			PRINTFORM 大好きな肛内射精をされた
		ENDIF
		PRINTFORM %CALLNAME:TARGET%は、
		PRINTFORM %TEXTR("気持ちよさのあまり", "堪えきれずに", "興奮のあまり", "身体を震わせて", "熱い吐息を漏らしながら", "我慢できずに")%
		PRINTFORMW 絶頂してしまった！
	ELSEIF SHOOT("ニプルファック")
		IF RAND:2
			IF COND("绝顶", PLAYER) >= 2 && RAND:2
				PRINTFORM %TEXTR("大量の", "溢れんばかりの", "凄い量の")%
			ELSE
				PRINTFORM %TEXTR("勢いよく", "熱い", "濃厚な")%
			ENDIF
			PRINTFORM 精液を%TEXTR("ニプルにご馳走された", "乳房の中に流し込まれた", "弱点に注がれた")%
		ELSEIF COND("绝顶", PLAYER) >= 2 && RAND:2
			PRINTFORM 大量に乳房に中出しをされた
		ELSE
			PRINTFORM 大好きな乳内射精をされた
		ENDIF
		PRINTFORM %CALLNAME:TARGET%は、
		PRINTFORM %TEXTR("気持ちよさのあまり", "堪えきれずに", "興奮のあまり", "身体を震わせて", "熱い吐息を漏らしながら", "我慢できずに")%
		PRINTFORMW 絶頂してしまった！
	ELSEIF SHOOT("尿道中出し")
		IF RAND:2
			IF COND("绝顶", PLAYER) >= 2 && RAND:2
				PRINTFORM %TEXTR("大量の", "溢れんばかりの", "凄い量の")%
			ELSE
				PRINTFORM %TEXTR("勢いよく", "熱い", "濃厚な")%
			ENDIF
			PRINTFORM 精液を%TEXTR("大好きな場所にご馳走された", "オシッコの穴に流し込まれた", "弱点に注がれた")%
		ELSEIF COND("绝顶", PLAYER) >= 2 && RAND:2
			PRINTFORM 大量に膀胱への中出しをされた
		ELSE
			PRINTFORM 大好きな尿道への射精をされた
		ENDIF
		PRINTFORM %CALLNAME:TARGET%は、
		PRINTFORM %TEXTR("気持ちよさのあまり", "堪えきれずに", "興奮のあまり", "身体を震わせて", "熱い吐息を漏らしながら", "我慢できずに")%
		PRINTFORMW 絶頂してしまった！
	ENDIF
	RESETCOLOR
	CALL ORGASM_ADD_TEXT, TARGET
ENDSELECT


@EVENT_EXSTART
TEQUIP:逆调教 = 1
TEQUIP:PLAYER:逆调教 = 1
PRINTFORML 
DRAWLINE
PRINTFORML 
ALIGNMENT CENTER
TRYCALLFORM KOJO_EVENT_EXSTART_{NO:TARGET}_{CFLAG:209}, "タイトル"
ALIGNMENT LEFT
PRINTFORML 
DRAWLINE
;ループ開始
$EX_LOOP
NOWEX:爆发 = 0
CALL EVENT_EXPLAY
TRYCALLFORM KOJO_EVENT_EXSTART_{NO:TARGET}_{CFLAG:209}, "プレイ直後"
TRYCALLFORM KOJO_EVENT_EXSTART_{NO:TARGET}_{CFLAG:209}, "選択肢"

SIF GETBIT(TEQUIP:逆调教, 1)
	GOTO EX_END

;精力が無くなり、かつストックが有るなら精力補充
IF BASE:PLAYER:精力 <= 0 && BASE:PLAYER:STOCK
	BASE:PLAYER:STOCK -= 1
	BASE:PLAYER:精力 = MAXBASE:PLAYER:精力
	PRINTFORML %CALLNAME:PLAYER%の精力が回復した！（させられた！）
	PRINTFORML 
	SETCOLOR DEF_COLOR("イエロー")
	PRINTFORML %CALLNAME:PLAYER%の精力残り …… {BASE:PLAYER:精力}！
	PRINTFORMW ストック残り …… {BASE:PLAYER:STOCK}！
	RESETCOLOR
	PRINTFORML 
ENDIF
TRYCALLFORM KOJO_EVENT_EXSTART_{NO:TARGET}_{CFLAG:209}, "プレイ一区切り"

SIF BASE:PLAYER:精力 > 0
	GOTO EX_LOOP

$EX_END
;ストックが0（かつ精力が0以下）
IF BASE:PLAYER:精力 <= 0 && BASE:PLAYER:STOCK == 0 && RAND:3
	;快楽刻印・淫乱を取得
	DRAWLINE
	PRINTFORML 
	TRYCALLFORM KOJO_KAIRAKU_{NO:TARGET}_{CFLAG:209}
	PRINTFORML 
	DRAWLINE
ENDIF


;-------------------------------------------------
;快楽調教関数
;なすがままからの逆調教で使用
;奉仕系メイン
;-------------------------------------------------
@EVENT_EXPLAY
PRINTFORML 
;調教メニュー一覧
VARSET LOCALS
LOCAL = ABL:技巧 * 1000 + MAX(ABL:ＬＶ - ABL:PLAYER:ＬＶ, 0) * 1000 + ABL:ＬＶ * 500 + (MARK:PLAYER:快乐刻印 + TALENT:PLAYER:淫乱) * 1500

TRYCALLFORM SET_EVENT_EXPLAY_{NO:TARGET}_{CFLAG:209}
LOCALS = %STR:100%

SELECTCASE LOCALS
CASE "指挿れ", "バイブ責め", "オナニー"
	LOCAL += ABL:指技 * 1000 + EXP:侍奉快乐经验 + RAND:10
	EXP:手淫经验 += ABL:技巧/2 + 1
CASE "快楽レイプ"
	LOCAL += ABL:腰技 * 1000 + EXP:侍奉快乐经验 + RAND:10
	EXP:Ｖ插入经验 += ABL:技巧/2 + 1
	EXP:PLAYER:Ｖ性交经验 += ABL:TARGET:技巧/2 + 1
	EXP:PLAYER:Ｖ经验 += ABL:TARGET:技巧/2 + 1
CASE "クンニ"
	LOCAL += ABL:舌技 * 1000 + EXP:侍奉快乐经验 + RAND:10
	EXP:舔阴经验 += ABL:技巧/2 + 1
CASE "フェラチオ"
	LOCAL += ABL:舌技 * 1000 + EXP:侍奉快乐经验 + RAND:100 + RAND:10
	EXP:口交经验 += ABL:技巧/2 + 1
CASE "パイズリ"
	LOCAL += (ABL:舌技 + ABL:魔乳) * 1000 +  EXP:侍奉快乐经验 + RAND:10
	EXP:乳交经验 += ABL:技巧/2 + 1
CASE "手淫", "オナホコキ"
	LOCAL += ABL:指技 * 1000 + EXP:侍奉快乐经验 + RAND:10
	EXP:手淫经验 += ABL:技巧/2 + 1
CASE "キス"
	LOCAL += ABL:舌技 * 1000 + EXP:侍奉快乐经验 + RAND:10
	EXP:亲吻经验 += ABL:技巧/2 + 1
	EXP:PLAYER:亲吻经验 += ABL:技巧/2 + 1
CASE "胸爱抚", "搾乳プレイ"
	LOCAL += (ABL:指技 + ABL:魔乳) * 1000 + EXP:侍奉快乐经验 + RAND:10
	EXP:手淫经验 += ABL:技巧/2 + 1
CASE "尻尾ペニス責め", "尻尾責め"
	LOCAL += ABL:技巧 * 1000 + MIN(EXP:侍奉快乐经验 + CFLAG:2, MAXBASE:PLAYER:绝顶) + RAND:1000
	LOCAL *= 9
	EXP:嗜虐快乐经验 += ABL:技巧*9
ENDSELECT
;その他経験
IF TALENT:男性 == 0 && TALENT:PLAYER:男性 == 0
	EXP:百合经验 += RAND:5 + 1
	EXP:PLAYER:百合经验 += RAND:5 + 1
ELSEIF TALENT:男性 && TALENT:PLAYER:男性
	EXP:ＢＬ经验 += RAND:5 + 1
	EXP:PLAYER:ＢＬ经验 += RAND:5 + 1
ENDIF
EXP:嗜虐快乐经验 += ABL:技巧

TRYCALLFORM KOJO_EVENT_EXPLAY_{NO:TARGET}_{CFLAG:209}, LOCALS

LOCAL = MULTIPLY(CALCF("基礎補正", LOCAL), CALCF("快楽補正", 100) )

CALL TEXT, "絶頂計算", LOCAL
;絶頂できなかった
IF CALCF("绝顶", PLAYER) == 0
	RETURN 0
;絶頂した
ELSE
	LOCALS:1 = 絶頂：%LOCALS%
	TRYCALLFORM KOJO_EVENT_EXPLAY_{NO:TARGET}_{CFLAG:209}, LOCALS:1

	CALL BOUHATSU
	;追撃判定
	;残精力20以下だと一定確率で追撃、それ以上だとより低確率で追撃。それとは別にストックでも判定
	IF (BASE:PLAYER:精力 <= 20 && RAND:2) || (BASE:PLAYER:精力 > 20 && RAND:4 == 0) || (BASE:PLAYER:STOCK && RAND:5 == 0)
		IF BASE:PLAYER:STOCK <= 0 && BASE:PLAYER:精力 <= 0
			PRINTFORMW %CALLNAME:PLAYER%はもはや限界だが、%CALLNAME:TARGET%はその手を緩めない！
		ELSEIF BASE:PLAYER:精力 <= 0
			PRINTFORMW %CALLNAME:PLAYER%は精力を残さず%CALLNAME:TARGET%に献上してしまった！
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%の精力残り …… {BASE:PLAYER:精力}！
		ENDIF
		PRINTFORML 

		LOCALS:1 = 追撃：%LOCALS%
		TRYCALLFORM KOJO_EVENT_EXPLAY_{NO:TARGET}_{CFLAG:209}, LOCALS:1

		;追撃された場合は必殺
		LOCAL = 100000 + EXP:侍奉快乐经验 + RAND:10000
		CALL TEXT, "絶頂計算", LOCAL
		CALL BOUHATSU

		;余力がある場合はさらに追撃判定(CALL BOUHATUなどはこの台詞の中で行う)
		IF BASE:PLAYER:精力 >= 10
			LOCALS:1 = ＥＸ追撃：%LOCALS%
			TRYCALLFORM KOJO_EVENT_EXPLAY_{NO:TARGET}_{CFLAG:209}, LOCALS:1
		ENDIF

		IF BASE:PLAYER:精力 <= 0 && BASE:PLAYER:STOCK == 0 
			IF PENIS(PLAYER) && RAND:4 == 0
				PRINTFORMW %CALLNAME:PLAYER%はすっからかんになるまで%CALLNAME:TARGET%にしゃぶり尽くされてしまった！
			ELSEIF RAND:3 == 0
				PRINTFORMW %CALLNAME:PLAYER%は精力を根こそぎ%CALLNAME:TARGET%に搾りつくされてしまった！
			ELSEIF RAND:2 == 0
				PRINTFORMW %CALLNAME:PLAYER%は精力を全て%CALLNAME:TARGET%に美味しく頂かれてしまった！
			ELSE
				PRINTFORMW %CALLNAME:PLAYER%はカラッカラのヒモノになってしまった！
			ENDIF
		ELSEIF BASE:PLAYER:精力 <= 0
			PRINTFORMW %CALLNAME:PLAYER%は精力を残さず%CALLNAME:TARGET%に献上してしまった！
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%の精力残り …… {BASE:PLAYER:精力}！
		ENDIF
		PRINTFORML 
	ENDIF
ENDIF
