;-------------------------------------------------
;調教コマンド実行前のパラメータの記録
;TCVAR:100〜119を"調教コマンド実行前の重要パラメータの記録"用として確保してある
;-------------------------------------------------
@SAVE_PALAM_BEFORECOM
#DIM LCOUNT
FOR LCOUNT, 1, CHARANUM
	;射精or絶頂ゲージの開始値＆増加量
	TCVAR:LCOUNT:100 = BASE:LCOUNT:絶頂
	TCVAR:LCOUNT:101 = 0
	;母乳ゲージの開始値＆増加量
	TCVAR:LCOUNT:102 = BASE:LCOUNT:噴乳
	TCVAR:LCOUNT:103 = 0
	;触手射精ゲージの開始値＆増加量
	TCVAR:LCOUNT:104 = BASE:LCOUNT:触手
	TCVAR:LCOUNT:105 = 0
	;ドラゴン射精ゲージの開始値＆増加量
	TCVAR:LCOUNT:106 = BASE:LCOUNT:ドラゴン
	TCVAR:LCOUNT:107 = 0
	;精力の開始値＆減少量
	TCVAR:LCOUNT:108 = BASE:LCOUNT:精力
	TCVAR:LCOUNT:109 = 0
	;受精確率
	TCVAR:LCOUNT:110 = COND("受精確率", LCOUNT)
NEXT


;-------------------------------------------------
;PALAMの増減を表示させる（Ｐ潤は表示しない）
;対応する珠の増加分も表示するように変更
;ARGにはTARGETかASSIかTARGET:1しか入らない
;-------------------------------------------------
@NEW_UPCHECK_PALAM, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT
;このターンでの増減を行う前のPALAMの値の記録に使う
#DIM PALAM_PRE, 16
#DIM PALAM_NUM


;Ｗ押し倒しの時には名前を表示させる
SIF TEQUIP:Ｗ押し倒し && SELECTCOM:1 >= 0
	PRINTFORML 　＜%CALLNAME:ARG%＞

PRINTL 

MEMO_LINECOUNT = LINECOUNT

FOR LCOUNT, 0, 16
	PALAM_PRE:LCOUNT = PALAM:ARG:LCOUNT
	PALAM:ARG:LCOUNT = MIN(PALAM_LV(16), PALAM:ARG:LCOUNT + CUP:ARG:LCOUNT - CDOWN:ARG:LCOUNT)
NEXT

FOR LCOUNT, 0, 16
	SELECTCASE LCOUNT
	CASE 0 TO 2
		PALAM_NUM = LCOUNT
	CASE 3
		PALAM_NUM = 14
	CASE 4
		PALAM_NUM = 15
	CASEELSE
		PALAM_NUM = LCOUNT - 2
	ENDSELECT

	SIF CUP:ARG:PALAM_NUM == 0 && CDOWN:ARG:PALAM_NUM == 0
		CONTINUE

	PRINTFORM 　%TEXT_LJ(PALAMNAME:PALAM_NUM, 8)% = {PALAM_PRE:PALAM_NUM, 8} + 

	SELECTCASE GET_PALAMLV(CUP:ARG:PALAM_NUM)
	CASE 3
		SETCOLOR DEF_COLOR("黄色")
	CASE IS >= 4
		SETCOLOR DEF_COLOR("ピンク")
	ENDSELECT
	PRINTFORM {CUP:ARG:PALAM_NUM, 7}
	RESETCOLOR

	PRINTFORM  - {CDOWN:ARG:PALAM_NUM, 7} = {PALAM:ARG:PALAM_NUM, 8}　

	;宝珠を獲得するようならそれを表示
	IF GET_JUEL(PALAM_NUM)
		PRINTFORM %PALAMNAME:PALAM_NUM%の珠＋
		SETCOLOR DEF_COLOR("黄色")
		PRINTFORM {GET_JUEL(PALAM_NUM), 5}
	ENDIF

	RESETCOLOR
	PRINTL 
NEXT

;変動したPALAMを表示した場合には改行
SIF LINECOUNT != MEMO_LINECOUNT
	PRINTL 


;-------------------------------------------------
;表示を整形したUPCHECK
;-------------------------------------------------
@NEW_UPCHECK
#DIM MEMO_LINECOUNT
#DIM LCOUNT
#DIM BASE_PRE, 10
#DIM BASE_AFT, 10
#DIM UP_BASE, 10
#DIM DOWN_BASE, 10
#DIM BASE_MAX, 10
#DIMS NAME_BASE, 10

;改行をするかどうかに用いる
MEMO_LINECOUNT = LINECOUNT

CALL NEW_UPCHECK_PALAM, TARGET

SIF TEQUIP:Ｗ押し倒し && SELECTCOM:1 >= 0
	CALL NEW_UPCHECK_PALAM, TARGET:1

SIF LINECOUNT == MEMO_LINECOUNT
	PRINTL 

;PLAYERのペニスのＰ潤の処理
CALL SETFLAG, "Ｐ潤変動処理", MASTER
IF COND("３Ｐ")
	SWAP SELECTCOM, SELECTCOM:2
	CALL SETFLAG, "Ｐ潤変動処理", PLAYER:1
	SWAP SELECTCOM, SELECTCOM:2
ENDIF

VARSET BASE_PRE
VARSET BASE_AFT
VARSET UP_BASE
VARSET DOWN_BASE
VARSET BASE_MAX
VARSET NAME_BASE

;射精or絶頂
BASE_AFT:0 = BASE:MASTER:絶頂
BASE_PRE:0 = TCVAR:MASTER:100
UP_BASE:0 = MAX(TCVAR:MASTER:101, 0)
DOWN_BASE:0 = BASE_PRE:0 + UP_BASE:0 - BASE_AFT:0
BASE_MAX:0 = MAXBASE:MASTER:絶頂

NAME_BASE:0 = \@ PENIS(MASTER) ? 射精 # 絶頂 \@

;精力
BASE_AFT:1 = BASE:MASTER:精力
BASE_PRE:1 = TCVAR:MASTER:108
DOWN_BASE:1 = TCVAR:MASTER:109
;UP_BASE:1 = MAX(BASE_AFT:1 + UP_BASE:1 - BASE_PRE:1, 0)
UP_BASE:1 = MAX(BASE_AFT:1 + DOWN_BASE:1 - BASE_PRE:1, 0)
NAME_BASE:1 = 精力

;射精or絶頂（助手）
IF ASSI
	BASE_AFT:2 = BASE:ASSI:絶頂
	BASE_PRE:2 = TCVAR:ASSI:100
	UP_BASE:2 = MAX(TCVAR:ASSI:101, 0)
	DOWN_BASE:2 = BASE_PRE:2 + UP_BASE:2 - BASE_AFT:2
	BASE_MAX:2 = MAXBASE:ASSI:絶頂
	NAME_BASE:2 = \@ PENIS(ASSI) ? 射精 # 絶頂 \@

	;精力（助手）
	BASE_AFT:3 = BASE:ASSI:精力
	BASE_PRE:3 = TCVAR:ASSI:108
	DOWN_BASE:3 = TCVAR:ASSI:109
	UP_BASE:3 = MAX(BASE_AFT:3 + DOWN_BASE:3 - BASE_PRE:3, 0)
	NAME_BASE:3 = 精力(助)
ENDIF

;射精（調教対象）
IF PENIS(TARGET)
	BASE_AFT:4 = BASE:絶頂
	BASE_PRE:4 = TCVAR:100
	UP_BASE:4 = MAX(TCVAR:101, 0)
	DOWN_BASE:4 = BASE_PRE:4 + UP_BASE:4 - BASE_AFT:4
	BASE_MAX:4 = MAXBASE:絶頂
	NAME_BASE:4 = 射精(調)
ENDIF

;精力（調教対象）
BASE_AFT:5 = BASE:精力
BASE_PRE:5 = TCVAR:108
DOWN_BASE:5 = TCVAR:109
UP_BASE:5 = MAX(BASE_AFT:5 + DOWN_BASE:5 - BASE_PRE:5, 0)
NAME_BASE:5 = 精力(調)

;母乳（調教対象）
IF TALENT:母乳体質
	BASE_AFT:6 = BASE:噴乳
	BASE_PRE:6 = TCVAR:102
	UP_BASE:6 = MAX(TCVAR:103, 0)
	DOWN_BASE:6 = BASE_PRE:6 + UP_BASE:6 - BASE_AFT:6
	BASE_MAX:6 = MAXBASE:噴乳
	NAME_BASE:6 = 母乳(調)
ENDIF
;射精（触手）
IF TEQUIP:触手
	BASE_AFT:7 = BASE:MASTER:触手
	BASE_PRE:7 = TCVAR:MASTER:104
	DOWN_BASE:7 = TFLAG:15*MAXBASE:MASTER:触手
	UP_BASE:7 = MAX(BASE_AFT:7 + DOWN_BASE:7 - BASE_PRE:7, 0)
	NAME_BASE:7 = 射精(触)
ENDIF
;射精（ドラ）
IF SELECTCOM == 304 || SELECTCOM == 305
	BASE_AFT:8 = BASE:MASTER:ドラゴン
	BASE_PRE:8 = TCVAR:MASTER:106
	DOWN_BASE:8 = TFLAG:9 * MAXBASE:MASTER:ドラゴン
	UP_BASE:8 = MAX(BASE_AFT:8 + DOWN_BASE:8 - BASE_PRE:8, 0)
	NAME_BASE:8 = 射精(ド)
ENDIF

MEMO_LINECOUNT = LINECOUNT

FOR LCOUNT, 0, 9
	SIF NAME_BASE:LCOUNT == ""
		CONTINUE
	SIF UP_BASE:LCOUNT == 0 && DOWN_BASE:LCOUNT == 0
		CONTINUE

	PRINTFORM 　%TEXT_LJ(NAME_BASE:LCOUNT, 8)% = {BASE_PRE:LCOUNT, 8} + 

	;射精の時などは色を変える
	IF BASE_MAX:LCOUNT
		SELECTCASE UP_BASE:LCOUNT
		CASE IS >= BASE_MAX:LCOUNT*4/5
			SETCOLOR DEF_COLOR("ピンク")
		CASE IS >= BASE_MAX:LCOUNT/4
			SETCOLOR DEF_COLOR("黄色")
		ENDSELECT
	ENDIF

	PRINTFORM {UP_BASE:LCOUNT, 7}

	RESETCOLOR

	PRINTFORML  - {DOWN_BASE:LCOUNT, 7} = {BASE_AFT:LCOUNT, 8}　%NAME_BASE:LCOUNT%
NEXT

SIF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE 1

CFLAG:490 = 0

IF BASE:PLAYER:精力 <= 0 && ASSIPLAY && STATE("撃沈", ASSI)
	PRINTL  
	PRINTFORMW ★調教者を%CALLNAME:MASTER%に交代しました。★
	BASE:ASSI:気力 = 0
	TEQUIP:押し倒し = 0
	SIF TARGET:1 == ASSI
		CALL SETFLAG, "Ｗ押し倒し解除"
	CALL REMOVE_STATE, ASSI, "恍惚"
	ASSIPLAY = 0
	PLAYER = MASTER
ENDIF
