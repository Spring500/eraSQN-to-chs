;[ライセンス]パブリックドメイン
;本ファイルに係る著作権を放棄する。
;本ファイルに係る著作人格権は行使しない。
;2009/3/28 MinorShift(Emuera作者)
;2010/4/2 MinorShift(Emuera作者) ver 1.736に合わせて一部修正。

;独自のセーブ・ロードシステムの例（Emuera  ver 1.736以降対象）
;LOADGAMEの代わりにCALL LOADGAME_EX、SAVEGAMEの代わりにCALL SAVEGAME_EXを実行してください。
;実際に使用するには旧来の@SAVEINFOに合わせて@SAVEINFO_EXを編集する必要があります。
;使用している命令の詳細については下記のEmueraWikiを参照してください。
;http://sourceforge.jp/projects/emuera/wiki/FrontPage

@SAVEGAME_EX
PRINTL
LOCAL:2 = RESULT
LOCALS:2 = %RESULTS%

$REDRAW_SAVEDATA
REDRAW 0
SIF STRLENS(SAVESTR:11)
	PRINTFORM 目前的故事的名称是『%SAVESTR:11%』。
PRINTL 要将目前的故事保存到几号？
$REDRAW_SAVEDATA2
CALL PRINT_SAVEDATA, FLAG:1
PRINTL [100] 返回
PRINTL [999] 给目前的故事命名
REDRAW 1
$INPUT_DATANUM
INPUT
IF RESULT == 100
	RESULTS = %LOCALS:2%
	RETURN LOCAL:2
ELSEIF RESULT == 999
	CALL SET_SAVEDATA_NAME
	RESTART
ELSEIF RESULT < 0 || RESULT >= SAVENOS()

ELSEIF RESULT >= FLAG:1 * 20 && RESULT < FLAG:1 * 20 + 20
	LOCAL = RESULT
	CHKDATA LOCAL
	;元のSAVEGAMEではRESULT == 0のとき以外は確認をとらない
	IF RESULT == 0
		PRINTL 已存在游戏数据。仍然要保存在这里吗？
		PRINTLC [0] 好的
		PRINTLC [1] 不要
		PRINTL
$YESNO_INPUT_LOOP
		INPUT
		IF RESULT == 1
			GOTO REDRAW_SAVEDATA
		ELSEIF RESULT != 0
			CLEARLINE 1
			REUSELASTLINE 数值无效
			GOTO YESNO_INPUT_LOOP
		ENDIF
	ENDIF

	;フラグに使う現実時間を取得
	CALL GET_REALTIMEFORFLAG
	;一言イベントフラグ初期化
	FLAG:23 = 0

	CALL SAVEINFO_EX
	SAVEDATA LOCAL, RESULTS

	RESULTS = %LOCALS:2%
	RETURN LOCAL:2
ELSE
	FLAG:1 = RESULT/20
	REDRAW 0
	GOTO REDRAW_SAVEDATA2
ENDIF
CLEARLINE 1
REUSELASTLINE 数值无效
GOTO INPUT_DATANUM

@LOADGAME_EX
PRINTL
LOCAL:2 = RESULT
LOCALS:2 = %RESULTS%

$REDRAW_SAVEDATA
REDRAW 0
SIF STRLENS(SAVESTR:11)
	PRINTFORM 目前的故事的名称是『%SAVESTR:11%』。
PRINTL 要读取几号的故事？
$REDRAW_SAVEDATA2
CALL PRINT_SAVEDATA, FLAG:1
CHKDATA 99
PRINTFORML  [99] %RESULTS:0%
PRINTL [100] 返回
REDRAW 1
$INPUT_DATANUM
INPUT
IF RESULT == 100
	RESULTS = %LOCALS:2%
	RETURN LOCAL:2
ELSEIF ((RESULT < 0) || (RESULT >= SAVENOS())) && RESULT != 99

ELSEIF ((RESULT >= FLAG:1 * 20) && (RESULT < FLAG:1 * 20 + 20)) || RESULT == 99
	LOCAL = RESULT
	CHKDATA LOCAL
	;RESULT == 0ならロード可能
	IF RESULT != 0
		PRINTVL LOCAL
		PRINTL 数据不存在
		GOTO REDRAW_SAVEDATA
	ENDIF
	LOADDATA LOCAL

	RESULTS = %LOCALS:2%
	RETURN LOCAL:2
ELSE
	FLAG:1 = RESULT/20
	REDRAW 0
	GOTO REDRAW_SAVEDATA2
ENDIF
CLEARLINE 1
REUSELASTLINE 数值无效
GOTO INPUT_DATANUM

@PRINT_SAVEDATA, ARG
#DIM LCOUNT
;ARG:0 需要跳转到第几页？
IF ARG > 0
	FOR LCOUNT, 0, ARG
		PRINTFORML  [{LCOUNT*20, 2}] 显示第{LCOUNT*20, 2}～{LCOUNT*20+19, 2}号故事
	NEXT
ENDIF
FOR LCOUNT, ARG * 20, ARG * 20 + 20
	SIF LCOUNT >= SAVENOS()
		BREAK

	CHKDATA LCOUNT
	PRINTFORML  [{LCOUNT, 2, RIGHT}] %RESULTS:0%
NEXT
IF (ARG + 1) < (SAVENOS() + 19 / 20)
	FOR LCOUNT, ARG + 1, (SAVENOS() + 19) / 20
		PRINTFORML  [{LCOUNT*20, 2}] 显示第{LCOUNT*20, 2}～{MIN(LCOUNT*20+19, SAVENOS() - 1), 2}号故事
	NEXT
ENDIF
RETURN

@SAVEINFO_EX
;SAVEDATA命令は@SAVEINFO関数を呼ばないのでPUTFORMの代わりの文字列を用意する必要がある
;GETTIME関数でRESULTSに現在時刻を表す文字列を代入
RESULTS = %GETTIMES_CUSTOM()%
SIF STRLENS(SAVESTR:11)
	RESULTS = 『%SAVESTR:11%』%RESULTS%
RESULTS = %RESULTS% %SAVEINFO_MESSAGE()%
RETURN RESULT

@GETTIMES_CUSTOM()
#FUNCTIONS
;カスタム時間表示の例
;まずは取得
LOCAL = GETTIME()
;年。下二桁のみに簡略化
LOCAL:1 = (LOCAL / 10000000000000) % 100
LOCALS = \@(LOCAL:1 < 10) ? 0{LOCAL:1} # {LOCAL:1}\@
;月
LOCAL:1 = (LOCAL / 100000000000) % 100
LOCALS = %LOCALS%/\@(LOCAL:1 < 10) ? 0{LOCAL:1} # {LOCAL:1}\@
;日
LOCAL:1 = (LOCAL / 1000000000) % 100
LOCALS = %LOCALS%/\@(LOCAL:1 < 10) ? 0{LOCAL:1} # {LOCAL:1}\@
;時
LOCAL:1 = ((LOCAL / 10000000) % 100) % 24
LOCALS = %LOCALS% \@ LOCAL:1 < 10 ? 0 # \@{LOCAL:1}

;午前午後表示
;LOCAL:2 = (((LOCAL / 10000000) % 100) / 12) % 2
;LOCALS = %LOCALS% \@(LOCAL:2) ? 上午 # 下午\@\@LOCAL:1 < 10 ? 0 # \@{LOCAL:1}

;分
LOCAL:1 = (LOCAL / 100000) % 100
LOCALS = %LOCALS%:\@LOCAL:1 < 10 ? 0 # \@{LOCAL:1}
;秒は省略
RETURNF LOCALS

@SAVEINFO
PUTFORM %SAVEINFO_MESSAGE()%

@SAVEINFO_MESSAGE()
#FUNCTIONS

LOCALS = {FLAG:4 + 1}周目
IF TIME == 0
	LOCALS = %LOCALS% {DAY}日 昼
ELSE
	LOCALS = %LOCALS% {DAY}日 夜
ENDIF
LOCALS = %LOCALS% LV{FLAG:255}
IF MONEY >= 100000000
	LOCALS = %LOCALS% 百万富翁
ELSEIF MONEY >= 100000
	LOCALS = %LOCALS% ${MONEY/10000}万
ELSE
	LOCALS = %LOCALS% %MONEY_C(MONEY)%
ENDIF
;SIF FLAG:31
;	LOCALS = %LOCALS% 已攻略：{FLAG:31}人
SIF TARGET
	LOCALS = %LOCALS% %NAME:TARGET%(LV{ABL:ＬＶ})调教中

RETURNF LOCALS

@SET_SAVEDATA_NAME
IF STRLENS(SAVESTR:11)
	PRINTFORML 目前的故事的名称是『%SAVESTR:11%』。
	PRINTFORML  [ 0] 修改名称
	PRINTFORML  [ 1] 清空名称
	PRINTFORML [100] 返回
	CALL INPUT_SELECT, 2, 100
ELSE
	PRINTFORML 目前的故事，还没有名称。
	PRINTFORML 给故事命名，可以更轻松地区分不同的故事。
	PRINTFORML  [ 0] 命名
	PRINTFORML [100] 返回
	CALL INPUT_SELECT, 1, 100
ENDIF
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 1
	SAVESTR:11 =
	PRINTFORMW 已经清空故事的名称，需要重新保存才能反映到故事列表中。
ELSE
	PRINTFORML 已更改故事的名称，需要重新保存才能反映到故事列表中。
	PRINTFORML 最多只能输入16个全角字符。什么都不输入的话就不会作出任何改变。
	CALL SAVENAME_CONVERT_INPUT
ENDIF

@SAVENAME_CONVERT_INPUT
INPUTS
IF STRLENS(RESULTS) > 32
	PRINTFORMW 你输入的名称太长了。最多只能输入16个全角字符。
	RESTART
ELSEIF STRLENS(RESULTS)
	PRINTFORMW 故事的名称变更为『%RESULTS%』，需要重新保存才能反映到故事列表中。
	SAVESTR:11 = %RESULTS%
ELSE
	PRINTW 没有作出任何变更
ENDIF

@TITLE_LOADGAME
;タイトル画面のロードでも独自のロード画面を使う場合、@TITLE_LOADGAMEを定義する
;@TITLE_LOADGAMEが定義されていると、標準のロード画面の代わりにTITLE_LOADGAMEが呼ばれる。
CALL LOADGAME_EX
RETURN
;RETURNするとタイトルへ戻る。

;@SYSTEM_AUTOSAVE
;オートセーブの処理を独自に行う場合、@SYSTEM_AUTOSAVEを定義する
;@SYSTEM_AUTOSAVEが定義されていると、標準のオートセーブの代わりにSYSTEM_AUTOSAVEが呼ばれる。;@SYSTEM_AUTOSAVEを定義し何もしないでRETURNすればオートセーブを禁止できる。
;その場合はLOADDATA_EXから99番のデータに関する記述を削除しておくと親切。
;CALL SAVEINFO_EX
;SAVEDATA 99, RESULTS

@EVENTLOAD
#PRI
SIF LASTLOAD_NO != 99
	FLAG:1 = LASTLOAD_NO/20
