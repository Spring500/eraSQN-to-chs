@ADD_CHARACTER_EXPLORER, ARG
FLAG:8 = 0

SELECTCASE ARG
;ヴィレッジャは、たまに処女
CASE 8
	SIF RAND:10 == 0
		ARG = 9
;ボスを倒しちゃってる場合
CASE 303
	SIF PALAM_EXPLORER("ボス撃破")
		ARG = 304
CASE 313
	SIF PALAM_EXPLORER("ボス撃破")
		ARG = 215
CASE 34
	SIF PALAM_EXPLORER("ボス撃破")
		ARG = 412
CASE 35
	SIF PALAM_EXPLORER("中ボス撃破")
		ARG = 412
CASE 38
	SIF PALAM_EXPLORER("ボス撃破")
		ARG = 311
CASE 444
	IF PALAM_EXPLORER("ボス撃破")
		IF RAND:2
			ARG = 222
		ELSE
			ARG = 316
		ENDIF
	ENDIF
CASE 47
	SIF PALAM_EXPLORER("ボス撃破")
		ARG = 319
ENDSELECT

;100以上の値なら、上位種
LOCAL = ARG / 100
ARG %= 100

ADDCHARA ARG

;呼んだキャラをTARGETにする
TARGET = CHARANUM-1
;基本種で無いなら素質とかが変化
IF LOCAL
	CALL HIGH_RANKER, ARG, LOCAL
	SIF CFLAG:8 == 0
		CFLAG:8 = LOCAL
ENDIF

CALL SET_NEWNAME

;スキュラ短編だったり長編だったり
IF NO:TARGET == 15
	IF RAND:2
		CFLAG:209 = 2
		EXP:百合经验 += 20
		ABL:百合倾向 += 1
		TALENT:坦率 = 1
		TALENT:双性恋 = 1
		TALENT:旧支配者的末裔 = 1
	ELSE
		CFLAG:209 = 1
		TALENT:怕痛 = 1
		TALENT:旧支配者的末裔 = 1
	ENDIF
ENDIF

;追加技能獲得
CALL RESET_EXP_GAIN, TARGET
CALL GET_EXTALENT, TARGET
CALL GET_EXABL, TARGET

;名前ありキャラは変動無し
SIF COND("ユニーク") == 0 && RAND:4 == 0
	CALL CHAR_SAMON_T, ARG
;LV
CALL SET_LV_MUMA, TARGET
;ボディミミック
SIF NO:TARGET == 33
	ABL:ＬＶ = ABL:PLAYER:ＬＶ
;精力セット
CALL SET_COUNTER_BASE
;MAXBASE:精力 = TALENT:上位梦魔*5 + ABL:欲望 + ABL:技巧*3 + ABL:ＬＶ + PALAM_EXPLORER("敵の強さ")
;BASE:精力 = MAXBASE:精力

FLAG:8 = 1

;相手の人数を設定
CALL SET_PALAM_EXPLORER, "敵キャラ数", 1

@WANDERING_MONSTER
CALL ADD_CHARACTER_EXPLORER, PALAM_EXPLORER("制限エンカウント")

;ARGが0だと全部消去。それ以外の数字だとARG人残す
@DELMONSTER, ARG
IF ARG == 0
	DELCHARA CHARANUM-1
	TARGET = 0
ENDIF
SIF PALAM_EXPLORER("敵キャラ数") == 3 && ARG <= 2
	DELCHARA CHARANUM-1
SIF PALAM_EXPLORER("敵キャラ数") >= 2 && ARG <= 1
	DELCHARA CHARANUM-1


@SEXBATTLE_EXPLORER
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM MEMO_BASE6

;エンカウント回数+1
TFLAG:13 += 1

;戦闘パラメーターの初期化
VARSET TFLAG, 0, 15, 30
VARSET LOCALS
VARSET LOCAL

;一人目決定
CALL ADD_CHARACTER_EXPLORER, PALAM_EXPLORER("ランダムエンカウント")
LOCALS:1 = %NAME:TARGET%(LV:{ABL:ＬＶ})

;複数登場判定（探索場所ごと）
SELECTCASE TEXTS("探索先")
CASE "草原"
	;上位種は単独
	IF RAND:3 == 0 && CFLAG:8 == 0
		CALL WANDERING_MONSTER
		SIF RAND:3 == 0
			CALL WANDERING_MONSTER
	ENDIF
CASE "エルトゥム森林"
	;上位種は単独
	IF TALENT:上位梦魔 || CFLAG:8 >= 2
	ELSEIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:20
		CALL WANDERING_MONSTER
		SIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:30
			CALL WANDERING_MONSTER
	ENDIF
CASE "リュードゥフ洞"
	;ストゥーナはメローナが倒されていないならメローナを伴って現れる
	IF NO:TARGET == 34 && PALAM_EXPLORER("中ボス撃破") == 0 && (CONFIG("探索ボス") || GETCHARA(35) < 0)
		CALL ADD_CHARACTER_EXPLORER, 35
	;ヴィーヴル、メローナ、上位種でないなら複数かも
	ELSEIF NO:TARGET == 34 || NO:TARGET == 35 || (NO:TARGET == 12 && CFLAG:8 == 4)
	ELSEIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:20 && CFLAG:8 == 0
		CALL WANDERING_MONSTER
		SIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:20
			CALL WANDERING_MONSTER
	ENDIF
CASE "アンショス海"
	;レヴィアタンはマーメイド２体とともに現れる
	IF NO:TARGET == 13 && CFLAG:8 == 3
		CALL ADD_CHARACTER_EXPLORER, 13
		CALL ADD_CHARACTER_EXPLORER, 13
	ELSEIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:15
		CALL WANDERING_MONSTER
		SIF RAND:5 == 0
			CALL WANDERING_MONSTER
	ENDIF
CASE "ベルナドットの大迷路"
	;ベルナドットは２匹のアルプとともに現れる
	IF NO:TARGET == 47
		CALL ADD_CHARACTER_EXPLORER, 202
		CALL ADD_CHARACTER_EXPLORER, 202
	;ミミック以外なら基本的に複数
	ELSEIF NO:TARGET == 33
	ELSE
		CALL WANDERING_MONSTER
		SIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:20
			CALL WANDERING_MONSTER
	ENDIF
CASE "ゴズバーン砂漠"
	;エスプラーナは妹二人とともに現れる
	IF NO:TARGET == 38
		SIF CONFIG("探索ボス") || GETCHARA(39) < 0
			CALL ADD_CHARACTER_EXPLORER, 39
		SIF CONFIG("探索ボス") || GETCHARA(40) < 0
			CALL ADD_CHARACTER_EXPLORER, 40
	ELSE
		CALL WANDERING_MONSTER
		SIF RAND:5 == 0
			CALL WANDERING_MONSTER
	ENDIF
CASE "キアズマの遊郭"
	;リンはタマモとハツメを伴う
	IF NO:TARGET == 44
		SIF RAND:2
			CALL ADD_CHARACTER_EXPLORER, 222
		SIF RAND:2
			CALL ADD_CHARACTER_EXPLORER, 316
	;ハツメ
	ELSEIF NO:TARGET == 22 && CFLAG:8 == 2 && RAND:3 == 0
	;リン以外は複数率高い
	ELSE
		SIF RAND:3 || ABL:ＬＶ < ABL:PLAYER:ＬＶ
			CALL WANDERING_MONSTER
		SIF PALAM_EXPLORER("敵の強さ") + PALAM_EXPLORER("ハーレム")*2 > RAND:20
			CALL WANDERING_MONSTER
	ENDIF
ENDSELECT

;この時点で、PALAM_EXPLORER("敵キャラ数")に相手の人数が記録され、TARGETは最後に呼ばれたキャラとなっている
;かつ、PALAM_EXPLORER("一人目or二人目or三人目")に、それぞれのキャラ登録番号が代入されている
;LOCALS:1〜3はそれぞれの名前。
SIF PALAM_EXPLORER("敵キャラ数") == 3
	LOCALS:3 = %NAME:PALAM_EXPLORER("三人目")%(LV:{ABL:PALAM_EXPLORER("三人目"):90})
SIF PALAM_EXPLORER("敵キャラ数") >= 2
	LOCALS:2 = %NAME:PALAM_EXPLORER("二人目")%(LV:{ABL:PALAM_EXPLORER("二人目"):90})

SETCOLOR DEF_COLOR("イエロー")

;全員の名前一緒
IF LOCALS:1 == LOCALS:2 && LOCALS:1 == LOCALS:3
	PRINTFORM %LOCALS:1%×３
;二人の名前が一緒
ELSEIF LOCALS:1 == LOCALS:2
	PRINTFORM %LOCALS:1%×２\@ PALAM_EXPLORER("敵キャラ数") == 3 ? と%LOCALS:3% # \@
ELSEIF LOCALS:1 == LOCALS:3
	PRINTFORM %LOCALS:1%×２と%LOCALS:2%
ELSEIF PALAM_EXPLORER("敵キャラ数") >= 2 && LOCALS:2 == LOCALS:3
	PRINTFORM %LOCALS:1%と%LOCALS:2%×２
;全員別
ELSE
	PRINTFORM %LOCALS:1%
	SIF PALAM_EXPLORER("敵キャラ数") >= 2
		PRINTFORM と%LOCALS:2%
	SIF PALAM_EXPLORER("敵キャラ数") >= 3
		PRINTFORM と%LOCALS:3%
ENDIF
PRINTFORMW が現れた！

RESETCOLOR

;遭遇口上を喋るのは一人目が代表
TRYCALL KOJO_MESSAGE_SEXBATTLE_A, NO:PALAM_EXPLORER("一人目")

;ハツメの分身の術
IF NO:TARGET == 22 && CFLAG:8 == 2 && PALAM_EXPLORER("敵キャラ数") == 1
	PRINTFORML %CALLNAME:TARGET%は分身の術を使った！
	CALL ADD_CHARACTER_EXPLORER, 222
	CALL ADD_CHARACTER_EXPLORER, 222
	LOCALS:2 = %NAME:PALAM_EXPLORER("二人目")%(LV:{ABL:PALAM_EXPLORER("二人目"):90})
	LOCALS:3 = %NAME:PALAM_EXPLORER("三人目")%(LV:{ABL:PALAM_EXPLORER("三人目"):90})
	PRINTFORML %CALLNAME:TARGET%は３人に分身して襲い掛かってきた！
ENDIF

;ムード 0,1〜4,5〜8,9以上でランク分け。
CALL SET_PALAM_EXPLORER, "ムード", TALENT:PLAYER:魅惑+TALENT:PLAYER:谜之魅力+TALENT:PLAYER:魅力

;戦闘ターン初期化
TFLAG:12 = 0
$START_BATTLE
TFLAG:12 += 1

;プレイヤーが攻撃選択
CALL SELECTCOM_SEXBATTLE_EXPLORER

;相手がイってない
IF BASE:精力 > 0
	;相手の反撃
	CALL COUNTER_SEXBATTLE_EXPLORER

	;イかされてしまったなら、ストックの判定
	SIF BASE:PLAYER:精力 <= 0
		CALL TEXT_LOSE_EXPLORER

	;勝負がついていないなら続行
	SIF BASE:PLAYER:精力 > 0
		GOTO START_BATTLE

	;敗北確定
	CALL DELMONSTER
	RETURN 0
;相手をイかせた
ELSEIF BASE:精力 <= 0
	CALL TEXT_WIN_EXPLORER
	;勝負がついていないなら続行
	SIF BASE:精力 > 0
		GOTO START_BATTLE
ENDIF

;PRINTFORML "戦闘ターン数" = {PALAM_EXPLORER("戦闘ターン数")}
;戦闘ターン数に応じて時間制限が迫る
COUNT:99 += PALAM_EXPLORER("戦闘ターン数")/5

;経験値算出
LOCAL = (MAX(1, ABL:ＬＶ - ABL:PLAYER:ＬＶ) + ABL:ＬＶ/4 + TALENT:上位梦魔*5) * PALAM_EXPLORER("敵キャラ数")

;経験値Get
EXP:PLAYER:经验值 += MULTIPLY(LOCAL, 100 + TALENT:MASTER:取得经验值倍率)
;助手にもおこぼれ
IF PLAYER == MASTER && ASSI
	EXP:ASSI:助手经验 += MAX(LOCAL/2, 1)
	EXP:ASSI:经验值 += MAX(LOCAL/2, 1)
ENDIF
;PRINTFORM {LOCAL}Ｐの経験値を得た。

;IF PALAM_EXPLORER("ムード") > 10 && RAND:100 == 0
;	PRINTFORM %CALLNAME:TARGET%のパンツ獲得。
IF PALAM_EXPLORER("ムード") > 10 && RAND:20 == 0 && ITEM:月之糖果 + I:99 < 99
	PRINTFORM %CALLNAME:TARGET%は
	CALL PRINT_COLORTEXT, "月之糖果", DEF_COLOR("黄色")
	PRINTFORM を落とした。
	IF PLAYER != MASTER && COND("夢魔", PLAYER)
		PRINTFORM %CALLNAME:PLAYER%はご満悦だ。
		CFLAG:PLAYER:2 += 50
		BASE:PLAYER:精力 = MAXBASE:PLAYER:精力
	ELSE
		I:99 += 1
	ENDIF
	PRINTL
ELSEIF PALAM_EXPLORER("ムード") > RAND:20
	PRINTFORM %CALLNAME:TARGET%は宝箱を落とした。
	CALL SET_PALAM_EXPLORER, "財宝期待値", PALAM_EXPLORER("ムード") * (4 + RAND:7)
	;ボーナス
	CALL SET_PALAM_EXPLORER, "アイテムレベル", 1
	CALL TREASURE_BOX_EXPLORER_B, TARGET
	CALL SET_PALAM_EXPLORER, "アイテムレベル", -1
ELSE
	IF PALAM_EXPLORER("敵キャラ数") > RAND:3
		IF ITEM:淫魔水滴 + I:78 < 999 && COND("夢魔") && RAND:5
			PRINTFORM %CALLNAME:PLAYER%は
			CALL PRINT_COLORTEXT, "淫魔の雫", DEF_COLOR("黄色")
			PRINTFORM を手に入れた。
			I:78 += 1
		ENDIF
	ENDIF
	SELECTCASE TEXTS("探索先")
	CASE "キアズマの遊郭"
	CASEELSE
		MONEY:5 = (PALAM_EXPLORER("敵の強さ") + ABL:ＬＶ) * (5 + RAND:5) * PALAM_EXPLORER("敵キャラ数")
		CALL PRINT_COLORTEXT, MONEY_C(MONEY:5), DEF_COLOR("黄色")
		PRINTFORML 獲得。
		MONEY:6 += MONEY:5
	ENDSELECT
ENDIF

;ボス撃破フラグ
SELECTCASE NO:TARGET
CASE 3
	IF CFLAG:8 == 3
		CALL SET_PALAM_EXPLORER, "エルトゥム森林：ボス撃破"
		CALL SET_PALAM_EXPLORER, "ボス撃破"
	ENDIF
CASE 13
	IF CFLAG:8 == 3
		CALL SET_PALAM_EXPLORER, "アンショス海：ボス撃破"
		CALL SET_PALAM_EXPLORER, "ボス撃破"
	ENDIF
CASE 34
	CALL SET_PALAM_EXPLORER, "リュードゥフ洞：ボス撃破"
	CALL SET_PALAM_EXPLORER, "ボス撃破"
	IF PALAM_EXPLORER("敵キャラ数") >= 2 && NO:PALAM_EXPLORER("二人目") == 35
		CALL SET_PALAM_EXPLORER, "リュードゥフ洞：中ボス撃破"
		CALL SET_PALAM_EXPLORER, "中ボス撃破"
	ENDIF
CASE 35
	CALL SET_PALAM_EXPLORER, "リュードゥフ洞：中ボス撃破"
	CALL SET_PALAM_EXPLORER, "中ボス撃破"
CASE 38
	CALL SET_PALAM_EXPLORER, "ゴズバーン砂漠：ボス撃破"
	CALL SET_PALAM_EXPLORER, "ボス撃破"
CASE 44
	CALL SET_PALAM_EXPLORER, "キアズマの遊郭：ボス撃破"
	CALL SET_PALAM_EXPLORER, "ボス撃破"
CASE 47
	CALL SET_PALAM_EXPLORER, "ベルナドットの大迷路：ボス撃破"
	CALL SET_PALAM_EXPLORER, "ボス撃破"
ENDSELECT

;仲魔判定(名前ありのボスはいつでも仲魔に入りたがる)
SELECTCASE TEXTS("探索先")
CASE "キアズマの遊郭"
CASEELSE
	IF FLAG:246 == 3 || NO:TARGET == 34 || NO:TARGET == 35 || NO:TARGET == 38 || TALENT:初体验对象
		LOCAL = 3
		;ムードが高い
		SIF PALAM_EXPLORER("ムード") > 10
			LOCAL += PALAM_EXPLORER("ムード") - 9
		;謎の魅力
		SIF TALENT:PLAYER:谜之魅力
			LOCAL += 1
		;初めての人
		SIF TALENT:初体验对象
			LOCAL = 10

		;ボス
		SELECTCASE NO:TARGET
		CASE 34
			;ストゥーナが仲間に居る？
			IF FIND_COND("Ｃストゥーナ")
				LOCAL = 0
			ELSE
				LOCAL = 10
			ENDIF
		CASE 35
			;メローナが仲間に居る？
			IF FIND_COND("Ｃメローナ")
				LOCAL = 0
			ELSE
				LOCAL = 10
			ENDIF
		CASE 38
			;エスプラーナが仲間に居る？
			IF FIND_COND("Ｃエスプラーナ")
				LOCAL = 0
			ELSE
				LOCAL = 10
			ENDIF
		ENDSELECT

		;仲魔になる
		IF LOCAL > RAND:10
			PRINTL
			PRINTFORM %CALLNAME:TARGET%
			;ストメロ限定
			IF NO:TARGET == 34 && PALAM_EXPLORER("敵キャラ数") >= 2 && NO:PALAM_EXPLORER("二人目") == 35
				PRINTFORML とメローナは起き上がり、仲魔になりたそうに%CALLNAME:PLAYER%を見ている…
			;ハーピー姉妹
			ELSEIF NO:TARGET == 38 && PALAM_EXPLORER("敵キャラ数") >= 2
				PRINTFORML と妹は起き上がり、仲魔になりたそうに%CALLNAME:PLAYER%を見ている…
			;強気かツンデレ（ストゥーナとリザードマン）限定
			ELSEIF NO:TARGET == 12 || NO:TARGET == 34
				PRINTFORM は
				IF RAND:3 == 0
					PRINTFORM 悔しそうにしながらも、
				ELSEIF RAND:2 == 0
					PRINTFORM 悪態をつきながらも、
				ELSE
					PRINTFORM 息を荒げたまま、
				ENDIF
				PRINTFORM %CALLNAME:PLAYER%
				IF RAND:3 == 0
					PRINTFORML をどこか期待したような瞳で睨み続けている…
				ELSEIF RAND:2 == 0
					PRINTFORML をちらちらと見ながら赤くなっている…
				ELSE
					PRINTFORML を誘惑するかのように挑発している…
				ENDIF
			;その他汎用
			ELSE
				PRINTFORM は
				IF RAND:4 == 0
					PRINTFORM 横たわったまま顔だけをこちらに向け、
				ELSEIF RAND:3 == 0
					PRINTFORM びくびくと快感に身を震わせながら、
				ELSEIF RAND:2 == 0
					PRINTFORM 火照った身体を自分でまさぐりながら、
				ELSE
					PRINTFORM ゆっくりと身を起こし、
				ENDIF
				PRINTFORM %CALLNAME:PLAYER%
				IF RAND:5 == 0
					PRINTFORML の名前を呼び続けている…
				ELSEIF RAND:4 == 0
					PRINTFORML に熱い視線を送っている…
				ELSEIF RAND:3 == 0
					PRINTFORML の足元に擦り寄ってきた…
				ELSEIF RAND:2 == 0
					PRINTFORML に手を伸ばし追い縋っている…
				ELSE
					PRINTFORML に更なる行為を求めている…
				ENDIF
			ENDIF

			PRINTFORML [0] 手をとる [1] 振り返らない
			CALL INPUT_SELECT, 2

			IF RESULT == 0
				TRYCALL KOJO_MESSAGE_SEXBATTLE_C, NO:TARGET
				PRINTFORM %CALLNAME:TARGET%
				;再召喚不可フラグ(TARGETがボス以外なら SETFLAG, "アイテムフラグ調整" で元に戻る)
				FLAG:(NO:TARGET + 999) = 0
				;調教の初回は表示しない
				CFLAG:201 = 1
				;好感度ボーナス
				CFLAG:2 = 100
				IF NO:TARGET == 34 && PALAM_EXPLORER("敵キャラ数") >= 2 && NO:PALAM_EXPLORER("二人目") == 35
					PRINTFORM とメローナ
					FLAG:1034 = 0
					CFLAG:PALAM_EXPLORER("二人目"):201 = 1
					;好感度ボーナス
					CFLAG:PALAM_EXPLORER("二人目"):2 = 100
					CALL DELMONSTER, 2
				ELSEIF NO:TARGET == 38 && PALAM_EXPLORER("敵キャラ数") >= 2
					IF PALAM_EXPLORER("敵キャラ数") >= 3
						PRINTFORM と妹たち
						CFLAG:PALAM_EXPLORER("二人目"):201 = 1
						CFLAG:PALAM_EXPLORER("二人目"):2 = 100
						CFLAG:PALAM_EXPLORER("三人目"):201 = 1
						CFLAG:PALAM_EXPLORER("三人目"):2 = 100
					ELSEIF NO:PALAM_EXPLORER("二人目") == 39
						PRINTFORM とリズ・ラズ
						FLAG:1038 = 0
						CFLAG:PALAM_EXPLORER("二人目"):201 = 1
						CFLAG:PALAM_EXPLORER("二人目"):2 = 100
					ELSEIF NO:PALAM_EXPLORER("二人目") == 40
						PRINTFORM とロッコ・バロッコ
						FLAG:1039 = 0
						CFLAG:PALAM_EXPLORER("二人目"):201 = 1
						CFLAG:PALAM_EXPLORER("二人目"):2 = 100
					ENDIF
				ELSE
					CALL DELMONSTER, 1
				ENDIF
				PRINTFORML を連れ帰ります
				I:999 = 1
				COUNT:99 = 29
				RETURN 0
			ENDIF
		ENDIF
	ENDIF
ENDSELECT

PRINTL
;精力とLINECOUNTを記録
MEMO_BASE6 = BASE:PLAYER:精力
MEMO_LINECOUNT = LINECOUNT

;エンカウント回数が3+4nでセリフを出してみる
SIF PLAYER == MASTER && ASSI && (PALAM_EXPLORER("エンカウント回数") + 1)%4 == 0
	TRYCALLFORM GUEST_KOJO_K{NO:ASSI}_{CFLAG:ASSI:209}, "探索：ご休憩", ASSI

;セリフが無いor精力の変動がなければ汎用地の文出力
IF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE 1
	CALL HEAL_EXPLORER, "一息ついて"
ELSEIF MEMO_BASE6 == BASE:PLAYER:精力
	PRINTL
	CALL HEAL_EXPLORER, "一息ついて"
ENDIF

PRINTFORML 探索続行。
CALL DELMONSTER

WAIT

@HEAL_EXPLORER, ARGS, ARGS:1
PRINTFORM %ARGS%精力回復 {BASE:PLAYER:精力}→

;精力が33%回復。食事を追加してるなら、その分も加味。
BASE:PLAYER:精力 = MIN(BASE:PLAYER:精力 + MAXBASE:PLAYER:精力/3 + PALAM_EXPLORER("精力回復補正"), MAXBASE:PLAYER:精力)

PRINTFORM {BASE:PLAYER:精力}/{MAXBASE:PLAYER:精力}%ARGS:1%。


@TEXT_LOSE_EXPLORER
SETCOLOR DEF_COLOR("イエロー")

PRINTFORM %CALLNAME:PLAYER%は
IF PALAM_EXPLORER("Ｖインサート")
	PRINTFORML %CALLNAME:TARGET%の膣内に大量に射精してしまった！
	CALL SETFLAG, "膣内精液経験", 1
ELSEIF PALAM_EXPLORER("Ａインサート")
	PRINTFORML %CALLNAME:TARGET%のアナルに大量に射精してしまった！
	CALL SETFLAG, "肛内精液経験", 1
ELSEIF PALAM_EXPLORER("エースオブスペード")
	PRINTFORML %CALLNAME:TARGET%のアソコに自分のアソコを擦られて、イかされてしまった！
ELSEIF PALAM_EXPLORER("キス") || PALAM_EXPLORER("フェラチオ")
	PRINTFORML %CALLNAME:TARGET%の巧みな舌使いにイかされてしまった！
	CALL SETFLAG, "精飲経験", 1
ELSEIF PALAM_EXPLORER("手コキ")
	PRINTFORML %CALLNAME:TARGET%の巧みな手淫でイかされてしまった！
ELSEIF PALAM_EXPLORER("足交")
	PRINTFORML %CALLNAME:TARGET%の足コキでイかされてしまった！
ELSEIF PALAM_EXPLORER("颜面骑乘") || PALAM_EXPLORER("６９式")
	PRINTFORML %CALLNAME:TARGET%に激しい顔面騎乗をされて、イかされてしまった！
ELSEIF PALAM_EXPLORER("パイズリ")
	PRINTFORML %CALLNAME:TARGET%の胸の魔力に屈してしまった！
ELSE
	PRINTFORML %CALLNAME:TARGET%にイかされてしまった！
ENDIF

RESETCOLOR

EXP:经验值 += MAX(2*(ABL:PLAYER:ＬＶ - ABL:ＬＶ), 0) + 3
EXP:高潮次数 += 1
IF TALENT:PLAYER:女性
	EXP:PLAYER:绝顶经验 += 1
ELSEIF TALENT:PLAYER:扶她
	EXP:PLAYER:绝顶经验 += 1
	EXP:PLAYER:射精经验 += 1
	EXP:精液经验 += 2
ELSE
	EXP:PLAYER:射精经验 += 1
	EXP:精液经验 += 2
ENDIF

;決着台詞
TRYCALL KOJO_MESSAGE_SEXBATTLE_B, NO:TARGET

IF BASE:PLAYER:STOCK > 0
	BASE:PLAYER:精力 = MAXBASE:PLAYER:精力
	BASE:PLAYER:STOCK -= 1
	PRINTFORML しかし、気力を振り絞って立ち上がった！
ENDIF

WAIT

@TEXT_WIN_EXPLORER
PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%

R = RAND:3

IF PALAM_EXPLORER("両穴征服")
	PRINTFORML の両方の穴を犯して満足させた。
ELSEIF PALAM_EXPLORER("Ｖインサート")
	PRINTFORML を激しく犯して満足させた。
ELSEIF PALAM_EXPLORER("Ａインサート")
	PRINTFORML のアナルを激しく犯して満足させた。
ELSEIF PALAM_EXPLORER("エースオブスペード")
	PRINTFORML のアソコと自分のアソコを擦りあわせて満足させた。
ELSEIF PALAM_EXPLORER("ヴァッジ")
	IF R == 2
		PRINTFORML のアソコを舌で愛撫して満足させた。
	ELSEIF R == 1
		PRINTFORML のアソコに指を這わせ、巧みに愛撫して満足させた。
	ELSE
		PRINTFORML のアソコを指で激しく愛撫して満足させた。
	ENDIF
ELSEIF PALAM_EXPLORER("アナル")
	IF R == 2
		PRINTFORML のアナルを舌で愛撫して満足させた。
	ELSEIF R == 1
		PRINTFORML のアナルに指を這わせ、巧みに愛撫して満足させた。
	ELSE
		PRINTFORML のアナルを指で激しく愛撫して満足させた。
	ENDIF
ELSEIF PALAM_EXPLORER("胸围")
	IF R == 2
		PRINTFORML の乳首を舌で愛撫して満足させた。
	ELSEIF R == 1
		PRINTFORML の胸を激しく揉みしだいて満足させた。
	ELSE
		PRINTFORML の胸を包み込むように揉みほぐして満足させた。
	ENDIF
ELSEIF PALAM_EXPLORER("キス")
	IF R == 2
		PRINTFORML に情熱的なキスを交わして満足させた。
	ELSEIF R == 1
		PRINTFORML に濃厚なキスを交わして満足させた。
	ELSE
		PRINTFORML に熱いキスを交わして満足させた。
	ENDIF
ELSE
	PRINTFORML を激しく犯して満足させた。
ENDIF

EXP:绝顶经验 += 1
TRYCALL KOJO_MESSAGE_SEXBATTLE_B, NO:TARGET

;後が控えてる？
IF TARGET != PALAM_EXPLORER("一人目")
	TARGET -= 1
	;ムード以外の脱衣、インサート、行動フラグリセット
	CALL SET_PALAM_EXPLORER, "戦闘フラグリセット"
ENDIF

@SELECTCOM_SEXBATTLE_EXPLORER
CALL SET_PALAM_EXPLORER, "行動", 0
SELECTCASE PALAM_EXPLORER("ムード")
;ムードLV0
CASE 0
	CALL SET_PALAM_EXPLORER, "テンダーキッス"
;ムードLV1
CASE IS <= 4
	IF RAND:(PALAM_EXPLORER("ムード") + 1) == 0
		CALL SET_PALAM_EXPLORER, "テンダーキッス"
	ELSEIF TALENT:PLAYER:甜言蜜语 && RAND:(PALAM_EXPLORER("ムード") + 3) == 0
		CALL SET_PALAM_EXPLORER, "甘い言葉"
	ELSEIF PALAM_EXPLORER("脱衣") == 0 && RAND:PALAM_EXPLORER("ムード") > RAND:4
		CALL SET_PALAM_EXPLORER, "脱衣", 1
	ELSEIF RAND:4 == 0
		CALL SET_PALAM_EXPLORER, "アナル"
	ELSEIF RAND:3 == 0
		CALL SET_PALAM_EXPLORER, "ヴァッジ"
	ELSEIF RAND:2
		CALL SET_PALAM_EXPLORER, "胸围"
	ELSE
		CALL SET_PALAM_EXPLORER, "キス"
	ENDIF
;ムードLV2以上
CASEELSE
	IF PALAM_EXPLORER("ムード") < 10 && RAND:(PALAM_EXPLORER("ムード") + 1) == 0
		CALL SET_PALAM_EXPLORER, "テンダーキッス"
	ELSEIF PALAM_EXPLORER("ムード") < 10 && TALENT:PLAYER:甜言蜜语 && RAND:PALAM_EXPLORER("ムード") <= 1
		CALL SET_PALAM_EXPLORER, "甘い言葉"
	ELSEIF PALAM_EXPLORER("Ａインサート") && RAND:PALAM_EXPLORER("ムード")
		SIF RAND:4 == 0
			CALL SET_PALAM_EXPLORER, "Ｖインサート"
	ELSEIF PALAM_EXPLORER("Ｖインサート") && RAND:PALAM_EXPLORER("ムード")
		SIF RAND:4 == 0 && (TALENT:Ａ敏感 || TALENT:PLAYER:抖Ｓ || COND("♀に弱い", PLAYER)) && RAND:(1 + EXP:PLAYER:Ａ插入经验) > RAND:(1 + EXP:PLAYER:Ｖ插入经验)
			CALL SET_PALAM_EXPLORER, "Ａインサート"
	;インサート等
	ELSEIF PALAM_EXPLORER("脱衣") && PALAM_EXPLORER("体位") == 0 && RAND:PALAM_EXPLORER("ムード") >= 2 && TALENT:妖精 == 0
		;女ならエースオブスペード。ふたなりは相手が両刀ならたまにやる
		IF TALENT:PLAYER:女性 || (TALENT:PLAYER:扶她 && TALENT:双性恋 && RAND:4 == 0)
			CALL SET_PALAM_EXPLORER, "エースオブスペード"
		;アナルセックス
		ELSEIF (TALENT:Ａ敏感 || TALENT:PLAYER:抖Ｓ || COND("♀に弱い", PLAYER)) && RAND:(1 + EXP:PLAYER:Ａ插入经验) > RAND:(1 + EXP:PLAYER:Ｖ插入经验)
			CALL SET_PALAM_EXPLORER, "Ａインサート"
		ELSEIF (TALENT:PLAYER:童贞 && CONFIG("脱童貞フィルタ") ) || TALENT:处女
			IF RAND:4 == 0
				CALL SET_PALAM_EXPLORER, "アナル"
			ELSEIF RAND:3 == 0
				CALL SET_PALAM_EXPLORER, "ヴァッジ"
			ELSEIF RAND:2
				CALL SET_PALAM_EXPLORER, "胸围"
			ELSE
				CALL SET_PALAM_EXPLORER, "キス"
			ENDIF
		ELSE
			CALL SET_PALAM_EXPLORER, "Ｖインサート"
		ENDIF
	ELSEIF PALAM_EXPLORER("脱衣") == 0 && RAND:PALAM_EXPLORER("ムード") >= 1
		CALL SET_PALAM_EXPLORER, "脱衣", 1
	ELSEIF RAND:4 == 0
		CALL SET_PALAM_EXPLORER, "アナル"
	ELSEIF RAND:3 == 0
		CALL SET_PALAM_EXPLORER, "ヴァッジ"
	ELSEIF RAND:2
		CALL SET_PALAM_EXPLORER, "胸围"
	ELSE
		CALL SET_PALAM_EXPLORER, "キス"
	ENDIF
ENDSELECT

VARSET LOCAL
IF PALAM_EXPLORER("テンダーキッス")
	BASE:精力 -= 1
	CALL SET_PALAM_EXPLORER, "ムード", 2
	EXP:亲吻经验 += 1
	EXP:PLAYER:亲吻经验 += RAND:2
ELSEIF PALAM_EXPLORER("キス")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= ABL:PLAYER:舌技
	LOCAL = TALENT:Ｍ敏感
	EXP:亲吻经验 += 1
	EXP:PLAYER:亲吻经验 += 1
ELSEIF PALAM_EXPLORER("胸围")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= (ABL:PLAYER:舌技 + ABL:PLAYER:指技)/2
	LOCAL = TALENT:Ｂ敏感
	EXP:PLAYER:手淫经验 += 1
ELSEIF PALAM_EXPLORER("アナル")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= (ABL:PLAYER:舌技 + ABL:PLAYER:指技)/2
	LOCAL = TALENT:Ａ敏感
	EXP:Ａ经验 += 1
	EXP:PLAYER:Ａ刺激经验 += 1
ELSEIF PALAM_EXPLORER("アナル")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= (ABL:PLAYER:舌技 + ABL:PLAYER:指技)/2
	LOCAL = TALENT:Ｃ敏感 + TALENT:Ｖ敏感
	IF RAND:2 == 0
		EXP:PLAYER:舔阴经验 += 1
	ELSE
		EXP:PLAYER:手淫经验 += 1
	ENDIF
ELSEIF PALAM_EXPLORER("Ｖインサート")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= ABL:PLAYER:腰技 + 1
	LOCAL = TALENT:Ｖ敏感

	IF TALENT:PLAYER:童贞
		SETCOLOR DEF_COLOR("イエロー")
		PRINTFORML %CALLNAME:PLAYER%は童貞を失った！
		RESETCOLOR
		TALENT:PLAYER:童贞 = 0
		TALENT:初体验对象 = 1
	ENDIF
	IF PENIS(PLAYER)
		EXP:PLAYER:Ｖ插入经验 += 1
		EXP:Ｖ性交经验 += 1
	ENDIF
	EXP:PLAYER:腰技经验 += 1
	TALENT:处女 = 0
	EXP:Ｖ经验 += 2
ELSEIF PALAM_EXPLORER("Ａインサート")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= ABL:PLAYER:腰技
	IF TALENT:Ａ敏感
		BASE:精力 -= 2
		CALL SET_PALAM_EXPLORER, "ムード", RAND:2
	ELSE
		CALL SET_PALAM_EXPLORER, "ムード", -2
	ENDIF
	IF PENIS(PLAYER)
		EXP:PLAYER:Ａ插入经验 += 1
		EXP:Ａ性交经验 += 1
	ENDIF
	EXP:PLAYER:腰技经验 += 1
	TALENT:Ａ处女 = 0
	EXP:Ａ经验 += 2
;エースオブスペード
ELSEIF PALAM_EXPLORER("エースオブスペード")
	CALL ATTACK_SEXBATTLE_EXPLORER
	BASE:精力 -= ABL:PLAYER:腰技 + 1
	LOCAL = TALENT:Ｃ敏感
	EXP:PLAYER:腰技经验 += 1
;甘い言葉
ELSEIF PALAM_EXPLORER("甘い言葉")
	CALL SET_PALAM_EXPLORER, "ムード", 3
ENDIF

;弱点突き
IF LOCAL
	BASE:精力 -= 1 + (1 + ABL:PLAYER:技巧)/2
	CALL SET_PALAM_EXPLORER, "ムード", 1
ENDIF

;イきそう
SIF BASE:精力 < MAXBASE:精力/3 && PALAM_EXPLORER("ムード") < 5
	CALL SET_PALAM_EXPLORER, "ムード", 1

;レズ、BL経験
IF TALENT:男性 == 0 && TALENT:PLAYER:男性 == 0
	EXP:PLAYER:百合经验 += 2
	EXP:百合经验 += 2
ELSEIF TALENT:男性 && TALENT:PLAYER:男性
	EXP:PLAYER:ＢＬ经验 += 2
	EXP:ＢＬ经验 += 2
ENDIF


@ATTACK_SEXBATTLE_EXPLORER
LOCAL = PALAM_EXPLORER("探索者攻撃力", PLAYER)
;アルカナの罠で不器用になっている
SIF PALAM_EXPLORER("クラムジ")
	LOCAL -= 1 + RAND:3
;LVの差
SIF ABL:PLAYER:ＬＶ - ABL:ＬＶ
	LOCAL += 1 + (ABL:PLAYER:ＬＶ - ABL:ＬＶ) / 3
;脱衣済み
SIF PALAM_EXPLORER("脱衣")
	LOCAL += 1

;MASTER専用補正
IF PLAYER == MASTER
	;助手
	IF ASSI
		LOCAL += PALAM_EXPLORER("助手攻撃力", ASSI) + PALAM_EXPLORER("アルカナ補助", ASSI)
		SIF ABL:ASSI:ＬＶ > ABL:ＬＶ
			LOCAL += 1
	ENDIF
ELSE
	;上位夢魔
	IF TALENT:PLAYER:上位梦魔
		LOCAL += TALENT:PLAYER:上位梦魔
		SIF TALENT:上位梦魔 == 0
			LOCAL += 1
	ENDIF
ENDIF

;ハーピー三姉妹
IF NO:TARGET == 38 && PALAM_EXPLORER("敵キャラ数") >= 2
	IF NO:PALAM_EXPLORER("二人目") == 39 && NO:PALAM_EXPLORER("三人目") == 40
		LOCAL = LOCAL/4 + 1
	ELSEIF PALAM_EXPLORER("敵キャラ数") >= 2
		LOCAL = LOCAL/2 + 1
	ENDIF
ENDIF

SIF LOCAL <= 0
	BASE:精力 = MAX(RAND:2, 0)
BASE:精力 -= LOCAL

@COUNTER_SEXBATTLE_EXPLORER
;甘い言葉の時には待ってくれるかも
SIF PALAM_EXPLORER("甘い言葉") && RAND:2 == 0
	RETURN 0

IF PALAM_EXPLORER("Ｖインサート")
	TALENT:处女 = 0
	EXP:Ｖ经验 += 2
	EXP:腰技经验 += 1
	IF PENIS(PLAYER)
		EXP:PLAYER:Ｖ插入经验 += 1
		EXP:Ｖ性交经验 += 1
	ENDIF
ELSEIF PALAM_EXPLORER("Ａインサート")
	TALENT:Ａ处女 = 0
	EXP:Ａ经验 += 2
	EXP:腰技经验 += 1
	IF PENIS(PLAYER)
		EXP:PLAYER:Ａ插入经验 += 1
		EXP:Ａ性交经验 += 1
	ENDIF
ELSEIF PALAM_EXPLORER("エースオブスペード")
	EXP:腰技经验 += 1
;舌使い
ELSEIF ABL:舌技 >= MAX(ABL:指技, ABL:足技, ABL:腰技, ABL:魔乳, 1) && RAND:3
	IF RAND:2 == 0 || TALENT:PLAYER:女性
		CALL SET_PALAM_EXPLORER, "キス"
		EXP:亲吻经验 += 1
	ELSE
		IF PALAM_EXPLORER("ヴァッジ")
			CALL SET_PALAM_EXPLORER, "６９式"
		ELSE
			CALL SET_PALAM_EXPLORER, "フェラチオ"
		ENDIF
		IF PENIS(PLAYER)
			EXP:口交经验 += 1
		ELSE
			EXP:舔阴经验 += 1
		ENDIF
	ENDIF
;指使い
ELSEIF ABL:指技 >= MAX(ABL:舌技, ABL:足技, ABL:腰技, ABL:魔乳, 1) && RAND:3
	IF TALENT:抖Ｓ && RAND:2 == 0
		CALL SET_PALAM_EXPLORER, "アナル責め"
		EXP:Ａ刺激经验 += 1
	ELSE
		CALL SET_PALAM_EXPLORER, "手コキ"
		EXP:手淫经验 += 1
	ENDIF
;足使い
ELSEIF COND("脚が無い") == 0 && ABL:足技 >= MAX(ABL:舌技, ABL:指技, ABL:腰技, ABL:魔乳, 1) && RAND:3
	CALL SET_PALAM_EXPLORER, "足交"
	EXP:足交经验 += 1
;腰使い
ELSEIF ABL:腰技 >= MAX(ABL:舌技, ABL:指技, ABL:足技, ABL:魔乳, 1) && RAND:3
	CALL SET_PALAM_EXPLORER, "颜面骑乘"
	EXP:腰技经验 += 1
;魔乳
ELSEIF ABL:魔乳 >= MAX(ABL:舌技, ABL:指技, ABL:足技, ABL:腰技, 1) && RAND:3
	CALL SET_PALAM_EXPLORER, "パイズリ"
	EXP:乳交经验 += 1
ELSEIF RAND:7 == 0
	CALL SET_PALAM_EXPLORER, "足交"
ELSEIF RAND:6 == 0
	CALL SET_PALAM_EXPLORER, "フェラチオ"
ELSEIF RAND:5 == 0
	CALL SET_PALAM_EXPLORER, "手コキ"
ELSEIF RAND:4 == 0
	CALL SET_PALAM_EXPLORER, "颜面骑乘"
ELSEIF RAND:3 == 0
	CALL SET_PALAM_EXPLORER, "アナル責め"
ELSEIF RAND:2
	CALL SET_PALAM_EXPLORER, "パイズリ"
ELSE
	CALL SET_PALAM_EXPLORER, "キス"
ENDIF

LOCAL = PALAM_EXPLORER("攻撃力")

SIF RAND:(PALAM_EXPLORER("敵の強さ") + 1)
	LOCAL += 1 + RAND:(PALAM_EXPLORER("敵の強さ") + 1)/3

SELECTCASE PALAM_EXPLORER("敵キャラ数")
CASE IS >= 3
	LOCAL += 3
CASE 2
	LOCAL += 1
ENDSELECT

LOCAL = DIVIDE(LOCAL, 100 + TALENT:PLAYER:精神力强化)

SIF PALAM_EXPLORER("高敏术")
	LOCAL += 2
;行動
IF PALAM_EXPLORER("キス") || PALAM_EXPLORER("フェラチオ")
	LOCAL += RAND:(ABL:舌技 + 1) + MIN(COND("口に弱い", PLAYER) + COND("唇に弱い", PLAYER), 1)
ELSEIF PALAM_EXPLORER("６９式")
	LOCAL += RAND:(ABL:舌技 + 1) + MIN(COND("口に弱い", PLAYER) + COND("唇に弱い", PLAYER), 1)
	LOCAL += RAND:(ABL:腰技 + 1) + MIN(COND("♀に弱い", PLAYER), 1)
ELSEIF PALAM_EXPLORER("アナル責め") || PALAM_EXPLORER("手コキ")
	LOCAL += RAND:(ABL:指技 + 1) + MIN(COND("手に弱い", PLAYER), 1)
ELSEIF PALAM_EXPLORER("足交")
	LOCAL += RAND:(ABL:足技 + 1) + MIN(COND("脚に弱い", PLAYER), 1)
ELSEIF PALAM_EXPLORER("颜面骑乘") || PALAM_EXPLORER("Ｖインサート") || PALAM_EXPLORER("エースオブスペード")
	LOCAL += RAND:(ABL:腰技 + 1) + MIN(COND("♀に弱い", PLAYER), 1)
ELSEIF PALAM_EXPLORER("Ａインサート")
	LOCAL += RAND:(ABL:腰技 + 1) + MIN(COND("♀に弱い", PLAYER), 1)
ELSEIF PALAM_EXPLORER("パイズリ")
	LOCAL += RAND:(ABL:魔乳 + 1) + MIN(COND("胸に弱い", PLAYER), 1)
ENDIF
;ハーピー三姉妹
IF NO:TARGET == 38 && PALAM_EXPLORER("敵キャラ数") >= 2
	IF NO:PALAM_EXPLORER("二人目") == 39 && NO:PALAM_EXPLORER("三人目") == 40
		LOCAL += 10
	ELSEIF PALAM_EXPLORER("敵キャラ数") >= 2
		LOCAL += 5
	ENDIF
ENDIF

;助手がいる
IF PLAYER == MASTER && ASSI
	SIF PALAM_EXPLORER("アルカナ補助", ASSI) >= 2
		LOCAL -= 1
	SIF ABL:ASSI:技巧 >= ABL:技巧
		LOCAL -= 1
	SIF ABL:ASSI:ＬＶ > ABL:ＬＶ
		LOCAL -= 1
ENDIF
IF LOCAL < 1
	IF RAND:(ABL:技巧 + 2)
		LOCAL = 1
	ELSE
		LOCAL = 0
	ENDIF
ENDIF

BASE:PLAYER:精力 -= LOCAL
