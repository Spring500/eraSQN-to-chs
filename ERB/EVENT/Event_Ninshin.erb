;---------------------------------------------------------
;妊娠イベントカウント処理
;---------------------------------------------------------
@EVENT_N1, ARG
LOCAL = TARGET
TARGET = ARG

CALL CHECK_NINSHIN, TARGET, "システム処理"

;妊娠イベントカウント処理
IF COND("夜") && CONFIG("妊娠パッチ")
	;お腹の中の胎児の成長
	IF COND("妊娠中")
		;すくすく育ってます
		BASE:胎儿 += 1
		;人でないなら進行率がＵＰ(中出しによって速度アップ)
		SIF TALENT:梦魔 + TALENT:上位梦魔 + TALENT:魔王残片 && RAND:(3 + BASE:胎儿营养/10)
			BASE:胎儿 += 1

		;分娩室待ち
		IF COND("妊娠中：分娩直前")
			CALL SETFLAG, "妊娠中：分娩", TARGET
		;臨月〜出産日(人なら三日ほど)
		ELSEIF COND("妊娠中：臨月")
			;出産間近イベント(子育て中には発生しない)
			IF COND("妊娠中：各イベント進行度") >= 3
				IF BASE:育儿中
					BASE:胎儿 -= 1 + RAND:2
				ELSE
					CALL SETFLAG, "妊娠中：分娩直前", TARGET
				ENDIF
			ENDIF
		;妊娠発覚終了〜臨月まで(人なら10日ほど)
		ELSEIF COND("妊娠中：妊娠発覚済み")
			;臨月イベント
			SIF COND("妊娠中：各イベント進行度") >= 11
				CALL SETFLAG, "妊娠中：臨月", TARGET
		;妊娠発覚イベント待ち
		ELSEIF COND("妊娠中：妊娠発覚直前")
		;妊娠潜伏期間〜妊娠発覚イベント(人なら三日ほど)
		ELSEIF COND("妊娠中：各イベント進行度") >= 4
			CALL SETFLAG, "妊娠中：妊娠発覚直前", TARGET
		ENDIF
	ENDIF
	;子育て。人で２週間、夢魔たちなら１週間程度。
	IF BASE:育儿中
		BASE:育儿中 += 1
		;母親が素質【夢魔、上位夢魔、エルダーサキュバス、魔王の欠片】持ち
		SIF TALENT:梦魔 + TALENT:上位梦魔 + TALENT:魔王残片
			BASE:育儿中 += 1
		;子育て終了
		SIF COND("育児中：子供が大きくなった")
			CALL SETFLAG, "育児中：育児終了", TARGET
	ENDIF
ENDIF

TARGET = LOCAL

;---------------------------------------------------------
;妊娠判定処理
;ARGSには"システム処理"が入る。これ以外の場合は時間経過によるシステム処理以外での妊娠判定となる
;CHECK_NINSHIN_ALLACTORは全部のPLAYER:0〜1とTARGET:0〜3について行う
;---------------------------------------------------------
@CHECK_NINSHIN_ALLACTOR, ARGS
#DIM LCOUNT

FOR LCOUNT, 1, CHARANUM
	SELECTCASE LCOUNT
	CASE PLAYER, PLAYER:1, TARGET, TARGET:1, TARGET:2, TARGET:3
		CALL CHECK_NINSHIN, LCOUNT, ARGS
	ENDSELECT
NEXT

@CHECK_NINSHIN, ARG, ARGS
#DIM LCOUNT
#DIM MEMO_LINECOUNT

SIF ARG < 0
	RETURN 0
SIF ARG == 0 && TARGET
	ARG = TARGET

;妊娠判定があって男なら、ここで消去。
IF TALENT:ARG:男性
	BASE:ARG:子宫内精液量 = 0
	BASE:ARG:子宫内精子量 = 0
ENDIF

SELECTCASE ARGS
CASE "システム処理"
	;PRINTFORM [{ARG, 2}]%TEXT_LJ(CALLNAME:ARG, 16)% {DAY - CFLAG:ARG:499, 3}日前, 妊娠率：{COND("受精確率"), 3}％, {COND("胎内の精液量", ARG), 6}/{BASE:ARG:子宫内精子量, 4}→

	;淫紋持ちはチェックをここ以外の場所でやっているので、精子量の減少をチェック前に行う
	SIF MARK:ARG:淫纹
		CALL CHECK_NINSHIN_SPERM, ARG

	;避妊薬使用
	IF TALENT:ARG:避孕药服用 > 0 && TALENT:ARG:怀孕 == 0
		IF COND("夜")
			TALENT:ARG:避孕药服用 -= 1
			SIF TALENT:ARG:避孕药服用 == 0
				PRINTFORMW %CALLNAME:ARG%が避妊薬を使い切りました。
		ENDIF
	;妊娠判定(出産後すぐに妊娠できるようにしてみる)
	ELSEIF BASE:ARG:子宫内精子量 && BASE:ARG:胎儿 == 0 && COND("妊娠可能", ARG)
		IF COND("排卵中", ARG) || RAND:10 == 0 || (TALENT:ARG:史莱姆 && RAND:3 == 0)
			IF RAND:100 < COND("受精確率", ARG)
				BASE:ARG:胎儿 = 1
				;20%で息子（産まれる時に選べるようにもしてあるので実はなくても良い）
				SIF RAND:5 == 0
					CALL SETFLAG, "妊娠中：息子", ARG
			ENDIF
		ENDIF
	ENDIF
	SIF MARK:ARG:淫纹 == 0
		CALL CHECK_NINSHIN_SPERM, ARG
CASEELSE
	;受精確率が変動しているかをチェック
	SIF COND("受精確率", ARG) == TCVAR:ARG:110
		RETURN 0

	;受精確率の変動を表示
	IF CONFIG("妊娠パッチ：受精確率表示")
		LOCALS = %CALLNAME:ARG%の受精確率：_

		;最終確率
		SELECTCASE COND("受精確率", ARG)
		CASE IS >= 100
			LOCALS = %LOCALS%ハートピンク_妊娠確実！_
		CASE IS > 50
			LOCALS = %LOCALS%ハートピンク_{COND("受精確率", ARG)}％_
		CASE IS > 20
			LOCALS = %LOCALS%黄色_{COND("受精確率", ARG)}％_
		CASEELSE
			LOCALS = %LOCALS%{COND("受精確率", ARG)}％_
		ENDSELECT

		;伸び幅
		SELECTCASE COND("受精確率", ARG) - TCVAR:ARG:110
		CASE IS >= 15
			LOCALS = %LOCALS%ピンク_（↑{COND("受精確率", ARG) - TCVAR:ARG:110}％）_
		CASE IS >= 10
			LOCALS = %LOCALS%ハートピンク_（↑{COND("受精確率", ARG) - TCVAR:ARG:110}％）_
		CASE IS >= 5
			LOCALS = %LOCALS%黄色_（↑{COND("受精確率", ARG) - TCVAR:ARG:110}％）_
		CASE IS > 0
			LOCALS = %LOCALS%（↑{COND("受精確率", ARG) - TCVAR:ARG:110}％）_
		CASE IS < 0
			LOCALS = %LOCALS%青_（↓{TCVAR:ARG:110 - COND("受精確率", ARG)}％）_
		ENDSELECT

		CALL PRINT_STR, LOCALS

		IF COND("受精確率", ARG) >= 100
			FORCEWAIT
		ELSE
			WAIT
		ENDIF
		PRINTL 
	ENDIF

	;淫紋持ちはここで妊娠判定も行ってしまう
	IF MARK:ARG:淫纹 && (COND("受精確率", ARG) >= 100 || RAND:100 < COND("受精確率", ARG) - TCVAR:ARG:110)
		;おめでた
		BASE:ARG:胎儿 = 1
		;20%で息子（産まれる時に選べるようにもしてあるので実はなくても良い）
		SIF RAND:5 == 0
			CALL SETFLAG, "妊娠中：息子", ARG

		;避妊薬は返してもらう
		MONEY += TALENT:ARG:避孕药服用*150
		TALENT:ARG:避孕药服用 = 0

		CALL SETFLAG, "受精確認フラグ", ARG

		PRINTFORMW %CALLNAME:ARG%の淫紋の様子がおかしい……目まぐるしく色と形が変わっていく！
		SETCOLOR DEF_COLOR("ハートピンク")
		CALL HEARTMARK
		PRINTFORM %CALLNAME:ARG%は%CONDS("精液的优势", ARG)%の精子で受精しました
		CALL HEARTMARK
		RESETCOLOR

		FORCEWAIT
		PRINTL 

		;セリフがあるかの判定
		MEMO_LINECOUNT = LINECOUNT

		TRYCALLFORM GUEST_KOJO_K{NO:ARG}_{CFLAG:ARG:209}, "受精確認", ARG

		SIF LINECOUNT > MEMO_LINECOUNT
			PRINTL 
	ENDIF

	TCVAR:ARG:110 = COND("受精確率", ARG)

ENDSELECT

;---------------------------------------------------------
;胎内の精液量＆精子量を減少させる。だいたい３日程で死滅。
;ちなみにCFLAG:499は最新の中出し日
;---------------------------------------------------------
@CHECK_NINSHIN_SPERM, ARG

VARSET LOCAL

SELECTCASE DAY - CFLAG:ARG:499
;出した直後
CASE 0
	LOCAL = 150 + TALENT:ARG:吸精体质*50 + MARK:ARG:淫纹*20 + 10*RAND:6
	LOCAL:1 = 120 + TALENT:ARG:吸精体质*10 + MARK:ARG:淫纹*5 + 10*RAND:6
CASE 1
	LOCAL = 200 + TALENT:ARG:吸精体质*50 + MARK:ARG:淫纹*20 + 10*RAND:11
	LOCAL:1 = 150 + TALENT:ARG:吸精体质*10 + MARK:ARG:淫纹*50 + 10*RAND:11
CASE 2
	IF COND("昼")
		LOCAL = 300 + TALENT:ARG:吸精体质*50 + MARK:ARG:淫纹*20 + 10*RAND:11
		LOCAL:1 = 200 + TALENT:ARG:吸精体质*10 + MARK:ARG:淫纹*5 + 10*RAND:11
	ENDIF
ENDSELECT
IF LOCAL || LOCAL:1
	BASE:ARG:子宫内精子量 = DIVIDE(BASE:ARG:子宫内精子量, LOCAL)
	BASE:ARG:子宫内精液量 = DIVIDE(BASE:ARG:子宫内精液量, LOCAL:1)
ELSE
	BASE:ARG:子宫内精子量 = 0
	BASE:ARG:子宫内精液量 = 0
ENDIF

;PRINTFORML {COND("胎内の精液量", ARG), 6}/{BASE:ARG:子宫内精子量, 4}, {COND("精液的优势", ARG), 2}\@ BASE:ARG:胎儿 ? , 受精！ # \@

;精子が死滅したら父親フラグは消滅
SIF BASE:ARG:子宫内精子量 == 0 && BASE:ARG:胎儿 == 0
	BASE:ARG:精液的优势 = 0

;---------------------------------------------------------
;妊娠発覚＆出産予定報告＆子の親離れ処理
;---------------------------------------------------------
@EVENT_N2, ARG
#DIM MEMO_TARGET
MEMO_TARGET = TARGET

TARGET = ARG

;子育て中
IF BASE:育儿中
	;子の親離れ
	IF COND("育児中：育児終了")
		DRAWLINE
		;夢魔以外の場合も考えないとなぁ…
		PRINTFORM %CALLNAME:TARGET%の育てていた子供は
		IF TALENT:梦魔 || TALENT:上位梦魔 || TALENT:魔王残片
			PRINTFORM 人にはありえない早さで
		ELSE
			PRINTFORM 館に充満する力のせいか、かなり早く
		ENDIF
		PRINTFORMW 大きくなりました。
		PRINTFORMW もう付きっ切りでなくても良さそうです。

		CALL SELF_KOJO, "子離れ"

		;子供対象化
		CALL DAUGHTER_CHECK, "通常"

		BASE:育儿中 = 0
	ENDIF
ENDIF

;出産予定報告(子育て中には産まれない)
IF COND("妊娠中：分娩直前") && TALENT:怀孕 == 2 && BASE:育儿中 == 0
	DRAWLINE
	PRINTFORMW %CALLNAME:TARGET%のお腹の中では胎児が活発に動いているようです。
	PRINTFORMW 明日にでも産まれそうだ…
	TALENT:怀孕 = 3
ELSEIF COND("妊娠中：臨月") && TALENT:怀孕 == 1
	DRAWLINE
	PRINTFORMW %CALLNAME:TARGET%のお腹は目に見えて大きくなってきました。
	PRINTFORMW あと何日かで産まれそうです…
	TALENT:怀孕 = 2
;子育て中以外の妊娠発覚
ELSEIF COND("妊娠中：妊娠発覚直前") && TALENT:怀孕 == 0
	TALENT:怀孕 = 1
	;妊娠確定フラグに変更
	CALL SETFLAG, "妊娠中：妊娠発覚済み", TARGET

	CALL EVENT_N2_TEXT

	;スライムが分裂した場合
	IF TALENT:史莱姆 && CONFIG("妊娠パッチ：スライム分裂")
		;以下は通常の妊娠時の動作の一部の抜出し
		;妊娠＆胎児フラグ終了
		TALENT:怀孕 = 0
		BASE:胎儿 = 0
		CFLAG:469 += 1
		;CALL SELF_KOJO, "出産"
		;子供の数+1
		TALENT:生产经历 += 1
		;父親フラグの確定
		BASE:胎儿的父亲 = BASE:精液的优势
		BASE:精液的优势 = 0
		;CALL SELF_KOJO, "子離れ"
		;子供対象化
		CALL DAUGHTER_CHECK, "スライム"
	ELSEIF COND("妊娠中") && CFLAG:468 == 0
		PRINTL 
		PRINTFORMW 妊娠に伴い、%CALLNAME:TARGET%の胸が張ってきています。
		;素質変更など
		IF TALENT:母乳体质 == 0
			TALENT:母乳体质 = 2
			CFLAG:468 |= 1p2
		ENDIF
		;胸の大きさ
		IF TALENT:贫乳
			CFLAG:468 |= 1p0
			;２階級特進
			IF RAND:3 == 0
				PRINTFORMW こんなこともあるのか不思議だが、\@ TALENT:娇小体型 ? %CALLNAME:MASTER%の予想を上回って # \@たゆんたゆんになりそうだ…
				PRINTFORMW %CALLNAME:TARGET%は\@ GETBIT(CFLAG:468, 2) ? [母乳体質]と # \@[巨乳]を得ました。
				CALL CALC, "巨乳化", TARGET
			ELSE
				PRINTFORMW 様子を見た感じ、普通程度の大きさにはなりそうだ…
				PRINTFORMW %CALLNAME:TARGET%は\@ GETBIT(CFLAG:468, 2) ? [母乳体質]を得て、 # \@[貧乳]を失いました。
				CALL CALC, "並乳化", TARGET
			ENDIF
			PRINTFORMW 
		ELSEIF TALENT:贫乳 == 0 && TALENT:巨乳 == 0
			CFLAG:468 |= 1p1
			PRINTFORMW どうやら、結構な大きさになりそうだ…
			PRINTFORMW %CALLNAME:TARGET%は\@ GETBIT(CFLAG:468, 2) ? [母乳体質]と # \@[巨乳]を得ました。
			CALL CALC, "巨乳化", TARGET
		ELSEIF GETBIT(CFLAG:468, 2)
			PRINTFORMW %CALLNAME:TARGET%は[母乳体質]を得ました。
		ENDIF
	ENDIF
ENDIF

;妊娠に付随する母乳体質や巨乳の消去
IF CFLAG:468 && COND("妊娠中") == 0 && BASE:育儿中 == 0
	PRINTL 
	PRINTFORMW （役目を終えた%CALLNAME:TARGET%の胸は元の大きさに戻りました）
	SIF GETBIT(CFLAG:468, 0)
		CALL CALC, "貧乳化", TARGET
	SIF GETBIT(CFLAG:468, 1)
		CALL CALC, "並乳化", TARGET
	SIF GETBIT(CFLAG:468, 2)
		TALENT:母乳体质 = 0

	CFLAG:468 = 0
ENDIF

TARGET = MEMO_TARGET

;---------------------------------------------------------
;妊娠発覚イベント
;CALLNAME:(COND("精液的优势") ) とかなると見苦しいので、FATHER = COND("精液的优势")とする
;---------------------------------------------------------
@EVENT_N2_TEXT
#DIM FATHER

FATHER = COND("精液的优势")

DRAWLINE
;スライム分裂！
IF TALENT:史莱姆 && CONFIG("妊娠パッチ：スライム分裂")
	IF TARGET == MASTER
		PRINTFORMW 突然%CALLNAME:MASTER%は全身の痛みに襲われ、身体の形状を保てなくなってしまった
		PRINTFORMW 粘液状に戻っても%CALLNAME:MASTER%はしばらく痛みに悶え続けた…
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		PRINTFORMW 
		PRINTFORM しばらく経って辺りを見渡すと%CALLNAME:MASTER%
		PRINTFORM \@ !TALENT:MASTER:娇小体型 ? の少女時代 # \@によく似た、しかし
	ELSE
		PRINTFORMW 突然%CALLNAME:TARGET%がびくりと震え、身体が粘液に戻ってしまった
		PRINTFORMW %CALLNAME:TARGET%だった粘液はぷるぷるしながら体積を増やしていく…
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		PRINTFORMW 
		PRINTFORMW ぷるぷるが収まると粘液は二人の少女の姿をかたどった。
		PRINTFORML いつもの分裂かと思ったが、片方の少女はもう一人の%CALLNAME:TARGET%より幼く、
	ENDIF

	SELECTCASE FATHER
	;父親はキャラの誰か
	CASE IS > 0
		PRINTFORM 顔つきに%CALLNAME:FATHER%の面影がある
	;父親はドラゴン
	CASE -3
		PRINTFORM 内に秘められた力強さを感じさせる
	;父親は触手
	CASE -5
		PRINTFORM 内に禍々しい魔力を感じさせる
	;父親は行きずりの男
	CASEELSE
		PRINTFORM 顔つきが%CALLNAME:MASTER%とやや異なっている
	ENDSELECT
	IF TARGET == MASTER
		PRINTL 
		PRINTFORMW スライムの少女が%CALLNAME:MASTER%をじっと見つめていた…
	ELSE
		PRINTFORMW ような気がする…
	ENDIF

	PRINTFORM %CALLNAME:TARGET%は……
	SELECTCASE FATHER
	;父親はキャラの誰か
	CASE IS > 0
		PRINTFORM %CALLNAME:FATHER%
	;父親はドラゴン
	CASE -3
		PRINT 驚いたことにドラゴン
	;父親は触手
	CASE -5
		PRINT 驚いたことに触手
	;父親は行きずりの男
	CASEELSE
		PRINT 誰か
	ENDSELECT
	PRINTFORML の精液を子宮に受けて分裂してしまったようだ！

	FORCEWAIT

	CALL SELF_KOJO, "妊娠発覚"
ELSE
	IF TARGET == MASTER
		PRINTFORMW %CALLNAME:MASTER%はどうも体調が優れないので、少し念入りに検査してみました…
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		PRINTFORMW 調べた結果、\@ BASE:育儿中 ? 子育ての途中だというのに # どうやら \@
		SELECTCASE FATHER
		CASE IS > 0
			PRINTFORM %CALLNAME:FATHER%の
		CASE -3
			PRINTFORM 驚いたことにドラゴンの
		CASE -5
			PRINTFORM 驚いたことに触手の
		ENDSELECT
		PRINTFORML 子供を妊娠してしまったようです。
	;子育て中
	ELSEIF BASE:育儿中
		PRINTFORMW %CALLNAME:TARGET%は体調の変化を訴えている…
		PRINTFORML 調べた結果、%CALLNAME:TARGET%は子育ての途中に再び妊娠したようだ。
	;恋慕
	ELSEIF TALENT:恋慕
		PRINTFORMW %CALLNAME:TARGET%は妊娠したと訴えてきた…
		PRINTFORML 調べた結果、確かに%CALLNAME:TARGET%は妊娠したようだ。
	;その他
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は体調の変化を訴えている…
		PRINTFORML 調べた結果、%CALLNAME:TARGET%は妊娠したようだ。
	ENDIF

	FORCEWAIT

	;避妊薬は回収
	IF TALENT:避孕药服用 > 0
		PRINTFORML …避妊薬を飲むのが遅かったらしい。
		PRINTFORML 仕方ないので、余った薬は換金します。
		PRINTFORMW 所持金＋%MONEY_C(TALENT:避孕药服用*150)%
		MONEY += TALENT:避孕药服用*150
		TALENT:避孕药服用 = 0
	ENDIF

	CALL SELF_KOJO, "妊娠発覚"

	;MASTER以外はリアクション
	IF TARGET == MASTER
	;父親はドラゴン
	ELSEIF FATHER == -3
		PRINTFORML お腹の中の子供の父親がドラゴンだと判ると、
		PRINTFORMW 予想外の出来事に%CALLNAME:TARGET%は驚きを通り越して呆然としてしまっている…
	;父親は触手！
	ELSEIF FATHER == -5
		PRINTFORML お腹の中の子供の父親が異形の触手だと判ると、
		IF TALENT:恋慕
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%以外の子供を身ごもったことに残念な様子を見せた…
		ELSEIF TALENT:旧支配者的末裔
			PRINTFORMW %CALLNAME:TARGET%はなんともいえない顔をしている…
		;臆病or大人しいor悲観的
		ELSEIF TALENT:胆怯 || TALENT:腼腆 || TALENT:阴郁
			PRINTFORMW %CALLNAME:TARGET%は込み上げるものを抑えることも出来ずに激しく嘔吐しだした…
		;抑圧or抵抗or一線or保守的or気丈
		ELSEIF TALENT:压抑 || TALENT:抵抗 || TALENT:底线明确 || TALENT:保守 || TALENT:刚强
			PRINTFORMW %CALLNAME:TARGET%は呆然としながら膨らんだお腹を見つめだした…
		ELSE
			PRINTFORMW あんまりな事実に%CALLNAME:TARGET%はガクリと膝をついて、呆然としてしまった…
		ENDIF
	;恋慕
	ELSEIF TALENT:恋慕
		;父親はMASTER
		IF FATHER == MASTER
			;反抗的orプライド高いor強気
			IF TALENT:叛逆 || TALENT:高傲 || TALENT:强硬
				PRINTFORMW %CALLNAME:TARGET%は膨らんだお腹を見せて、撫でて欲しいと頼んできた…
			;素直or楽観的
			ELSEIF TALENT:坦率 || TALENT:开朗
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%に微笑みながら、膨らんだお腹を撫でている…
			ELSE
				PRINTFORMW %CALLNAME:TARGET%は膨らんだお腹を撫でて、嬉しそうにしている…
			ENDIF
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%以外の子供を身ごもったことが残念な様子です…
		ENDIF
	;子育て中
	ELSEIF BASE:育儿中
		PRINTFORMW %CALLNAME:TARGET%はなんともいえない顔をしている…
	;反発刻印Lv3or反抗的
	ELSEIF MARK:排斥刻印 == 3 || TALENT:叛逆
		;出産経験あり
		IF TALENT:生产经历
			PRINTFORMW %CALLNAME:TARGET%は何かを諦めた笑みを浮かべながら、膨らんだお腹を撫でている…
		;出産経験なし
		;反抗的orプライド高いor強気
		ELSEIF TALENT:叛逆 || TALENT:高傲 || TALENT:强硬
			PRINTFORMW %CALLNAME:TARGET%は膨らんだお腹を優しく撫でていたが、%CALLNAME:MASTER%が近づくと激しく睨み付けてきた…
		;臆病or大人しいor悲観的
		ELSEIF TALENT:胆怯 || TALENT:腼腆 || TALENT:阴郁
			PRINTFORML そう伝えられた%CALLNAME:TARGET%は青ざめ、口を押さえたが次の瞬間に嘔吐し、
			PRINTFORMW 吐瀉物を床にぶちまけた…
		;抑圧or抵抗or一線or保守的or気丈
		ELSEIF TALENT:压抑 || TALENT:抵抗 || TALENT:底线明确 || TALENT:保守 || TALENT:刚强
			PRINTFORMW %CALLNAME:TARGET%は膨らんだお腹を虚ろな目で見つめている…
		ELSE
			PRINTFORMW そう伝えられた%CALLNAME:TARGET%は嗚咽をあげた…
		ENDIF
	;屈服刻印Lv2or従順4LV
	ELSEIF MARK:屈服刻印 >= 2 || ABL:服从 >= 4
		;反抗的orプライド高いor強気
		IF TALENT:叛逆 || TALENT:高傲 || TALENT:强硬
			PRINTFORMW %CALLNAME:TARGET%は複雑そうな顔をしながら、膨らんだお腹を撫でている…
		;臆病or大人しいor悲観的
		ELSEIF TALENT:胆怯 || TALENT:腼腆 || TALENT:阴郁
			PRINTFORMW そう伝えられた%CALLNAME:TARGET%は不安げに、%CALLNAME:MASTER%の手をそっと握ってきた…
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は愛情のこもった手つきでお腹を撫でている…
		ENDIF
	;その他
	ELSEIF FATHER > 0
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:FATHER%との間に子供が出来たことに驚いている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は子供が出来たことに驚いている…
	ENDIF
ENDIF

;望まぬ妊娠の場合は異常経験Get
CALL EXP50_CHECK

;---------------------------------------------------------
;その他の妊娠イベント処理
;CALLNAME:(COND("精液的优势") ) とかなると見苦しいので、FATHER に代入する
;---------------------------------------------------------
@EVENT_N3, ARG
#DIM FATHER

LOCAL = TARGET
TARGET = ARG

;お産(子育て中には出産しない)
IF COND("妊娠中：分娩") && TALENT:怀孕 && BASE:育儿中 == 0
	FATHER = COND("精液的优势")
	DRAWLINE
	IF TARGET == MASTER
		PRINTFORM %CALLNAME:MASTER%は
		SELECTCASE FATHER
		CASE IS > 0
			SIF TALENT:FATHER:恋慕
				PRINTFORM %CALLNAME:FATHER%
			SIF TALENT:生产经历
				PRINTFORM \@ TALENT:FATHER:恋慕 ? と # \@子供
		ENDSELECT
		SIF CFLAG:469/100 + CFLAG:469 % 100 >= 2
			PRINT たち
		SIF FATHER > 0 || TALENT:生产经历
			PRINT の見守る中で
		PRINT 、
	ELSE
		PRINTFORMW %CALLNAME:TARGET%が産気づいたようです。
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		PRINTFORMW 
		PRINTFORM %CALLNAME:TARGET%は%CALLNAME:MASTER%
		SIF TALENT:生产经历
			PRINT と子供
		SIF CFLAG:469/100 + CFLAG:469 % 100 >= 2
			PRINT たち
		PRINTFORM に励まされながら、
	ENDIF
	PRINTFORMW 無事に出産しました。

	;試しに子供の性別選ばせてみるぜ
	PRINTL 
	PRINTFORML 生まれた子は……
	PRINTFORML [ 0] 女の子
	PRINTFORML [ 1] 男の娘
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		CLEARBIT BASE:胎儿, 20
	ELSE
		CALL SETFLAG, "妊娠中：息子", TARGET
	ENDIF

	IF COND("妊娠中：息子")
		PRINTFORMW \@ FATHER > 0 ? 父親似の # \@元気な男の子です。
		CFLAG:469 += 100
		;息子フラグ
		CALL SETFLAG, "育児中：息子", TARGET
	ELSE
		PRINTFORMW \@ TARGET == MASTER ? %CALLNAME:MASTER% # 母親 \@似の可愛い女の子です。
		CFLAG:469 += 1
	ENDIF

	;恋慕
	IF TALENT:恋慕
		PRINTFORMW 赤ん坊を抱っこした%CALLNAME:TARGET%は、%CALLNAME:MASTER%に我が子を抱いてくれとせがんできました。
	;反発刻印Lv2
	ELSEIF MARK:排斥刻印 == 2
		PRINTFORMW 赤ん坊を抱っこした%CALLNAME:TARGET%は、我が子に向けて微笑んでいます。
	;反発刻印Lv3
	ELSEIF MARK:排斥刻印 == 3
		IF TALENT:叛逆 || TALENT:高傲 || TALENT:强硬
			PRINTFORMW 赤ん坊を抱っこした%CALLNAME:TARGET%は、%CALLNAME:MASTER%を威嚇するように睨みつけてきました。
		ELSE
			PRINTFORMW 赤ん坊を抱っこした%CALLNAME:TARGET%は、我が子を%CALLNAME:MASTER%から庇うように抱きしめました。
		ENDIF
	ELSEIF TARGET != MASTER
		PRINTFORMW 赤ん坊を抱っこした%CALLNAME:TARGET%は、我が子と%CALLNAME:MASTER%に向けて微笑んでいます。
	ENDIF

	CALL SELF_KOJO, "出産"

	CALL BEFOREHAND_DAUGHTER_NAME
	CFLAG:485 = DAY
	TARGET = ARG

	SIF TALENT:梦魔 || TALENT:上位梦魔
		PRINTFORMW 夢魔の子は成長が早いそうなので、すぐに遊びまわる姿を見れることだろう…
	SIF TALENT:魔王残片
		PRINTFORMW 魔王の魔力の残滓かみるみる子供が成長していく、すぐに遊びまわる姿を見れることだろう…

	;妊娠＆胎児フラグ終了
	BASE:胎儿 = 0
	TALENT:怀孕 = 0

	;子育て開始
	BASE:育儿中 += 1
	;子供の数+1
	TALENT:生产经历 += 1
	;父親フラグの確定
	BASE:胎儿的父亲 = BASE:精液的优势
	BASE:精液的优势 = 0

	;一時的な素質の消去(まだ消えません)
	IF CFLAG:468
		PRINTL 
		PRINTFORMW （出産を終え、%CALLNAME:TARGET%の胸は元に戻っていくことでしょう…）
	ENDIF
	SETBIT CFLAG:MASTER:95, 0
;子育て中
ELSEIF BASE:育儿中 && RAND:4 && COND("手一杯", MASTER) + COND("同衾イベント", MASTER) == 0
	FATHER = COND("胎儿的父亲")
	DRAWLINE
	;妊娠しているのはMASTER
	IF TARGET == MASTER
		PRINTFORMW %CALLNAME:MASTER%は子供の世話に忙殺されています…
			SETBIT CFLAG:MASTER:95, 0
	;父親はMASTERor既にいないor恋慕
	ELSEIF FATHER == MASTER || FATHER < 0 || TALENT:恋慕
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%と子供の様子を見に行きました。
		;反発刻印Lv3
		IF MARK:排斥刻印 == 3
			IF RAND:5 == 0 && (TALENT:叛逆 || TALENT:高傲 || TALENT:强硬)
				PRINTFORMW 部屋に入れませんでした…
			ELSEIF RAND:4 == 0
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%を睨んでいます…
			ELSEIF RAND:3 == 0
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の顔を見ると子供を庇うように抱きしめました…
			ELSEIF RAND:2 == 0
				PRINTFORMW %CALLNAME:TARGET%は楽しそうに子供と遊んでいました。
				PRINTFORMW %CALLNAME:MASTER%はそのまま自室に戻りました…
			ELSE
				PRINTFORMW %CALLNAME:TARGET%は歓声を上げて子供と遊んでいましたが、
				PRINTFORMW %CALLNAME:MASTER%が部屋に入るとやめてしまいました…
			ENDIF
		ELSEIF RAND:6 == 0
			PRINTFORMW %CALLNAME:TARGET%は子供にお乳を上げているところでした。
			PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%の母親らしい姿を見て、何だか和みました…
		ELSEIF RAND:5 == 0
			PRINTFORMW 二人して眠そうに出迎えたので、今夜は一緒に寝てあげることにします…
			SETBIT CFLAG:MASTER:95, 0
			CFLAG:MASTER:96 = TARGET
			CFLAG:96 = MASTER
		ELSEIF RAND:4 == 0
			PRINTFORMW %CALLNAME:TARGET%が寝付いてくれない子供に困っていたので、一緒に徹夜覚悟であやすことにしました…
			SETBIT CFLAG:MASTER:95, 0
			CFLAG:MASTER:96 = TARGET
			CFLAG:96 = MASTER
		ELSEIF RAND:3 == 0
			PRINTFORMW %CALLNAME:TARGET%は子供とペタペタさわりっこしている%CALLNAME:MASTER%を、幸せそうに眺めています…
		ELSEIF RAND:2 == 0
			PRINTFORMW 子供がボーっとしていたので３人で一緒に簡単な遊びをしてバタバタと過ごしました…
		ELSE
			PRINTFORM %CALLNAME:TARGET%がなんとなく子育てに不安そうなので、
			IF RAND:3 == 0
				PRINT 髪を梳きながら
			ELSEIF RAND:2 == 0
				PRINT 優しく撫でながら
			ELSE
				IF  TALENT:娇小体型
					PRINT 抱っこしながら
				ELSE
					PRINT スキンシップしながら
				ENDIF
			ENDIF
			PRINTFORMW 悩みを聞いてあげました…
		ENDIF
	;父親はMASTER以外（現状保留）
	;ELSEIF FATHER > MASTER
	ENDIF
;妊娠中
ELSEIF TALENT:怀孕 && COND("手一杯", MASTER) + COND("同衾イベント", MASTER) == 0 && RAND:4
	FATHER = COND("精液的优势")
	DRAWLINE
	;妊娠しているのはMASTER
	IF TARGET == MASTER
		PRINTFORMW %CALLNAME:MASTER%は出歩くのが億劫なので、自室で休むことにしました。
			SETBIT CFLAG:MASTER:95, 0
	;父親はMASTERor既にいないor恋慕
	ELSEIF FATHER == MASTER || FATHER < 0 || TALENT:恋慕
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%の様子を見に行きました。
		;反発刻印Lv3
		IF MARK:排斥刻印 == 3
			IF RAND:4 == 0
				PRINTFORMW %CALLNAME:TARGET%は虚ろな目で膨らんだお腹を見つめていました…
			ELSEIF RAND:3 == 0
				PRINTFORMW %CALLNAME:TARGET%はお腹に耳を当てたりする%CALLNAME:MASTER%を、無感情に見下ろしています…
			ELSEIF RAND:2 == 0
				PRINTFORMW %CALLNAME:TARGET%は虚ろに笑いながらお腹の子に話しかけています…
			ELSE
				PRINTFORMW %CALLNAME:TARGET%は楽しげにお腹の子に話しかけていましたが、
				PRINTFORMW %CALLNAME:MASTER%が見ていると突然涙を流し始めました…
			ENDIF
		ELSEIF FATHER == MASTER && RAND:4
			IF RAND:4 == 0
				PRINTFORMW %CALLNAME:TARGET%が眠そうに出迎えたので、今夜は一緒に寝てあげることにします…
				SETBIT CFLAG:MASTER:95, 0
				CFLAG:MASTER:96 = TARGET
				CFLAG:96 = MASTER
			ELSEIF RAND:3 == 0
				PRINTFORMW …が、%CALLNAME:TARGET%が既に寝ているようだったので、キスだけして引き返しました…
			ELSEIF RAND:2
				PRINTFORMW %CALLNAME:TARGET%はお腹に耳を当てたりする%CALLNAME:MASTER%を、目を細めて嬉しそうに眺めています…
			ELSE
				PRINTFORMW %CALLNAME:TARGET%と産まれてくる子の事を話したり、楽しい時間を過ごしました…
			ENDIF
		ELSE
			PRINTFORM %CALLNAME:TARGET%がなんとなく不安そうなので、
			IF RAND:3 == 0
				PRINT 髪を梳きながら
			ELSEIF RAND:2 == 0
				PRINT 優しく撫でながら
			ELSE
				IF  TALENT:娇小体型
					PRINT 抱っこしながら
				ELSE
					PRINT スキンシップしながら
				ENDIF
			ENDIF
			PRINTFORMW 悩みを聞いてあげました…
		ENDIF
	;父親はMASTER以外（現状保留）
	;ELSEIF FATHER > MASTER
	ENDIF
ENDIF

TARGET = LOCAL

;---------------------------------------------------------
;主人と夢魔たちによる妊娠判定チェック
;シチュエーションARGSでARGが妊娠するかどうかを判定する。ARG:1は精液量。
;ARGSは"通常", "調教外", "龙", "一般人相手", "触手"
;SPERMで子宮に入る精子量、IS_FATHERで誰が父親になるかの番号管理を行う
;---------------------------------------------------------
@CHECK_PREGNANT, ARG, ARG:1, ARGS
#DIM SPERM
#DIM SPERM_DENS
#DIM IS_FATHER

;精液量がない場合
SIF ARG:1 <= 0
	RETURN 0

;調教中かどうかとPLAYERが一般人かどうかをチェック
SELECTCASE ARGS
;指定されている場合にはARGSの変化なし
CASE "龙", "触手", "一般人相手", "調教外", "通常"
CASEELSE
	IF TALENT:PLAYER:普通人
		ARGS = 一般人相手
	ELSEIF COND("調教中")
		ARGS = 通常
	ELSE
		ARGS = 調教外
	ENDIF
ENDSELECT

;SPERMで子宮に入る精子量、IS_FATHERで誰が父親になるかの番号管理を行う
SPERM = ARG:1 * (10 + RAND:21)
SPERM_DENS = 90 + RAND:21

SELECTCASE ARGS
CASE "龙"
	IS_FATHER = -3
CASE "一般人相手"
	;処女は判定しない
	SIF TALENT:ARG:处女
		RETURN 0
	IS_FATHER = -4
	;精液量は館のメンバーに比べると少ない
	SPERM = MAX(SPERM/2, 1)
CASE "触手"
	IS_FATHER = -5

	;触手はあなた？
	SIF FLAG:87 == 1
		IS_FATHER = MASTER
CASEELSE
	SELECTCASE ARG
	;調教者の妊娠チェック
	CASE PLAYER
		IS_FATHER = TARGET

	;TARGETやASSI等の妊娠チェック
	CASEELSE
		SELECTCASE BASE:PLAYER:识别号码
		CASE IS > 0
			IS_FATHER = PLAYER
		CASEELSE
			IS_FATHER = -4
		ENDSELECT

		;絶頂を迎える事で確率上昇
		SIF NOWEX:ARG:绝顶数合计
			SPERM += MIN(NOWEX:ARG:绝顶数合计, 2)/2

		IF COND("子宮挿入フラグ", ARG)
			SPERM *= 10
		;挿入子宮口責めの時には妊娠率上昇
		ELSEIF SELECTCOM == 67
			TIMES SPERM, 1.20
		ENDIF
	ENDSELECT
	;胡蝶の夢
	SIF ITEM:化蝶之梦 && IS_FATHER > 0 && COND("化蝶之梦", IS_FATHER) > 0
		IS_FATHER = COND("化蝶之梦", IS_FATHER)
ENDSELECT

;妊娠判定フラグ
BASE:ARG:子宫内精液量 += SPERM * SPERM_DENS

;避妊中は精子は受け入れない
IF COND("避妊中", ARG)
	SPERM = 0
;妊娠する可能性ある人は主人？
ELSEIF CONFIG("妊娠パッチ：主人妊娠可能") == 0 && ARG == MASTER
	SPERM = 0
;主人以外が父親でも妊娠可能？
ELSEIF CONFIG("妊娠パッチ：主人の精子のみ") && IS_FATHER != MASTER
	SPERM = 0
ENDIF

;調教以外でも妊娠可能？
SELECTCASE ARGS
CASE "龙", "触手"
	;有効精子量が少ない
	SPERM = MAX(SPERM/3, 1)
CASE "一般人相手"
	SIF CONFIG("妊娠パッチ：調教以外で妊娠可能") == 0
		SPERM = 0
	;夢魔の人はパンピー相手にはかなり強い
	SIF COND("夢魔", ARG)
		SPERM = MAX(SPERM/(TALENT:ARG:梦魔 + TALENT:ARG:上位梦魔*2 + 1), 1)
CASE "調教外"
	SIF CONFIG("妊娠パッチ：調教以外で妊娠可能") == 0
		SPERM = 0
ENDSELECT
;吸精体質の人は精子を頂いちゃう
SIF TALENT:ARG:吸精体质
	SPERM = MAX(MIN(1 + EX:ARG:Ｖ中出*2, SPERM), SPERM/POWER(2, TALENT:ARG:吸精体质) )

BASE:ARG:子宫内精子量 += SPERM

;父親一時フラグ
IF SPERM && BASE:ARG:胎儿 == 0 && COND("妊娠確実", ARG) == 0 && COND("避妊中", ARG) == 0
	;百発百中
	IF IS_FATHER > 0 && TALENT:IS_FATHER:百发百中
		BASE:ARG:精液的优势 = BASE:IS_FATHER:识别号码
		SETBIT BASE:ARG:生理周期, 14
 	ELSEIF BASE:ARG:子宫内精子量 == 0 || COND("精液的优势", ARG) == 0 || RAND:3 == 0
 		SELECTCASE IS_FATHER
		CASE IS > 0
			BASE:ARG:精液的优势 = BASE:IS_FATHER:识别号码
		CASEELSE
			BASE:ARG:精液的优势 = IS_FATHER
		ENDSELECT
	ENDIF
ENDIF
;排卵誘発剤
SIF TEQUIP:ARG:排卵诱发剂
	SETBIT BASE:ARG:生理周期, 13
;中出しされた最新の日付を上書き
CFLAG:ARG:499 = DAY

