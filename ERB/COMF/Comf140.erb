@COM140
;イラマチオ

PRINTL イラマチオ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 140

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
SIF TEQUIP:束缚
	TCVAR:28 -= 30
SIF TEQUIP:口塞 == 2
	TCVAR:28 -= 30
TCVAR:28 = MAX(TCVAR:28, 0)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = イラマチオ

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1


@FLAG_COM140, ARG
;対象の口⇔調教者のＰの汚れが移動
CALL EQ_STAIN, TARGET, PLAYER, 0, 2

@SOURCE_COM140, ARG
VARSET LOCAL

LOCAL:20 += 150
LOCAL:21 += 100

IF TEQUIP:触手 && TEQUIP:触手口辱
	LOCAL:8 = 1000
ELSE
	LOCAL:8 = COM_ORDER_STAIN("Ｐ", "Ｍ")*200 + 100
ENDIF

LOCAL:13 = 3000
LOCAL:14 = 3000

;快Mのソース・M感覚を見る
LOCAL:18 = CALCF("Ｍ刺激", 2, 120)

;喉性感がないならば激減
IF TALENT:喉性感 == 0
	TIMES LOCAL:18, 0.05
	SIF LOCAL:18 < 100
		LOCAL:18 = 0
ENDIF

IF TEQUIP:触手 && TEQUIP:触手口辱
	LOCAL:30 = ABL:喉扩张 - TEQUIP:触手口辱
ELSE
	LOCAL:30 = ABL:喉扩张 - SIZE("ペニス", PLAYER)
ENDIF

;喉ＬＶを見る
SELECTCASE LOCAL:30
CASE IS <= -2
	LOCAL:6 = 3000
	LOCAL:9 = 8000
	TIMES LOCAL:18, 0.00
CASE -1
	LOCAL:6 = 1000
	LOCAL:9 = 3000
	TIMES LOCAL:18, 0.00
CASE 0
	LOCAL:6 = 500
	LOCAL:9 = 1000
CASE 1
	LOCAL:6 = 100
	LOCAL:9 = 300
ENDSELECT

;堅さを見る
IF TEQUIP:触手口辱 == 0
	SELECTCASE HARD("ペニス", PLAYER)
	CASE 3
		TIMES LOCAL:6, 1.20
		TIMES LOCAL:9, 1.20
	CASE IS >= 4
		TIMES LOCAL:6, 1.50
		TIMES LOCAL:9, 1.50
	ENDSELECT
ENDIF

;ABL:侍奉精神をみる
SELECTCASE ABL:侍奉精神
CASE 0
	LOCAL:4 = 420
	LOCAL:5 = 50
	LOCAL:14 += 2000
CASE 1
	LOCAL:4 = 500
	LOCAL:5 = 100
	LOCAL:14 += 1200
CASE 2
	LOCAL:4 = 580
	LOCAL:5 = 200
	LOCAL:14 += 800
CASE 3
	LOCAL:4 = 660
	LOCAL:5 = 300
	LOCAL:14 += 500
CASE 4
	LOCAL:4 = 740
	LOCAL:5 = 500
	LOCAL:14 += 200
CASEELSE
	LOCAL:4 = 820
	LOCAL:5 = 700
ENDSELECT

;ABL:技巧をみる
SELECTCASE ABL:技巧
CASE 0
	TIMES LOCAL:4, 0.50
	TIMES LOCAL:5, 0.50
	TIMES LOCAL:6, 2.00
	TIMES LOCAL:9, 2.00
	TIMES LOCAL:14, 2.00
CASE 1
	TIMES LOCAL:4, 0.80
	TIMES LOCAL:5, 0.80
	TIMES LOCAL:6, 1.50
	TIMES LOCAL:9, 1.50
	TIMES LOCAL:14, 1.50
CASE 2
CASE 3
	TIMES LOCAL:4, 1.20
	TIMES LOCAL:5, 1.20
	TIMES LOCAL:6, 0.70
	TIMES LOCAL:9, 0.70
CASE 4
	TIMES LOCAL:4, 1.50
	TIMES LOCAL:5, 1.50
	TIMES LOCAL:6, 0.20
	TIMES LOCAL:9, 0.40
CASEELSE
	TIMES LOCAL:4, 2.00
	TIMES LOCAL:5, 2.00
	TIMES LOCAL:6, 0.05
	TIMES LOCAL:9, 0.20
ENDSELECT

;ABL:受虐倾向をみる
SELECTCASE ABL:受虐倾向
CASE 1
	TIMES LOCAL:13, 1.20
	TIMES LOCAL:9, 0.80
CASE 2
	TIMES LOCAL:13, 1.50
	TIMES LOCAL:9, 0.50
CASE 3
	TIMES LOCAL:13, 2.00
	TIMES LOCAL:9, 0.20
CASE 4
	TIMES LOCAL:13, 2.50
	TIMES LOCAL:9, 0.10
CASE IS >= 5
	TIMES LOCAL:13, 3.00
	TIMES LOCAL:9, 0.05
ENDSELECT

CALL SOURCE_SMPLAY, "イラマチオ", ARG

;ディープスロート中？
IF TEQUIP:深喉
	IF TALENT:呕吐反射耐性 == 0
		LOCAL:20 += 50
		LOCAL:21 += 50
	ENDIF
	TIMES LOCAL:4, 1.50
	TIMES LOCAL:5, 1.50
	TIMES LOCAL:6, 1.50
	TIMES LOCAL:8, 1.50
	TIMES LOCAL:9, 1.50
	TIMES LOCAL:13, 1.50
	TIMES LOCAL:14, 1.50
	TIMES LOCAL:18, 1.50
ENDIF

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM140
IF CHECK_COM("３Ｐ奉仕")
	CALL TRAIN_MESSAGE_3P
;前回もイラマチオ
ELSEIF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の
	;角がありますか？
	PRINTFORM \@ COND("ツノハンドル") ? 角を掴んで # 頭を押さえて \@、
	IF SIZE("ペニス", PLAYER) >= 2 && RANDIF(2, ABL:PLAYER:施虐倾向 >= 3)
		PRINTFORM 長いペニスで
	ELSEIF ABL:PLAYER:施虐倾向 >= 3 && RAND:2
		PRINTFORM 息継ぎもさせない勢いで
	ELSEIF ABL:受虐倾向 >= 3 && CHECK("喉姦")
		PRINTFORM 望まれるがままに
	ENDIF
	IF (ABL:技巧 >= 4 || TALENT:PLAYER:抖Ｓ || TEQUIP:深喉) && SIZE("ペニス", PLAYER) && RAND:2
		IF SIZE("ペニス", PLAYER) >= 3 && TEQUIP:深喉 && RAND:2
			PRINT 食道
		ELSE
			PRINT 喉
		ENDIF
	ELSE
		PRINT 口
	ENDIF
	PRINTFORMW を容赦なく犯し続けている…
ELSE
	;特に[恋慕＋奉仕精神Lv5]、[奉仕精神Lv3]、[PLAYERがサド]、[直前の調教]、[角があるか]、[アイマスク]等でテキストが変わります。
	PRINTFORM %CALLNAME:PLAYER%は
	;前回はフェラチオ
	IF PREVCOM == 91 && COND("前Ｔ調教者") == PLAYER
		;汚れてますか？汚臭鈍感で無いならば、実行値の10分の一の値とRAND:5を比べて判定
		IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｖ", "Ａ") && TALENT:污臭钝感 == 0 && RAND:6 > TCVAR:28/10
			PRINTFORM %TEXTS("匂い")%に眉根を寄せつつ
		;恋慕or献身的or奉仕精神3以上or汚臭鈍感
		ELSEIF (TALENT:恋慕 || TALENT:献身 || ABL:侍奉精神 >= 3 || TALENT:污臭钝感) && RAND:2 == 0
			PRINT 美味しそうに
		;恥じらい
		ELSEIF TALENT:羞耻心强 && RAND:3 == 0
			PRINT 恥ずかしそうに
		;欲情4以上
		ELSEIF PALAM:欲情 >= PALAMLV:4 && RAND:3 == 0
			SIF TALENT:性变态 && RAND:3 == 0
				PRINTFORM %TEXTS("匂い")%に
			PRINT 興奮しつつ
		;恋慕かつ奉仕精神5or献身的かつ奉仕精神3以上
		ELSEIF ( (TALENT:恋慕 && ABL:侍奉精神 >= 5) || (TALENT:献身 && ABL:侍奉精神 >= 3) ) && RAND:3 == 0
			PRINT 嬉しそうに
		;恋慕or献身的or奉仕精神3以上
		ELSEIF TALENT:恋慕 || TALENT:献身 || ABL:侍奉精神 >= 3
			PRINT 美味しそうに
		ELSEIF RAND:2 == 0 && ABL:技巧 < 2
			PRINT えづきながらも
		ENDIF
		;前回お掃除フェラ
		IF PREVPLAY("お掃除フェラ")
			PRINTFORM ペニスをお掃除している%CALLNAME:TARGET%の
		ELSE
			PRINTFORM 奉仕している%CALLNAME:TARGET%の
		ENDIF
	;ディープスロート中
	ELSEIF TEQUIP:深喉
		PRINTFORM 根元までペニスを咥え込んだ%CALLNAME:TARGET%の
	ELSE
		IF BIND("ベッド", TEQUIP:束缚) && RAND:2
			PRINTFORM 仰向けにされて喉と一直線になっている%CALLNAME:TARGET%の口の中に
		;リングギャグ
		ELSEIF TEQUIP:口塞 == 2
			PRINTFORM 器具で開きっぱなしにされている%CALLNAME:TARGET%の口の中に
		;アイマスク+縄
		ELSEIF TEQUIP:眼罩 && TEQUIP:束缚
			PRINTFORM 完全に拘束されている%CALLNAME:TARGET%の口の中に
		;アイマスクあり？
		ELSEIF TEQUIP:眼罩
			PRINTFORM 目隠しをした%CALLNAME:TARGET%の口の中に
		;縛ってますか？
		ELSEIF TEQUIP:束缚
			PRINTFORM 身動きの取れない%CALLNAME:TARGET%の口の中に
		;恋慕＋奉仕精神Lv5
		ELSEIF ABL:侍奉精神 >= 5 && TALENT:恋慕 && RAND:2 == 0
			PRINTFORM %NAMES("ペニス", PLAYER)%に頬擦りをしてきた%CALLNAME:TARGET%の口の中に
		ELSE
			PRINTFORM 跪かせた%CALLNAME:TARGET%の口の中に
		ENDIF
		;直前の調教と汚れで分岐
		IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｖ", "Ａ", "尿")
			CALL TEXT, "追加地の文：穴から穴へ"
		ELSEIF PENIS(PLAYER) == 0
		;セックス系を直前にしていないなら乱数分岐
		ELSE
			PRINTFORM %TEXTS("ペニス：視覚", PLAYER)%
		ENDIF
		IF PENIS(PLAYER)
			PRINTFORM ペニスを
		ELSE
			PRINTFORM ペニバンを
		ENDIF
		;奉仕精神と「PLAYERがサドか」で分岐
		IF (TALENT:PLAYER:抖Ｓ || ABL:侍奉精神 >= 5) && RAND:3 == 0
			PRINT 一気に
		ELSEIF TALENT:PLAYER:抖Ｓ || (ABL:侍奉精神 >= 3 && RAND:2 == 0)
			PRINT 奥まで
		ELSE
			PRINT ゆっくりと
		ENDIF
		;奉仕精神と「PLAYERがサドか」で分岐
		IF (TALENT:PLAYER:抖Ｓ || ABL:侍奉精神 >= 5) && RAND:3 == 0
			PRINT 突き入れると、
		ELSEIF ABL:侍奉精神 >= 3 && RAND:2 == 0
			PRINT 挿入すると、
		ELSE
			PRINT 入れると、
		ENDIF
		;奉仕精神5かつ恋慕で追加
		SIF ABL:侍奉精神 >= 5 && TALENT:恋慕 && RAND:3 >= 1
			PRINT 舌を絡めさせながら、
	ENDIF
	;角がありますか？
	IF COND("ツノハンドル")
		PRINT 角を掴んで
	ELSE
		PRINT 頭を持って
	ENDIF
	SIF PREVCOM == 91 && COND("前Ｔ調教者") == PLAYER
		PRINT 、
	IF PREVCOM >= 60 && PREVCOM <= 79 && RAND:3 == 0
		PRINT 先ほど犯していた穴と比べながら
	;奉仕精神3以上か「PLAYERがサドか」で追加
	ELSEIF (TALENT:PLAYER:抖Ｓ || ABL:侍奉精神 >= 3) && RAND:3 == 0
		PRINT セックスする時のように
	ENDIF
	;奉仕精神と「PLAYERがサドか」で分岐
	IF (TALENT:PLAYER:抖Ｓ || ABL:侍奉精神 >= 5) && RAND:3 == 0
		PRINTFORMW 荒々しく口の中を陵辱した…
	ELSEIF TALENT:PLAYER:抖Ｓ || (ABL:侍奉精神 >= 3 && RAND:2 == 0)
		PRINTFORMW 激しく出し入れした…
	ELSE
		PRINTFORMW 腰を前後に動かした…
	ENDIF
ENDIF

;いけるようなら喉奥まで
IF CHECK("喉姦")
	TEQUIP:深喉 = PLAYER
	;フラグとTEQUIPが分かれているのは、射精後などにTEQUIP:深喉が０となっている時でも参照するため
	CALL SETFLAG, "ディープスロートフラグ", TARGET
ENDIF

@COM_ORDER_COM140, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:侍奉精神
IF ABL:侍奉精神
	LOCAL += ABL:侍奉精神 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:侍奉精神}({ABL:侍奉精神 * 4})
ENDIF
;ABL:受虐倾向
IF ABL:受虐倾向
	LOCAL += ABL:受虐倾向 * 3
	LOCALS = %LOCALS% + %ABLNAME:8%LV{ABL:受虐倾向}({ABL:受虐倾向 * 3})
ENDIF
;ABL:精液上瘾
IF ABL:精液上瘾
	LOCAL += ABL:精液上瘾 * 3
	LOCALS = %LOCALS% + %ABLNAME:11%LV{ABL:精液上瘾}({ABL:精液上瘾 * 3})
ENDIF

;恥じらい
IF TALENT:羞耻心强
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:35%(5)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;快感の否定
IF TALENT:否认快感
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF

;縄
IF TEQUIP:束缚
	LOCAL += 30
	LOCALS = %LOCALS% + 緊縛中(30)
ENDIF

;リングギャグ
IF TEQUIP:口塞 == 2
	LOCAL += 30
	LOCALS = %LOCALS% + リングギャグ(30)
ENDIF

LOCAL:1 = COM_ORDER_STAIN("Ｐ", "Ｍ")
;サドなら、汚れていても、かまわずに突っ込みます
SIF ABL:PLAYER:施虐倾向 >= 3
	LOCAL:1 /= 3

;汚れあり
IF LOCAL:1
	LOCAL -= LOCAL:1
	LOCALS = %LOCALS% - 汚れあり
	;汚臭鈍感
	SIF TALENT:污臭钝感 || TALENT:污臭敏感
		LOCALS = %LOCALS%、\@TALENT:污臭钝感 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({LOCAL:1})
ENDIF

;合計を表示(50以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 50

RETURN RESULT
