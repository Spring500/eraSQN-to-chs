@COM178
;二人に押し倒される
;一人はTARGET、もう一人はTARGET:1で管理する。もしもTARGET:1が未設定の場合には助手がTARGET:1となる

PRINTL 二人に押し倒される

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 178

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 二人に押し倒される

CALL SETFLAG, "Ｗ推到"

RETURN 1

@FLAG_COM178, ARG

@SOURCE_COM178, ARG
VARSET LOCAL

LOCAL:20 += 60
LOCAL:21 += 150

LOCAL:12 = 400

;ABL:侍奉精神をみる
SELECTCASE ABL:侍奉精神
CASE 0
	LOCAL:4 = 200
	LOCAL:5 = 50
CASE 1
	LOCAL:4 = 250
	LOCAL:5 = 200
CASE 2
	LOCAL:4 = 350
	LOCAL:5 = 550
CASE 3
	LOCAL:4 = 450
	LOCAL:5 = 900
CASE 4
	LOCAL:4 = 600
	LOCAL:5 = 1500
CASEELSE
	LOCAL:4 = 750
	LOCAL:5 = 2200
ENDSELECT

;ABL:Ｖ感觉をみる
SELECTCASE ABL:Ｖ感觉
CASE 0
	LOCAL:3 = 25
	TIMES LOCAL:4 , 0.50
CASE 1
	LOCAL:3 = 75
	TIMES LOCAL:4 , 0.80
CASE 2
	LOCAL:3 = 125
	TIMES LOCAL:4 , 1.00
CASE 3
	LOCAL:3 = 175
	TIMES LOCAL:4 , 1.20
CASE 4
	LOCAL:3 = 300
	TIMES LOCAL:4 , 1.50
CASEELSE
	LOCAL:3 = 425
	TIMES LOCAL:4 , 2.00
ENDSELECT

;PALAM:欲情
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES LOCAL:3 , 0.30
CASE IS < PALAMLV:2
	TIMES LOCAL:3 , 0.60
CASE IS < PALAMLV:3
CASE IS < PALAMLV:4
	TIMES LOCAL:3 , 1.50
CASE IS < PALAMLV:5
	TIMES LOCAL:3 , 1.80
CASEELSE
	TIMES LOCAL:3 , 2.20
ENDSELECT

;ABL:服从をみる
SELECTCASE ABL:服从
CASE 0
	TIMES LOCAL:3 , 0.60
	LOCAL:13 += 700
CASE 1
	TIMES LOCAL:3 , 0.80
	LOCAL:13 += 500
CASE 2
	LOCAL:13 += 300
CASE 3
	TIMES LOCAL:3 , 1.20
	LOCAL:13 += 100
CASE 4
	TIMES LOCAL:3 , 1.40
	LOCAL:13 += 30
CASEELSE
	TIMES LOCAL:3 , 1.60
ENDSELECT

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM178
IF TEQUIP:推到
	PRINTFORMW %CALLNAME:TARGET%の誘いに応じて、%CALLNAME:(TARGET:1)%が%CALLNAME:PLAYER%に歩み寄る…
	IF TARGET:1 == ASSI && TEQUIP:录像带
		PRINTFORMW どうやら、%CALLNAME:(TARGET:1)%は撮影を中止して一緒に楽しむことにしたようだ…
	ELSE
		PRINTFORMW どうやら、%CALLNAME:(TARGET:1)%も参加するようだ…
	ENDIF
ELSE
	PRINTFORMW 妖しく微笑みながら%CALLNAME:TARGET%と%CALLNAME:(TARGET:1)%が%CALLNAME:PLAYER%に歩み寄る…
	PRINTFORMW %CALLNAME:PLAYER%は二人がかりで押し倒されてしまった…
ENDIF
TEQUIP:录像带 = 0


;@EVENTCOMでSELECTCOMに応じて決定される。
;もがく、なすがままの時にはそれぞれのCOMF中で決定するため、@EVENTCOMでは呼ばない。
@SET_SELECTCOM_W
#DIM LCOUNT
;それぞれ、TARGET:0orTARGET:1が顔面騎乗、インサート、奉仕中かを見る
#DIM IS_FENDAMU, 2
#DIM IS_INSERT, 2
#DIM IS_SERVICE, 2
;二穴挿入に派生しやすい条件を満たしていたらＯＮ
#DIM IS_WPENE
;TARGET:1がコマンド実行可能かどうか
#DIM CANCOM, 160


CALL SET_FORBIDCOM, TARGET:1, SELECTCOM

SWAP TARGET, TARGET:1

VARSET CANCOM
FOR LCOUNT, 0, 110
	;チェック必要なコマンドは実は少ない
	SELECTCASE LCOUNT
	CASE 1, 9, 65, 75, 83, 90 TO 95, 97, 104
		TRYCALLFORM COM_ABLE{LCOUNT}
		CANCOM:LCOUNT = RESULT
	ENDSELECT
NEXT

SWAP TARGET, TARGET:1

VARSET IS_FENDAMU
VARSET IS_INSERT
VARSET IS_SERVICE
VARSET IS_WPENE

SELECTCASE SELECTCOM
CASE 1, 9
	IS_FENDAMU = 1
CASE 60 TO 79, 83
	IS_INSERT = 1
CASE 90 TO 95, 97, 104
	IS_SERVICE = 1
CASE 120
	SIF PREVCOM:1 == 126
		IS_WPENE = 126
CASE 126
	SIF PREVCOM:1 == 120
		IS_WPENE = 120
ENDSELECT

SELECTCASE PREVCOM:1
CASE 1, 9
	IS_FENDAMU:1 = 1
CASE 60 TO 79, 83
	IS_INSERT:1 = 1
CASE 90 TO 95, 97, 104
	IS_SERVICE:1 = 1
ENDSELECT

;インサート中なら、続行
IF IS_INSERT:1 && FORBIDCOM(TARGET:1, "セックス", "Ｐ下半身") == 0
	SELECTCOM:1 = PREVCOM:1
;泡踊りはＷ泡踊り
ELSEIF SELECTCOM == 94 && CANCOM:94
	SELECTCOM:1 = 94
;Ｗ騎乗中なら高確率で続行
ELSEIF RAND:3 && IS_INSERT && IS_FENDAMU:1
	SELECTCOM:1 = PREVCOM:1
ELSEIF RAND:3 && IS_FENDAMU && IS_INSERT:1
	SELECTCOM:1 = PREVCOM:1
;Ｗ騎乗を起き易く
ELSEIF IS_INSERT && CANCOM:1 + CANCOM:9 && RAND:3 == 0
	IF CANCOM:1 && RANDIF(1, CANCOM:9)
		SELECTCOM:1 = 1
	ELSE
		SELECTCOM:1 = 9
	ENDIF
ELSEIF IS_FENDAMU && CANCOM:65 + CANCOM:75 + CANCOM:83 && RAND:3 == 0
	IF CANCOM:83 && RANDIF(1, CANCOM:75, CANCOM:65)
		SELECTCOM:1 = 83
	ELSEIF CANCOM:65 && RANDIF(1, CANCOM:75)
		SELECTCOM:1 = 65
	ELSE
		SELECTCOM:1 = 75
	ENDIF
;同時奉仕中なら続行
ELSEIF IS_SERVICE && SELECTCOM == PREVCOM && PREVCOM:1 == SELECTCOM
	SELECTCOM:1 = PREVCOM:1
;同時奉仕を起き易く
ELSEIF IS_SERVICE && PREVCOM:1 == SELECTCOM
	SELECTCOM:1 = PREVCOM:1
ELSEIF IS_SERVICE && CANCOM:SELECTCOM && RAND:3 == 0
	SELECTCOM:1 = SELECTCOM
;二穴挿入中なら、続行
ELSEIF INSERT("二穴挿入", PLAYER)
	SELECTCOM:1 = PREVCOM:1
;二穴挿入になりやすくしてみる
ELSEIF IS_WPENE && RAND:2 == 0
	SELECTCOM:1 = IS_WPENE
ELSEIF FORBIDCOM(TARGET:1)
	SELECTCOM:1 = -1
ELSE
	;補佐役のABL:手〜ABL:レイプを決定
	CALL CALC_COUNTER_ABL, TARGET:1

	;レイプの後は連続レイプ
	IF (PREVCOM:1 == 120 || PREVCOM:1 == 126) && ABL:(TARGET:1):强奸 && RAND:4
		ABL:(TARGET:1):手 = 0
		ABL:(TARGET:1):口 = 0
		ABL:(TARGET:1):胸 = 0
		ABL:(TARGET:1):足 = 0
		ABL:(TARGET:1):颜面骑乘 = 0
		ABL:(TARGET:1):骑乘位 = 0
		ABL:(TARGET:1):亲吻 = 0
		ABL:(TARGET:1):虐肛？ = 0
		ABL:(TARGET:1):素股 = 0
	ENDIF

	SELECTCOM:1 = SET_COUNTERCOM(TARGET:1)
ENDIF

;禁止コマンド消去
CALL SET_FORBIDCOM, TARGET:1, -1


@COM_ORDER_COM178, ARG
VARSET LOCAL
LOCALS =

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:侍奉精神
IF ABL:侍奉精神
	LOCAL += ABL:侍奉精神 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:侍奉精神}({ABL:侍奉精神 * 4})
ENDIF

;強気
IF TALENT:强硬
	LOCAL += 10
	LOCALS = %LOCALS% + %TALENTNAME:18%(10)
ENDIF
;恥じらい
IF TALENT:羞耻心强
	LOCAL -= 10
	LOCALS = %LOCALS% - %TALENTNAME:35%(10)
ENDIF
;快感の否定
IF TALENT:否认快感
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:71%(5)
ENDIF
;サド
IF TALENT:抖Ｓ
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:83%(5)
ENDIF

;処女
IF TALENT:处女
	LOCAL -= 20
	LOCALS = %LOCALS% - %TALENTNAME:0%(20)
ELSEIF EXP:Ｖ经验 < EXPLV:2
	LOCAL -= 5
	LOCALS = %LOCALS% - %EXPNAME:0%不足(5)
ENDIF

;媚薬
IF TEQUIP:媚药
	LOCAL += 6
	LOCALS = %LOCALS% + %ITEMNAME:31%(6)
ENDIF

;合計を表示(70以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 70

RETURN RESULT
