@COM4
;フェラする
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM4, ARG
SIF CHECK_CLO("Ｖ露出") == 0
	CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
;対象のＰ⇔調教者の口の汚れが移動
CALL EQ_STAIN, TARGET, PLAYER, 2, 0
SIF CHECK_COM("お掃除フェラ", 4)
	CALL SETFLAG, "お掃除フェラ", PLAYER

@SOURCE_COM4, ARG
VARSET LOCAL

LOCAL:20 += 5
LOCAL:21 += 50

LOCAL:12 = 220
LOCAL:14 = 50

;オトコかふたなり
IF PENIS(TARGET)
	;ABL:Ｃ感觉をみる
	LOCAL:0 = CALCF("Ｃ刺激", 1)
	;調教者の舌使いをみる
	LOCAL:0 = SOURCE_EXABL(LOCAL:0, 30, PLAYER)
ENDIF

SIF CHECK_COM("お掃除フェラ", 4)
	TIMES LOCAL:0, 0.10

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM4
;押し倒されているとき(同時含む)
IF TEQUIP:押し倒し
	PRINTFORM %CALLNAME:TARGET%は
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ押し倒し中")%
	ELSEIF SELECTCOM == PREVCOM && TALENT:MASTER:男性 == 0
		PRINTFORM 息苦しさに涙を浮かべる
	ELSE
		PRINTFORM 身動きの取れない
	ENDIF
	IF SELECTCOM == PREVCOM
		PRINTFORM %CALLNAME:PLAYER%の
		IF (ABL:PLAYER:技巧 >= 4 || TALENT:抖Ｓ) && RAND:2 == 0
			PRINT 喉を
		ELSE
			PRINT 口を
		ENDIF
		SIF ABL:施虐倾向 >= 3
			PRINT 容赦なく
		PRINTFORMW 犯し続けている…
	ELSE
		PRINTFORM %CALLNAME:PLAYER%の口の中に、
		IF CHECK_STAIN(STAIN:Ｐ, "Ｖ", "Ａ") && COND("前Ｔ調教者") == PLAYER
			IF PREVPLAY("Ａインサート", PLAYER)
				;前回中出し
				IF PREVEX("射精")
					PRINT 腸内に射精したばかりの
				ELSE
					PRINT アナルから抜いたばかりの
				ENDIF
			ELSEIF PREVPLAY("Ｖインサート", PLAYER)
				;前回中出し
				IF PREVEX("射精")
					PRINT 子宮を精液で汚したばかりの
				ELSE
					PRINT 膣から引き抜いたばかりの
				ENDIF
			;まだ無い…っていうか作るのだろうかこれ
			ELSEIF PREVPLAY("尿道インサート", PLAYER)
				;前回中出し
				IF PREVEX("射精")
					PRINT 膀胱を精液で汚したばかりの
				ELSEIF PENIS(PLAYER)
					PRINT ペニスから引き抜いたばかりの
				ELSE
					PRINT 尿道から引き抜いたばかりの
				ENDIF
			ELSEIF PREVPLAY("尿道インサート")
				;前回中出しされた
				IF PREVEX("尿道内射精")
					PRINT 精液を注がれたばかりの
				ELSE
					PRINT 尿道で愉しんでいたばかりの
				ENDIF
			ELSEIF CHECK_STAIN(STAIN:Ｐ, "Ａ")
				PRINT べっとりと
			ELSEIF CHECK_STAIN(STAIN:Ｐ, "Ｖ")
				PRINT 愛液で
			ENDIF
			PRINTFORM 汚れた
		ELSE
			PRINTFORM %TEXTS("ペニス：視覚", TARGET)%
		ENDIF
		PRINT ペニスを
		IF ABL:施虐倾向 >= 5 && RAND:3 == 0
			PRINT 一気に
		ELSEIF TALENT:抖Ｓ || (ABL:施虐倾向 >= 4 && RAND:2 == 0)
			PRINT 奥まで
		ELSEIF RAND:2 == 0
			PRINT ゆっくりと
		ENDIF
		IF ABL:施虐倾向 >= 5 && RAND:3 == 0
			PRINT 突き入れると、
		ELSEIF ABL:施虐倾向 >= 3 && RAND:2 == 0
			PRINT 挿入すると、
		ELSE
			PRINT 咥えさせると、
		ENDIF
		PRINT 頭を持って
		SIF ABL:施虐倾向 >= 3 && RAND:3 == 0
			PRINT セックスする時のように
		IF ABL:施虐倾向 >= 5 && RAND:3 == 0
			PRINTFORMW 荒々しく口の中を陵辱した…
		ELSEIF TALENT:抖Ｓ || (ABL:施虐倾向 >= 3 && RAND:2 == 0)
			PRINTFORMW 激しく出し入れした…
		ELSE
			PRINTFORMW 腰を前後に動かした…
		ENDIF
	ENDIF
ELSE
	PRINTFORM %CALLNAME:PLAYER%は
	;前回尿道で楽しんでいた
	IF PREVPLAY("尿道インサート")
		;前回中出しされた
		IF PREVEX("尿道内射精")
			PRINT 精液を注ぎこんだばかりで白い涎を垂らしているペニス
		ELSE
			PRINT 自分の肉棒を引き抜いたばかりで尿道が開き気味のペニス
		ENDIF
	;前回尿道指挿入れ
	ELSEIF PREVCOM == 15
		IF RAND:2
			PRINTFORM 指を挿し入れて虐めたばかりのペニス
		ELSE
			PRINTFORM 指を出し入れされて口を開けているペニス
		ENDIF
	;前回ブジーで弄られた
	ELSEIF PREVCOM == 151
		PRINTFORM 金属の棒%TEXTR("を出し入れされ", "で掻き回され", "で拡張させられ", "でたっぷり虐められ")%て口を開けているペニス
	;チンカスフェラ
	ELSEIF CHECK_COM("恥垢お掃除フェラ")
		PRINTFORM 、%CALLNAME:TARGET%のペニス
		PRINTFORM %TEXTR("の恥垢", "にこびりつくチンカス", "に張り付いた垢", "のチーズの様な垢")%
		PRINTFORMW までも%TEXTR("徹底的に綺麗に", "、しっかりと味わいつつ口で丁寧にお掃除")%した…
		RETURN 1
	;汚れあり
	ELSEIF CHECK_STAIN(STAIN:Ｐ, "Ｖ", "Ａ") && COND("前Ｔ調教者") > 0
		IF PREVPLAY("インサート", COND("前Ｔ調教者") )
			LOCALS = \@ COND("前Ｔ調教者") == PLAYER ? 自分 # %CALLNAME:COND("前Ｔ調教者")% \@の
			IF PREVPLAY("Ａインサート", COND("前Ｔ調教者") )
				IF PREVEX("射精")
					LOCALS = %LOCALS%腸内に射精した
				ELSE
					LOCALS = %LOCALS%アナルから抜かれた
				ENDIF
			ELSEIF PREVPLAY("Ｖインサート", COND("前Ｔ調教者") )
				IF PREVEX("射精")
					LOCALS = %LOCALS%子宮を精液で汚した
				ELSE
					LOCALS = %LOCALS%膣から引き抜かれた
				ENDIF
			ENDIF
			IF PREVEX("射精")
				IF TALENT:相思相爱 && RAND:3 == 0
					LOCALS = %LOCALS%ばかりの愛しい
				ELSEIF SIZE("ペニス", TARGET) >= 2 && RAND:2
					LOCALS = %LOCALS%ばかりの\@ ABL:施虐倾向 >= 3 ? 凶悪な # おっきな \@
				ELSEIF SIZE("ペニス", TARGET) < 2 && AGE(TARGET) <= 2 && RAND:2
					LOCALS = %LOCALS%ばかりで可愛くなってしまった
				ELSE
					LOCALS = %LOCALS%ばかりで半勃起状態の
				ENDIF
			ELSEIF RAND:3 == 0
				LOCALS = %LOCALS%ばかりで、ほかほかと湯気の立ちそうな状態の
			ELSEIF RAND:2
				LOCALS = %LOCALS%ばかりの、凄い%TEXTS("匂い")%をさせている
			ELSE
				LOCALS = %LOCALS%ばかりの
			ENDIF
		ENDIF
		IF LOCALS == ""
			IF CHECK_STAIN(STAIN:Ｐ, "Ａ")
				LOCALS = べっとりと汚れた
			ELSEIF CHECK_STAIN(STAIN:Ｐ, "Ｖ")
				LOCALS = 愛液で汚れた
			ENDIF
		ENDIF
		PRINTFORM %LOCALS%ペニス
	ELSE
		PRINTFORM %CALLNAME:TARGET%の%TEXTS("ペニス：視覚", TARGET)%ペニス
	ENDIF
	IF CHECK_COM("お掃除フェラ")
		IF PREVPLAY("尿道インサート") && PREVEX("尿道内射精")
			PRINTFORMW を%TEXTR("啜り上げて精液をしっかり吸いだ", "舐めしゃぶって綺麗に", "口で念入りにお掃除")%した…
		ELSE
			PRINTFORMW を%TEXTR("舐めしゃぶって綺麗に", "口で念入りにお掃除")%した…
		ENDIF
	ELSEIF TEQUIP:高潮管理
		PRINTFORMW を舌で焦らすように責め立てた…
	ELSEIF ABL:PLAYER:受虐倾向 >= 3 && CHECK_STAIN(STAIN:Ｐ, "Ｖ", "Ａ") && RAND:2
		PRINTFORMW を念入りにお掃除しながら存分に味わった…
		STAIN:PLAYER:Ｍ |= STAIN:Ｐ
		CALL RESET_STAIN_EX, "Ｐ", TARGET
	ELSEIF ABL:尿道扩张 >= 0 && RAND:3 == 0
		PRINTFORMW に舌を%TEXTR("差し込んで", "出し入れして")%攻め立てた…
	ELSEIF RAND:2
		PRINTFORMW を咥えて舐めまわした…
	ELSE
		PRINTFORMW を咥えて激しく扱きたてた…
	ENDIF
ENDIF
