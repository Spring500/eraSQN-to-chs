@COM71
;アナルセックス
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM71, ARG
CALL FLAG_INSERTION, "Ａインサート", ARG, 110

@SOURCE_COM71, ARG
CALL SOURCE_INSERTION, "Ａインサート", ARG, 110

VARSET LOCAL

LOCAL:12 = 2000
;動物耳
SIF TALENT:兽耳
	LOCAL:12 = 1500

;ABL:Ａ感觉をみる
IF ABL:Ａ感觉 == 0
	LOCAL:3 = 10
ELSEIF ABL:Ａ感觉 == 1
	LOCAL:3 = 30
ELSEIF ABL:Ａ感觉 == 2
	LOCAL:3 = 100
ELSEIF ABL:Ａ感觉 == 3
	LOCAL:3 = 200
ELSEIF ABL:Ａ感觉 == 4
	LOCAL:3 = 450
ELSE
	LOCAL:3 = 750
ENDIF

;ABL:Ａ感觉をみる
SELECTCASE ABL:Ａ感觉
CASE 0
	LOCAL:3 = 10
CASE 1
	LOCAL:3 = 30
CASE 2
	LOCAL:3 = 100
CASE 3
	LOCAL:3 = 200
CASE 4
	LOCAL:3 = 450
CASEELSE
	LOCAL:3 = 750
ENDSELECT

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES LOCAL:3 , 0.30
CASE IS < PALAMLV:2
	TIMES LOCAL:3 , 0.60
CASE IS < PALAMLV:3
	TIMES LOCAL:3 , 1.00
CASE IS < PALAMLV:4
	TIMES LOCAL:3 , 1.50
CASE IS >= PALAMLV:4
	TIMES LOCAL:3 , 1.80
ENDSELECT

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM71
IF CHECK_COM("Ａ二本挿入")
	IF INSERT("Ａ二本挿入")
		PRINTFORMW %CALLNAME:TARGET%のアナルを同時に犯している…
	ELSE
		IF INSERT("背面座位")
			PRINTFORM 結合部が丸見えの体勢で%CALLNAME:(PLAYER:1)%に犯されている%CALLNAME:TARGET%の股を押し開き、
		ELSEIF INSERT("騎乗位")
			PRINTFORM %CALLNAME:(PLAYER:1)%に跨って腰を振っている%CALLNAME:TARGET%に前傾姿勢をとらせて、
		ELSE
			PRINTFORM %CALLNAME:(PLAYER:1)%に背面座位の体勢をとらせて、犯されている%CALLNAME:TARGET%の股を押し開き、
		ENDIF
		PRINTFORMW 余裕のありそうなアナルに%NAMES("ペニス", PLAYER)%を捻じ込んだ…
	ENDIF
ELSEIF CHECK_COM("二穴挿入")
	CALL TRAIN_MESSAGE_COM60
ELSEIF CHECK_COM("３Ｐ奉仕")
	CALL TRAIN_MESSAGE_3P
ELSEIF INSERT("Ａインサート") && SELECTCOM == PREVCOM && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%の腰をしっかりと掴み、
	SIF (PENIS(PLAYER)) && STATE("ハードスキン", PLAYER)
		PRINT ガチガチのペニスに
	PRINTFORMW 体重を乗せて激しくアナルを犯した…
ELSEIF INSERT("Ａインサート") && COND("前Ｔ調教者") == PLAYER
	IF INSERT("正常位") || INSERT("屈曲位")
		PRINTFORM 繋がりあったまま%CALLNAME:TARGET%を四つん這いにして腰を高く上げさせると、
	ELSEIF INSERT("後背位")
		IF PENIS(PLAYER)
			PRINTFORM %CALLNAME:TARGET%の腸内の感触を味わいながら、
		ELSE
			PRINTFORM %CALLNAME:TARGET%のアナルから抜かずに、
		ENDIF
	ELSEIF INSERT("対面座位")
		PRINTFORM アナルから抜き去ることなく%CALLNAME:TARGET%の後ろに回り、
	ELSEIF INSERT("背面座位")
		PRINTFORM %CALLNAME:TARGET%の身体を前に倒して四つん這いにし、
	ELSEIF INSERT("騎乗位")
		PRINTFORM アナルから抜き去ることなく%CALLNAME:TARGET%の後ろに回り、
	ENDIF
	IF RAND:2
		PRINTFORM %TEXTS("お尻：感触")%を鷲掴んで
	ELSE
		PRINTFORM %TEXTS("お尻：感触")%を引き寄せて
	ENDIF
	PRINTW 再び腰を動かし始めた…
ELSE
	;前回と今回の調教者が同じで、抜かないフラグがある場合
	IF INSERT("Ｖインサート") && COND("前Ｔ調教者") == PLAYER
		IF INSERT("後背位")
			PRINTFORM %CALLNAME:TARGET%のアソコの上で息づいている
		ELSEIF INSERT("背面座位")
			PRINTFORM %CALLNAME:TARGET%の身体を前に倒して四つん這いにし、今度は
		ELSE
			PRINTFORM %CALLNAME:TARGET%を四つん這いにして腰を高く上げさせ、よく見えるようになった
		ENDIF
	ELSEIF COND("前Ｔから対象のみ変更") && PREVCOM:1 >= 60 && PREVCOM:1 <= 79
		CALL TEXT, "追加地の文：穴から穴へ"
	;アナルが露出していないで、パンツをずらしていないなら自動的にずらす
	ELSEIF !CHECK_CLO("Ａ露出") && CHECK_CLO("Ａ露出可")
		SIF TEQUIP:束缚
			PRINTFORM 拘束されている
		PRINTFORM %CALLNAME:TARGET%の
		CALL SETFLAG, "ボトムを露出：テキスト有り", TARGET
	;アナルセックスに慣れている、かつ欲情LV4以上で縛られてないと誘ってきます
	ELSEIF CHECK("Ａインサート") && PALAM:欲情 >= PALAMLV:4 && TEQUIP:束缚 == 0 && RAND:2 == 0
		IF PREVCOM == 13
			PRINT アソコを指で広げて
		ELSEIF PREVCOM == 14
			PRINT お尻を指で押し広げて
		;淫乱
		ELSEIF (TALENT:淫乱 || ABL:Ａ感觉 >= 3) && RAND:2 == 0
			IF RAND:2
				IF TALENT:小臀
					PRINT 小さな
				ELSEIF TALENT:大臀
					PRINT 大きな
				ENDIF
				PRINT お尻を割り開いて見せ付けながら
			ELSE
				PRINT 大事な場所を見せ付けながら
			ENDIF
		ELSEIF RAND:2 == 0
			PRINT 恥ずかしい所が丸見えの格好で
		ELSE
			PRINT 股間をさらけ出した格好で
		ENDIF
		;小悪魔
		IF TALENT:小恶魔 && RAND:3
			PRINT ニヤニヤしている
		;(反抗的でなくて淫乱) or(反抗的かつ恋慕)
		ELSEIF  ( (TALENT:淫乱 && TALENT:叛逆 == 0) || (TALENT:叛逆 && TALENT:恋慕) ) && RAND:3
			SIF TALENT:小恶魔 && RAND:2 == 0
				PRINT 、生意気にも
			PRINT 誘っている
		;反抗的かつ淫乱
		ELSEIF TALENT:叛逆 && TALENT:淫乱
			PRINT そっぽを向きながら顔を赤くしている
		;恥じらい
		ELSEIF TALENT:羞耻心强
			PRINT 恥ずかしそうにしている
		ELSEIF RAND:2 == 0
			PRINT 微笑んでいる
		ELSE
			PRINT 横たわっている
		ENDIF
		PRINTFORM %CALLNAME:TARGET%の
	ELSE
		;縛ってますか？
		IF TEQUIP:束缚
			PRINTFORM 拘束されている%CALLNAME:TARGET%の
		;ラミアですか？
		ELSEIF TALENT:拉弥亚
			SIF CHECK("Ａインサート") && RAND:2 == 0
				PRINT 擦り寄ってきた
			PRINTFORM %CALLNAME:TARGET%の
		;アナルセックスに慣れているなら誘ってきます
		ELSEIF CHECK("Ａインサート") && RAND:2 == 0
			SIF TALENT:羞耻心强
				PRINT 恥ずかしがりながら
			PRINT 四つん這いになっ
			;淫乱かつ欲情4以上
			IF TALENT:淫乱 && PALAM:欲情 >= PALAMLV:4 && RAND:2 == 0
				PRINT て、お尻を振りながら誘っている
			ELSE
				PRINT た
			ENDIF
			PRINTFORM %CALLNAME:TARGET%の
		ELSE
			IF TALENT:叛逆 && ABL:Ａ感觉 <= 2 && TALENT:恋慕 == 0
				PRINT 嫌がる
			ELSEIF TALENT:羞耻心强
				PRINT 恥ずかしがる
			ENDIF
			PRINTFORM %CALLNAME:TARGET%を四つん這いにさせて
			SIF TALENT:羞耻心强 || (TALENT:叛逆 && ABL:Ａ感觉 <= 2 && TALENT:恋慕 == 0)
				PRINT 、
		ENDIF
		PRINTFORM %TEXTS("アナル")%
	ENDIF
	IF RAND:2 == 0
		PRINT お尻の穴に
	ELSE
		PRINT アナルに
	ENDIF
	IF COND("前Ｔから対象のみ変更") && PREVCOM:1 >= 60 && PREVCOM:1 <= 79
	ELSE
		PRINTFORM %NAMES("ペニス", PLAYER)%を
	ENDIF
	;PLAYERがサドかつA経験20以下orＡ潤1LV未満
	IF (TALENT:PLAYER:抖Ｓ && EXP:Ａ经验 <= 20) || PALAM:Ａ润 < PALAMLV:1
		PRINT 力任せに
	;PLAYERがサドorアナルセックスに慣れている
	ELSEIF (CHECK("Ａインサート") || TALENT:PLAYER:抖Ｓ) && RAND:2 == 0
		PRINT 一気に
	ELSEIF RAND:2 == 0 || EXP:Ａ经验 <= EXPLV:3
		PRINT 慎重に
	ENDIF
	;PLAYERがサドorA経験20以上or恋慕かつA感覚Lv3以上
	SIF TALENT:PLAYER:抖Ｓ || MIN(EXP:Ａ经验, EXP:Ａ性交经验) >= 20 || (TALENT:恋慕 && ABL:Ａ感觉 >= 3)
		PRINT 根元まで
	;PLAYERがサドorＡ潤1LV未満
	IF TALENT:PLAYER:抖Ｓ || PALAM:Ａ润 < PALAMLV:1
		PRINT 押し込み、
	ELSE
		PRINT 挿入し、
	ENDIF
	IF TALENT:怀孕 && TALENT:PLAYER:抖Ｓ
		PRINT 妊娠していることなど構わずに
	ELSEIF TALENT:怀孕 && RAND:2 == 0
		PRINT 大きなお腹をさすりながら
	ELSEIF TALENT:怀孕
		PRINT 負担がかからないよう注意しながら
	ELSEIF RAND:2
		PRINT お尻を
		IF RAND:3 == 0
			PRINT 撫でながら
		ELSEIF RAND:2
			PRINT 鷲掴んで
		ELSE
			PRINT 揉みしだきながら
		ENDIF
	;PLAYERがサドorA経験20以上or恋慕かつA感覚Lv3以上
	ELSEIF TALENT:娇小体型 && (TALENT:PLAYER:抖Ｓ || MIN(EXP:Ａ经验, EXP:Ａ性交经验) >= 20 || (TALENT:恋慕 && ABL:Ａ感觉 >= 3) )
		PRINT おなかを掴んで
	ELSEIF TALENT:PLAYER:抖Ｓ || MIN(EXP:Ａ经验, EXP:Ａ性交经验) >= 20 || (TALENT:恋慕 && ABL:Ａ感觉 >= 3)
		PRINT くびれを掴んで
	ENDIF
	;PLAYERがサドor妊娠していない、かつA経験3以上or恋慕かつA感覚Lv4以上
	IF (TALENT:PLAYER:抖Ｓ || TALENT:怀孕 == 0) && (MIN(EXP:Ａ经验, EXP:Ａ性交经验) >= 20 || (TALENT:恋慕 && ABL:Ａ感觉 >= 3) ) && RAND:3 == 0
		PRINT 荒々しく
	ELSEIF (TALENT:PLAYER:抖Ｓ || MIN(EXP:Ａ经验, EXP:Ａ性交经验) >= 20 || ABL:Ａ感觉 >= 3 || ABL:PLAYER:腰技) && SIZE("ペニス", PLAYER) && RAND:2 == 0
		SIF ABL:PLAYER:腰技 && RAND:2
			PRINT 入り口から奥まで責める
		PRINT 大きなストロークで
	ELSEIF MAX(EXP:Ａ经验, EXP:Ａ性交经验) < 50
		IF RAND:2 == 0
			PRINT 浅めに
		ELSE
			PRINT ゆっくりと
		ENDIF
	ELSE
		IF RAND:2 == 0
			PRINT 腸壁を
			IF RAND:2 == 0 && TALENT:男性 == 0
				PRINT 子宮に向かって押し込みながら
			ELSE
				PRINT ゴシゴシ擦りながら
			ENDIF
		ELSE
			PRINT 激しく
		ENDIF
	ENDIF
	;PLAYERがサドorA経験20以上orA感覚Lv3以上
	IF (TALENT:PLAYER:抖Ｓ || MIN(EXP:Ａ经验, EXP:Ａ性交经验) >= 20 || ABL:Ａ感觉 >= 3) && RAND:3 == 0
		PRINTFORMW 腰を打ちつけた…
	ELSEIF RAND:3 == 0
		PRINTFORMW 出し入れした…
	ELSEIF RAND:2 == 0
		PRINTFORMW 腰を前後させた…
	ELSE
		PRINTFORMW 腰を振った…
	ENDIF
ENDIF
