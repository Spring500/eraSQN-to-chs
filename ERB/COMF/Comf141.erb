@COM141
;フィストファック
PRINTL フィストファック

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = フィストファック

RETURN 1

@FLAG_COM141, ARG
CALL FLAG_INSERTION, "Ｖフィスト", ARG, 100
;アソコを露出させる
SIF CHECK_CLO("Ｖ露出") == 0
	CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET

@SOURCE_COM141, ARG
CALL SOURCE_INSERTION, "Ｖフィスト", ARG, 100
CALL SOURCE_SMPLAY, "フィスト", ARG

@TRAIN_MESSAGE_COM141
IF SELECTCOM == PREVCOM || SELECTCOM == PREVCOM:2
	PRINTFORM %CALLNAME:PLAYER%は
	IF COND("前Ｔ調教者") == PLAYER
		PRINTFORMW %CALLNAME:TARGET%の膣を執拗に\@ CHECK_COM("二本フィスト") ? 二本の腕 # 拳 \@で攪拌し続けた…
	ELSE
		IF PLAYER == MASTER
			PRINTFORM %CALLNAME:ASSI%
		ELSE
			PRINTFORM %CALLNAME:MASTER%
		ENDIF
		PRINTFORMW に続いて、%CALLNAME:TARGET%の膣を拳でズボズボと掻き回した…
	ENDIF
	RETURN 1
ENDIF
IF ABL:PLAYER:施虐倾向 >= 3 && ABL:Ｖ扩张 >= SIZE("二本フィスト") - 1
	PRINTFORML %CALLNAME:TARGET%の\@ ABL:Ｖ名器度 <= 20 ? ユルい # 拡張された \@膣なら、両腕同時でもいけそうです。
	PRINTFORML どうしますか？
	PRINTFORML [ 0] 両腕を捻じ込む
	PRINTFORML [ 1] 片手で楽しむ
	CALL INPUT_SELECT, 2
	SIF RESULT == 0
		CALL SETFLAG, "二本フィスト", PLAYER
ENDIF
;膣の拡張具合の余裕 LOCAL == 3 でぴったり　2以下はかなり厳しい
LOCAL = 3 + ABL:Ｖ扩张 - SIZE("フィスト")
LOCAL:1 = RAND:3

PRINTFORM %CALLNAME:PLAYER%は
;前回アナルフィスト
IF PREVCOM == 142 && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%のアナルを犯していた腕を引き抜いて、今度は
	IF TALENT:处女
		PRINT 再生された膜で塞がれた
	ELSEIF LOCAL >= 4 && RAND:2 == 0
		PRINT 緩められた
	ELSEIF PALAM:Ｖ润 < 500
		PRINT 濡れていない
	ELSEIF PALAM:Ｖ润 >= 10000 && CHECK("Ｖフィスト")
		PRINT 涎を垂らしている
	ENDIF
	PRINT 膣穴に
ELSE
	;前回指挿入れ
	IF PREVCOM == 8 && COND("前Ｔ調教者") == PLAYER
		PRINTFORM %CALLNAME:TARGET%の膣から指を引き抜いて愛液を手に塗り広げると、
	ELSE
		;シックスナイン
		IF TEQUIP:６９式
			PRINTFORM %TEXTS("６９")%
		ELSEIF TEQUIP:束缚
			PRINT 大きく股を開いた格好で縛られている
		ELSEIF (ABL:受虐倾向 >= 3 || (LOCAL >= 2 && ABL:Ｖ感觉 >= 4) ) && LOCAL:1
			IF TALENT:好色 + TALENT:淫乱 && LOCAL >= 3 && ABL:Ｖ感觉 >= 3 && RAND:3 == 0
				PRINT アソコを指で拡げて
			ELSEIF ABL:服从 + ABL:欲望 >= 6 && RAND:3 == 0
				PRINT 両腕で脚を抱えて
			ELSEIF RAND:2 == 0
				PRINT 股を広げて
			ELSE
				PRINT 腰を突き出して
			ENDIF
			IF TALENT:淫乱 || LOCAL >= 3
				PRINT 誘っている
			ELSEIF RAND:2 == 0
				PRINT 待ち構えている
			ELSE
				PRINT 準備している
			ENDIF
		ENDIF
		PRINTFORM %CALLNAME:TARGET%の
		SIF (ABL:受虐倾向 < 3 || LOCAL:1 == 0) && TEQUIP:束缚 + TEQUIP:６９式 == 0 && ABL:服从 + ABL:欲望 < 6
			PRINT 脚を大きく開かせると、
	ENDIF
	IF TALENT:处女
		PRINT 処女膜に
	ELSE
		IF LOCAL >= 4 && RAND:2 == 0
			PRINT 緩められた
		ELSEIF PALAM:Ｖ润 < 500
			PRINT 濡れていない
		ELSEIF PALAM:Ｖ润 >= 10000
			PRINT ビショビショの
		ENDIF
		PRINT 膣口に
	ENDIF

	;拡張ＬＶがかなりあるなら握り拳ルート
	IF LOCAL >= 4 && RAND:2
		PRINTFORM \@ CHECK_COM("二本フィスト") ? 両手の # \@握り拳
		;拳フラグ
		SETBIT TFLAG:16, 0
		LOCAL -= 1
	ELSEIF CHECK_COM("二本フィスト")
		PRINTFORM 合掌させた指先
	ELSEIF LOCAL >= 2 && RAND:2 == 0
		PRINTFORM 揃えた指先
	ELSE
		PRINTFORM すぼめた指先
	ENDIF
	PRINT を押し当て、
ENDIF
IF TALENT:恋慕 && TEQUIP:６９式 == 0 && RAND:3 == 0
	PRINT 顔を見つめながら
ELSEIF TALENT:PLAYER:抖Ｓ && RAND:3 == 0
	PRINT 力任せに
ELSEIF LOCAL >= 4 && RAND:3 == 0
	PRINT 抵抗を感じることなく
ELSEIF LOCAL >= 3 && RAND:3 == 0
	PRINT 一気に
ELSEIF LOCAL >= 2 && RAND:2 == 0
	PRINT 捻りながら
ELSEIF (LOCAL >= 2 && TFLAG:16) || LOCAL <= 2
	PRINT 慎重に
ELSE
	PRINT ゆっくりと
ENDIF
IF LOCAL >= 4 && CHECK_COM("二本フィスト") == 0 && RAND:2
	IF LOCAL >= 7 && RAND:2 == 0
		PRINT 肘まで
	ELSEIF LOCAL >= 5 && RAND:2 == 0
		PRINT 腕の半ばまで
	ELSEIF TALENT:娇小体型 && RAND:2 == 0
		PRINT お腹がぽっこりするまで
	ELSE
		PRINT 下腹が微かに膨らむまで
	ENDIF
ELSEIF LOCAL >= 4 || (LOCAL >= 3 && RAND:2)
	IF RAND:2 == 0
		PRINT 子宮口に当たるまで
	ELSE
		PRINT 膣の最奥まで
	ENDIF
ELSEIF LOCAL <= 2
	PRINT 手の甲まで
ELSE
	PRINT 手首まで
ENDIF
IF RAND:3 == 0
	PRINT 埋め込
ELSEIF RAND:2 == 0
	PRINT 捻じ込
ELSE
	PRINT 押し込
ENDIF
IF TFLAG:16 == 0 && LOCAL >= 3 && RAND:2
	PRINTFORMW むと、中で\@ CHECK_COM("二本フィスト") ? 両手を握り合わせた。 # 握りこぶしを作った。 \@
	SETBIT TFLAG:16, 0
	LOCAL -= 1
ELSE
	PRINTFORMW んだ。
ENDIF

;1bit膣口、 2bit膣道、3bit膣奥を攻めるイメージ
SETBIT TFLAG:16, 1 + RAND:3

IF (TALENT:PLAYER:抖Ｓ || LOCAL >= 3) && GETBIT(TFLAG:16, 3)
	IF (TALENT:PLAYER:抖Ｓ || LOCAL >= 4) && RAND:3 == 0
		PRINT 容赦なく
	ELSEIF RAND:2
		PRINT 勢い良く
	ELSE
		PRINT ズボズボと
	ENDIF
	IF LOCAL >= 4 && RAND:2 == 0
		PRINT 最奥に
	ELSEIF LOCAL >= 3
		PRINT 子宮に
	ELSE
		PRINT 敏感な場所に
	ENDIF
	PRINT 叩き込まれる
ELSEIF (TALENT:PLAYER:抖Ｓ || LOCAL >= 2) && GETBIT(TFLAG:16, 1)
	PRINT ズボズボと
	SIF RAND:2 == 0
		PRINT 激しく
	PRINT 出し入れされる
ELSE
	IF ABL:Ｖ感觉 >= 3 && GETBIT(TFLAG:16, 2)
		PRINT Ｇスポットも刺激しながら
	ELSEIF LOCAL >= 3  && RAND:2 == 0
		PRINT 中を探るように
	ELSE
		PRINT 中を拡げるように
	ENDIF
	PRINT 動かされる
ENDIF
IF GETBIT(TFLAG:16, 0)
	PRINTFORM \@ CHECK_COM("二本フィスト") ? 束ねられた # \@握り拳
ELSEIF CHECK_COM("二本フィスト")
	PRINTFORM 両腕
ELSEIF LOCAL <= 1
	PRINTFORM 手
ELSE
	SIF RAND:2 && TALENT:PLAYER:男性 && COND("男の娘", PLAYER) == 0
		PRINTFORM \@ RAND:2 ? 男の # 太い \@
	PRINTFORM 腕
ENDIF
;膣口描写
IF (TALENT:PLAYER:抖Ｓ || LOCAL >= 2) && GETBIT(TFLAG:16, 1)
	PRINT に
;受け入れられる
ELSEIF LOCAL >= 4 || TALENT:史莱姆
	PRINT を
;まだまだ厳しい
ELSE
	PRINT に
ENDIF
;前回アナルフィストならＡとＶの大きさ比較
IF PREVCOM == 142 && ABL:Ｖ扩张 > ABL:Ａ扩张 && CHOSECOM(SELECTCOM) == 0 && LOCAL >= 4
	IF (TALENT:PLAYER:抖Ｓ || LOCAL >= 2) && GETBIT(TFLAG:16, 1)
		PRINT 、肛門
	ELSE
		IF RAND:2
			PRINT 、後ろの穴
		ELSE
			PRINT 、アナル
		ENDIF
	ENDIF
	IF ABL:Ｖ扩张 > ABL:Ａ扩张 + 1
		PRINTFORM とは比べものにならない程に拡張された
	ELSE
		PRINTFORM よりもずっと柔らかな
	ENDIF
ENDIF
SIF TALENT:娇小体型 && RAND:3 == 0
	PRINT 幼い
IF (TALENT:PLAYER:抖Ｓ || LOCAL >= 2) && GETBIT(TFLAG:16, 1)
	PRINT 膣口は
ELSE
	PRINT 膣は
ENDIF
IF LOCAL >= 4 || TALENT:史莱姆
	IF (TALENT:PLAYER:抖Ｓ || LOCAL >= 2) && GETBIT(TFLAG:16, 1)
		IF RAND:2 == 0
			PRINT ぽっかりと
		ELSE
			PRINT 驚くほど
		ENDIF
		PRINT 拡げられ、その
	ELSE
		IF GETBIT(TFLAG:16, 3) && RAND:3 == 0
			PRINT 深々と
		ELSEIF RAND:2 == 0
			PRINT 柔らかく
		ELSE
			PRINT 楽々
		ENDIF
		PRINT 受け止め、その
	ENDIF
ELSEIF (TALENT:PLAYER:抖Ｓ || LOCAL >= 2) && GETBIT(TFLAG:16, 1) && RAND:2 == 0
	PRINT 捲りあげられ、その
ELSEIF (TALENT:PLAYER:抖Ｓ || LOCAL >= 3) && GETBIT(TFLAG:16, 3) && RAND:2 == 0
	PRINT パンパンに拡げられ、その
ELSEIF LOCAL >= 2 || ABL:Ｖ感觉 >= 5 || PALAM:Ｖ润 >= PALAMLV:4 || RAND:2 == 0
	PRINT 裂けそうなほどに押し広げられ、その
ELSE
	PRINT 無残に傷つけられ、その
ENDIF
;乱数分岐
IF CHECK("Ｖフィスト") && LOCAL >= 4
	PRINT たまらない
ELSEIF RAND:2
	PRINT 圧倒的な
ELSE
	PRINT 暴力的な
ENDIF
;Ｖ快感で分岐
IF CHECK("Ｖフィスト") && ABL:Ｖ感觉 >= SIZE("フィスト") && RAND:3 == 0
	PRINT 快感に
ELSEIF CUP:痛苦 >= 3000 && LOCAL <= 2 && CHECK("Ｖフィスト") == 0 && ABL:受虐倾向 + TALENT:抖Ｍ < SIZE("フィスト") && RAND:3 == 0
	PRINT 痛みに
ELSEIF LOCAL <= 2 && (RAND:2 || CUP:痛苦 >= 500)
	PRINT 拡張感に
ELSE
	PRINT 挿入感に
ENDIF
PRINTFORM %CALLNAME:TARGET%は
;倒錯的かマゾっ気が3以上
IF (ABL:受虐倾向 + TALENT:抖Ｍ >= SIZE("フィスト") || TALENT:性变态) && SIZE("フィスト") >= 3 && RAND:2
	IF CHECK("Ｖフィスト")
		PRINTFORM 自分の大切な所が\@ ABL:Ｖ扩张 >= 6 && RAND:2 ? 取り返しがつかない程に # \@拡張されてしまったと興奮し、
	ELSE
		PRINTFORM 自分の大切な所を\@ ABL:Ｖ名器度 >= 50 && RAND:2 ? ガバガバに # 壊 \@されるかもしれないと興奮し、
	ENDIF
ELSEIF CHECK("Ｖフィスト") && ABL:Ｖ感觉 >= SIZE("フィスト") && RAND:2
	PRINT 悦楽に蕩けた艶やかな声をあげて
ELSEIF (ABL:受虐倾向 < 3 || LOCAL <= 2) && CHECK("Ｖフィスト") == 0 && RAND:2
	PRINT 目尻に涙を浮かばせて
;プライド高いか恥じらい
ELSEIF (TALENT:高傲 || TALENT:羞耻心强) && RAND:3
	PRINT 顔を真っ赤に染めて
ELSE
	PRINT 獣のような声をあげて
ENDIF
PRINTW 激しく身をくねらせた…
