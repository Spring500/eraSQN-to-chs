@COM106
;逆バイブ
;PLAYER側がバイブで楽しむコマンド。通常のバイブコマンドとは違い、入れっぱなしにはしない。
PRINT 逆バイブ
SIF TALENT:PLAYER:妖精
	PRINT (妖精用)
PRINTL 

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 106

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 逆バイブ
;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@TRAIN_MESSAGE_COM106
;バイブ選択
CALL SELECT_VIB, PLAYER

SIF REFUSE_CHECK()
	RETURN 0

IF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%は、%CALLNAME:PLAYER%のアソコを
	IF SIZE("按摩棒", PLAYER) >= 3
		PRINT 極太の
	ELSEIF SIZE("按摩棒", PLAYER) == 2
		PRINT 大きな
	ENDIF
	PRINTW バイブでかき回し続けている…
	RETURN 1
ENDIF

PRINTFORM %CALLNAME:TARGET%は
IF TEQUIP:按摩棒 && SIZE("按摩棒", PLAYER) > SIZE("按摩棒", TARGET)
	IF RAND:2
		PRINT 自らが咥え込んでいるものより
	ELSE
		PRINT 自分の膣を掻き回しているものより
	ENDIF
	SIF SIZE("按摩棒", PLAYER) > SIZE("按摩棒", TARGET) + 1
		PRINT 遥かに
	SIF SIZE("按摩棒", PLAYER) >= 3
		PRINT 凶悪な
ELSEIF SIZE("按摩棒", PLAYER) >= 2 && RAND:2
	IF RAND:2
		PRINT 大きな亀頭を備えた
	ELSE
		PRINT ずっしりとした
	ENDIF
ELSEIF RAND:2
	IF RAND:2
		PRINT イボに覆われた
	ELSE
		PRINT ローションでぬめる
	ENDIF
ELSE
	IF PLAYER == MASTER && ASSI && RAND:2
		PRINTFORM %CALLNAME:ASSI%に手渡された
	ELSE
		PRINT 用意された
	ENDIF
ENDIF
IF SIZE("按摩棒", PLAYER) >= 3
	PRINT 極太の
ELSEIF SIZE("按摩棒", PLAYER) == 2
	PRINT 大きな
ENDIF
PRINT バイブを、
IF (TALENT:恋慕 || TALENT:PLAYER:好色) && RAND:2
	PRINT 脚を開いて誘っている
ELSE
	PRINT 無防備に目の前に晒された
ENDIF
PRINTFORM %CALLNAME:PLAYER%の
IF COND("阴毛", PLAYER) && COND("阴毛", PLAYER) <= 3 && RAND:3 == 0
	PRINT ツルツルの
ELSEIF TALENT:PLAYER:娇小体型 && RAND:3 == 0
	PRINT 幼い
ELSEIF RAND:2
	PRINT 濡れた
ENDIF
PRINT アソコに、
IF TALENT:抖Ｓ && RAND:3 == 0
	PRINT 捻りながら
ELSEIF (ABL:PLAYER:Ｖ扩张 >= SIZE("按摩棒", PLAYER) || TALENT:抖Ｓ) && RAND:2
	IF ABL:PLAYER:Ｖ扩张 > SIZE("按摩棒", PLAYER) && RAND:2
		PRINT 抵抗を感じることなく
	ELSEIF RAND:2 == 0
		PRINT 一気に
	ELSE
		PRINT 深々と
	ENDIF
	SIF ABL:PLAYER:Ｖ扩张 > SIZE("按摩棒", PLAYER) && RAND:2
		PRINT 根元まで
ELSEIF RAND:2
	PRINT 肉襞を擦り上げながら
ELSE
	PRINT ゆっくりと
ENDIF
IF (ABL:PLAYER:Ｖ扩张 >= SIZE("按摩棒", PLAYER) || TALENT:抖Ｓ) && RAND:3 == 0
	IF RAND:2
		PRINTW 押し込んだ。
	ELSE
		PRINTW 突っ込んだ。
	ENDIF
ELSEIF RAND:2
	PRINTW 埋め込んだ。
ELSE
	PRINTW 挿入した。
ENDIF

LOCAL = RAND:2

IF LOCAL == 0
ELSEIF ABL:施虐倾向 >= 3 && RAND:3 == 0
	PRINT ガシガシと凄い勢いで
ELSEIF ABL:技巧 >= 3 && RAND:3 == 0
	PRINT 長いストロークで
ELSEIF RAND:2
	PRINT うねりながら
ELSE
	PRINT ゆっくりと
ENDIF
IF (ABL:技巧 >= 4 || ABL:施虐倾向 >= 3) && RAND:3 == 0
	IF LOCAL == 0 && RAND:2 == 0
		PRINT 容赦なく
	ELSEIF SIZE("按摩棒", PLAYER) >= 2
		PRINT 膣奥に
	ENDIF
	PRINT 叩き込まれる
ELSEIF RAND:2
	PRINT 出し入れされる
ELSE
	PRINT 膣壁を掻き回す
ENDIF
PRINT バイブ
IF LOCAL
	PRINT で
ELSEIF SIZE("按摩棒", PLAYER) >= 2 && RAND:3 == 0
	IF RAND:2 && ABL:PLAYER:Ｖ扩张 <= SIZE("按摩棒", PLAYER)
		PRINT の膣を壊しかねない太さで
	ELSE
		PRINT が膣を押し広げる感覚で
	ENDIF
ELSEIF (ABL:PLAYER:Ｖ扩张 < SIZE("按摩棒", PLAYER) && TALENT:抖Ｓ) || (SIZE("按摩棒", PLAYER) >= 3 && RAND:3 == 0)
	PRINT の凶暴さで
ELSEIF RAND:3 == 0
	PRINT の蠢きで
ELSEIF RAND:2
	PRINT の威力で
ELSE
	PRINT の気持ちよさで
ENDIF
PRINTFORM %CALLNAME:PLAYER%が
IF ABL:PLAYER:Ｖ扩张 >= SIZE("按摩棒", PLAYER) && EXP:PLAYER:Ｖ经验 >= EXP_LV(SIZE("按摩棒", PLAYER)) && RAND:3 == 0
	IF (TALENT:恋慕 || TALENT:PLAYER:好色) && RAND:2
		PRINT 大股開いてよがる
	ELSEIF RAND:2
		PRINT 愛液を垂れ流す
	ELSE
		PRINT 恍惚としている
	ENDIF
ELSEIF ABL:PLAYER:Ｖ扩张 < SIZE("按摩棒", PLAYER) && TALENT:抖Ｓ
	PRINT 泣きそうになっている
	LOCAL = 2
ELSEIF RAND:3 == 0
	PRINT 腰を突き出す
ELSEIF RAND:2
	PRINT 身体を跳ねさせる
ELSE
	PRINT 甘い吐息を漏らす
ENDIF
PRINTFORM 様子を
IF RAND:2
	PRINT 横目で見ながら、
ELSE
	PRINT 眺めながら、
ENDIF
PRINTFORM %CALLNAME:TARGET%は
IF LOCAL == 2
	PRINTW 嗜虐の楽しみに浸っている…
ELSEIF TALENT:聪慧 && RANDIF(3, TALENT:小恶魔 + TALENT:开朗, TALENT:坏心眼)
	PRINTW 巧みに言葉責めをしている…
ELSEIF TALENT:坏心眼 && RANDIF(3, TALENT:小恶魔 + TALENT:开朗)
	IF ABL:PLAYER:Ｖ扩张 >= 3
		PRINT 膣の緩さを
	ELSE
		PRINT 淫乱振りを
	ENDIF
	PRINTW 意地悪く詰っている…
;陽気
ELSEIF TALENT:开朗 + TALENT:小恶魔 && RANDIF(3, TALENT:小恶魔)
	SIF TALENT:小恶魔
		PRINT ニヤニヤと
	PRINTW 楽しそうに微笑んでいる…
ELSEIF ABL:施虐倾向 >= 3 && RAND:3 == 0
	IF SIZE("按摩棒", PLAYER) >= 2 && RAND:2
		PRINTW 野太いソレでいじめ続けている…
	ELSE
		PRINTW 心配しているように見せかけて言葉責めをしている…
	ENDIF
ELSE
	;IF RAND:2
	;	PRINT 愛撫も交えながら
	;ELSE
	;	PRINT 動きを変えたりしながら
	;ENDIF
	IF ABL:欲望 >= 3 && SIZE("按摩棒", PLAYER) >= 2 && RANDIF(2, ABL:施虐倾向 >= 3, ABL:技巧 >= 3, ABL:侍奉精神 >= 3)
		PRINTW お腹を撫でて胎内で暴れるバイブの感触を楽しんでいる…
	ELSEIF ABL:侍奉精神 >= 3 && RANDIF(2, ABL:施虐倾向 >= 3, ABL:技巧 >= 3)
		PRINTW 更なる奉仕に没頭していった…
	ELSEIF ABL:技巧 >= 3 && RANDIF(2, ABL:施虐倾向 >= 3)
		PRINTW 更に巧みに責めていった…
	ELSEIF ABL:施虐倾向 >= 3 && RAND:2
		PRINTW 更に責めを激しくしていった…
	ELSE
		PRINTW 奉仕を続けた…
	ENDIF
ENDIF

@FLAG_COM106, ARG
IF TALENT:PLAYER:处女
	CALL SETFLAG, "処女喪失フラグ", PLAYER
	CALL SETFLAG, "処女簒奪フラグ", TARGET
ENDIF

@SOURCE_COM106, ARG
VARSET LOCAL

LOCAL:20 += 100
LOCAL:21 += 100

LOCAL:3 = 1000
LOCAL:11 = CALCF("実行値による欲情追加")
LOCAL:14 = 500
;バイブの大きさなど
IF SIZE("按摩棒", PLAYER) >= 3
	TIMES LOCAL:11 , 3.00
ELSEIF SIZE("按摩棒", PLAYER) >= 2
	TIMES LOCAL:11 , 1.50
ENDIF
IF SIZE("按摩棒", PLAYER) > ABL:PLAYER:Ｖ扩张
	TIMES LOCAL:11 , 1.50
ELSEIF SIZE("按摩棒", PLAYER) < ABL:PLAYER:Ｖ扩张
	TIMES LOCAL:11 , 0.50
ENDIF

;ABL:侍奉精神をみる
SELECTCASE ABL:侍奉精神
CASE 0
	LOCAL:4 = 420
	LOCAL:5 = 150
CASE 1
	LOCAL:4 = 500
	LOCAL:5 = 300
CASE 2
	LOCAL:4 = 580
	LOCAL:5 = 600
CASE 3
	LOCAL:4 = 660
	LOCAL:5 = 900
CASE 4
	LOCAL:4 = 740
	LOCAL:5 = 1500
CASEELSE
	LOCAL:4 = 820
	LOCAL:5 = 2200
ENDSELECT

;ABL:技巧をみる
SELECTCASE ABL:技巧
CASE 0
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
CASE 1
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
CASE 2
CASE 3
	TIMES LOCAL:4 , 1.20
	TIMES LOCAL:5 , 1.20
CASE 4
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
CASEELSE
	TIMES LOCAL:4 , 2.00
	TIMES LOCAL:5 , 2.00
ENDSELECT

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@COM_ORDER_COM106, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:侍奉精神
IF ABL:侍奉精神
	LOCAL += ABL:侍奉精神 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:侍奉精神}({ABL:侍奉精神 * 4})
ENDIF
;ABL:施虐倾向
IF ABL:施虐倾向
	LOCAL += ABL:施虐倾向 * 5
	LOCALS = %LOCALS% + %ABLNAME:15%LV{ABL:施虐倾向}({ABL:施虐倾向 * 5})
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;快感の否定
IF TALENT:否认快感
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF
;サド
IF TALENT:抖Ｓ
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:83%(5)
ENDIF
;オトコ
IF TALENT:男性
	LOCAL += 20
	LOCALS = %LOCALS% + %TALENTNAME:122%(20)
ENDIF

;合計を表示(30以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 30

RETURN RESULT
