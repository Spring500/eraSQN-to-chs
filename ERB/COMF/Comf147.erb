@COM147
;精飲
PRINTL 精飲
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 147

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 精飲

RETURN 1

@FLAG_COM147, ARG
;夢魔の口に精液汚れ追加
CALL ADD_STAIN, TARGET, "Ｍ", "精液"

@SOURCE_COM147, ARG
VARSET LOCAL

;関係するのは、欲望+奉仕精神と精液中毒と、EXP:精液経験 精液経験。

LOCAL:20 += 10
LOCAL:21 += 50

LOCAL:8 = TFLAG:16 * 200
LOCAL:7 = TFLAG:16 * 300
LOCAL:13 = TFLAG:16 * 500
LOCAL:14 = TFLAG:16 * 300

SIF CALCF("精液好き度") > TFLAG:16
	TIMES LOCAL:13 , 1.50

;精液経験をみる
SELECTCASE EXP:精液経験
CASE IS < 5
	TIMES LOCAL:14 , 4.00
CASE IS < 20
	TIMES LOCAL:14 , 2.00
CASE IS < 50
	TIMES LOCAL:14 , 1.50
CASE IS < 100
	TIMES LOCAL:14 , 1.20
CASE IS < 300
	TIMES LOCAL:14 , 1.00
CASEELSE
	TIMES LOCAL:14 , 0.50
ENDSELECT

;ABL:精液中毒をみる
SELECTCASE ABL:精液中毒
CASE 0
	TIMES LOCAL:7 , 0.10
	TIMES LOCAL:13 , 0.50
CASE 1
	TIMES LOCAL:7 , 1.00
	TIMES LOCAL:13 , 1.00
CASE 2
	TIMES LOCAL:7 , 1.20
	TIMES LOCAL:13 , 1.30
CASE 3
	TIMES LOCAL:7 , 1.40
	TIMES LOCAL:13 , 1.60
CASE 4
	TIMES LOCAL:7 , 1.80
	TIMES LOCAL:13 , 2.00
CASEELSE
	TIMES LOCAL:7 , 2.00
	TIMES LOCAL:13 , 3.00
ENDSELECT

;夢魔
IF TALENT:夢魔 || TALENT:上位夢魔
	LOCAL:8 = 0
	TIMES LOCAL:7 , 2.00
	TIMES LOCAL:13 , 1.50
	TIMES LOCAL:14 , 0.10
ENDIF

CALL SOURCE_SMPLAY, "精飲", ARG

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM147
;飲む量をTFLAG:16で管理
IF ITEM:精液貯蓄量 >= 6 && CALCF("精液好き度") >= 4
	CALL LOOP_DRINK147
ELSE
	IF CHECK_SHIRT("和服") + CHECK_SHIRT("巫女服")
		PRINTFORM %CALLNAME:PLAYER%は檜の香りのする枡に精液を
	ELSE
		PRINTFORM %CALLNAME:PLAYER%はワイングラスに精液を
	ENDIF
	TFLAG:16 = 3
ENDIF

IF CALCF("精液好き度") >= TFLAG:16 + 2 && ITEM:精液貯蓄量 >= TFLAG:16 + 2
	PRINT 溢れんばかりに
	TFLAG:16 += 2
;ELSEIF CALCF("精液好き度") <= TFLAG:16 - 2
;	PRINT たっぷりと
;	TFLAG:16 -= 2
ENDIF

;魔法瓶の中の量がＭＡＸ
SIF ITEM:精液貯蓄量 <= TFLAG:16
	TFLAG:16 = ITEM:精液貯蓄量
SIF TFLAG:16 >= CALCF("精液好き度")
	TFLAG:16 = CALCF("精液好き度")
PRINTFORMW 注いで、%CALLNAME:TARGET%に手渡した。
PRINTFORM %CALLNAME:TARGET%は
IF ABL:精液中毒 == 0
	PRINT 口の中に溜めた唾液を利用して、なんとか
ELSEIF ABL:精液中毒 < 3
	PRINT 
ELSEIF ABL:精液中毒 < 5
	IF RAND:3 && CALCF("精液好き度") > TFLAG:16
		PRINT 涼しい顔で
	ELSE
		PRINT ゴクゴクと喉を鳴らしながら
	ENDIF
ELSE
	IF RAND:3 && CALCF("精液好き度") > TFLAG:16
		PRINT なんとも美味しそうに
	ELSE
		PRINT 味わいながら
	ENDIF
ENDIF
PRINTFORMW 精液を飲み干していく…


@LOOP_DRINK147
PRINTFORML どれに注ぎますか？
IF CHECK_SHIRT("和服") + CHECK_SHIRT("巫女服")
	PRINTL [0] 枡
ELSE
	PRINTL [0] ワイングラス
ENDIF
SIF ITEM:精液貯蓄量 >= 6 && CALCF("精液好き度") >= 4
	PRINTL [1] ビールジョッキ
IF ITEM:精液貯蓄量 >= 10 && CALCF("精液好き度") >= 8
	PRINTL [2] PETボトル
	CALL INPUT_SELECT, 3
ELSE
	CALL INPUT_SELECT, 2
ENDIF

IF RESULT == 0
	IF CHECK_SHIRT("和服") + CHECK_SHIRT("巫女服")
		PRINTFORM %CALLNAME:PLAYER%は檜の香りのする枡に精液を
	ELSE
		PRINTFORM %CALLNAME:PLAYER%はワイングラスに精液を
	ENDIF
	TFLAG:16 = 3
ELSEIF RESULT == 1
	PRINTFORM %CALLNAME:PLAYER%は大きなジョッキに精液を
	TFLAG:16 = 6
ELSEIF RESULT == 2
	PRINTFORM %CALLNAME:PLAYER%はPETボトルに精液を
	TFLAG:16 = 10
ENDIF


@COM_ORDER_COM147, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += ABL:奉仕精神 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:奉仕精神}({ABL:奉仕精神 * 4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += ABL:精液中毒 * 3
	LOCALS = %LOCALS% + %ABLNAME:11%LV{ABL:精液中毒}({ABL:精液中毒 * 3})
ENDIF
;恥じらい
IF TALENT:恥じらい
	LOCAL -= 3
	LOCALS = %LOCALS% - %TALENTNAME:35%(3)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF

;合計を表示(30以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 30

RETURN RESULT
