@COM261
;媚薬

PRINTL 媚薬

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 媚薬

TEQUIP:媚薬 = 0

;触手状態で無いなら媚薬が必要
SIF TEQUIP:触手 == 0
	ITEM:媚药 -= 1

RETURN 1

@FLAG_COM261, ARG
IF TEQUIP:触手
	TEQUIP:媚薬 = 200
ELSEIF TEQUIP:押し倒し
	;調合知識や禁断の知識があれば効果ＵＰ
	SELECTCASE MAX(TALENT:ASSI:调合知识 + TALENT:ASSI:禁忌知识, TALENT:调合知识 + TALENT:禁忌知识)
	CASE 2
		TEQUIP:PLAYER:媚薬 = 200
	CASE 1
		SIF TALENT:ASSI:调合知识 + TALENT:调合知识
			TEQUIP:PLAYER:媚薬 = 100
	ENDSELECT

	CALL ADD_STATE, PLAYER, "媚薬"
ELSE
	;調合知識や禁断の知識があれば効果ＵＰ
	SELECTCASE MAX(TALENT:ASSI:调合知识 + TALENT:ASSI:禁忌知识, TALENT:MASTER:调合知识 + TALENT:MASTER:禁忌知识)
	CASE 2
		TEQUIP:媚薬 = 300
	CASE 1
		SIF TALENT:ASSI:调合知识 + TALENT:MASTER:调合知识
			TEQUIP:媚薬 = 100
	ENDSELECT
ENDIF
;ここでTEQUIP:媚薬がさらに+100（基本値）され、薬毒耐性はその後に75％軽減される
CALL ADD_STATE, TARGET, "媚薬"

@SOURCE_COM261, ARG
VARSET LOCAL

LOCAL:20 += 10
LOCAL:21 += 50

LOCAL:14 = 2000
SIF TALENT:好色
	LOCAL:14 /= 2
SIF TEQUIP:押し倒し
	LOCAL:14 /= 2

;媚薬効果で欲情のアップが2倍になるので実際には+10000
LOCAL:11 = 5000

SIF TALENT:药物耐性
	LOCAL:11 = 2000

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM261
IF TEQUIP:触手
	IF RAND:2 == 0
		PRINTFORML 凄まじい催淫効果のある液体が触手から染み出してきた…
	ELSE
		PRINTFORML %CALLNAME:TARGET%の口に管状の触手が入り込み、凶悪な催淫効果のある液体を流し込んだ…
	ENDIF
ELSEIF TEQUIP:押し倒し
	;騎乗位出来るなら騎乗位ルート
	CALL COM_ABLE65
	IF RESULT && TALENT:好色*3 + TALENT:淫乱*3 + ABL:欲望 >= 3 && PENIS(PLAYER) && COND("ベテラン：Ｖセックス")
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%のペニスに媚薬を垂らし、敏感になったそれを咥え込んで腰を振り始めた。
		CALL SET_INSERT, TARGET, PLAYER, 65
	ELSEIF CHECK_COM("キス")
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%を押さえつけ、口移しで媚薬を飲ませてきた。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は用意された薬品の中から媚薬を選び出し、身動きの取れない%CALLNAME:PLAYER%に飲ませてきた。
	ENDIF
ELSE
	PRINTFORM %CALLNAME:TARGET%に
	SELECTCASE MAX(TALENT:ASSI:调合知识 + TALENT:ASSI:禁忌知识, TALENT:MASTER:调合知识 + TALENT:MASTER:禁忌知识)
	CASE 2
		PRINTFORM 禁断の知識をもって調合した、処女だろうと快感に溺れる強力な
	CASE 1
		SIF TALENT:ASSI:调合知识 + TALENT:MASTER:调合知识
			PRINTFORM 特別に調合した
	ENDSELECT
	PRINTFORML 媚薬を飲ませた。
ENDIF
IF TALENT:药物耐性
	PRINTFORMW %CALLNAME:TARGET%は薬に耐性を持っているようだが少しは効果があるようで、ほんのりと肌を紅潮させている…
ELSE
	PRINTFORMW %CALLNAME:TARGET%は荒い息をついて肌を紅潮させている…
ENDIF
SETCOLOR DEF_COLOR("イエロー")
PRINTFORMW ※媚薬の副作用として、獲得する珠の量が減少します※
RESETCOLOR
