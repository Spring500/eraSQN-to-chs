@COM302
;歌を歌う
;TEQUIP:演奏中で歌の種類を判定する。歌っている人のTEQUIP:演奏中が残り歌唱ターン。
;平穏の歌		歌唱経験に応じてコマンド実行時にTARGETの体力気力消費を軽減する
;ロマンスソング		歌唱経験に応じてムードＵＰ
;雨上がりの輪舞曲	歌唱経験に応じて実行値ＵＰ

IF TEQUIP:演奏中
	PRINTL 演奏中断
ELSE
	PRINTL 歌を歌う
ENDIF

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

SIF TEQUIP:演奏中 == 0
	RETURN 0

IF TEQUIP:演奏中 == 1
	STR:0 = 平穏の歌
ELSEIF TEQUIP:演奏中 == 2
	STR:0 = ロマンスソング
ELSEIF TEQUIP:演奏中 == 3
	STR:0 = 雨上がりの輪舞曲
ELSE
	STR:0 = 演奏中断
ENDIF

CALL KOJO_MESSAGE_COM
PRINTL 

PREVCOM = 302

RETURN 0

@TRAIN_MESSAGE_COM302
;演奏されていたら中断
IF TEQUIP:演奏中
	IF TEQUIP:MASTER:演奏中
		LOCAL = MASTER
	ELSE
		LOCAL = ASSI
	ENDIF
	PRINTFORMW %CALLNAME:LOCAL%は演奏を中断した…
	TEQUIP:演奏中 = 0
	TEQUIP:LOCAL:演奏中 = 0
ELSE
	IF PLAYER == MASTER
		LOCAL = ASSI
	ELSE
		LOCAL = MASTER
	ENDIF
	CALL SONG_SELECT, LOCAL
	IF TEQUIP:演奏中 == 1
		PRINTFORMW %CALLNAME:LOCAL%は穏やかで優しい歌を奏で始めた…
	ELSEIF TEQUIP:演奏中 == 2
		PRINTFORML %CALLNAME:LOCAL%は美しい声で歌いはじめた。
		PRINTFORMW あたりにロマンチックなムードが漂う…
	ELSEIF TEQUIP:演奏中 == 3
		PRINTFORML %CALLNAME:LOCAL%は曲を奏で始めた！
		PRINTFORMW あたりに美しい旋律がこだまする…
	ENDIF
ENDIF

@SONG_SELECT, LOCAL
PRINTFORML %CALLNAME:LOCAL%に何の歌を歌ってもらいますか？
PRINTL  [ 0] 平穏の歌　　　　　体力気力の消費を抑える
PRINTL  [ 1] ロマンスソング　　ムード上昇
PRINTL  [ 2] 雨上がりの輪舞曲　行動実行値上昇
;ディスペルソング
;誘惑の歌
PRINTL [100] 戻る

$INPUT_LOOP
INPUT

IF RESULT == 100
	RETURN 0
ELSEIF RESULT >= 0 && RESULT <= 2
	TEQUIP:演奏中 = RESULT + 1
	TEQUIP:LOCAL:演奏中 = 10
ELSE
	GOTO INPUT_LOOP
ENDIF


@EQUIP_COM302
;歌の効果は歌い手の歌唱経験に比例
IF TEQUIP:MASTER:演奏中
	LOCAL = MASTER
ELSE
	LOCAL = ASSI
ENDIF

IF TEQUIP:演奏中 == 1
	PRINTFORML ＜平穏の歌(第{11 - TEQUIP:LOCAL:演奏中}フレーズ)＞
	PRINTFORML %CALLNAME:LOCAL%は心安らぐ歌を奏でている…
	DOWNBASE:体力 = MULTIPLY(DOWNBASE:体力, 100 - TALENT:LOCAL:歌唱技能*10)
	DOWNBASE:気力 = MULTIPLY(DOWNBASE:気力, 100 - TALENT:LOCAL:歌唱技能*10)
	PRINTFORML %CALLNAME:TARGET%の体力気力消費{TALENT:LOCAL:歌唱技能*10}％軽減
ELSEIF TEQUIP:演奏中 == 2
	PRINTFORML ＜ロマンスソング(第{11 - TEQUIP:LOCAL:演奏中}フレーズ)＞
	PRINTFORML %CALLNAME:LOCAL%はロマンチックな歌を奏でている…
	TFLAG:33 += RAND:(TALENT:LOCAL:歌唱技能 + 1) + 1
ELSEIF TEQUIP:演奏中 == 3
	PRINTFORML ＜雨上がりの輪舞曲(第{11 - TEQUIP:LOCAL:演奏中}フレーズ)＞
	PRINTFORML %CALLNAME:LOCAL%は美しい旋律を奏でている…
ENDIF

;歌唱経験を加算＆疲労
EXP:LOCAL:歌唱経験 += 1
BASE:LOCAL:精力 -= 1
PRINTFORML %CALLNAME:LOCAL%の歌唱経験＋1 ＆ 精力−1

RETURN 1

@EQUIP_COM302_END
IF TEQUIP:MASTER:演奏中
	LOCAL = MASTER
ELSE
	LOCAL = ASSI
ENDIF

TEQUIP:LOCAL:演奏中 -= 1

IF TEQUIP:LOCAL:演奏中 == 0
	PRINTL 
	PRINTFORML %CALLNAME:LOCAL%は演奏を終えた
	;歌唱経験ボーナス
	EXP:LOCAL:歌唱経験 += 5
	PRINTFORML %CALLNAME:LOCAL%の歌唱経験＋5
	TEQUIP:演奏中 = 0
;疲れた
ELSEIF BASE:LOCAL:精力 <= 4
	PRINTL 
	PRINTFORML %CALLNAME:LOCAL%はあまりの疲れに演奏を中断した
	TEQUIP:LOCAL:演奏中 = 0
	TEQUIP:演奏中 = 0
ENDIF

RETURN 1
