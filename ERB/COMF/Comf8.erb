@COM8
;指挿入れ
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

RETURN 1

@FLAG_COM8, ARG
IF CHECK_COM("Ｇスポ責め")
	CALL FLAG_INSERTION, "指挿入れ", ARG, 150
ELSEIF TEQUIP:按摩棒
	CALL FLAG_COM31, 130
ELSE
	CALL FLAG_INSERTION, "指挿入れ", ARG, 100
ENDIF

@SOURCE_COM8, ARG
IF CHECK_COM("Ｇスポ責め")
	CALL SOURCE_INSERTION, "指挿入れ", ARG, 150
ELSEIF TEQUIP:按摩棒
	CALL SOURCE_INSERTION, "按摩棒", ARG, 130
ELSE
	CALL SOURCE_INSERTION, "指挿入れ", ARG, 100
ENDIF


@TRAIN_MESSAGE_COM8
PRINTFORM %CALLNAME:PLAYER%は
IF TEQUIP:亲吻
	PRINTFORM %TEXTS("キス継続")%、
	CALL SETFLAG, "追加キスフラグ", TARGET
ELSEIF TEQUIP:６９式
	PRINTFORM %TEXTS("６９")%
ELSEIF INSERT("Ａインサート") && PREVCOM != SELECTCOM
	PRINTFORM アナルを抉りながら、
ELSEIF PREVCOM == 1 && PENIS(TARGET) == 0 && CHECK_CLO("Ｖ露出")
	SIF PALAM:欲情 >= 3000 && ABL:Ｃ感觉 >= 3
		PRINT 勃起した
	PRINTFORM クリトリスを舐めながら、
ELSEIF PREVCOM == 13 && TEQUIP:按摩棒 == 0
	PRINT 目の前で開帳されている
ENDIF
PRINTFORM %CALLNAME:TARGET%の

;バイブ
IF TEQUIP:按摩棒
	;穴開きでないパンツをはいていて、ずらされていないなら、必ずずらす
	SIF CHECK_CLO("Ｖ露出") == 0
		CALL SETFLAG, "ボトムを露出：テキスト有り", TARGET
	IF RAND:2 == 0
		PRINTFORM 膣を埋めている
	ELSE
		PRINTFORM 膣を震わせている
	ENDIF

	SELECTCASE SIZE("按摩棒")
	CASE IS >= 3
		IF RAND:2 == 0
			PRINT 凶悪な
		ELSE
			PRINT 極太
		ENDIF
	CASE 2
		IF RAND:2
			PRINT 太い
		ELSE
			PRINT 大きな
		ENDIF
	ENDSELECT

	;ピストン
	IF RAND:2 == 0
		PRINTFORM バイブを
		IF (ABL:Ｖ感觉 >= 3 || ABL:Ｖ扩张 >= SIZE("按摩棒") || TALENT:PLAYER:抖Ｓ) && RAND:2 == 0
			PRINT ガシガシと激しい
		ELSEIF RAND:2 == 0
			PRINT 緩急をつけた
		ELSE
			PRINT ゆっくりと大きな
		ENDIF
		PRINTFORM ストロークで動かし
		IF ABL:Ｖ扩张 < SIZE("按摩棒") && RAND:3 == 0
			PRINTFORMW て、大きな異物に押し広げられている穴を蹂躙した…
		ELSEIF (ABL:Ｖ感觉 >= 3 || TALENT:PLAYER:抖Ｓ) && RAND:4 == 0
			PRINTFORMW 、硬い先端で子宮口を突き上げた…
		ELSEIF RAND:3 == 0
			PRINTFORMW て、膣内の粘液が泡立つほどに攪拌した…
		ELSEIF RAND:2 == 0
			PRINTFORMW て、膣の粘膜を擦りあげた…
		ELSE
			PRINTFORMW た…
		ENDIF
	;スイッチON
	ELSE
		PRINTFORM バイブのスイッチを最強にセットし、
		IF (ABL:Ｖ感觉 >= 4 || ABL:Ｖ扩张 >= SIZE("按摩棒") || TALENT:PLAYER:抖Ｓ) && RAND:3 == 0
			IF RAND:2 == 0
				PRINTFORM 抉るように
			ELSE
				PRINTFORM 容赦なく
			ENDIF
		ELSEIF ABL:Ｖ扩张 <= SIZE("按摩棒") && (ABL:Ｖ感觉 >= 3 || TALENT:PLAYER:抖Ｓ) && RAND:2 == 0
			PRINTFORM 膣が拡がるように力を込めて
		ELSEIF RAND:3 == 0
			PRINTFORM グリグリと
		ELSEIF RAND:2 == 0
			PRINTFORM 様子を伺いながら
		ELSE
			PRINTFORM 左右に捻りながら
		ENDIF
		PRINTFORMW 蜜壷を掻き回した…
	ENDIF
ELSEIF CHECK_COM("Ｇスポ責め")
	IF RAND:2
		PRINTFORM 膣の敏感な
		IF RAND:2
			PRINTFORM 部分を執拗に指でこすりあげた
		ELSE
			PRINTFORM 粘膜を擦るようにして指を動かした
		ENDIF
	ELSE
		PRINTFORM 膣の中で指を曲げ、
		IF RAND:2
			PRINTFORM ザラついた天井を刺激した
		ELSE
			PRINTFORM 僅かに触りごこちの異なるデリケートな粘膜を集中して愛撫した
		ENDIF
	ENDIF
	;前回こらえた＆そろそろ限界
	IF ABL:Ｖ感觉 >= 3 && PREVEX("イくのをこらえた") && UP_PALAM(1) > MAX(MULTIPLY(MAXBASE:快感容许值（女）, 70), 10000)
		PRINTFORMW 
		IF RAND:3 == 0
			PRINTFORM 与えられる快感に応える様に、
		ELSEIF RAND:2
			PRINTFORM 激しい刺激に耐えかねるのか、
		ELSE
			PRINTFORM よほど気持ち良いのか、
		ENDIF
		IF ABL:Ｖ名器度 >= 30 && RANDIF(2, CUP:Ｖ润 >= 1000)
			PRINTFORMW %CALLNAME:TARGET%の膣は指の動きに合わせて痙攣する様な動きを見せたり不意に強い力で入り口を締めたりしている…
		ELSEIF CUP:Ｖ润 >= 1000 && RAND:2
			PRINTFORMW %CALLNAME:TARGET%の膣は凄い量の愛液を溢れさせて%CALLNAME:PLAYER%の手を汚している…
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は\@ TALENT:好色 && RAND:2 ? 積極的 # 無意識 \@に%CALLNAME:PLAYER%の動きに合わせて腰を動かしている…
		ENDIF
	ELSE
		PRINTFORMW …
	ENDIF
;指
ELSE
	LOCALS = 
	;穴開きでないパンツをはいていて、ずらされていないならずらすことも
	IF CHECK_CLO("Ｖ露出") == 0
		IF RAND:3 == 0
			CALL SETFLAG, "ボトムを露出：テキスト有り", TARGET
		ELSE
			PRINTFORM %CLO("股間")%に手を忍ばせて
		ENDIF
		LOCALS = ボトム描写
	ENDIF
	IF PREVEX("Ｖ中出し") && CHECK_STAIN(STAIN:Ｖ, "精液")
		PRINTFORMW 中出し直後の膣に指を挿入し、襞に絡み付いている精液をゆっくりと掻きだしていった…
	ELSE
		IF CHECK_CLO("Ｖ露出") == 0 || RAND:3 == 0
			SIF PALAM:Ｖ润 >= PALAMLV:4
				PRINT 濡れそぼった
			IF TALENT:娇小体型
				PRINT 幼いワレメ
			ELSE
				PRINT 秘裂
			ENDIF
		ELSE
			PRINTFORM %TEXTS("アソコ：感触")%
		ENDIF

		IF PREVCOM == SELECTCOM
			PRINTFORMW を指でかき回している…
		ELSE
			PRINTFORM に
			IF ABL:Ｖ扩张 >= 4 && EXP:Ｖ拳交经验 >= 5 && CHECK("Ｖフィスト") && RAND:2
				PRINTFORM 揃えた指を手の甲を過ぎるまで挿入
			ELSE
				IF ABL:Ｖ扩张 >= 4 || (ABL:Ｖ扩张 >= 2 && RAND:2)
					PRINTFORM 中指と薬指
				ELSE
					PRINTFORM \@ RAND:2 ? 中 # \@指
				ENDIF
				PRINTFORM を\@ RAND:2 ? 根元まで # ゆっくりと \@挿入
			ENDIF

			IF LOCALS == "ボトム描写"
				PRINTFORMW した…
			ELSE
				IF RAND:3 == 0
					PRINTFORMW して、膣壁を指の腹でくすぐった…
				ELSEIF RAND:2
					PRINTFORMW して、襞を擦るようにして掻き回した…
				ELSE
					PRINTFORMW して、中の具合を楽しんだ…
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	IF PREVEX("Ｖ中出し") && CHECK_STAIN(STAIN:Ｖ, "精液")
		PRINTFORM 膣から抜いた精液まみれの指を見せると、
		IF ABL:精液上瘾 > 3 && PALAM:欲情 >= 10000
			PRINTFORMW %CALLNAME:TARGET%は物欲しそうに舌を出してきた…
		ELSEIF TALENT:恋慕
			PRINTFORMW %CALLNAME:TARGET%は嬉しそうな微笑を見せた…
		ELSE
			IF RAND:2
				PRINTFORMW %CALLNAME:TARGET%は恥ずかしそうな顔を見せた…
			ELSE
				PRINTFORMW %CALLNAME:TARGET%は恥ずかしそうに目線を逸らした…
			ENDIF
		ENDIF
	ELSEIF EXP:Ｖ经验
		SELECTCASE EXP:Ｖ经验
		CASE IS <= 30
			PRINTFORMW %CALLNAME:TARGET%は指の感触に吐息を吐いて耐えている…
		CASE IS <= 50
			PRINTFORMW %CALLNAME:TARGET%は指の感触を楽しんでいるようだ…
		CASE IS <= 80
			PRINTFORMW %CALLNAME:TARGET%は指のうごめく感触にうっとりとしている…
		CASE IS <= 120
			PRINTFORMW %CALLNAME:TARGET%はお腹の中をかき回される感覚に熱い吐息を吐いて悶えている…
		CASEELSE
			IF ABL:Ｖ感觉 >= 3 && RAND:2
				PRINTFORMW %CALLNAME:TARGET%の快楽を教え込まれた雌穴は虐められても苦しそうな様子は無く、むしろ悦んで指を咥え込んでいる…
			ELSE
				PRINTFORMW %CALLNAME:TARGET%は涎をたらし、自ら腰を振って快楽を貪っている…
			ENDIF
		ENDSELECT
	ENDIF
ENDIF
