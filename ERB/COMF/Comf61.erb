@COM61
;後背位
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM61, ARG
CALL FLAG_INSERTION, "Ｖインサート", ARG, 110

@SOURCE_COM61, ARG
CALL SOURCE_INSERTION, "Ｖインサート", ARG, 110

VARSET LOCAL

LOCAL:12 = 1000
;動物耳
SIF TALENT:兽耳
	LOCAL:12 = 200

;ABL:Ｖ感觉をみる
SELECTCASE ABL:Ｖ感觉
CASE 0
	LOCAL:3 = 50
CASE 1
	LOCAL:3 = 150
CASE 2
	LOCAL:3 = 250
CASE 3
	LOCAL:3 = 350
CASE 4
	LOCAL:3 = 600
CASEELSE
	LOCAL:3 = 850
ENDSELECT

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES LOCAL:3 , 0.30
CASE IS < PALAMLV:2
	TIMES LOCAL:3 , 0.60
CASE IS < PALAMLV:3
	TIMES LOCAL:3 , 1.00
CASE IS < PALAMLV:4
	TIMES LOCAL:3 , 1.50
CASE IS >= PALAMLV:4
	TIMES LOCAL:3 , 1.80
ENDSELECT

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM61
;ラブトラップ
IF TEQUIP:爱情圈套？
	;前回と今回の調教者が同じで、抜かないフラグがある場合
	IF INSERT("インサート") && COND("前Ｔ調教者") == PLAYER
		;前回の調教が後背位
		IF INSERT("後背位")
			PRINTFORMW %CALLNAME:TARGET%は膣内のペニスの感触を味わいながら、再び腰を動かし始めた…
		ELSE
			PRINTFORMW %CALLNAME:TARGET%はペニスを咥え込んだまま%CALLNAME:PLAYER%の前に回り、再び腰を激しく打ち付けてきた…
		ENDIF
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の前に回り、腰を激しく打ち付けてきた…
	ENDIF
ELSEIF CHECK_COM("３Ｐ奉仕")
	CALL TRAIN_MESSAGE_3P
;それ以外
ELSE
	;前回と今回の調教者が同じで、抜かないフラグがある場合
	IF INSERT("Ｖインサート") && COND("前Ｔ調教者") == PLAYER
		IF INSERT("正常位") || INSERT("屈曲位")
			PRINTFORM 繋がりあったまま%CALLNAME:TARGET%を四つん這いにして腰を高く上げさせると、
		ELSEIF INSERT("後背位")
			IF PENIS(PLAYER)
				PRINTFORM %CALLNAME:TARGET%の膣内の感触を味わいながら、
			ELSE
				PRINTFORM %CALLNAME:TARGET%の膣内から抜かずに、
			ENDIF
		ELSEIF INSERT("対面座位")
			PRINTFORM 膣内から抜き去ることなく%CALLNAME:TARGET%の後ろに回り、
		ELSEIF INSERT("背面座位")
			PRINTFORM %CALLNAME:TARGET%の身体を前に倒して四つん這いにし、
		ELSEIF INSERT("騎乗位")
			PRINTFORM 膣内から抜き去ることなく%CALLNAME:TARGET%の後ろに回り、
		ENDIF
		IF RAND:2
			PRINTFORM %TEXTS("お尻：感触")%を鷲掴んで
			SIF RAND:2 && ABL:PLAYER:施虐倾向 && USE_A(TARGET) && CHECK_CLO("Ａ露出")
				PRINT アナルを眺めながら
		ELSE
			PRINTFORM %TEXTS("お尻：感触")%を引き寄せて
		ENDIF
		IF RAND:2
			PRINTW 再び激しく打ち付け始めた…
		ELSE
			PRINTW 再び腰を動かし始めた…
		ENDIF
	ELSEIF INSERT("Ａインサート") && COND("前Ｔ調教者") == PLAYER
		IF INSERT("後背位")
			PRINTFORM %CALLNAME:TARGET%のアナルから%NAMES("ペニス", PLAYER)%を抜いて、勢い良く
		ELSEIF INSERT("背面座位")
			PRINTFORM %CALLNAME:TARGET%の身体を前に倒して四つん這いにし、今度は
		ELSE
			PRINTFORM %CALLNAME:TARGET%を四つん這いにして腰を高く上げさせ、お尻を鷲掴みながら
		ENDIF
		PRINTFORMW %TEXTS("Ｖ挿入前")%の最奥まで突き入れた…
	ELSE
		;前回、挿入前に暴発してしまった
		IF PREVCOM == 511 && COND("前Ｔ調教者") == PLAYER
			PRINTFORM 気を取り直した%CALLNAME:PLAYER%は、
		ELSE
			PRINTFORM %CALLNAME:PLAYER%は
		ENDIF
		IF COND("前Ｔから対象のみ変更") && PREVCOM:1 >= 60 && PREVCOM:1 <= 79
			CALL TEXT, "追加地の文：穴から穴へ"
			PRINTFORM 後ろから%CALLNAME:TARGET%の%TEXTS("Ｖ挿入前")%
		;穴開きでないパンツをはいていて、ずらされていないなら、必ずずらす
		ELSEIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
			PRINTFORM %CALLNAME:TARGET%の
			CALL SETFLAG, "ボトムを露出：テキスト有り", TARGET
			PRINTFORM 、後ろから%TEXTS("Ｖ挿入前")%
		ELSE
			PRINTFORM 後ろから%CALLNAME:TARGET%の%TEXTS("Ｖ挿入前")%
		ENDIF

		IF TALENT:怀孕 && TALENT:PLAYER:抖Ｓ == 0
			PRINT にゆっくり
		ELSE
			PRINT の最奥まで
		ENDIF
		PRINTFORMW 突き入れた…
	ENDIF
ENDIF
