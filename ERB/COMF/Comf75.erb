@COM75
;騎乗位アナル
CALL PRINT_TRAIN_NAME(SELECTCOM)
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 75

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM75, ARG
IF INSERT("ワイルドスウィング")
	CALL FLAG_INSERTION, "Ａインサート", ARG, 70
ELSE
	CALL FLAG_INSERTION, "Ａインサート", ARG, 50
ENDIF

@SOURCE_COM75, ARG
VARSET LOCAL

LOCAL:20 += 100
LOCAL:21 += 50
IF INSERT("ワイルドスウィング")
	LOCAL:20 += 50
	LOCAL:21 += 20
	CALL SOURCE_INSERTION, "Ａインサート", ARG, 70
ELSE
	CALL SOURCE_INSERTION, "Ａインサート", ARG, 50
ENDIF
IF TALENT:怪力
	TIMES LOCAL:20, 0.50
	TIMES LOCAL:21, 0.50
ENDIF

LOCAL:11 = CALCF("実行値による欲情追加")
LOCAL:12 = 1800

;ABL:侍奉精神をみる
SELECTCASE ABL:侍奉精神
CASE 0
	LOCAL:4 = 200
	LOCAL:5 = 50
	LOCAL:14 = 300
CASE 1
	LOCAL:4 = 250
	LOCAL:5 = 200
	LOCAL:14 = 100
CASE 2
	LOCAL:4 = 350
	LOCAL:5 = 550
	LOCAL:14 = 30
CASE 3
	LOCAL:4 = 450
	LOCAL:5 = 900
	LOCAL:14 = 0
CASE 4
	LOCAL:4 = 600
	LOCAL:5 = 1500
	LOCAL:14 = 0
CASEELSE
	LOCAL:4 = 750
	LOCAL:5 = 2200
	LOCAL:14 = 0
ENDSELECT

;ABL:Ａ感觉をみる
SELECTCASE ABL:Ａ感觉
CASE 0
	LOCAL:3 = 25
	TIMES LOCAL:4 , 0.50
CASE 1
	LOCAL:3 = 75
	TIMES LOCAL:4 , 0.80
CASE 2
	LOCAL:3 = 125
CASE 3
	LOCAL:3 = 200
	TIMES LOCAL:4 , 1.20
CASE 4
	LOCAL:3 = 300
	TIMES LOCAL:4 , 1.50
CASEELSE
	LOCAL:3 = 500
	TIMES LOCAL:4 , 2.00
ENDSELECT

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES LOCAL:3 , 0.30
CASE IS < PALAMLV:2
	TIMES LOCAL:3 , 0.60
CASE IS < PALAMLV:3
	TIMES LOCAL:3 , 1.00
CASE IS < PALAMLV:4
	TIMES LOCAL:3 , 1.50
CASE IS >= PALAMLV:4
	TIMES LOCAL:3 , 1.80
ENDSELECT

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM75
;同時押し倒し
IF TEQUIP:Ｗ押し倒し
	PRINTFORM %CALLNAME:TARGET%は
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ押し倒し中")%
	ELSEIF PREVCOM:1 == 65 || PREVCOM:1 == 66 || PREVCOM:1 == 67 || PREVCOM:1 == 75
		PRINTFORM %CALLNAME:(TARGET:1)%の下の口から解放されたばかりの
	ELSEIF PREVCOM:1 == 75 || PREVCOM:1 == 83
		PRINTFORM %CALLNAME:(TARGET:1)%から解放されたばかりの
	ENDIF
	IF INSERT("Ａインサート")
		PRINTFORMW %CALLNAME:MASTER%のペニスをアナルで締め付けながら、激しく腰を動かして愉しんでいる…
	ELSEIF INSERT("Ｖインサート")
		PRINTFORMW %CALLNAME:MASTER%のペニスを膣から抜き、今度はアナルで呑み込んできた…
	ELSEIF INSERT("尿道インサート")
		PRINTFORMW %CALLNAME:MASTER%のペニスを尿道から抜き、今度は膣で根元まで呑み込んできた…
	ELSE
		PRINTFORMW %CALLNAME:MASTER%のペニスにまたがり、ゆっくりとアナルで呑み込んできた…
	ENDIF
	;アナルを露出させる
	SIF CHECK_CLO("Ａ露出") == 0
		CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
;押し倒されているとき
ELSEIF TEQUIP:押し倒し
	;前回と今回の調教者が同じで、抜かないフラグがある場合
	IF INSERT("インサート") && COND("前Ｔ調教者") == PLAYER
		;ワイルドスウィング
		IF INSERT("ワイルドスウィング")
			CALL TEXT, "ワイルドスウィング"
		;前回の調教が騎乗位
		ELSEIF INSERT("Ｖインサート")
			;調教者がオトコかふたなりである
			IF PENIS(PLAYER)
				PRINTFORMW %CALLNAME:TARGET%は腰を浮かして膣から抜いたペニスをアナルで呑み込むと、再び腰を動かし始めた…
			ELSE
			;ペニスバンドを使用している場合
				PRINTFORMW %CALLNAME:TARGET%は腰を浮かして張型をアナルで呑み込むと、再び腰を動かし始めた…
			ENDIF
		ELSEIF INSERT("Ａインサート")
			IF TEQUIP:亲吻
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%を解放せず、%TEXTS("キス継続")%再び腰を動かし始めた…
				CALL SETFLAG, "追加キスフラグ", TARGET
			ELSE
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%を解放せず、ペニスを締め付けたまま再び腰を動かし始めた…
				CALL TEXT, "追加地の文：騎乗位"
			ENDIF
		ELSEIF INSERT("尿道インサート")
			;調教者がオトコかふたなりである
			IF PENIS(PLAYER)
				PRINTFORMW %CALLNAME:TARGET%は腰を浮かして今度はペニスをアナルで呑み込むと、再び腰を動かし始めた…
			ELSE
			;ペニスバンドを使用している場合
				PRINTFORMW %CALLNAME:TARGET%は腰を浮かして今度はアナルで呑み込むと、再び腰を動かし始めた…
			ENDIF
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:TARGET%は
		;アナルが露出していないで、パンツをずらしていないなら自動的にずらす
		SIF !CHECK_CLO("Ａ露出") && CHECK_CLO("Ａ露出可")
			CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
		PRINTFORM %CALLNAME:PLAYER%の上にまたがると、ゆっくりと腰を下ろしてアナルで
		SIF ABL:Ａ扩张 > SIZE("ペニス", PLAYER)
			PRINT 根元まで
		PRINTFORMW 呑み込んできた…
	ENDIF
ELSEIF CHECK_COM("３Ｐ奉仕")
	CALL TRAIN_MESSAGE_3P
ELSEIF INSERT("Ａインサート") && SELECTCOM == PREVCOM && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%の腰をしっかりと掴み、
	SIF (PENIS(PLAYER)) && STATE("ハードスキン", PLAYER)
		PRINT ガチガチのペニスで
	PRINTFORMW 激しくアナルを突き上げた…
;同様に、抜かないフラグがある場合
ELSEIF INSERT("インサート") && COND("前Ｔ調教者") == PLAYER
	;前回と今回の調教者が同じで、前回の調教がアナル騎乗位であり、抜かないフラグがある場合
	IF INSERT("Ａインサート") && INSERT("騎乗位") && COND("前Ｔ調教者") == PLAYER
		PRINTFORMW %CALLNAME:TARGET%は挿入されたままのペニスを締め付け、再び腰を動かし始めた…
	ELSEIF INSERT("Ａインサート")
		PRINTFORMW 腸内から抜き去ることなく%CALLNAME:TARGET%を抱き上げ、下から腰を突き上げた…
	ELSEIF INSERT("騎乗位")
		PRINTFORM %CALLNAME:TARGET%は腰を浮かして\@ PENIS(PLAYER) ? ペニス # 張型\@をアナルで呑み込むと、
		IF TEQUIP:亲吻
			PRINTFORMW %CALLNAME:PLAYER%%TEXTS("キス継続助詞付き")%腰を動かし始めた…
			CALL SETFLAG, "追加キスフラグ", TARGET
		ELSE
			PRINTFORMW 再び腰を動かし始めた…
		ENDIF
	ELSE
		PRINTFORMW %CALLNAME:TARGET%を抱き上げ、下からアナルを突き上げた…
	ENDIF
ELSE
	PRINTFORM %CALLNAME:TARGET%は
	IF COND("前Ｔから対象のみ変更") && PREVCOM:1 >= 60 && PREVCOM:1 <= 79
		CALL TEXT, "追加地の文：穴から穴へ"
	;穴開きでないパンツをはいていて、ずらされていないなら、必ずずらす
	ELSEIF !CHECK_CLO("Ａ露出") && CHECK_CLO("Ａ露出可")
		CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
	ENDIF
	IF PREVCOM == 1 || PREVCOM == 9 || (PREVCOM >= 90 && PREVCOM <= 119)
		PRINTFORM %CALLNAME:PLAYER%の上にまたがると、
	ELSEIF TALENT:好色 && RAND:2 == 0
		PRINTFORM %CALLNAME:PLAYER%のペニスに照準を合わせると、
	ELSE
		PRINTFORM %CALLNAME:PLAYER%のペニス%TEXTR("の位置を確認する", "に肛門を密着させる")%と、
	ENDIF

	IF TALENT:好色 + TALENT:淫乱 + TALENT:恋慕 && COND("視力") && RAND:3 == 0
		PRINT 顔を見つめながら
	ELSEIF TEQUIP:押し倒し && ABL:施虐倾向 >= 2 && RAND:2
		IF TALENT:高傲 + TALENT:强硬 && RANDIF(2, TALENT:自大 + TALENT:小恶魔)
			PRINT 不敵な笑みを浮かべながら
		ELSEIF TALENT:自大 + TALENT:小恶魔 && RAND:2
			PRINT ニヤニヤしながら
		ELSE
			PRINT 嗜虐的な悦びを滲ませながら
		ENDIF
	ELSEIF ABL:Ａ感觉 >= 3 && RAND:2
		IF RAND:2
			PRINT 甘い吐息を漏らしながら
		ELSE
			PRINT 気持ち良さそうな顔を見せながら
		ENDIF
	ELSE
		PRINT 熱っぽい息を吐きながら
	ENDIF
	IF ABL:Ａ扩张 >= SIZE("ペニス", PLAYER) && ABL:施虐倾向 && RAND:2
		PRINT 一気に
	ELSE
		PRINT ゆっくりと
	ENDIF
	IF RAND:2
		PRINTFORM %TEXTS("お尻：視覚")%を下ろして
	ELSE
		PRINTFORM 腰を下ろして
	ENDIF
	IF CHECK("Ａインサート") && RAND:3
		IF ABL:Ａ扩张 > SIZE("ペニス", PLAYER) && RAND:2
			PRINTW 、後ろの穴でペニスを呑み込んで腰を動かしはじめた…
		ELSE
			PRINTW 、アナルでペニスを呑み込んできた…
		ENDIF
	ELSE
		PRINTW アナルで扱いてきた…
	ENDIF
ENDIF


@COM_ORDER_COM75, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;一定のＡ経験かＡ感覚が必要
IF EXP:Ａ经验 < 100 && ABL:Ａ感觉 < 3
	SELECTCASE EXP:Ａ经验
	CASE 0
		LOCAL:1 += 50
	CASE IS < 5
		LOCAL:1 += 45
	CASE IS < 20
		LOCAL:1 += 40
	CASE IS < 50
		LOCAL:1 += 35
	CASE IS < 100
		LOCAL:1 += 30
	ENDSELECT
	SELECTCASE ABL:Ａ感觉
	CASE 0
		LOCAL:1 += 50
	CASE 1
		LOCAL:1 += 40
	CASE 2
		LOCAL:1 += 30
	ENDSELECT
	LOCAL -= LOCAL:1
	IF EXP:Ａ经验 < 100
		LOCALS = %LOCALS% - Ａ経験不足({LOCAL:1})
	ELSEIF ABL:Ａ感觉 < 3
		LOCALS = %LOCALS% - Ａ感覚不足({LOCAL:1})
	ENDIF
ELSEIF ABL:Ａ感觉
	LOCAL += ABL:Ａ感觉 * 2
	LOCALS = %LOCALS% + %ABLNAME:5%LV{ABL:Ａ感觉}({ABL:Ａ感觉 * 2})
ENDIF

;ABL:侍奉精神
IF ABL:侍奉精神
	LOCAL += ABL:侍奉精神 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:侍奉精神}({ABL:侍奉精神 * 4})
ENDIF
;ABL:精液上瘾
IF ABL:精液上瘾
	LOCAL += ABL:精液上瘾 * 3
	LOCALS = %LOCALS% + %ABLNAME:11%LV{ABL:精液上瘾}({ABL:精液上瘾 * 3})
ENDIF

;PALAM:Ａ潤（の不足）
SELECTCASE PALAM:Ａ潤
CASE IS < 3000
	LOCAL -= 20
	LOCALS = %LOCALS% - Ａ潤不足(20)
CASE IS < 10000
	LOCAL -= 5
	LOCALS = %LOCALS% - Ａ潤不足(5)
ENDSELECT

;強気
IF TALENT:强硬
	LOCAL += 10
	LOCALS = %LOCALS% + %TALENTNAME:18%(10)
ENDIF
;恥じらい
IF TALENT:羞耻心强
	LOCAL -= 20
	LOCALS = %LOCALS% - %TALENTNAME:35%(20)
ENDIF
;Ａ損傷
IF TALENT:Ａ损伤
	LOCAL -= 30 * TALENT:Ａ损伤
	LOCALS = %LOCALS%- %TALENTNAME:45%({30 * TALENT:Ａ损伤})
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;快感の否定
IF TALENT:否认快感
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF

;媚薬
IF TEQUIP:媚薬
	LOCAL += 6
	LOCALS = %LOCALS% + %ITEMNAME:31%(6)
ENDIF

;合計を表示(70以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 70

RETURN RESULT
