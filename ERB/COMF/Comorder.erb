@SHOW_COM_ORDER_COM, ARG, ARG:1
;ARG:1が1ならテキストが出力されない
TRYCALLFORM COM_ORDER_COM{ARG}, ARG:1
TCVAR:28 = RESULT

@PRINT_COM_ORDER_COM, ARGS, ARG, ARG:1, ARG:2
IF ARG == 0
	PRINTFORM %ARGS% = {ARG:1}
	SELECTCASE ARG:1
	CASE IS < ARG:2
		PRINT <
	CASE ARG:2
		PRINT =
	CASEELSE
		PRINT >
	ENDSELECT
	PRINTFORMW  実行値{ARG:2}
ENDIF

;RESULT > 0で実行値クリア
RETURN MAX(ARG:1 - ARG:2 + 1, 0)


@COM_ORDER, ARG
VARSET LOCAL
;少しは早くなるか…？
IF TCVAR:27 && ARG
	LOCAL = TCVAR:27
	RETURN LOCAL
ENDIF
LOCALS =
;夢魔
IF TALENT:梦魔 || TALENT:上位梦魔
	LOCAL += 5
	LOCALS = %LOCALS%夢魔(5)
ELSEIF TALENT:魔王残片
	LOCAL -= 5
	LOCALS = %LOCALS%魔王のかけら(-5)
ELSE
	LOCALS = %LOCALS%夢魔でない(0)
ENDIF

;ABL:服从
IF ABL:服从
	LOCAL += ABL:服从 * 4
	LOCALS = %LOCALS% + %ABLNAME:0%LV{ABL:服从}({ABL:服从 * 4})
ENDIF
;ABL:欲望
IF ABL:欲望
	LOCAL += ABL:欲望 * 1
	LOCALS = %LOCALS% + %ABLNAME:1%LV{ABL:欲望}({ABL:欲望 * 1})
ENDIF

;レズorＢＬ調教
IF (TALENT:PLAYER:女性 && TALENT:女性) || (TALENT:PLAYER:男性 && TALENT:男性)
	IF TALENT:PLAYER:女性 && TALENT:女性
		;ABL:百合倾向
		IF ABL:百合倾向
			LOCAL += ABL:百合倾向*2
			LOCALS = %LOCALS% + %ABLNAME:9%LV{ABL:百合倾向}({ABL:百合倾向*2})
		ELSE
			LOCAL -= 10
			LOCALS = %LOCALS% - レズっ気無し(10)
		ENDIF
	ELSEIF TALENT:PLAYER:男性 && TALENT:男性
		;ABL:ＢＬ倾向
		IF ABL:ＢＬ倾向
			LOCAL += ABL:ＢＬ倾向*2
			LOCALS = %LOCALS% + %ABLNAME:13%LV{ABL:ＢＬ倾向}({ABL:ＢＬ倾向*2})
		ELSE
			LOCAL -= 10
			LOCALS = %LOCALS% - ＢＬっ気無し(10)
		ENDIF
	ENDIF
	;TALENT:双性恋
	IF TALENT:双性恋
		LOCAL += 5
		LOCALS = %LOCALS% + %TALENTNAME:81%(5)
	ENDIF
	;好奇心
	IF TALENT:好奇心
		LOCAL += 7
		LOCALS = %LOCALS% + %TALENTNAME:23%(7)
	ENDIF
	;保守的
	IF TALENT:保守
		LOCAL -= 13
		LOCALS = %LOCALS% - %TALENTNAME:24%(13)
	ENDIF
;ＢＬ調教
ELSE
	;好奇心
	IF TALENT:好奇心
		LOCAL += 5
		LOCALS = %LOCALS% + %TALENTNAME:23%(5)
	ENDIF
	;保守的
	IF TALENT:保守
		LOCAL -= 10
		LOCALS = %LOCALS% - %TALENTNAME:24%(10)
	ENDIF
ENDIF

;刻印チェック
IF MARK:痛苦刻印
	LOCAL += MARK:痛苦刻印 * 5
	LOCALS = %LOCALS% + %MARKNAME:0%LV{MARK:痛苦刻印}({MARK:痛苦刻印 * 5})
ENDIF

IF MARK:快乐刻印
	LOCAL += MARK:快乐刻印 * 1
	LOCALS = %LOCALS% + %MARKNAME:1%LV{MARK:快乐刻印}({MARK:快乐刻印 * 1})
ENDIF
IF MARK:淫纹 && TALENT:男性 == 0
	LOCAL += MARK:淫纹 * 1
	LOCALS = %LOCALS% + %MARKNAME:8%LV{MARK:淫纹}({MARK:淫纹 * 1})
ENDIF

IF TALENT:高傲
	LOCAL:1 = 6
ELSEIF TALENT:自卑
	LOCAL:1 = 2
ELSE
	LOCAL:1 = 3
ENDIF
IF MARK:屈服刻印
	LOCAL += MARK:屈服刻印 * LOCAL:1
	LOCALS = %LOCALS% + %MARKNAME:2%LV{MARK:屈服刻印}({MARK:屈服刻印 * LOCAL:1})
ENDIF

SELECTCASE CONDS("機嫌")
CASE "様子見"
	LOCAL -= 15
	LOCALS = %LOCALS% - 様子見(15)
CASE "やる気なし"
	LOCAL -= 30
	LOCALS = %LOCALS% - やる気なし(30)
ENDSELECT

IF TALENT:高傲
	LOCAL:1 = 4
ELSEIF TALENT:自卑
	LOCAL:1 = 1
ELSE
	LOCAL:1 = 2
ENDIF
IF MARK:排斥刻印
	LOCAL -= MARK:排斥刻印 * 2 * LOCAL:1
	LOCALS = %LOCALS% - %MARKNAME:3%LV{MARK:排斥刻印}({MARK:排斥刻印 * 2 * LOCAL:1})
ENDIF

SELECTCASE ABS(CFLAG:2)
CASE IS < 20
	LOCAL:1 = 0
CASE IS < 50
	LOCAL:1 = 1
CASE IS < 90
	LOCAL:1 = 2
CASE IS < 150
	LOCAL:1 = 3
CASE IS < 220
	LOCAL:1 = 4
CASE IS < 300
	LOCAL:1 = 5
CASE IS < 780
	LOCAL:1 = (CFLAG:2-300)/80 + 6
CASE IS < 1500
	LOCAL:1 = (CFLAG:2-780)/120 + 12
CASE IS < 17500
	LOCAL:1 = (CFLAG:2-1500)/500 + 18
CASEELSE
	LOCAL:1 = 50
ENDSELECT

SIF CFLAG:2 < 0
	LOCAL:1 = (-LOCAL:1) * 2

SIF TALENT:高傲 == 0 && LOCAL:1 > 0
	TIMES LOCAL:1 , 0.50

;好感度(補正のMAXは50)
IF LOCAL:1 && PLAYER == MASTER
	LOCAL += LOCAL:1
	IF LOCAL:1 > 0
		LOCALS = %LOCALS% + 好感度({LOCAL:1})
	ELSEIF LOCAL:1 < 0
		LOCALS = %LOCALS% - 好感度({-LOCAL:1})
	ENDIF
ENDIF

;PALAM:欲情
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	LOCAL:1 = 0
CASE IS < PALAMLV:2
	LOCAL:1 = 1
CASE IS < PALAMLV:3
	LOCAL:1 = 2
CASE IS < PALAMLV:4
	LOCAL:1 = 3
CASE IS < PALAMLV:5
	LOCAL:1 = 4
CASEELSE
	LOCAL:1 = 5
ENDSELECT
IF LOCAL:1
	LOCAL += LOCAL:1 * 1
	LOCALS = %LOCALS% + %PALAMNAME:5%LV{LOCAL:1}({LOCAL:1 * 1})
ENDIF

;PALAM:恐惧
SELECTCASE PALAM:恐惧
CASE IS < PALAMLV:1
	LOCAL:1 = 0
CASE IS < PALAMLV:2
	LOCAL:1 = 1
CASE IS < PALAMLV:3
	LOCAL:1 = 2
CASE IS < PALAMLV:4
	LOCAL:1 = 3
CASE IS < PALAMLV:5
	LOCAL:1 = 4
CASEELSE
	LOCAL:1 = 5
ENDSELECT
IF LOCAL:1
	LOCAL += LOCAL:1 * 2
	LOCALS = %LOCALS% + %PALAMNAME:10%LV{LOCAL:1}({LOCAL:1 * 2})
ENDIF

;反抗的
IF TALENT:叛逆
	LOCAL -= 10
	LOCALS = %LOCALS% - %TALENTNAME:11%(10)
ENDIF
;気丈
IF TALENT:刚强
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:12%(5)
ENDIF
;素直
IF TALENT:坦率
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:13%(5)
ENDIF
;高慢(押し倒してるなら無視)
IF TALENT:高傲 && TEQUIP:推到 == 0
	LOCAL -= 15
	LOCALS = %LOCALS% - %TALENTNAME:15%(15)
ENDIF
;プライド低い
IF TALENT:自卑
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:17%(5)
ENDIF
;目立ちたがり
IF TALENT:喜欢被关注
	LOCAL += 2
	LOCALS = %LOCALS% + %TALENTNAME:28%(2)
ENDIF
;抑圧
IF TALENT:压抑
	LOCAL -= 10
	LOCALS = %LOCALS% - %TALENTNAME:32%(10)
ENDIF
;抵抗
IF TALENT:抵抗
	LOCAL -= 20
	LOCALS = %LOCALS% - %TALENTNAME:34%(20)
ENDIF

;好色
IF TALENT:好色
	LOCAL += 10
	LOCALS = %LOCALS% + %TALENTNAME:75%(10)
ENDIF

;淫乱
IF TALENT:淫乱
	LOCAL += 10
	LOCALS = %LOCALS% + %TALENTNAME:76%(10)
ENDIF

;男嫌い
IF TALENT:讨厌男性 && TALENT:PLAYER:男性
	LOCAL -= 10
	LOCALS = %LOCALS% - %TALENTNAME:82%(10)
ENDIF

;相思相愛
IF TALENT:相思相爱 && PLAYER == MASTER
	LOCAL += 10
	LOCALS = %LOCALS% + %TALENTNAME:211%(10)
;恋慕
ELSEIF TALENT:恋慕 && PLAYER == MASTER
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:85%(5)
ENDIF

;妄信
IF TALENT:盲信 && PLAYER == MASTER
	LOCAL += 20
	LOCALS = %LOCALS% + %TALENTNAME:86%(20)
ENDIF

;ドランク
IF STATE("悪酔い", TARGET)
	LOCAL += 20
	LOCALS = %LOCALS% + 悪酔い(20)
ELSEIF STATE("ドランク", TARGET)
	LOCAL += 10
	LOCALS = %LOCALS% + ドランク(10)
ENDIF

;魔導人形
IF TALENT:魔导人偶
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:154%(5)
ENDIF

;誓約
IF TALENT:誓约 && PLAYER == MASTER
	LOCAL += 3
	LOCALS = %LOCALS% + %TALENTNAME:210%(3)
ENDIF

;気力０
IF BASE:气力 <= 0
	LOCAL -= 30
	LOCALS = %LOCALS% - 気力０(30)
ENDIF

;調教者の素質
;魅惑
IF TALENT:PLAYER:魅惑
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:91%(5)
ENDIF
;謎の魅力
IF TALENT:PLAYER:谜之魅力
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:92%(5)
ENDIF
IF TALENT:PLAYER:鼓舞
	LOCAL += 10
	LOCALS = %LOCALS% + %TALENTNAME:118%(10)
ENDIF

;館内露出
IF TEQUIP:馆内露出Play
	LOCAL -= 5
	LOCALS = %LOCALS% - 館内露出(5)
ENDIF

;撮影中
IF TEQUIP:录像带
	LOCAL -= 10
	LOCALS = %LOCALS% - 撮影中(10)
ENDIF

;野外プレイ
IF TEQUIP:野外Play
	IF COND("人里")
		LOCAL -= 10
		LOCALS = %LOCALS% - 野外(人里)(10)
	ELSE
		LOCAL -= 5
		LOCALS = %LOCALS% - 野外(5)
	ENDIF
ENDIF

;押し倒し時のボーナスを追加
IF TEQUIP:推到
	LOCAL += 10
	LOCALS = %LOCALS% + 押し倒され特別ボーナス(10)
ENDIF

;助手の素質
;鼓舞
IF ASSI
	IF TALENT:ASSI:鼓舞
		LOCAL += 10
		LOCALS = %LOCALS% + %TEXTS("助手の名称")%の鼓舞(10)
	ENDIF
ENDIF

;インセンス
IF TEQUIP:MASTER:薰香
	LOCAL:1 = 0
	SELECTCASE GETBITS(TEQUIP:MASTER:薰香, 4, 6)
	CASE 1p4
		LOCAL:1 = 1
	CASE 1p5
		LOCAL:1 = 3
	CASE 1p6
		LOCAL:1 = 6
	ENDSELECT
	LOCALS = %LOCALS% + %TEXTS("薰香", TEQUIP:MASTER:薰香)%({LOCAL:1})
	LOCAL += LOCAL:1
ENDIF

;甘い言葉効果発揮中
IF TEQUIP:甜言蜜语 >= 1 && TEQUIP:甜言蜜语 <= 8
	IF TEQUIP:甜言蜜语 <= 4
		LOCAL:1 = 7
	ELSE
		LOCAL:1 = 5
	ENDIF
	LOCAL += LOCAL:1
	LOCALS = %LOCALS% + 甘い言葉({LOCAL:1})
ENDIF

;演奏(雨上がりの輪舞曲)
IF TEQUIP:演奏中 == 3
	IF TEQUIP:MASTER:演奏中
		LOCAL += TALENT:MASTER:歌唱技能*3
		LOCALS = %LOCALS% + 雨上がりの輪舞曲({TALENT:MASTER:歌唱技能*3})
	ELSE
		LOCAL += TALENT:ASSI:歌唱技能*3
		LOCALS = %LOCALS% + 雨上がりの輪舞曲({TALENT:ASSI:歌唱技能*3})
	ENDIF
ENDIF

;ムード
LOCAL:1 = MOOD() / 10
IF LOCAL:1
	LOCAL += LOCAL:1
	LOCALS = %LOCALS% + ムード({LOCAL:1})
ENDIF

;アクセサリ効果（実行値補正）
IF TALENT:PLAYER:判定值
	LOCAL += TALENT:PLAYER:判定值
	LOCALS = %LOCALS% + アクセサリ効果({TALENT:PLAYER:判定值})
ENDIF

;相性チェック
SELECTCASE RELATION:(NO:PLAYER)
CASE 1 TO 30
	LOCAL -= 30
	LOCALS = %LOCALS% - %TEXTS("助手の名称")%との相性が最悪(30)
CASE 31 TO 50
	LOCAL -= 15
	LOCALS = %LOCALS% - %TEXTS("助手の名称")%との相性が悪い(15)
CASE 51 TO 80
	LOCAL -= 5
	LOCALS = %LOCALS% - %TEXTS("助手の名称")%との相性がいまいち(5)
CASE 120 TO 149
	LOCAL += 5
	LOCALS = %LOCALS% + %TEXTS("助手の名称")%との相性なかなか(5)
CASE 150 TO 199
	LOCAL += 15
	LOCALS = %LOCALS% + %TEXTS("助手の名称")%との相性が良好(15)
CASE IS >= 200
	LOCAL += 30
	LOCALS = %LOCALS% + %TEXTS("助手の名称")%との相性が最高(30)
ENDSELECT

IF ASSI
	SELECTCASE RELATION:(NO:ASSI)
	CASE 1 TO 30
		LOCAL -= 30
		LOCALS = %LOCALS% - 相性最悪(30)
	CASE 31 TO 50
		LOCAL -= 15
		LOCALS = %LOCALS% - 相性悪い(15)
	CASE 51 TO 80
		LOCAL -= 5
		LOCALS = %LOCALS% - 相性いまいち(5)
	CASE 120 TO 149
		LOCAL += 5
		LOCALS = %LOCALS% + 相性なかなか(5)
	CASE 150 TO 199
		LOCAL += 15
		LOCALS = %LOCALS% + 相性良好(15)
	CASE IS >= 200
		LOCAL += 30
		LOCALS = %LOCALS% + 相性最高(30)
	ENDSELECT
ENDIF

SIF ARG == 0
	PRINTFORM %LOCALS%

;@COMORDERでの実行値を記録する
TCVAR:27 = LOCAL

RETURN LOCAL


;ARGSにPLAYERの場所、ARGS:1にTARGETの場所
@COM_ORDER_STAIN(ARGS, ARGS:1)
#FUNCTION
#DIM STAIN_TARGET
#DIM STAIN_PLAYER

;汚れ無視
SIF TALENT:无视污臭
	RETURNF 0

SELECTCASE ARGS
CASE "Ｍ", "手", "Ｐ", "Ｖ", "Ａ", "Ｂ"
	STAIN_PLAYER = STAIN:PLAYER:ARGS
CASEELSE
	RETURNF 0
ENDSELECT

SELECTCASE ARGS:1
CASE "Ｍ", "手", "Ｐ", "Ｖ", "Ａ", "Ｂ"
	STAIN_TARGET = STAIN:(ARGS:1)
CASEELSE
	STAIN_TARGET = 0
ENDSELECT

VARSET LOCAL
;愛液の汚れ(レズっ気で相殺)
IF CHECK_STAIN(STAIN_PLAYER, "Ｖ")
	LOCAL:0 = 17 - ABL:百合倾向*4
	SIF TALENT:男性
		LOCAL:0 -= 6
	SIF TALENT:弱气味
		LOCAL:0 -= 2
	SIF COND("夢魔")
		LOCAL:0 -= 5
	;場所自体がＶでお風呂上りならペナ軽減
	SIF LOCAL:0 > 0 && ARGS == "Ｖ" && TEQUIP:PLAYER:结束淋浴
		LOCAL:0 /= 2
ENDIF
;ペニスの汚れ
IF CHECK_STAIN(STAIN_PLAYER, "Ｐ")
	LOCAL:1 = 5
	SIF TALENT:讨厌男性 && TALENT:PLAYER:男性
		LOCAL:1 += 10
	SIF TALENT:弱气味
		LOCAL:1 -= 2
	;場所自体がＰでお風呂上りならペナ軽減
	SIF LOCAL:1 > 0 && ARGS == "Ｐ" && TEQUIP:PLAYER:结束淋浴
		LOCAL:1 /= 2
	SIF CALCF("ペニス好き度", TARGET) > 0
		LOCAL:1 -= CALCF("ペニス好き度", TARGET)
ENDIF
;精液の汚れ(夢魔、精液中毒で相殺)
IF CHECK_STAIN(STAIN_PLAYER, "精液")
	SIF ABL:精液上瘾
		LOCAL:2 = 6 - ABL:精液上瘾*2 - (TALENT:弱气味 > 0)
	SIF COND("夢魔") || TALENT:吸精体质
		LOCAL:2 -= 6
ENDIF
;アナルの汚れ
IF CHECK_STAIN(STAIN_PLAYER, "Ａ")
	LOCAL:3 = 65 - ABL:受虐倾向*5 - ABL:施虐倾向*2
	SIF COND("夢魔")
		LOCAL:3 -= 10
	SIF TALENT:尻穴狂
		LOCAL:3 -= 20
	SIF TALENT:抖Ｍ
		LOCAL:3 -= 10
	SIF TALENT:抖Ｓ
		LOCAL:3 -= 10
	SIF TALENT:弱气味 && TALENT:恋慕
		LOCAL:3 /= 2
	;場所自体がＡでお風呂上りならペナ軽減
	SIF LOCAL:3 > 0 && ARGS == "Ａ" && TEQUIP:PLAYER:结束淋浴
		LOCAL:3 /= 2
ENDIF
;母乳の汚れ
IF CHECK_STAIN(STAIN_PLAYER, "乳")
	LOCAL:4 += 1
	;幼稚で相殺
	SIF TALENT:幼儿／幼儿退行 || TALENT:幼稚
		LOCAL:4 -= 4
ENDIF
;尿の汚れ
IF CHECK_STAIN(STAIN_PLAYER, "尿")
	LOCAL:5 += 15
	SIF TALENT:弱气味 && TALENT:恋慕
		LOCAL:5 /= 2
	SIF COND("夢魔")
		LOCAL:5 -= 7
ENDIF
;触手の汚れ
IF CHECK_STAIN(STAIN_PLAYER, "触手")
	LOCAL:6 += 20
	SIF TALENT:弱气味 && TALENT:恋慕
		LOCAL:6 /= 2
	;触手経験,旧支配者の名残
	SELECTCASE EXP:触手经验 + TALENT:旧支配者的末裔*500
	CASE IS >= 500
		LOCAL:6 -= 20
	CASE IS >= 300
		LOCAL:6 -= 15
	CASE IS >= 100
		LOCAL:6 -= 10
	CASE IS >= 20
		LOCAL:6 -= 5
	ENDSELECT
ENDIF
;アソコの恥垢汚れ
IF CHECK_STAIN(STAIN_PLAYER, "Ｖ恥垢")
	LOCAL:20 = 20
	SIF TALENT:弱气味 && TALENT:恋慕
		LOCAL:20 /= 2
	;場所自体がＶでお風呂上りならペナ軽減
	SIF LOCAL:20 > 0 && ARGS == "Ｖ" && TEQUIP:PLAYER:结束淋浴
		LOCAL:20 /= 2
ENDIF
;ちんかすの汚れ
IF CHECK_STAIN(STAIN_PLAYER, "Ｐ恥垢")
	LOCAL:21 = 20
	SIF TALENT:讨厌男性 && TALENT:PLAYER:男性
		LOCAL:21 += 10
	SIF TALENT:弱气味 && TALENT:恋慕
		LOCAL:21 /= 2
	;場所自体がＰでお風呂上りならペナ軽減
	SIF LOCAL:21 > 0 && ARGS == "Ｐ" && TEQUIP:PLAYER:结束淋浴
		LOCAL:21 /= 2
	IF CALCF("ペニス好き度", TARGET) > 0
		LOCAL:21 /= CALCF("ペニス好き度", TARGET)
		SIF TALENT:献身
			LOCAL:21 -= 5
		SIF TALENT:性变态
			LOCAL:21 -= 5
	ENDIF
ENDIF


;もともとある汚れは無視する＆場所によっては汚れの強さＵＰ
SELECTCASE ARGS:1
CASE "Ｍ"
	LOCAL:3 = MIN(MULTIPLY(LOCAL:3, 150), LOCAL:3 + 20)
CASE "Ｐ"
	LOCAL:0 = 0
	LOCAL:1 = 0
	LOCAL:2 = 0
	LOCAL:5 = 0
CASE "Ｖ"
	LOCAL:0 = 0
	LOCAL:1 = 0
	LOCAL:3 = MIN(MULTIPLY(LOCAL:3, 200), LOCAL:3 + 50)
CASE "Ａ"
	LOCAL:0 = 0
	LOCAL:1 = 0
	LOCAL:3 = 0
CASE "尿道"
	LOCAL:5 = 0
	LOCAL:3 = MIN(MULTIPLY(LOCAL:3, 200), LOCAL:3 + 50)
ENDSELECT

LOCAL = LOCAL:0 + LOCAL:1 + LOCAL:2 + LOCAL:3 + LOCAL:4 + LOCAL:5 + LOCAL:6 + LOCAL:20 + LOCAL:21

SIF TALENT:污臭钝感
	LOCAL = MAX(DIVIDE(LOCAL, 200), LOCAL - 20)
SIF TALENT:污臭敏感
	LOCAL = MIN(MULTIPLY(LOCAL, 200), LOCAL + 20)

LOCAL = MAX(LOCAL, 0)

RETURNF LOCAL
