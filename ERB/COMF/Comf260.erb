@COM260
;ローション
CALL PRINT_TRAIN_NAME(SELECTCOM)

;実際にローションの数値が反映されるまでの間の仮潤滑操作
PALAM:Ａ潤 += 10000
SIF TALENT:オトコ == 0
	PALAM:Ｖ潤 += 10000
SIF PENIS(TARGET)
	PALAM:Ｐ潤 += 10000

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

PALAM:Ａ潤 -= 10000
SIF TALENT:オトコ == 0
	PALAM:Ｖ潤 -= 10000
SIF PENIS(TARGET)
	PALAM:Ｐ潤 -= 10000

CALL SETFLAG, "潤滑追加", 10000

SETBIT TEQUIP:ローション, 0
ITEM:ローション -= 1

RETURN 1

;ローションマッサージ中の変化はすべてここに書く
@FLAG_COM260, ARG
IF CHECK_COM("ローションマッサージ", SELECTCOM)
	CALL FLAG_COM2, 30
	CALL FLAG_COM13, 30
	CALL FLAG_COM14, 30
	SIF CHECK_COM("ローションマッサージ", SELECTCOM) == 260
		CALL FLAG_COM0, 100
	SETBIT TEQUIP:ローション, 1
ENDIF

@SOURCE_COM260, ARG
VARSET LOCAL
IF CHECK_COM("ローションマッサージ", SELECTCOM)
	LOCAL:11 = 500
	CALL SOURCE_COM2, 30
	CALL SOURCE_COM13, 30
	CALL SOURCE_COM14, 30
	SIF CHECK_COM("ローションマッサージ", SELECTCOM) == 260
		CALL SOURCE_COM0, 100
ENDIF

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM260
IF CHECK_COM("ローションマッサージ", SELECTCOM)
	PRINTFORM %CALLNAME:TARGET%の
	IF TALENT:小尻
		IF TALENT:力持ち && RAND:3 == 0
			PRINTFORM 引き締まったお尻
		ELSEIF RAND:3 == 0
			IF AGE(TARGET) <= 2
				PRINTFORM 張りと滑らかさを併せ持った尻の柔肉
			ELSE
				PRINTFORM 小振りながらも柔らかさも併せ持った尻の柔肉
			ENDIF
		ELSEIF RAND:2
			PRINTFORM 肉付きの薄いお尻
		ELSE
			PRINTFORM 小ぶりなお尻
		ENDIF
	ELSE
		IF RAND:4 == 0
			SIF TALENT:巨尻
				PRINTFORM 肉乎乎的
			SELECTCASE CONDS("肌の色")
			CASE "褐色肌"
				PRINTFORM 褐色
			CASE "色白"
				PRINTFORM 苍白
			CASEELSE
				PRINTFORM 艳丽
			ENDSELECT
			PRINTFORM 双丘
		ELSEIF RAND:3 == 0
			PRINTFORM ふっくらと丸みを帯びた腰周りの肉
		ELSEIF RAND:2
			PRINTFORM \@ TALENT:巨尻 ? お餅の様に # \@柔らかい尻肉
		ELSE
			PRINTFORM むっちりとしたお尻
		ENDIF
	ENDIF

	IF TALENT:巨尻 && RAND:3 == 0
		PRINTFORM に指をめり込ませて%TEXTR("揉みしだき", "存分に捏ね", "触り心地を堪能し")%ながら、
	ELSEIF RAND:3 == 0
		PRINTFORM を左右に押し開いて
		IF TALENT:オトコ == 0 && RAND:2
			PRINTFORM アソコ
		ELSE
			PRINTFORM アナル
		ENDIF
		PRINTFORM を観察しながら、
	ELSEIF RAND:2
		PRINTFORM を鷲掴んで%TEXTR("窄まり", "肛門")%の皺を%TEXTR("親指の腹で撫で", "優しく愛撫し", "丹念に指で撫で")%ながら、
	ELSE
		PRINTFORM の感触を手の平で%TEXTR("堪能し", "味わい", "楽しみ")%ながら、
	ENDIF

	PRINTFORMW ローションを股間の粘膜%TEXTR("に", "へと念入りに")%%TEXTR("塗り広げ", "馴染ませ")%た…
ELSE
	PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%の全身にローションをぬりたくった…
ENDIF

IF SELECTCOM == 260
	PRINTFORM %CALLNAME:TARGET%の
	IF TALENT:小柄体型
		PRINT 小さな
	ELSEIF TALENT:巨乳
		PRINT 豊満な
	ELSEIF TALENT:長身
		PRINT すらっとした
	ENDIF
	PRINTFORM 身体は塗り広げられたローションでてらてらと光を反射し、
	IF TALENT:スライム
		PRINTFORM いつにも増して
	ELSEIF TALENT:小柄体型 || COND("近親")
		PRINTFORM 背徳的で
	ELSEIF TALENT:清楚 || TALENT:気品
		PRINTFORM いつもの%CALLNAME:TARGET%にはない
	ENDIF
	PRINTFORM 妖しい魅力を
	IF RAND:2
		PRINTFORMW 振りまいている……
	ELSE
		PRINTFORMW 感じずには居られない…
	ENDIF
ENDIF
