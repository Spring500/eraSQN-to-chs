@COM171
;野外プレイ
;TEUIP:54のビットが 0人里, 1森林, 2花園, 3水辺, 4海
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

IF TEQUIP:野外プレイ == 0
	PREVCOM = 171
	RETURN 0
ELSEIF GETBIT(TFLAG:16, 0)
	STR:0 = 野外プレイ(人里)
ELSEIF GETBIT(TFLAG:16, 1)
	STR:0 = 野外プレイ(森林)
ELSEIF GETBIT(TFLAG:16, 2)
	STR:0 = 野外プレイ(花園)
ELSEIF GETBIT(TFLAG:16, 3)
	STR:0 = 野外プレイ(水辺)
ELSEIF GETBIT(TFLAG:16, 4)
	STR:0 = 野外プレイ(海)
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM171
#DIM MEMO_KISEKAE
#DIM MEMO_LINECOUNT

IF TEQUIP:野外プレイ == 0
	PRINTFORML %CALLNAME:TARGET%を何処に連れ出しますか？
	PRINTFORML 
	PRINTL  [ 0] 人里に連れて行く
	PRINTL  [ 1] 森林に連れて行く
	PRINTL  [ 2] 花園に連れて行く
	PRINTL  [ 3] 水辺に連れて行く
	PRINTL  [ 4] 海に連れて行く
	PRINTL [100] やめる
	CALL INPUT_SELECT, 5, 100
ELSEIF TEQUIP:野外プレイ
	;野外プレイ行き先フラグ
	TFLAG:16 = TEQUIP:野外プレイ
	PRINTFORMW 部屋に戻ってきた…
	TEQUIP:野外プレイ = 0
	SIF ASSI
		TEQUIP:ASSI:野外プレイ = 0
	CALL KOJO_MESSAGE_COM
	RETURN 1
ENDIF

IF RESULT == 100
	TFLAG:14 = 1
	RETURN 0
ELSEIF RESULT == 0
	LOCALS = 人里
ELSEIF RESULT == 1
	LOCALS = 森林
ELSEIF RESULT == 2
	LOCALS = 花園
ELSEIF RESULT == 3
	LOCALS = 水辺
ELSEIF RESULT == 4
	LOCALS = 海
ENDIF

SETBIT TEQUIP:野外プレイ, RESULT
SIF ASSI
	TEQUIP:ASSI:野外プレイ = TEQUIP:野外プレイ

;野外プレイ行き先フラグ
TFLAG:16 = TEQUIP:野外プレイ

;着せ替えさせた状態をチェック
MEMO_KISEKAE = 0

;選択肢の存在判定に用いる
VARSET LOCAL
;選択肢の二重出現を防ぐ
MEMO_LINECOUNT = LINECOUNT

;外出時の服装は登録されている？
SELECTCASE LOCALS
CASE "水辺", "海"
	IF GETBIT(IS_FAVORITE_DRESS(TARGET), 7)
		PRINTFORML %CALLNAME:TARGET%を水着に着替えさせますか？
		SETCOLOR DEF_COLOR("黄色")
		PRINTFORM 水着：
		CALL PRINT_FAVORITE_DRESS, 7
		PRINTFORML 
		RESETCOLOR
		PRINTFORML 
		PRINTFORML  [ 0] はい
		PRINTFORML  [ 1] いいえ
		IF EQUIP:靴 == 0 && CHECK_FAVORITE("靴", 7)
			PRINTFORML  [ 2] 靴だけ履かせる
			LOCAL:2 = 2
		ENDIF
		IF EQUIP:パンツ
			PRINTFORML  [ 3] パンツだけ脱がせる
			LOCAL:3 = 3
		ENDIF

		CALL INPUT_SELECT, 2, LOCAL:2, LOCAL:3

		SELECTCASE RESULT
		CASE 0
			CALL SET_FAVORITE_DRESS, TARGET, 7
			MEMO_KISEKAE = 1
		CASE 2
			EQUIP:靴 = CHECK_FAVORITE("靴", 7)
			CSTR:47 = %CHECK_FAVORITES("靴", 7)%
			MEMO_KISEKAE = 2
		CASE 3
			CALL SET_PANTIES, TARGET
			MEMO_KISEKAE = 3
		ENDSELECT
		PRINTL 
	ENDIF
CASEELSE
	IF GETBIT(IS_FAVORITE_DRESS(TARGET), 8)
		PRINTFORML %CALLNAME:TARGET%を余所行きの服装に着替えさせますか？
		SETCOLOR DEF_COLOR("黄色")
		PRINTFORM 外出着：
		CALL PRINT_FAVORITE_DRESS, 8
		PRINTFORML 
		RESETCOLOR
		PRINTFORML 
		PRINTFORML  [ 0] はい
		PRINTFORML  [ 1] いいえ
		IF EQUIP:靴 == 0 && CHECK_FAVORITE("靴", 8)
			PRINTFORML  [ 2] 靴だけ履かせる
			LOCAL:2 = 2
		ENDIF
		IF EQUIP:パンツ
			PRINTFORML  [ 3] パンツだけ脱がせる
			LOCAL:3 = 3
		ENDIF

		CALL INPUT_SELECT, 2, LOCAL:2, LOCAL:3

		SELECTCASE RESULT
		CASE 0
			CALL SET_FAVORITE_DRESS, TARGET, 8
			MEMO_KISEKAE = 1
		CASE 2
			EQUIP:靴 = CHECK_FAVORITE("靴", 8)
			CSTR:47 = %CHECK_FAVORITES("靴", 8)%
			MEMO_KISEKAE = 2
		CASE 3
			CALL SET_PANTIES, TARGET
			MEMO_KISEKAE = 3
		ENDSELECT
		PRINTL 
	ENDIF
ENDSELECT
;その他の場合
IF MEMO_LINECOUNT == LINECOUNT && EQUIP:パンツ
	PRINTFORML …外出に先駆けて%CALLNAME:TARGET%のパンツを脱がせますか？
	SETCOLOR DEF_COLOR("黄色")
	PRINTFORM 現在の服装：
	CALL PRINT_DRESS, TARGET
	PRINTFORML 
	RESETCOLOR
	PRINTFORML 
	PRINTFORML  [ 0] はい
	PRINTFORML  [ 1] いいえ

	CALL INPUT_SELECT, 2

	SELECTCASE RESULT
	CASE 0
		CALL SET_PANTIES, TARGET
		MEMO_KISEKAE = 3
	ENDSELECT
	PRINTL 
ENDIF

IF MEMO_KISEKAE == 1
	PRINT 着替えさせた
ELSEIF CHECK_CLO("ハダカ")
	PRINT ハダカの
ENDIF
PRINTFORM %CALLNAME:TARGET%
IF MEMO_KISEKAE == 2
	PRINTFORM に%SHOES(TARGET, 1)%
	IF CHECK_CLO("ハダカ")
		PRINT だけ履かせて
	ELSE
		PRINT を履かせて
	ENDIF
ELSEIF MEMO_KISEKAE == 3 && CHECK_CLO("ハダカ") == 0
	PRINT からパンツを没収して
ELSEIF (MARK:屈服刻印 == 3 || TALENT:恋慕) && (ABL:受虐倾向 < 3 || RAND:5 == 0 || ITEM:颈圈 == 0)
	PRINT の手をとって
ELSE
	PRINT に首輪をつけて
ENDIF
PRINTFORMW %LOCALS%に連れ出した…
PRINTFORM %CALLNAME:TARGET%は
IF (ABL:露出癖 >= 3 || TALENT:羞耻心强) && GETBIT(TEQUIP:野外プレイ, 0)
	PRINTFORMW 何者かの視線を感じ、顔を紅潮させ震えている…
ELSEIF ABL:露出癖 >= 3
	PRINTFORMW 自分に向けられる視線を想像し、顔を紅潮させ震えている…
ELSEIF TALENT:羞耻心强
	PRINTFORMW わずかな物音にも敏感に反応し身体を震わせている…
ELSEIF TALENT:小恶魔 || TALENT:坏心眼
	PRINTFORM 歩きながら%CALLNAME:MASTER%に抱きつき、顔を見ながら
	IF TALENT:娇小体型
		PRINT 未発達な
	ELSEIF TALENT:巨乳
		PRINT 豊満な
	ELSE
		PRINT 柔らかな
	ENDIF
	PRINTW 体を擦りつけている…
ELSEIF TALENT:冷漠
	PRINTFORMW 周囲に興味を示さず、%CALLNAME:MASTER%の後を歩いている…
ELSEIF TALENT:好奇心
	PRINTFORMW 周りの景色に興味津々のようだ…
ELSE
	PRINTFORMW これから先のことを想像しているようだ…
ENDIF

RETURN 1

@FLAG_COM171, ARG

@SOURCE_COM171, ARG
VARSET LOCAL

IF GETBIT(TEQUIP:野外プレイ, 0)
	LOCAL:20 += 20
	LOCAL:21 += 80
	LOCAL:12 = 2000
	LOCAL:14 = 400
ELSEIF GETBIT(TEQUIP:野外プレイ, 1)
	LOCAL:20 += 0
	LOCAL:21 += 30
	LOCAL:12 = 400
ELSEIF GETBIT(TEQUIP:野外プレイ, 2)
	LOCAL:20 += 20
	LOCAL:21 += 30
	LOCAL:12 = 400
ELSEIF GETBIT(TEQUIP:野外プレイ, 3) || GETBIT(TEQUIP:野外プレイ, 4)
	LOCAL:20 += 30
	LOCAL:21 += 20
	LOCAL:12 = 600
ENDIF
LOCAL:14 += 100

;服の有無で羞恥、逸脱度が上昇。人里だと物凄い事に。
IF CHECK_CLO("ハダカ")
	;種族衣装がハダカでない
	IF CLO_ORI(TARGET, "服", "スーツ", "アウター", "下着")
		IF GETBIT(TEQUIP:野外プレイ, 0)
			LOCAL:12 += 6000
			LOCAL:14 += 1000
		ELSE
			LOCAL:12 += 2000
			LOCAL:14 += 500
		ENDIF
	ENDIF
ELSEIF CHECK_CLO("下着姿")
	;種族衣装が服orボディスーツorアウターを含む
	IF CLO_ORI(TARGET, "服", "スーツ", "アウター")
		IF GETBIT(TEQUIP:野外プレイ, 0)
			LOCAL:12 += 4000
			LOCAL:14 += 500
		ELSE
			LOCAL:12 += 1000
			LOCAL:14 += 200
		ENDIF
	ENDIF
ELSEIF CHECK_CLO("ノーパン")
	;種族衣装に下着あり
	IF CLO_ORI(TARGET, "下着")
		IF GETBIT(TEQUIP:野外プレイ, 0)
			LOCAL:12 += 2000
			LOCAL:14 += 300
		ELSE
			LOCAL:12 += 600
			LOCAL:14 += 100
		ENDIF
	ENDIF
ENDIF

;昼だと恥ずかしい
IF TIME == 0
	TIMES LOCAL:12 , 2.00
	TIMES LOCAL:14 , 2.00
ENDIF

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES LOCAL:12 , 0.80
CASE IS < PALAMLV:2
	TIMES LOCAL:12 , 0.90
CASE IS < PALAMLV:3
	TIMES LOCAL:12 , 1.00
CASE IS < PALAMLV:4
	TIMES LOCAL:12 , 1.10
CASE IS >= PALAMLV:4
	TIMES LOCAL:12 , 1.20
ENDSELECT
;ABL:露出癖をみる
SELECTCASE ABL:露出癖
CASE 0
	TIMES LOCAL:14 , 1.20
CASE 1
	LOCAL:7 += 50
	LOCAL:11 += 50
CASE 2
	LOCAL:7 += 150
	LOCAL:11 += 150
	TIMES LOCAL:14 , 0.80
CASE 3
	LOCAL:7 += 400
	LOCAL:11 += 400
	TIMES LOCAL:14 , 0.60
CASE 4
	LOCAL:7 += 750
	LOCAL:11 += 750
	TIMES LOCAL:14 , 0.40
CASEELSE
	LOCAL:7 += 1300
	LOCAL:11 += 1300
	TIMES LOCAL:14 , 0.20
ENDSELECT
;ABL:受虐倾向をみる
SELECTCASE ABL:受虐倾向
CASE 0
	TIMES LOCAL:12 , 0.80
CASE 1
	TIMES LOCAL:12 , 1.00
CASE 2
	TIMES LOCAL:12 , 1.30
CASE 3
	TIMES LOCAL:12 , 1.60
CASE 4
	TIMES LOCAL:12 , 2.00
CASEELSE
	TIMES LOCAL:12 , 3.00
ENDSELECT

;目立ちたがり
SIF TALENT:喜欢被关注
	TIMES LOCAL:12 , 1.50
;解放
SIF TALENT:奔放
	TIMES LOCAL:12 , 1.50
;臆病
SIF TALENT:胆怯
	TIMES LOCAL:14 , 2.00
;恥じらい
SIF TALENT:羞耻心强
	TIMES LOCAL:14 , 2.00

LOCAL:16 += LOCAL:12 / 4
LOCAL:9 += LOCAL:14 / 2

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@EQUIP_COM54_1
CALL SOURCE_COM171, 100

@EQUIP_COM54_2
;露出経験の値を管理
LOCAL = 1
IF GETBIT(TEQUIP:野外プレイ, 0)
	PRINTL ＜野外プレイ中(人里)＞
	IF TIME == 0
		LOCAL = 3
	ELSE
		LOCAL = 2
	ENDIF
ELSEIF GETBIT(TEQUIP:野外プレイ, 1)
	PRINTL ＜野外プレイ中(森林)＞

	;獣耳ならボーナス
	SIF PLAYER == MASTER && TALENT:兽耳
		TFLAG:30 += 1
	;一部種族にボーナス
	SIF BASE:種族 == 3
		TFLAG:30 += 1
ELSEIF GETBIT(TEQUIP:野外プレイ, 2)
	PRINTL ＜野外プレイ中(花園)＞

	;花園補正。好感度にボーナス。
	SIF PLAYER == MASTER
		TFLAG:30 += 1
ELSEIF GETBIT(TEQUIP:野外プレイ, 3) || GETBIT(TEQUIP:野外プレイ, 4)
	PRINTL ＜野外プレイ中(水辺)＞

	;夏ならボーナス
	SIF PLAYER == MASTER && FLAG:873 == 1
		TFLAG:30 += 2
	;一部種族にボーナス
	SIF TALENT:人鱼
		TFLAG:30 += 1

	;水辺補正。汚れを洗い流せる。
	;汚れを初期化
	CALL RESET_STAIN_EX, "全部", MASTER
	CALL RESET_STAIN_EX, "全部", TARGET
	;助手がいる場合は、助手もきれいに
	SIF ASSI
		CALL RESET_STAIN_EX, "全部", ASSI
ENDIF

EXP:露出经验 += LOCAL
PRINTFORML %EXPNAME:34%＋{LOCAL}

SIF PLAYER == MASTER && ABL:露出癖 >= 3
	TFLAG:30 += 1
