@COM280
;もがく
CALL PRINT_TRAIN_NAME(SELECTCOM)

STR:0 = %STR:2%

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
CALL SOURCE_COM280, 100

;FIRSTCOMフラグON
CALL SET_CHOSECOM, TARGET, 280

;成否判定
SIF TFLAG:39 <= 0
	TFLAG:39 = 0

LOCAL = 2 + ABL:技巧/2 + ABL:サドっ気 - CFLAG:9*10

SIF TALENT:強気
	LOCAL += 1
SIF TALENT:長身
	LOCAL += 2
SIF TALENT:多腕
	LOCAL += MAX(TALENT:多腕/2, 1)
SIF TALENT:力持ち
	LOCAL += 3
SIF TALENT:好色
	LOCAL += 1
;体操服着用なら
SIF CHECK_SHIRT("体操服")
	LOCAL += 2
;縛って！MY MAID！モードなら
SIF NO:TARGET == 46 && CFLAG:465 == 4
	LOCAL += 3
;二人で押し倒してるなら
SIF TEQUIP:Ｗ押し倒し
	LOCAL += 5
;縛られているなら
SIF TEQUIP:PLAYER:拘束 || STATE("バインド", PLAYER)
	LOCAL += 5
SIF TEQUIP:ラブトラップ
	LOCAL += 3
;騎乗位でインサート状態
SIF INSERT("騎乗位")
	LOCAL += 5
SIF STATE("センシブ", PLAYER)
	LOCAL += 5
SIF CONFIG("ＨＡＲＤ")
	LOCAL = MAX(LOCAL + 5, 10)
;前ターンの絶頂具合
	LOCAL += PREVEX("射精", PLAYER) * 2

;アクセサリ効果（技巧）
SIF TALENT:PLAYER:技巧強化 > 0
	LOCAL = DIVIDE(LOCAL, 100 + TALENT:PLAYER:技巧強化)

;気力による補正
LOCAL = LOCAL * (BASE:気力*2 + MAXBASE:気力) / (MAXBASE:気力*3)

;マイナス補正はこの後
SIF TALENT:小柄体型
	LOCAL -= 1
;前ターンの絶頂具合
LOCAL -= PREVEX("絶頂数合計") * 2

;PLAYER側の補正
SIF TALENT:PLAYER:力持ち
	LOCAL -= 5

PRINTFORML 
IF ABL:PLAYER:技巧 + TFLAG:39 + STATE("エオルグン", PLAYER)*3 > LOCAL
	TFLAG:39 = 0
	IF TEQUIP:PLAYER:拘束 || STATE("バインド", PLAYER)
		PRINTFORMW %CALLNAME:PLAYER%は何とか拘束から逃れた！
		TEQUIP:PLAYER:拘束 = 0
		CALL REMOVE_STATE, PLAYER, "バインド"
	ELSE
		PRINTFORM %CALLNAME:PLAYER%は
		SIF PREVEX("絶頂数合計")
			PRINTFORM 、絶頂の余韻に浸っている%CALLNAME:TARGET%の隙を突いて
		PRINTFORMW 主導権を取り返した！
		CVARSET TEQUIP, 30, 0
		CALL KOJO_MESSAGE_COM
		;Ｗ押し倒し等での口上処理
		CALL KOJO_MESSAGE_COM_MULTI
		;助手とのインサートはここで解消
		CALL SETFLAG, "Ｗ押し倒し解除"
		CALL SETFLAG, "今回調教コマンド"
		;ラブトラップも解除
		CVARSET TEQUIP, 29, 0
		;TARGETにPLAYERがインサート中なら正常位に移行
		IF INSERT("Ｖインサート", TARGET, PLAYER)
			CALL SET_INSERT, TARGET, PLAYER, 60
		ELSEIF INSERT("Ａインサート", TARGET, PLAYER)
			CALL SET_INSERT, TARGET, PLAYER, 70
		ELSEIF INSERT("尿道インサート", TARGET, PLAYER)
			CALL SET_INSERT, TARGET, PLAYER, 83
		ENDIF
	ENDIF
	RETURN 0
ELSE
	;前回の調教によって文章変化
	;顔面騎乗系
	IF PREVCOM == 1 || PREVCOM == 9 || TEQUIP:顔面騎乗 || TEQUIP:シックスナイン
		PRINTFORM 顔に跨っている%CALLNAME:TARGET%を押しのけて、
	;逆イラマチオ
	ELSEIF PREVCOM == 4
		PRINTFORM %CALLNAME:PLAYER%の口を犯している%CALLNAME:TARGET%から、
	;奉仕系
	ELSEIF (PREVCOM == 90 || PREVCOM == 91 || PREVCOM == 92 || PREVCOM == 95 || PREVCOM == 97 || PREVCOM == 99) && PENIS(PLAYER)
		SIF PREVCOM == 90 || PREVCOM == 91 || PREVCOM == 92 || PREVCOM == 99
			PRINT 圧し掛かって
		PRINTFORM ペニスを弄んでいる%CALLNAME:TARGET%から、
	;騎乗位系
	ELSEIF (PREVCOM >= 65 && PREVCOM <= 67) || PREVCOM == 75 || PREVCOM == 80 || PREVCOM == 98 || PREVCOM == 120 || PREVCOM == 126
		PRINTFORM %CALLNAME:PLAYER%を犯して愉しんでいる%CALLNAME:TARGET%から、
	;アナル奉仕系
	ELSEIF PREVCOM == 96
		PRINTFORM アナルを弄んでいる%CALLNAME:TARGET%から、
	ELSE
		PRINTFORM %CALLNAME:PLAYER%は
	ENDIF
	PRINTFORML 何とか主導権を取り返そうともがいたが、%CALLNAME:TARGET%\@ TEQUIP:Ｗ押し倒し ? 達 # \@は隙を見せなかった。
	IF TEQUIP:キス && ABL:欲望 >= 3 && RAND:3 == 0
		PRINTFORMW それどころか、%CALLNAME:TARGET%は%CALLNAME:PLAYER%の唇を奪うと、舌を絡めて唾液をたっぷりと流し込んで飲ませてきた！
		CALL SETFLAG, "追加キスフラグ", TARGET
	ELSE
		PRINTFORMW それどころか、さらに%CALLNAME:PLAYER%を攻めてきた！
	ENDIF
	TFLAG:39 += 1 + RAND:2
	BASE:PLAYER:精力 -= 1
	PRINTFORML 精力-1\@ ASSIPLAY ? （%TEXTS("助手の名称")%） # (%CALLNAME:PLAYER%)\@
	CALL KOJO_MESSAGE_COM
	PRINTL 

	CALL SET_COUNTER_ABL

	SIF TEQUIP:Ｗ押し倒し
		CALL SET_SELECTCOM_MULTI, "Ｗ押し倒し"

	TRYJUMPFORM COM{SELECTCOM}
ENDIF

RETURN 1

@FLAG_COM280, ARG

@SOURCE_COM280, ARG
VARSET LOCAL

LOCAL:20 += 30
LOCAL:21 += 50

LOCAL:4 = 160

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES LOCAL:4 , 1.00
CASE IS < PALAMLV:2
	TIMES LOCAL:4 , 1.20
CASE IS < PALAMLV:3
	TIMES LOCAL:4 , 1.40
CASE IS < PALAMLV:4
	TIMES LOCAL:4 , 1.60
CASE IS >= PALAMLV:4
	TIMES LOCAL:4 , 1.80
ENDSELECT

;ABL:サドッ気をみる
SELECTCASE ABL:サドっ気
CASE 0
CASE 1
	TIMES LOCAL:4 , 1.20
CASE 2
	TIMES LOCAL:4 , 1.40
CASE 3
	TIMES LOCAL:4 , 1.60
CASE 4
	TIMES LOCAL:4 , 1.80
CASEELSE
	TIMES LOCAL:4 , 2.00
ENDSELECT

LOCAL:11 += LOCAL:4
LOCAL:12 += LOCAL:4

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG

