@COM174
;館内露出プレイ
;恥情・欲望があがりやすい。反感もやや高め。
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;観客フラグ初期化
VARSET TFLAG, 0, 151, 155
;終了時は修正なし
IF TEQUIP:馆内露出Play
	TEQUIP:馆内露出Play = 0
	SIF ASSI
		TEQUIP:ASSI:馆内露出Play = 0
	RETURN 1
ELSE
	TEQUIP:馆内露出Play = 1
	SIF ASSI
		TEQUIP:ASSI:馆内露出Play = 1
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM174
IF TEQUIP:馆内露出Play
	PRINTFORMW 部屋に戻ってきた…
ELSE
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%を
	IF RAND:3 == 0
		PRINT 廊下に
	ELSEIF RAND:2 == 0
		PRINT 広間に
	ELSE
		PRINT 食堂に
	ENDIF
	PRINTFORMW 連れ出した…
ENDIF


@EQUIP_COM35_2
;館内露出プレイ
PRINTL ＜館内露出プレイ＞

CALL AUDIENCE

CALL SOURCE_COM174, 100

EXP:露出经验 += TEQUIP:馆内露出Play
PRINTFORML %EXPNAME:34%＋{TEQUIP:馆内露出Play}

@FLAG_COM174, ARG

@SOURCE_COM174, ARG
VARSET LOCAL

LOCAL:20 += 50
LOCAL:21 += 100

;観客の数による補正あり
LOCAL:12 = 1000 + TEQUIP:馆内露出Play * 1000
LOCAL:14 = TEQUIP:馆内露出Play * 400

;服の有無
IF CHECK_CLO("ハダカ")
	LOCAL:12 += 2000
	LOCAL:14 += 500
;パンツのみ
ELSEIF CHECK_CLO("下着姿")
	LOCAL:12 += 1200
	LOCAL:14 += 200
;ノーパン
ELSEIF CHECK_CLO("ノーパン")
	LOCAL:12 += 600
	LOCAL:14 += 100
ENDIF

;ABL:露出癖をみる
SELECTCASE ABL:露出癖
CASE 0
	TIMES LOCAL:14 , 1.20
CASE 1
	LOCAL:7 += 50
	TIMES LOCAL:12, 1.20
CASE 2
	LOCAL:7 += 150
	TIMES LOCAL:12, 1.40
	TIMES LOCAL:14 , 0.80
CASE 3
	LOCAL:7 += 400
	TIMES LOCAL:12, 1.60
	TIMES LOCAL:14 , 0.60
CASE 4
	LOCAL:7 += 750
	TIMES LOCAL:12, 2.00
	TIMES LOCAL:14 , 0.40
CASEELSE
	LOCAL:7 += 1300
	TIMES LOCAL:12, 3.00
	TIMES LOCAL:14 , 0.20
ENDSELECT

;臆病
SIF TALENT:胆怯
	TIMES LOCAL:14 , 2.00
;恥じらい
SIF TALENT:羞耻心强
	TIMES LOCAL:14 , 2.00

LOCAL:16 += LOCAL:12 / 4
LOCAL:9 += LOCAL:14 / 2

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@AUDIENCE
;集まった夢魔たちをTFLAG:151〜TFLAG:154で管理
;これはCHARA_NUM("館内露出観客数")で参照も可能
;参加できる人数
IF ASSI
	LOCAL:100 = CHARANUM - 4
ELSE
	LOCAL:100 = CHARANUM - 3
ENDIF

TEQUIP:馆内露出Play = 1 + CHARA_NUM("館内露出観客数")

LOCAL:100 = MAX(LOCAL:100 - TEQUIP:馆内露出Play + 1, 0)
LOCAL = RAND:(LOCAL:100 + 5)
IF CHARA_NUM("館内露出観客数") < 4 && LOCAL >= 5 && LOCAL:100
	LOCAL:1 = AUDIENCE_APPEAR()
	;今後バリエーションを増やそう
	PRINTFORML [{LOCAL:1}]%CALLNAME:(LOCAL:1)%が見物に現れた。
	CALL AUDIENCE_SPEAK, "館内調教：登場台詞", LOCAL:1
ENDIF
IF CHARA_NUM("館内露出観客数")
	PRINTFORM [{TFLAG:151}]%CALLNAME:(TFLAG:151)%

	SELECTCASE CHARA_NUM("館内露出観客数")
	CASE IS >= 3
		PRINTFORM 達
	CASE 2
		PRINTFORM と[{TFLAG:152}]%CALLNAME:(TFLAG:152)%
	ENDSELECT

	;欲情依存で場の雰囲気を設定したい
	PRINTFORML は楽しそうに調教風景を眺めている…
	IF CHARA_NUM("館内露出観客数") < 4 && LOCAL >= 5 && LOCAL:100
		CALL AUDIENCE_SPEAK, "館内調教：ギャラリー増加台詞"
	ELSE
		CALL AUDIENCE_SPEAK, "館内調教：感想台詞"
	ENDIF
ENDIF
IF LOCAL >= 5
	IF TFLAG:151 == 0
		TFLAG:151 = LOCAL:1
	ELSEIF TFLAG:152 == 0
		TFLAG:152 = LOCAL:1
	ELSEIF TFLAG:153 == 0
		TFLAG:153 = LOCAL:1
	ELSEIF TFLAG:154 == 0
		TFLAG:154 = LOCAL:1
	ENDIF
ENDIF

RETURN 1

@AUDIENCE_APPEAR()
#FUNCTION
$START_AUDIENCE_APPEAR
LOCAL = RAND:(CHARANUM - 2) + 2
SIF LOCAL == TARGET || LOCAL == ASSI || LOCAL == TFLAG:151 || LOCAL == TFLAG:152 || LOCAL == TFLAG:153
	GOTO START_AUDIENCE_APPEAR

RETURNF LOCAL

@AUDIENCE_SPEAK, ARGS, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT

SELECTCASE ARGS
CASE "館内調教：登場台詞"
	MEMO_LINECOUNT = LINECOUNT
	;台詞有る場合に、台詞の名前表示をする？
	SIF CONFIG("台詞の名前非表示") == 0
		PRINTFORM %CALLNAME:ARG%

	TRYCALLFORM GUEST_KOJO_K{NO:ARG}_{CFLAG:ARG:209}, ARGS, ARG

	;台詞が無いなら辻褄あわせ
	IF LINECOUNT == MEMO_LINECOUNT
		PRINTL 
		CLEARLINE 1
	ENDIF
CASEELSE
	SIF TEQUIP:馆内露出Play < 2
		RETURN 0

	FOR LCOUNT, 0, 4
		LOCAL = TFLAG:(151 + LCOUNT)
		SIF LOCAL == 0
			BREAK
		;喋りすぎてもうざいかも？という訳で、３回に一回程度で。
		SIF RAND:3
			CONTINUE

		MEMO_LINECOUNT = LINECOUNT
		;台詞有る場合に、台詞の名前表示をする？
		SIF CONFIG("台詞の名前非表示") == 0
			PRINTFORM %CALLNAME:LOCAL%

		TRYCALLFORM GUEST_KOJO_K{NO:LOCAL}_{CFLAG:LOCAL:209}, ARGS, LOCAL

		;台詞が無いなら辻褄あわせ
		IF LINECOUNT == MEMO_LINECOUNT
			PRINTL 
			CLEARLINE 1
		ENDIF

	NEXT
ENDSELECT


RETURN 1
