@COM105
;オナホコキ
PRINTL オナホコキ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 105

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = オナホコキ

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM105, ARG


@SOURCE_COM105, ARG
VARSET LOCAL

LOCAL:20 += 40
LOCAL:21 += 120

LOCAL:8 = COM_ORDER_STAIN("Ｐ", "")*10 + 60
LOCAL:11 = CALCF("実行値による欲情追加")
LOCAL:13 = 500
LOCAL:14 = 500

;汚れデータを元に、LOCAL:8は上で決定

;ABL:侍奉精神をみる
SELECTCASE ABL:侍奉精神
CASE 0
	LOCAL:4 = 250
	LOCAL:5 = 50
CASE 1
	LOCAL:4 = 300
	LOCAL:5 = 100
CASE 2
	LOCAL:4 = 350
	LOCAL:5 = 200
CASE 3
	LOCAL:4 = 400
	LOCAL:5 = 300
CASE 4
	LOCAL:4 = 450
	LOCAL:5 = 500
CASEELSE
	LOCAL:4 = 500
	LOCAL:5 = 750
ENDSELECT

;ABL:技巧をみる
SELECTCASE ABL:技巧
CASE 0
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
CASE 1
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
CASE 2
CASE 3
	TIMES LOCAL:4 , 1.20
	TIMES LOCAL:5 , 1.20
CASE 4
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
CASEELSE
	TIMES LOCAL:4 , 2.00
	TIMES LOCAL:5 , 2.00
ENDSELECT

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM105
;前回と同じ
IF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%は
	IF TEQUIP:亲吻
		PRINTFORM %CALLNAME:PLAYER%%TEXTS("キス継続助詞付き")%、
		CALL SETFLAG, "追加キスフラグ", TARGET
	ELSEIF TEQUIP:颜面骑乘
		PRINTFORM %CALLNAME:PLAYER%の顔に股間をこすりつけつつ
	ENDIF
	PRINT 、
	IF TALENT:恋慕 && TEQUIP:颜面骑乘 == 0 && RAND:4 == 0
		PRINTFORMW 楽しそうに%CALLNAME:PLAYER%のペニスをオナホで扱き続けている…
	ELSEIF ABL:侍奉精神 >= 3 && TEQUIP:颜面骑乘 == 0 && RAND:3 == 0
		PRINTFORMW %CALLNAME:PLAYER%の顔を見つめながらペニスをオナホで扱き続けている…
	ELSEIF ABL:技巧 >= 3 && RAND:2 == 0
		PRINTFORMW 緩急をつけながら長いストロークでペニスをオナホで扱き続けている…
	ELSE
		PRINTFORMW 夢中になってペニスをオナホで扱き続けている…
	ENDIF
;押し倒されているとき
ELSEIF TEQUIP:推到
	PRINTFORM %CALLNAME:TARGET%は
	IF TEQUIP:颜面骑乘
		PRINT 顔に跨ったまま
	;プライド高いor淫乱orサドorサドっ気Lv4以上
	ELSEIF (TALENT:高傲 || TALENT:淫乱 || TALENT:抖Ｓ || ABL:施虐倾向 >= 4) && RAND:2 == 0
		PRINT 愉快そうに微笑みながら
	;小悪魔
	ELSEIF TALENT:小恶魔
		PRINT ニヤニヤしながら
	;淫乱or欲情4LV以上
	ELSEIF (TALENT:淫乱 || PALAM:欲情 >= PALAMLV:4) && RAND:2 == 0
		PRINT 妖艶に微笑みながら
	ELSEIF (TALENT:淫乱 || PALAM:欲情 >= PALAMLV:4) && RAND:2 == 0
		PRINT 息を荒げながら
	ELSEIF RAND:2 == 0 || TEQUIP:眼罩
		SIF RAND:2 == 0
			PRINT 楽しそうに
		PRINT 微笑みながら
	ENDIF
	PRINTFORM ペニスをオナホで弄び、
	IF PREVCOM == 1 || PREVCOM == 9
		PRINT 股間を押し付けたりしながら
	ELSE
		PRINT 快感に悶える
	ENDIF
	PRINTFORMW %CALLNAME:PLAYER%の反応を楽しんでいる…
	CALL PLAY_ONANIE
;それ以外
ELSE
	PRINTFORM %CALLNAME:TARGET%は
	IF PALAM:PLAYER:Ｐ润 < 3000
		PRINTFORM 付属のローションをたっぷりと注いだオナホールで、%CALLNAME:PLAYER%のペニスを
	ELSEIF (PREVCOM >= 60 && PREVCOM <= 79) || INSERT("インサート")
		PRINTFORM 自身の恥ずかしい液でドロドロの%CALLNAME:PLAYER%のペニスを、オナホールで
	ELSE
		PRINTFORM 粘液で濡れ光る%CALLNAME:PLAYER%のペニスを、オナホールで
	ENDIF
	IF ABL:施虐倾向 >= 3 && RAND:3 == 0
		PRINTFORM 痛いぐらいに力を込めて
	ELSEIF TALENT:好奇心 && RAND:3 == 0
		PRINTFORM 興味深そうに
	ELSEIF MARK:屈服刻印 == 0 && TEQUIP:推到 == 0 && RAND:3 == 0
		PRINTFORM 複雑な顔をしながら
	ELSEIF RAND:3 == 0
		PRINTFORM 激しく
	ELSEIF RAND:2 == 0
		PRINTFORM グチュグチュと
	ELSE
		PRINTFORM ゆっくりと
	ENDIF
	PRINTFORMW 扱きはじめた…
	CALL PLAY_ONANIE
ENDIF


@COM_ORDER_COM105, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:侍奉精神
IF ABL:侍奉精神
	LOCAL += ABL:侍奉精神 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:侍奉精神}({ABL:侍奉精神 * 4})
ENDIF
;ABL:精液上瘾
IF ABL:精液上瘾
	LOCAL += ABL:精液上瘾 * 2
	LOCALS = %LOCALS% + %ABLNAME:11%LV{ABL:精液上瘾}({ABL:精液上瘾 * 2})
ENDIF

;強気
IF TALENT:强硬
	LOCAL += 10
	LOCALS = %LOCALS% + %TALENTNAME:18%(10)
ENDIF
;恥じらい
IF TALENT:羞耻心强
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:35%(5)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;快感の否定
IF TALENT:否认快感
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF

;汚れあり
IF COM_ORDER_STAIN("Ｐ", "")/5
	LOCAL -= COM_ORDER_STAIN("Ｐ", "")/5
	LOCALS = %LOCALS% - 汚れあり
	;汚臭鈍感
	SIF TALENT:污臭钝感 || TALENT:污臭敏感
		LOCALS = %LOCALS%、\@TALENT:污臭钝感 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "")/5})
ENDIF

;合計を表示(25以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 25

RETURN RESULT
