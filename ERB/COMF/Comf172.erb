@COM172
;お風呂場プレイ
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;風呂場への出入り
;終了時は修正なし
IF TEQUIP:お風呂プレイ
	TEQUIP:シャワー = 0
	TEQUIP:お風呂プレイ = 0
	IF ASSI
		TEQUIP:ASSI:シャワー = 0
		TEQUIP:ASSI:お風呂プレイ = 0
	ENDIF
	;入浴剤リセット
	TEQUIP:MASTER:入浴剤 = 0
	RETURN 1
ELSE
	TEQUIP:お風呂プレイ = 1
	TEQUIP:お風呂上り = 1
	IF ASSI
		TEQUIP:ASSI:お風呂プレイ = 1
		TEQUIP:ASSI:お風呂上り = 1
	ENDIF
	TEQUIP:MASTER:お風呂上り = 1
ENDIF

RETURN 1

@FLAG_COM172, ARG

@SOURCE_COM172, ARG
VARSET LOCAL

LOCAL:20 += 0
LOCAL:21 += 30

LOCAL:12 = 200

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES LOCAL:12 , 0.80
CASE IS < PALAMLV:2
	TIMES LOCAL:12 , 0.90
CASE IS < PALAMLV:3
	TIMES LOCAL:12 , 1.00
CASE IS < PALAMLV:4
	TIMES LOCAL:12 , 1.10
CASE IS >= PALAMLV:4
	TIMES LOCAL:12 , 1.20
ENDSELECT
;ABL:露出癖をみる
SELECTCASE ABL:露出癖
CASE 0
CASE 1
	LOCAL:7 += 50
CASE 2
	LOCAL:7 += 80
CASE 3
	LOCAL:7 += 100
CASE 4
	LOCAL:7 += 200
CASEELSE
	LOCAL:7 += 300
ENDSELECT

LOCAL:16 += LOCAL:12 / 4

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM172
#DIM MEMO_KISEKAE

IF TEQUIP:お風呂プレイ
	PRINTFORMW 部屋に戻ってきた…
ELSE
	;着せ替えさせた状態をチェック
	MEMO_KISEKAE = 0

	IF GETBIT(IS_FAVORITE_DRESS(TARGET), 9)
		PRINTFORML %CALLNAME:TARGET%をお風呂場プレイ用のコスチュームに着替えさせますか？
		SETCOLOR DEF_COLOR("黄色")
		PRINTFORM お風呂場プレイ用：
		CALL PRINT_FAVORITE_DRESS, 9
		PRINTFORML 
		RESETCOLOR
		PRINTFORML 
		PRINTFORML  [ 0] はい
		PRINTFORML  [ 1] いいえ

		CALL INPUT_SELECT, 2

		SELECTCASE RESULT
		CASE 0
			CALL SET_FAVORITE_DRESS, TARGET, 9
			MEMO_KISEKAE = 1
		ENDSELECT
		PRINTL 
	ENDIF

	PRINTFORM %CALLNAME:TARGET%を
	IF MEMO_KISEKAE
		PRINTFORM 専用の格好に着替えさせてから
	ELSEIF CHECK_CLO("水着姿")
		PRINTFORM 水着姿で
		CALL SET_SHIRT, TARGET
		CALL SET_OUTER, TARGET
		CALL SET_SOCKS, TARGET
		CALL SET_GLOVE, TARGET
		CALL SET_SHIRT, TARGET
	ELSEIF CHECK_CLO("全裸") == 0
		PRINTFORM 脱衣所で脱がせてから
		CALL SETFLAG, "衣装全脱ぎ", TARGET
	ENDIF
	PRINTFORMW 風呂場に連れて行った
	IF TEQUIP:アイマスク
		PRINTFORM 目隠しされている%CALLNAME:TARGET%は、%CALLNAME:PLAYER%
		IF TALENT:胆怯 || TALENT:坦率 || TALENT:腼腆
			PRINTFORM に手を引かれながら恐る恐る
		ELSE
			PRINTFORM の腕を掴んで滑らないように注意しながら
		ENDIF
		PRINTFORMW 風呂場に入ってきた…
	ELSEIF TALENT:羞耻心强 && ABL:露出癖 < 3 && CHECK_CLO("ハダカ")
		PRINTFORMW 風呂場に入ってきた%CALLNAME:TARGET%は顔を真っ赤にし、身体を手で隠している…
	ELSEIF TALENT:献身 && (TALENT:好色 || TALENT:淫乱) && ITEM:底垫
		PRINTFORMW 風呂場に入ってきた%CALLNAME:TARGET%は好色な笑みを浮かべ、石鹸を手にとった…
	ELSEIF TALENT:献身
		PRINTFORMW 風呂場に入ってきた%CALLNAME:TARGET%は、%CALLNAME:MASTER%の背中を流そうと桶を手にとった…
	ELSEIF (TALENT:好色 || TALENT:淫乱 || TALENT:小恶魔 || TALENT:坏心眼) && TALENT:羞耻心强 == 0 && CHECK_CLO("ハダカ")
		PRINTFORMW 風呂場に入ってきた%CALLNAME:TARGET%は露わになった裸体を隠そうともせず、%CALLNAME:MASTER%に%TEXTR("意地悪な", "不敵な")%笑みを向けている…
	ELSEIF TALENT:幼稚 || TALENT:幼儿／幼儿退行 || TALENT:哈比
		PRINTFORMW 風呂場に入ってきた%CALLNAME:TARGET%は、さっそく浴槽に入ろうとはしゃいでいる…
	ELSEIF TALENT:不知羞耻 && CHECK_CLO("ハダカ")
		PRINTFORMW %CALLNAME:TARGET%は露わになった裸体を隠そうともせず、涼しい顔をしている…
	ENDIF
ENDIF


@EQUIP_COM58
#DIM LCOUNT
SIF SELECTCOM == 172
	RETURN 0
;お風呂場プレイ
CALL SOURCE_COM172, 30
;入浴剤
IF TEQUIP:MASTER:入浴剤 >= 1 && TEQUIP:MASTER:入浴剤 <= 19
	TFLAG:33 += 1

	;バラの花びら+ドライアド
	IF TEQUIP:MASTER:入浴剤 == 4 && BASE:種族思考 == 3
		TFLAG:33 += 2
	;死湖の入浴剤+子供
	ELSEIF TEQUIP:MASTER:入浴剤 == 5
		;傷に染みる
		SIF TALENT:人鱼 == 0 && PALAM:苦痛 >= 500
			SOURCE:痛み += MIN(PALAM:苦痛/10, 300)
		SIF TALENT:娇小体型 && TALENT:幼稚 && TALENT:好奇心
			TFLAG:33 += 2
	;ひのき風呂+クズノハorクノイチ
	ELSEIF TEQUIP:MASTER:入浴剤 == 6
		TFLAG:33 += 1
		SIF BASE:種族思考 == 16 || BASE:種族思考 == 22
			TFLAG:33 += 2
	;ミステリアス風呂+魔女
	ELSEIF TEQUIP:MASTER:入浴剤 == 7
		SIF BASE:種族思考 == 19 || BASE:種族思考 == 47
			TFLAG:33 += 2
	ENDIF
ELSEIF TEQUIP:MASTER:入浴剤 == 30
	CALL SETFLAG, "潤滑追加", 1000, TARGET
	CALL SETFLAG, "潤滑追加", 1000, PLAYER
	SIF TEQUIP:Ｗ押し倒し
		CALL SETFLAG, "潤滑追加", 1000, TARGET:1
ELSEIF TEQUIP:MASTER:入浴剤 == 31
	IF TALENT:药物耐性 == 0
		SOURCE:欲情追加 += 1000
		SOURCE:逸脱 += 500
	ENDIF
ELSEIF TEQUIP:MASTER:入浴剤 == 79
	;PLAYERとTARGETは精液まみれ
	FOR LOCAL, 1, CHARANUM
		SIF LOCAL != PLAYER && LOCAL != TARGET
			CONTINUE
		FOR LCOUNT, 0, 6
			SIF LCOUNT == 2 && PENIS(LOCAL) == 0
				CONTINUE
			SIF LCOUNT == 3 && TALENT:LOCAL:男性
				CONTINUE
			;アナルの中には流れ込まない
			SIF LCOUNT == 4
				CONTINUE
			SIF LCOUNT == 5 && TALENT:LOCAL:男性
				CONTINUE
			SELECTCASE LCOUNT
			CASE 0
				LOCALS = Ｍ
			CASE 1
				LOCALS = 手
			CASE 2
				LOCALS = Ｐ
			CASE 3
				LOCALS = Ｖ
			CASE 4
				LOCALS = Ａ
			CASE 5
				LOCALS = Ｂ
			ENDSELECT

			CALL ADD_STAIN, LOCAL, LOCALS, "精液"
			STAIN:LOCAL:LOCALS = SET_STAIN(STAIN:LOCAL:LOCALS, "精液")
		NEXT
	NEXT
	LOCAL = TFLAG:16
	TFLAG:16 = 8
	CALL SOURCE_COM147, 50
	TFLAG:16 = LOCAL
ENDIF

RETURN 1

@EQUIP_COM58_2
SIF SELECTCOM == 172
	RETURN 0
VARSET LOCAL
SIF TEQUIP:MASTER:入浴剤 >= 1 && TEQUIP:MASTER:入浴剤 <= 19
	LOCAL += 1
;お風呂場プレイ
SELECTCASE TEQUIP:MASTER:入浴剤
CASE 1
	PRINTL ＜泡風呂＞
CASE 2
	PRINTL ＜秘湯風呂＞
CASE 3
	PRINTL ＜炭酸風呂＞
CASE 4
	PRINTL ＜バラ風呂＞
CASE 5
	PRINTL ＜塩水風呂＞
CASE 6
	PRINTL ＜ひのき風呂＞
CASE 7
	PRINTL ＜ミステリアス風呂＞
CASE 30
	PRINTL ＜ローション風呂＞
CASE 31
	PRINTL ＜媚薬風呂＞
	IF TEQUIP:媚薬 == 0 && RAND:5 == 0
		CALL ADD_STATE, TARGET, "媚薬"
		PRINTFORML %CALLNAME:TARGET%は媚薬が肌から吸収されたのか、艶かしい吐息を吐き出した…
	ENDIF
	IF TEQUIP:Ｗ押し倒し && TEQUIP:(TARGET:1):媚薬 == 0 && RAND:5 == 0
		CALL ADD_STATE, TARGET:1, "媚薬"
		PRINTFORML %CALLNAME:(TARGET:1)%はすっかり媚薬が回ってしまったらしく、蕩けた瞳で%CALLNAME:MASTER%を見ている…
	ENDIF
	IF STATE("高敏术", PLAYER) == 0 && RAND:5 == 0
		PRINTFORML %CALLNAME:PLAYER%は媚薬を溶かした風呂のせいで、敏感になってしまった…
		CALL ADD_STATE, PLAYER, "高敏术"
	ENDIF
CASE 79
	PRINTL ＜精液風呂＞
	PRINTFORML %EXPNAME:20%＋3
	EXP:精液经验 += 3
	IF TEQUIP:Ｗ押し倒し
		PRINTFORML %EXPNAME:20%＋3（%CALLNAME:(TARGET:1)%）
		EXP:(TARGET:1):精液经验 += 3
	ENDIF
	EXP:PLAYER:精液经验 += 3
	LOCAL += ABL:精液上瘾 - 1
CASEELSE
	PRINTL ＜お風呂場プレイ中＞
ENDSELECT

SIF ABL:服从 >= 1
	LOCAL += 1
SIF PLAYER == MASTER
	TFLAG:30 += LOCAL

RETURN 1
