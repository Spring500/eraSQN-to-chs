@COM137
;拡張バルーン

PRINTL 拡張バルーン

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 拡張バルーン

RETURN 1

@FLAG_COM137, ARG

@SOURCE_COM137, ARG
#DIM USE_EXP
#DIM USE_ABL
VARSET LOCAL

LOCAL:20 += 50
LOCAL:21 += 100

LOCAL:6 = 400
LOCAL:9 = 1000
LOCAL:11 = 1000
LOCAL:12 = 1000
LOCAL:13 = 1000
LOCAL:14 = 3000

IF TEQUIP:拡張バルーン
	IF TEQUIP:拡張バルーン == 1
		USE_ABL = ABL:Ｖ感覚
		USE_EXP = EXP:Ｖ拡張経験
	ELSE
		USE_ABL = ABL:Ａ感覚
		USE_EXP = EXP:Ａ拡張経験
	ENDIF

	;ABL:ＡorＶ感覚をみる
	SELECTCASE USE_ABL
	CASE 0
		LOCAL:(TEQUIP:拡張バルーン) = 10
	CASE 1
		LOCAL:(TEQUIP:拡張バルーン) = 30
	CASE 2
		LOCAL:(TEQUIP:拡張バルーン) = 300
	CASE 3
		LOCAL:(TEQUIP:拡張バルーン) = 600
		LOCAL:3 = 200
	CASE 4
		LOCAL:(TEQUIP:拡張バルーン) = 900
		LOCAL:3 = 450
	CASEELSE
		LOCAL:(TEQUIP:拡張バルーン) = 1200
		LOCAL:3 = 750
	ENDSELECT

	IF TEQUIP:拡張バルーン == 1
		;貞操観念
		IF TALENT:貞操観念
			IF TALENT:処女 == 1
				LOCAL:14 += 10000
				LOCAL:15 += 3000
			ENDIF
		;貞操無頓着
		ELSEIF TALENT:貞操無頓着
			IF TALENT:処女 == 1
				LOCAL:14 += 1000
				LOCAL:15 += 500
			ENDIF
		ELSEIF TALENT:処女 == 1
			LOCAL:14 += 3000
			LOCAL:15 += 1000
		ENDIF
	;Ａ敏感、鈍感をみる
	ELSE
		;快Ａ自体のチェックは後でまとめてやる
		IF TALENT:Ａ鈍感
			TIMES LOCAL:6, 0.50
			TIMES LOCAL:11, 0.50
			TIMES LOCAL:13, 0.50
			TIMES LOCAL:14, 0.50
		ELSEIF TALENT:Ａ敏感
			TIMES LOCAL:6, 1.50
			TIMES LOCAL:11, 1.50
			TIMES LOCAL:13, 1.50
			TIMES LOCAL:14, 1.50
		ENDIF

		;処女で貞操観念orお尻許可フラグ
		IF (TALENT:処女 == 1 && TALENT:貞操観念) || CFLAG:100
			LOCAL:14 /= 3
			LOCAL:15 /= 3
		ENDIF
	ENDIF

	;拡張経験に応じた快感、苦痛、恐怖、逸脱(反感)補正
	SELECTCASE USE_EXP
	CASE IS >= 300
		TIMES LOCAL:6, 0.50
		TIMES LOCAL:9, 0.20
		TIMES LOCAL:14, 0.20
	CASE IS >= 150
		TIMES LOCAL:6, 0.60
		TIMES LOCAL:9, 0.40
		TIMES LOCAL:14, 0.60
	CASE IS >= 50
		TIMES LOCAL:6, 0.80
		TIMES LOCAL:9, 0.70
	CASE IS >= 20
		TIMES LOCAL:14, 1.20
	CASE IS >= 5
		TIMES LOCAL:(TEQUIP:拡張バルーン), 0.50
		TIMES LOCAL:6, 1.50
		TIMES LOCAL:9, 1.50
		TIMES LOCAL:14, 1.50
	CASEELSE
		TIMES LOCAL:(TEQUIP:拡張バルーン), 0.10
		TIMES LOCAL:6, 2.00
		TIMES LOCAL:9, 2.00
		TIMES LOCAL:14, 2.00
	ENDSELECT
ENDIF

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES LOCAL:13, 0.60
CASE IS < PALAMLV:2
	TIMES LOCAL:13, 0.80
CASE IS < PALAMLV:3
	TIMES LOCAL:13, 1.00
CASE IS < PALAMLV:4
	TIMES LOCAL:13, 1.20
CASE IS >= PALAMLV:4
	TIMES LOCAL:13, 1.40
ENDSELECT

;ABL:マゾっ気をみる
SELECTCASE ABL:マゾっ気
CASE 0
	TIMES LOCAL:9, 2.00
	TIMES LOCAL:11, 0.00
	TIMES LOCAL:13, 1.50
	TIMES LOCAL:14, 2.00
CASE 1
	TIMES LOCAL:9, 1.50
	TIMES LOCAL:11, 0.10
	TIMES LOCAL:13, 1.20
	TIMES LOCAL:14, 1.50
CASE 2
	TIMES LOCAL:11, 0.40
CASE 3
	TIMES LOCAL:9, 0.50
	TIMES LOCAL:11, 0.80
	TIMES LOCAL:13, 0.70
	TIMES LOCAL:14, 0.70
CASE 4
	TIMES LOCAL:9, 0.20
	TIMES LOCAL:11, 1.20
	TIMES LOCAL:13, 0.60
	TIMES LOCAL:14, 0.40
CASEELSE
	TIMES LOCAL:9, 0.05
	TIMES LOCAL:11, 1.50
	TIMES LOCAL:13, 0.30
	TIMES LOCAL:14, 0.10
ENDSELECT

CALL SOURCE_SMPLAY, "拡張バルーン", ARG

CALL CALC, "調教ソース", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM137
;拡張バルーンの着脱
IF TEQUIP:拡張バルーン
	IF TEQUIP:拡張バルーン == 1
		LOCAL = 23
		LOCALS = 膣穴
	ELSE
		LOCAL = 26
		LOCALS = 肛門
	ENDIF
	PRINTFORML バルーンから空気を抜いて、%CALLNAME:TARGET%の拡がった穴から引き抜いた
	IF TEQUIP:拡張バルーン == 1 && BASE:尿意 >= 30000 && TALENT:オンナ
		PRINTFORML …密かに尿意に耐えていた%CALLNAME:TARGET%は、尿道の圧迫感の喪失と共におしっこを漏らしてしまった
		CALL EVENT_OMORASHI, 2
		PRINTL 
	ELSEIF TEQUIP:拡張バルーン == 2 && TEQUIP:浣腸
		CALL SETFLAG, "排泄"
		PRINTL 
	ENDIF
	IF ABL:LOCAL >= 6
		PRINTFORMW %CALLNAME:TARGET%は、自分の%LOCALS%をグッと拡げ、恍惚としている…
	ELSEIF ABL:LOCAL == 5
		PRINTFORMW %CALLNAME:TARGET%の拡がった%LOCALS%からは、お腹の中が丸見えになっている…
	ELSEIF ABL:LOCAL == 4
		PRINTFORMW %CALLNAME:TARGET%の%LOCALS%は、拡がったままなかなか戻らないようだ…
	ELSEIF ABL:LOCAL == 3
		PRINTFORMW %CALLNAME:TARGET%の%LOCALS%は、ぽっかり口を開いている…
	ELSEIF ABL:LOCAL == 2
		PRINTFORMW %CALLNAME:TARGET%の%LOCALS%は、ひくつきながらゆるゆると閉じていった…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%の%LOCALS%は、ゆっくりと閉じていった…
	ENDIF
	TEQUIP:拡張バルーン = 0
ELSE
	;どっちを拡張するか選択
	CALL SELECT_BALLOON

	SIF REFUSE_CHECK()
		RETURN 0

	IF TEQUIP:拡張バルーン == 1
		PRINTFORMW %CALLNAME:TARGET%の膣にエアバルーンを挿入し、空気を送り込んだ…
	ELSE
		IF COND("液体浣腸")
			PRINTFORMW %CALLNAME:TARGET%の浣腸液を注がれている肛門を、膨らませたバルーンできっちりと塞いだ…
		ELSE
			PRINTFORMW %CALLNAME:TARGET%の菊穴にエアバルーンを挿入し、空気を送り込んだ…
		ENDIF
	ENDIF
ENDIF

@SELECT_BALLOON
VARSET LOCAL
SIF USE_V(TARGET) && TALENT:処女 == 0 && TALENT:Ｖ損傷 == 0 && ABL:Ｖ拡張
	LOCAL:0 = 1
SIF USE_A(TARGET) && TALENT:Ａ損傷 == 0 && ABL:Ａ拡張
	LOCAL:1 = 1
IF LOCAL:0 && LOCAL:1
	PRINTFORML どちらの穴をバルーンで拡張しますか？
	PRINTFORML (パンパンになるまで拡げるので、両穴同時は無理です)
	PRINTFORML  [ 0] 膣
	PRINTFORML  [ 1] 肛門
	PRINTFORML [100] やめる
	CALL INPUT_SELECT, 2, 100
ELSEIF LOCAL:0
	RESULT = 0
ELSE
	RESULT = 1
ENDIF

IF RESULT == 100
	TEQUIP:拡張バルーン = 0
	TFLAG:14 = 1
	RETURN 0
ELSEIF RESULT == 0
	TEQUIP:拡張バルーン = 1
ELSEIF RESULT == 1
	TEQUIP:拡張バルーン = 2
ENDIF

RETURN 1


@EQUIP_COM48
SIF SELECTCOM == 137
	RETURN 0
;拡張バルーン
CALL SOURCE_COM137, 30

RETURN 1

@EQUIP_COM48_2
SIF SELECTCOM == 137
	RETURN 0
;拡張バルーン
PRINTL ＜拡張バルーン挿入中＞
IF TEQUIP:拡張バルーン == 1
	LOCAL = 0
	LOCAL:1 = 53
ELSEIF TEQUIP:拡張バルーン == 2
	LOCAL = 1
	LOCAL:1 = 57
ENDIF
EXP:LOCAL += 1
PRINTFORML %EXPNAME:LOCAL%＋１
EXP:(LOCAL:1) += 1
PRINTFORML %EXPNAME:(LOCAL:1)%＋１

RETURN 1
