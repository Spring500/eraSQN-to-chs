;-------------------------------------------------
;手袋の関数
;指定番号によって名前がCSTR:48に代入される。
;99番までは、その番号の夢魔の手袋。
;EQUIP:手袋は手袋の特徴フラグ。ビット管理する
;-------------------------------------------------
@SET_GLOVE, ARG, ARG:1
;ARGの手袋をARG:1番のものにする。ARG:1==0なら手袋を脱がす
;汚れ＆ずれたりしたフラグ等を初期化して新品にする
STAIN:ARG:手袋 = 0
TCVAR:ARG:26 = 0

IF ARG:1 == 0 || COND("指の使用", ARG) == 0
	EQUIP:ARG:手袋 = 0
	CSTR:ARG:48 = 
	RETURN 0
ENDIF

;名前記録
CSTR:ARG:48 = %PALAM_GLOVE(ARG:1, ARG, 1)%

TCVAR:ARG:26 = 0
RETURN ARG:1
;-------------------------------------------------
;ARGの穿いている手袋の名前。ARG:1==0なら正式名称 by CSTR。ARG:1==1なら短縮名称 by EQUIP。
;-------------------------------------------------
@GLOVE(ARG, ARG:1)
#FUNCTIONS
IF ARG:1 == 0
	LOCALS = %CSTR:ARG:48%
ELSE
	IF CHECK_GLOVE("手露出", ARG)
		LOCALS = 手
	ELSEIF CHECK_GLOVE("指露出", ARG)
		LOCALS = 指
	ELSEIF CHECK_GLOVE("鎧", ARG)
		LOCALS = 篭手
	ELSEIF CHECK_GLOVE("肉球", ARG)
		LOCALS = 肉球
	ELSE
		LOCALS = 手袋
	ENDIF
ENDIF
RETURNF LOCALS

;-------------------------------------------------
;ARG番の手袋の正式名称
;ARG:1 > 0ならARG:1はキャラ番号として働く
;-------------------------------------------------
@NAME_GLOVE(ARG, ARG:1)
#FUNCTIONS
LOCALS = %PALAM_GLOVE(ARG, ARG:1)%
RETURNF LOCALS

;-------------------------------------------------
;ARG番の手袋の詳細パラ
;ARG:1 > 0ならARG:1はキャラ番号として働き、ARG:1 > 0 && ARG:2 > 0ならEQUIPを定める
;-------------------------------------------------
@PALAM_GLOVE(ARG, ARG:1, ARG:2)
#FUNCTIONS
VARSET LOCAL
VARSET LOCALS

IF ARG:1 > 0 && COND("指の使用", ARG:1) == 0
	SIF ARG:2 > 0
		EQUIP:(ARG:1):手袋 = LOCAL
	RETURNF LOCALS
ENDIF

SELECTCASE ARG
CASE 1, 48
	IF CFLAG:(ARG:1):8 == 2
		LOCALS = ドレススリーブ
		LOCAL = SETFLAG_GLOVE("ロング", "手露出", "黒")
	ELSEIF CFLAG:(ARG:1):8 == 3
		LOCALS = ドレススリーブ
		LOCAL = SETFLAG_GLOVE("ロング", "手露出", "黒")
	ELSE
		LOCALS = ドレススリーブ
		LOCAL = SETFLAG_GLOVE("ロング", "手露出", "紫")
	ENDIF
CASE 2
	LOCALS = 小悪魔の手袋
	LOCAL = SETFLAG_GLOVE("手首まで")
CASE 4
	IF CFLAG:(ARG:1):8 == 3
		LOCALS = 妖精の手袋
		LOCAL = SETFLAG_GLOVE("肘まで", "手露出", "フリル", "紫")
	ELSE
		LOCALS = 妖精の手袋
		LOCAL = SETFLAG_GLOVE("肘まで", "手露出", "フリル", "純白")
	ENDIF
CASE 6
	IF CFLAG:(ARG:1):8 == 2
		LOCALS = 肉球グローブ
		LOCAL = SETFLAG_GLOVE("肉球", "毛皮", "青")
	ELSEIF CFLAG:(ARG:1):8 == 3
		LOCALS = 肉球グローブ
		LOCAL = SETFLAG_GLOVE("肉球", "毛皮", "黒")
	ELSEIF CFLAG:(ARG:1):8 == 4
		LOCALS = 肉球グローブ
		LOCAL = SETFLAG_GLOVE("肉球", "毛皮", "黄")
	ELSE
		LOCALS = 肉球グローブ
		LOCAL = SETFLAG_GLOVE("肉球", "毛皮")
	ENDIF
CASE 12
	IF CFLAG:(ARG:1):8 == 4
		LOCALS = 宝竜の鱗篭手
		LOCAL = SETFLAG_GLOVE("肘まで", "手露出", "鎧", "黒")
	ELSEIF CFLAG:(ARG:1):8 != 2
		LOCALS = 亜竜人の鱗篭手
		LOCAL = SETFLAG_GLOVE("肘まで", "手露出", "鎧", "薄緑")
	ENDIF
CASE 17
	LOCALS = レザースリーブ
	LOCAL = SETFLAG_GLOVE("ロング", "レザー", "黒")
CASE 19, 43, 47
	LOCALS = 手袋
	LOCAL = SETFLAG_GLOVE("手首まで")
CASE 22
	IF CFLAG:(ARG:1):8 == 2
		LOCALS = 忍手甲
		LOCAL = SETFLAG_GLOVE("肘まで", "手露出", "和風", "手甲", "橙色")
	ELSE
		LOCALS = 忍手甲
		LOCAL = SETFLAG_GLOVE("肘まで", "手露出", "和風", "手甲", "薄緑")
	ENDIF
CASE 23
	IF CFLAG:(ARG:1):8 == 2
		LOCALS = 不思議兎の手袋
		LOCAL = SETFLAG_GLOVE("肘まで", "フリル", "シルク", "赤")
	ELSE
		LOCALS = 不思議兎の手袋
		LOCAL = SETFLAG_GLOVE("肘まで", "フリル", "シルク", "白")
	ENDIF
CASE 26
	LOCALS = 竜騎兵の篭手
	LOCAL = SETFLAG_GLOVE("ロング", "手露出", "鎧", "薄緑")
CASE 28
	LOCALS = ガントレット
	LOCAL = SETFLAG_GLOVE("ロング", "鎧", "赤")
CASE 30
	LOCALS = 手袋
	LOCAL = SETFLAG_GLOVE("手首まで", "指露出", "純白")
CASE 34
	LOCALS = ドレスグローブ
	LOCAL = SETFLAG_GLOVE("ロング", "薄青")
CASE 35
	LOCALS = ドレスグローブ
	LOCAL = SETFLAG_GLOVE("ロング", "フリル", "紫")
CASE 41
	LOCALS = ワンダースケイル
	LOCAL = SETFLAG_GLOVE("コキ不可", "ロング", "手露出", "鎧", "巨大", "爪", "黒")
CASE 50
	LOCALS = ドレスグローブ
	LOCAL = SETFLAG_GLOVE("ロング", "フリル", "指露出", "黒")
CASE 52
	LOCALS = ドレススリーブ
	LOCAL = SETFLAG_GLOVE("ロング", "手露出", "薄青")
CASE 58
	LOCALS = 手袋
	LOCAL = SETFLAG_GLOVE("肘まで", "フリル", "シルク", "赤")
CASE 60
	LOCALS = 手袋
	LOCAL = SETFLAG_GLOVE("手首まで", "毛皮")
CASE 61
	LOCALS = ガントレット
	LOCAL = SETFLAG_GLOVE("ロング", "鎧", "白")
CASE 100
	LOCALS = シルクグローブ
	LOCAL = SETFLAG_GLOVE("シルク", "純白")
CASE 101
	LOCALS = ドレスグローブ
	LOCAL = SETFLAG_GLOVE("ロング", "レース")
CASE 102
	LOCALS = レザースリーブ
	LOCAL = SETFLAG_GLOVE("肘まで", "レザー", "手露出")
CASE 103
	LOCALS = ドレススリーブ
	LOCAL = SETFLAG_GLOVE("ロング", "レース", "手露出")
ENDSELECT

SIF ARG:1 > 0 && ARG:2 > 0
	EQUIP:(ARG:1):手袋 = LOCAL

RETURNF LOCALS

;-------------------------------------------------
;指示されたとおりの手袋のフラグを設定するために使う
;-------------------------------------------------
@SETFLAG_GLOVE(ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7, ARGS:8, ARGS:9, ARGS:10)
#FUNCTION
#DIM LCOUNT

LOCAL = 0
FOR LCOUNT, 1, 11
	ARGS = %ARGS:LCOUNT%
	SELECTCASE ARGS
	CASE "手首まで"
		SETBIT LOCAL, 0
	CASE "肘まで"
		SETBIT LOCAL, 1
	CASE "ロング"
		SETBIT LOCAL, 2
	CASE "フリル"
		SETBIT LOCAL, 3
	CASE "肉球"
		SETBIT LOCAL, 4
	CASE "爪"
		SETBIT LOCAL, 5
	CASE "和風"
		SETBIT LOCAL, 6
	CASE "手甲"
		SETBIT LOCAL, 6
		LOCAL |= SETFLAG_COMMON("鎧")
	CASE "巨大"
		SETBIT LOCAL, 7
	CASEELSE
		SIF STRLENS(ARGS)
			LOCAL |= SETFLAG_COMMON(ARGS)
	ENDSELECT
	SIF STRLENS(ARGS:(LCOUNT + 1) ) == 0
		RETURNF LOCAL
NEXT
RETURNF LOCAL

;-------------------------------------------------
;ARGの手袋が条件ARGSを満たすか確認する
;-------------------------------------------------
@CHECK_GLOVE(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF EQUIP:ARG:手袋 == 0
	RETURNF 0
SIF (SETFLAG_GLOVE(ARGS)&EQUIP:ARG:手袋) == SETFLAG_GLOVE(ARGS)
	RETURNF 1
RETURNF 0

