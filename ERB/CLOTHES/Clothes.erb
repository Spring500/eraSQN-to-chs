;-------------------------------------------------
;ARGの身につけている衣装全てから条件を判定して文字列を出力する。
;-------------------------------------------------
@CLO(ARGS, ARG)
#FUNCTIONS
LOCALS =
SIF ARG == 0 && TARGET
	ARG = TARGET
SELECTCASE ARGS
CASE "お気に入り：水着"

CASE "股間"
	IF CHECK_CLO("ボディスーツ露出", ARG)
		LOCALS = %BODYS(ARG, 1)%
	ELSEIF CHECK_CLO("アウター露出", ARG)
		LOCALS = %OUTER(ARG, 1)%
	ELSEIF CHECK_CLO("パンツ露出", ARG)
		LOCALS = %PANTIES(ARG, 1)%
	ENDIF
CASE "ぱんつコキ"
	IF EQUIP:ARG:アウター && !CHECK_OUTER("コキ不可", ARG)
		LOCALS = %OUTER(ARG, 1)%
	ELSEIF EQUIP:ARG:パンツ && !CHECK_PANTIES("コキ不可", ARG)
		LOCALS = %PANTIES(ARG, 1)%
	ENDIF
CASE "コスプレ"
	IF SHIRT(ARG, 1) == "Ｙシャツ"
		IF CHECK_CLO("ハダカ", ARG)
			LOCALS = ハダカＹシャツ
		ELSE
			LOCALS = Ｙシャツ
		ENDIF
	ELSEIF SHIRT(ARG) == "女教師"
		LOCALS = 女教师
	ELSEIF SHIRT(ARG) == "和服"
		LOCALS = 和服
	ELSEIF SHIRT(ARG, 1) == "メイド服"
		LOCALS = 妹抖服
	ELSEIF SHIRT(ARG, 1) == "体操服"
		LOCALS = 体操服
	ELSEIF SHIRT(ARG, 1) == "ナース服"
		LOCALS = 护士服
	ELSEIF SHIRT(ARG, 1) == "チャイナ服"
		LOCALS = 旗袍
	ELSEIF SHIRT(ARG, 1) == "セーラー服"
		LOCALS = 水手服
	ELSEIF SHIRT(ARG, 1) == "巫女服"
		LOCALS = 巫女服
	ELSEIF SHIRT(ARG, 1) == "ベビードール"
		IF CHECK_CLO("ハダカ", ARG)
			LOCALS = 吊带睡裙内衣
		ELSE
			LOCALS = 吊带睡裙
		ENDIF
	ELSEIF SHIRT(ARG) == "ウェディングドレス"
		LOCALS = 婚礼裙装
	ELSEIF SHIRT(ARG) == "ゴスロリドレス"
		LOCALS = 哥特萝莉洋装
	ELSEIF SHIRT(ARG) == "チアリーダー"
		LOCALS = 啦啦队服
	ELSEIF PANTIES(ARG, 1) == "ビキニ" && BRA(ARG, 1) == "ビキニ"
		LOCALS = 比基尼
	ELSEIF BODYS(ARG, 1) == "スク水"
		LOCALS = 死库水
	ELSEIF BODYS(ARG, 1) == "危ないレオタード"
		LOCALS = 糟糕的体操服
	ELSEIF BODYS(ARG, 1) == "ボンデージ"
		LOCALS = 拘束装
	ELSEIF BODYS(ARG, 1) == "バニースーツ"
		LOCALS = 兔女郎装
	ELSE
		LOCALS = 該当なし
	ENDIF
ENDSELECT
RETURNF LOCALS



;-------------------------------------------------
;ARGの身につけている衣装全てを総合して、ARGSが可能かを判定する
;また、どのコマンドを選択できるか、を判定する時の速度向上のためにTCVAR:26を用いる
;TCVAR:26はEVENTCOMの最初と、フラグを弄る関数などで初期化されるため、調教中には悪さをしない…はず。
;-------------------------------------------------
@CHECK_CLO(ARGS, ARG)
#FUNCTION
VARSET LOCAL
SIF ARG == 0 && TARGET
	ARG = TARGET
;下にはパンツなど、セットたりえるもの必須
SELECTCASE ARGS
CASE "トップレス"
	SIF EQUIP:ARG:パンツ + EQUIP:ARG:スカート + EQUIP:ARG:アウター && EQUIP:ARG:ブラ + EQUIP:ARG:上着 + EQUIP:ARG:ボディスーツ == 0
		RETURNF 1
;上にはブラなど、セットたりえるもの必須
CASE "ボトムレス"
	SIF EQUIP:ARG:ブラ && EQUIP:ARG:パンツ + EQUIP:ARG:スカート + EQUIP:ARG:ボディスーツ + EQUIP:ARG:アウター == 0
		RETURNF 1
;スカートなど、パンツが見えない状態必須
CASE "ノーパン"
	SIF EQUIP:ARG:上着 + EQUIP:ARG:スカート && EQUIP:ARG:パンツ + EQUIP:ARG:ボディスーツ + EQUIP:ARG:アウター == 0
		RETURNF 1
CASE "全裸"
	SIF EQUIP:ARG:パンツ + EQUIP:ARG:ブラ + EQUIP:ARG:上着 + EQUIP:ARG:スカート + EQUIP:ARG:ボディスーツ + EQUIP:ARG:アウター
		RETURNF 0
	SIF EQUIP:ARG:靴下 + EQUIP:ARG:靴 + EQUIP:ARG:手袋
		RETURNF 0
	RETURNF 1
CASE "ハダカ"
	SIF EQUIP:ARG:パンツ + EQUIP:ARG:ブラ + EQUIP:ARG:上着 + EQUIP:ARG:スカート + EQUIP:ARG:ボディスーツ + EQUIP:ARG:アウター
		RETURNF 0
	RETURNF 1
CASE "下着姿"
	SIF EQUIP:ARG:パンツ + EQUIP:ARG:ブラ && EQUIP:ARG:上着 + EQUIP:ARG:スカート + EQUIP:ARG:ボディスーツ + EQUIP:ARG:アウター == 0
		RETURNF 1
CASE "アウター直穿き"
	SIF EQUIP:ARG:アウター && EQUIP:ARG:パンツ == 0
		RETURNF 1
CASE "自分の服"
	SIF EQUIP:ARG:上着 && NAME_SHIRT(BASE:ARG:種族, ARG) == SHIRT(ARG, 2)
		RETURNF 1
	SIF EQUIP:ARG:ボディスーツ && NAME_BODYS(BASE:ARG:種族, ARG) == BODYS(ARG, 2)
		RETURNF 1

;以下、時間がかかるのでTCVAR使用
CASE "ぱんつコキ", "パンツコキ"
	SIF GETBIT(TCVAR:ARG:26, 1)
		RETURNF 1
	SIF EQUIP:ARG:ボディスーツ
		RETURNF 0
	IF EQUIP:ARG:アウター
		SIF !CHECK_OUTER("コキ不可", ARG)
			LOCAL = 1
	ELSEIF EQUIP:ARG:パンツ && !CHECK_PANTIES("コキ不可", ARG)
		LOCAL = 1
	ENDIF
CASE "食い込み"
	SIF GETBIT(TCVAR:ARG:26, 2)
		RETURNF 1
	;Ｖ露出
	SIF GETBIT(TCVAR:ARG:26, 14)
		RETURNF 0
	SIF EQUIP:ARG:パンツ + EQUIP:ARG:ボディスーツ + EQUIP:ARG:アウター == 0
		RETURNF 0
	SIF EQUIP:ARG:ボディスーツ && !CHECK_BODYS("食い込み", ARG)
		RETURNF 0
	SIF EQUIP:ARG:アウター && !CHECK_OUTER("食い込み", ARG)
		RETURNF 0
	SIF EQUIP:ARG:パンツ && !CHECK_PANTIES("食い込み", ARG)
		RETURNF 0
	LOCAL = 2
CASE "蒸れ"
	SIF GETBIT(TCVAR:ARG:26, 3)
		RETURNF 1
	IF EQUIP:ARG:ボディスーツ && CHECK_BODYS("蒸れ", ARG)
		LOCAL = 3
	ELSEIF EQUIP:ARG:アウター && CHECK_OUTER("蒸れ", ARG)
		LOCAL = 3
	ELSEIF EQUIP:ARG:パンツ && CHECK_PANTIES("蒸れ", ARG)
		LOCAL = 3
	ENDIF
CASE "染み", "濡れ"
	SIF GETBIT(TCVAR:ARG:26, 4)
		RETURNF 1
	IF EQUIP:ARG:ボディスーツ
		SIF CHECK_BODYS("染み", ARG)
			LOCAL = 4
	ELSEIF EQUIP:ARG:アウター
		SIF CHECK_OUTER("染み", ARG)
			LOCAL = 4
	ELSEIF EQUIP:ARG:パンツ
		SIF CHECK_PANTIES("染み", ARG)
			LOCAL = 4
	ENDIF
;ブラなど、胸を覆うものは"はだけ"る
CASE "はだけ可"
	SIF GETBIT(TCVAR:ARG:26, 5)
		RETURNF 1
	IF EQUIP:ARG:上着 && CHECK_SHIRT("はだけ可", ARG)
		LOCAL = 5
	ELSEIF EQUIP:ARG:ボディスーツ && CHECK_BODYS("はだけ可", ARG)
		LOCAL = 5
	ELSEIF EQUIP:ARG:ブラ && CHECK_BRA("はだけ可", ARG)
		SIF EQUIP:ARG:上着 && CHECK_SHIRT("はだけ不可", ARG)
			RETURNF 0
		SIF EQUIP:ARG:ボディスーツ && CHECK_BODYS("はだけ不可", ARG)
			RETURNF 0
		LOCAL = 5
	ENDIF
CASE "谷間露出"
	SIF GETBIT(TCVAR:ARG:26, 6)
		RETURNF 1
	SIF EQUIP:ARG:上着 && !CHECK_SHIRT("はだけた", ARG) && !CHECK_SHIRT("谷間露出", ARG)
		RETURNF 0
	SIF EQUIP:ARG:ボディスーツ && !CHECK_BODYS("はだけた", ARG) && !CHECK_BODYS("谷間露出", ARG)
		RETURNF 0
	SIF EQUIP:ARG:ブラ && !CHECK_BRA("はだけた", ARG) && !CHECK_BRA("谷間露出", ARG)
		RETURNF 0
	LOCAL = 6
CASE "乳首露出", "Ｂ露出"
	SIF GETBIT(TCVAR:ARG:26, 7)
		RETURNF 1
	SIF EQUIP:ARG:上着 && !CHECK_SHIRT("はだけた", ARG) && !CHECK_SHIRT("乳首露出", ARG)
		RETURNF 0
	SIF EQUIP:ARG:ボディスーツ && !CHECK_BODYS("はだけた", ARG) && !CHECK_BODYS("乳首露出", ARG)
		RETURNF 0
	SIF EQUIP:ARG:ブラ && !CHECK_BRA("はだけた", ARG) && !CHECK_BRA("乳首露出", ARG)
		RETURNF 0
	LOCAL = 7
CASE "ずらし可", "ずらせる"
	SIF GETBIT(TCVAR:ARG:26, 8)
		RETURNF 1
	IF EQUIP:ARG:ボディスーツ && CHECK_BODYS("ずらせる", ARG)
		LOCAL = 8
	ELSEIF EQUIP:ARG:アウター && CHECK_OUTER("ずらせる", ARG)
		LOCAL = 8
	ELSEIF EQUIP:ARG:パンツ && CHECK_PANTIES("ずらせる", ARG)
		LOCAL = 8
	ENDIF
CASE "ブルマ露出"
	SIF GETBIT(TCVAR:ARG:26, 9)
		RETURNF 1
	SIF EQUIP:ARG:ボディスーツ && CHECK_BODYS("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:アウター && CHECK_OUTER("ブルマ", ARG) && CHECK_OUTER("クロッチ", ARG)
		LOCAL = 9
CASE "アウター露出"
	SIF GETBIT(TCVAR:ARG:26, 10)
		RETURNF 1
	SIF EQUIP:ARG:ボディスーツ && CHECK_BODYS("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:アウター && CHECK_OUTER("クロッチ", ARG)
		LOCAL = 10
CASE "ボディスーツ露出"
	SIF GETBIT(TCVAR:ARG:26, 11)
		RETURNF 1
	SIF EQUIP:ARG:ボディスーツ && CHECK_BODYS("クロッチ", ARG)
		LOCAL = 11
CASE "パンスト露出"
	SIF GETBIT(TCVAR:ARG:26, 12)
		RETURNF 1
	SIF EQUIP:ARG:ボディスーツ && CHECK_BODYS("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:アウター && CHECK_OUTER("パンスト", ARG) && CHECK_OUTER("クロッチ", ARG)
		LOCAL = 12
CASE "パンツ露出"
	SIF GETBIT(TCVAR:ARG:26, 13)
		RETURNF 1
	SIF EQUIP:ARG:ボディスーツ && CHECK_BODYS("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:アウター && CHECK_OUTER("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:パンツ && CHECK_PANTIES("クロッチ", ARG)
		LOCAL = 13
CASE "Ｖ露出"
	SIF GETBIT(TCVAR:ARG:26, 14)
		RETURNF 1
	SIF EQUIP:ARG:ボディスーツ && CHECK_BODYS("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:アウター && CHECK_OUTER("クロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:パンツ && CHECK_PANTIES("クロッチ", ARG)
		RETURNF 0
	LOCAL = 14
;これはＶ露出を含む
CASE "Ｖ露出可"
	SIF GETBIT(TCVAR:ARG:26, 14) || GETBIT(TCVAR:ARG:26, 15)
		RETURNF 1
	;調教外ならＯＫとする
	SIF COND("調教中", ARG) == 0 && CHECK_PANTIES("貞操帯", ARG) == 0
		RETURNF 1
	SIF EQUIP:ARG:ボディスーツ && CHECK_BODYS("クロッチ", ARG) && !CHECK_BODYS("ずらせる", ARG)
		RETURNF 0
	SIF EQUIP:ARG:アウター && !CHECK_OUTER("パンスト", ARG) && CHECK_OUTER("クロッチ", ARG) && !CHECK_OUTER("ずらせる", ARG)
		RETURNF 0
	SIF EQUIP:ARG:パンツ && CHECK_PANTIES("クロッチ", ARG) && !CHECK_PANTIES("ずらせる", ARG)
		RETURNF 0
	LOCAL = 15
CASE "Ａ露出"
	SIF GETBIT(TCVAR:ARG:26, 16)
		RETURNF 1
	SIF EQUIP:ARG:ボディスーツ && CHECK_BODYS("Ａクロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:アウター && CHECK_OUTER("Ａクロッチ", ARG)
		RETURNF 0
	SIF EQUIP:ARG:パンツ && CHECK_PANTIES("Ａクロッチ", ARG)
		RETURNF 0
	LOCAL = 16
;これはＡ露出を含む
CASE "Ａ露出可"
	SIF GETBIT(TCVAR:ARG:26, 16) || GETBIT(TCVAR:ARG:26, 17)
		RETURNF 1
	;調教外ならＯＫとする
	SIF COND("調教中", ARG) == 0 && CHECK_PANTIES("貞操帯", ARG) == 0
		RETURNF 1
	SIF EQUIP:ARG:ボディスーツ && CHECK_BODYS("Ａクロッチ", ARG) && !CHECK_BODYS("ずらせる", ARG)
		RETURNF 0
	SIF EQUIP:ARG:アウター && !CHECK_OUTER("パンスト", ARG) && CHECK_OUTER("Ａクロッチ", ARG) && !CHECK_OUTER("ずらせる", ARG)
		RETURNF 0
	SIF EQUIP:ARG:パンツ && CHECK_PANTIES("Ａクロッチ", ARG) && !CHECK_PANTIES("ずらせる", ARG)
		RETURNF 0
	LOCAL = 17
CASE "水着姿"
	SIF GETBIT(TCVAR:ARG:26, 18)
		RETURNF 1
	SIF EQUIP:ARG:上着 + EQUIP:スカート + EQUIP:アウター
		RETURNF 0
	IF EQUIP:ARG:ボディスーツ && CHECK_BODYS("水着", ARG)
		LOCAL = 18
	ELSEIF EQUIP:ARG:パンツ && CHECK_PANTIES("水着", ARG)
		LOCAL = 18
	ENDIF
CASE "ビキニ姿"
	SIF GETBIT(TCVAR:ARG:26, 19)
		RETURNF 1
	SIF EQUIP:ARG:上着 + EQUIP:スカート + EQUIP:ARG:ボディスーツ + EQUIP:ARG:アウター
		RETURNF 0
	SIF EQUIP:ARG:パンツ && CHECK_PANTIES("水着", ARG)
		LOCAL = 19
CASE "ずらした"
	SIF GETBIT(TCVAR:ARG:26, 20)
		RETURNF 1
	IF EQUIP:ARG:ボディスーツ && CHECK_BODYS("ずらした", ARG)
		LOCAL = 20
	ELSEIF EQUIP:ARG:アウター && CHECK_OUTER("ずらした", ARG)
		LOCAL = 20
	ELSEIF EQUIP:ARG:パンツ && CHECK_PANTIES("ずらした", ARG)
		LOCAL = 20
	ENDIF
CASE "腋露出"
	SIF GETBIT(TCVAR:ARG:26, 21)
		RETURNF 1
	SIF EQUIP:ARG:上着 && CHECK_SHIRT("腋露出", ARG) == 0
		RETURNF 0
	LOCAL = 21
CASE "足露出"
	SIF GETBIT(TCVAR:ARG:26, 22)
		RETURNF 1
	SIF EQUIP:ARG:靴
		RETURNF 0
	SIF EQUIP:ARG:靴下 && CHECK_SOCKS("足露出", ARG) == 0
		RETURNF 0
	LOCAL = 22
ENDSELECT
;TCVARに可能な事柄を記録
IF LOCAL > 0
	SETBIT TCVAR:ARG:26, LOCAL
	RETURNF 1
ENDIF

RETURNF 0


;-------------------------------------------------
;色やゲーム的な制限など、それぞれにいちいち設定すると不具合起こしそうなフラグはまとめてここで設定
;ビットの19番まではそれぞれで個別設定。
;つまり、ここでは20～63を設定する
;-------------------------------------------------
@SETFLAG_COMMON(ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7, ARGS:8, ARGS:9, ARGS:10)
#FUNCTION
#DIM LCOUNT

VARSET LOCAL
FOR LCOUNT, 1, 11
	ARGS = %ARGS:LCOUNT%
	;材質、形状等
	SELECTCASE ARGS
	CASE "水着"
		SETBIT LOCAL, 20
	CASE "透け", "シースルー"
		SETBIT LOCAL, 21
	CASE "シルク", "絹"
		SETBIT LOCAL, 22
	CASE "レース"
		SETBIT LOCAL, 23
	CASE "革", "レザー"
		SETBIT LOCAL, 24
		SETBIT LOCAL, 49
		SETBIT LOCAL, 50
		SETBIT LOCAL, 52
	CASE "毛皮"
		SETBIT LOCAL, 25
	CASE "鎧"
		SETBIT LOCAL, 26
	CASE "羽"
		SETBIT LOCAL, 27
	CASE "エナメル"
		SETBIT LOCAL, 28
		SETBIT LOCAL, 52
	CASE "フリル"
		SETBIT LOCAL, 29
	CASE "ファスナー", "ジッパー"
		SETBIT LOCAL, 30
	CASE "スリット"
		SETBIT LOCAL, 31
	CASE "Ｖ露出"
		SETBIT LOCAL, 32
	CASE "Ａ露出"
		SETBIT LOCAL, 33
	CASE "ＶＡ露出"
		SETBIT LOCAL, 32
		SETBIT LOCAL, 33
	CASE "谷間露出"
		SETBIT LOCAL, 34
	CASE "乳首露出"
		SETBIT LOCAL, 35
	CASE "胸露出"
		SETBIT LOCAL, 36
		SETBIT LOCAL, 34
		SETBIT LOCAL, 35
	CASE "指露出", "足指露出"
		SETBIT LOCAL, 37
	CASE "手露出", "足露出"
		SETBIT LOCAL, 38
		SETBIT LOCAL, 37
	CASE "腋露出"
		SETBIT LOCAL, 39

	;ゲーム的な制限等
	CASE "つなぎ"
		SETBIT LOCAL, 47
	CASE "ワンピース"
		SETBIT LOCAL, 48
	CASE "ずれ不可", "はだけ不可"
		SETBIT LOCAL, 49
	CASE "コキ不可", "ズリ不可"
		SETBIT LOCAL, 50
	CASE "脱衣不可"
		SETBIT LOCAL, 51
	CASE "通気性なし", "通気性無し"
		SETBIT LOCAL, 52
	;色や模様
	CASE "縞々", "縞"
		SETBIT LOCAL, 53
	CASE "赤"
		SETBIT LOCAL, 54
	CASE "緑"
		SETBIT LOCAL, 55
	CASE "黄"
		SETBIT LOCAL, 56
	CASE "青"
		SETBIT LOCAL, 57
	CASE "白"
		SETBIT LOCAL, 58
	CASE "濃"
		SETBIT LOCAL, 59
	;合成色
	CASE "薄桃"
		SETBIT LOCAL, 54
		SETBIT LOCAL, 58
	CASE "真紅"
		SETBIT LOCAL, 54
		SETBIT LOCAL, 59
	CASE "薄緑"
		SETBIT LOCAL, 55
		SETBIT LOCAL, 58
	CASE "薄青"
		SETBIT LOCAL, 57
		SETBIT LOCAL, 58
	CASE "紺"
		SETBIT LOCAL, 57
		SETBIT LOCAL, 59
	CASE "橙"
		SETBIT LOCAL, 54
		SETBIT LOCAL, 56
	CASE "紫"
		SETBIT LOCAL, 54
		SETBIT LOCAL, 57
	CASE "純白"
		SETBIT LOCAL, 58
		SETBIT LOCAL, 59
	CASE "黒"
		SETBIT LOCAL, 54
		SETBIT LOCAL, 55
		SETBIT LOCAL, 56
		SETBIT LOCAL, 57
	;特殊な指定
	CASE "縞々色"
		IF RAND:5 == 0
			SETBIT LOCAL, 54
			SETBIT LOCAL, 55
			SETBIT LOCAL, 56
			SETBIT LOCAL, 57
		ELSEIF RAND:4 == 0
			SETBIT LOCAL, 57
			SETBIT LOCAL, 58
		ELSEIF RAND:3 == 0
			SETBIT LOCAL, 56
			SETBIT LOCAL, 58
		ELSEIF RAND:2
			SETBIT LOCAL, 55
			SETBIT LOCAL, 58
		ELSE
			SETBIT LOCAL, 54
			SETBIT LOCAL, 58
		ENDIF
	CASE "大人しめの色"
		SIF RAND:3 == 0
			SETBIT LOCAL, 53
		IF RAND:2 && GETBIT(LOCAL, 53) == 0
			IF RAND:3
				SETBIT LOCAL, 58
			ELSE
				SETBIT LOCAL, 58
				SETBIT LOCAL, 59
			ENDIF
		ELSEIF RAND:4 == 0
			SETBIT LOCAL, 57
			SETBIT LOCAL, 58
		ELSEIF RAND:3 == 0
			SETBIT LOCAL, 56
			SETBIT LOCAL, 58
		ELSEIF RAND:2
			SETBIT LOCAL, 55
			SETBIT LOCAL, 58
		ELSE
			SETBIT LOCAL, 54
			SETBIT LOCAL, 58
		ENDIF
	CASE "Ｈめの色"
		IF RAND:5 == 0
			SETBIT LOCAL, 58
			SETBIT LOCAL, 59
		ELSEIF RAND:4 == 0
			SETBIT LOCAL, 54
			SETBIT LOCAL, 59
		ELSEIF RAND:3 == 0
			SETBIT LOCAL, 54
		ELSEIF RAND:2
			SETBIT LOCAL, 54
			SETBIT LOCAL, 57
		ELSE
			SETBIT LOCAL, 54
			SETBIT LOCAL, 55
			SETBIT LOCAL, 56
			SETBIT LOCAL, 57
		ENDIF
	;状態変化60～63
	CASE "はだけた", "はだけ"
		SETBIT LOCAL, 60
	CASE "ずらした", "ずらし"
		SETBIT LOCAL, 61
	CASE "ファスナー開"
		SETBIT LOCAL, 62
	CASE "破いた"
		SETBIT LOCAL, 63
	ENDSELECT
	SIF STRLENS(ARGS:(LCOUNT + 1) ) == 0
		RETURNF LOCAL
NEXT
RETURNF LOCAL


;-------------------------------------------------
;服装の汚れやずらしなどを直して、初期状態に戻す
;-------------------------------------------------
@RESETFLAG_CLO, ARG
FOR LOCAL, 10, 20
	STAIN:ARG:LOCAL = 0
NEXT
FOR LOCAL, 40, 50
	FOR LOCAL:1, 60, 64
		CLEARBIT EQUIP:ARG:LOCAL, LOCAL:1
	NEXT
NEXT

;-------------------------------------------------
;ARGの種族衣装の有無を判定(どれか一つでも満たしていると1を返す)
;-------------------------------------------------
@CLO_ORI(ARG, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5)
#FUNCTION
#DIM LCOUNT
FOR LCOUNT, 1, 6
	ARGS = %ARGS:LCOUNT%

	SELECTCASE ARGS
	CASE "服"
		ARGS = %NAME_SHIRT(BASE:ARG:種族, ARG)%
	CASE "スーツ"
		ARGS = %NAME_BODYS(BASE:ARG:種族, ARG)%
	CASE "アウター"
		ARGS = %NAME_OUTER(BASE:ARG:種族, ARG)%
	CASE "下着"
		ARGS = %NAME_PANTIES(BASE:ARG:種族, ARG)%
		SIF STRLENS(ARGS)
			RETURNF 1
		ARGS = %NAME_BRA(BASE:ARG:種族, ARG)%
	ENDSELECT

	SIF STRLENS(ARGS)
		RETURNF 1
NEXT
RETURNF 0

;-------------------------------------------------
;ARG:2の色をARG:1と同じにする
;-------------------------------------------------
@SAME_COLOR, ARG, ARG:1, ARG:2
SIF EQUIP:ARG:(ARG:1) == 0 || EQUIP:ARG:(ARG:2) == 0
	RETURN 0
;色が固定されている衣装
SIF SUBSTRING(CSTR:ARG:(ARG:2), 0, 2) == SUBSTRING(TEXTS("衣装の色", EQUIP:ARG:(ARG:2) ), 0, 2)
	RETURN 0
;特殊な材質
SIF ARG:2 == 41 && CHECK_BRA("帷子", ARG)
	RETURN 0
FOR LOCAL, 54, 60
	CLEARBIT EQUIP:ARG:(ARG:2), LOCAL
	SIF GETBIT(EQUIP:ARG:(ARG:1), LOCAL)
		SETBIT EQUIP:ARG:(ARG:2), LOCAL
NEXT
