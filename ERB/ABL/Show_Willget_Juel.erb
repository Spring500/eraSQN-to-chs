;USERCOMから呼び出す
@SHOW_WILLGET_JUEL
#DIM LCOUNT
#DIM MEMO_LINECOUNT

PRINTL 現在の宝珠取得予定数
PRINTL 
VARSET LOCAL
FOR LCOUNT, 0, 14
	;パラメータを表示する順番を並べ替える・ＭＢＣＶＡ欲恭屈恥痛恐習反不鬱・Ｖ潤Ａ潤は無し
	SELECTCASE LCOUNT
	CASE 0
		LOCAL = 0
	CASE 1
		LOCAL = 1
	CASE 2
		LOCAL = 2
	CASE 3
		LOCAL = 14
	CASE 4
		LOCAL = 15
	CASE 5
		LOCAL = 4
	CASE 6
		LOCAL = 5
	CASE 7
		LOCAL = 6
	CASE 8
		LOCAL = 7
	CASE 9
		LOCAL = 8
	CASE 10
		LOCAL = 9
	CASE 11
		LOCAL = 10
	CASE 12
		LOCAL = 13
	CASE 13
		LOCAL = 11
	ENDSELECT

	;パラメータ名・桁数を揃えつつパラメータを表示
	PRINTFORM %PALAMNAME:LOCAL% {PALAM:LOCAL, 10} 
	;桁数を揃えつつ現在＋予定＝合計を表示
	SELECTCASE LOCAL
	CASE 3, 12, 13
	CASEELSE
		PRINTFORM %PALAMNAME:LOCAL%の珠×({JUEL:LOCAL, 10} + 

		SIF GET_JUEL(LOCAL)
			SETCOLOR DEF_COLOR("黄色")
		PRINTFORM {GET_JUEL(LOCAL), 10}
		RESETCOLOR

		PRINTFORM ) ={JUEL:LOCAL + GET_JUEL(LOCAL), 10}
	ENDSELECT

	;快ＣＶＡＢＭは絶頂回数を表示
	IF LOCAL == 0 && EX:Ｃ絶頂 + EX:尿道絶頂 >= 1
		PRINTFORM  < \@ EX:Ｃ絶頂 ? Ｃ絶頂:{EX:Ｃ絶頂, 2}回%BL(1)% # \@\@ EX:尿道絶頂 ? 尿道絶頂:{EX:尿道絶頂, 2}回%BL(1)% # \@>
	ELSEIF LOCAL == 1 && EX:Ｖ絶頂 >= 1
		PRINTFORM  < Ｖ絶頂:{EX:Ｖ絶頂, 2}回 >
	ELSEIF LOCAL == 2 && EX:Ａ絶頂 >= 1
		PRINTFORM  < Ａ絶頂:{EX:Ａ絶頂, 2}回 >
	ELSEIF LOCAL == 14 && EX:Ｂ絶頂 >= 1
		PRINTFORM  < Ｂ絶頂:{EX:Ｂ絶頂, 2}回 >
	ELSEIF LOCAL == 15 && EX:Ｍ絶頂 >= 1
		PRINTFORM  < Ｍ絶頂:{EX:Ｍ絶頂, 2}回 >
	ENDIF
	PRINTL 
NEXT

PRINTL 

MEMO_LINECOUNT = LINECOUNT
;ABLUPも表示
CALL PRINT_SHOW_ABLUP_SELECT, "予定"
SIF LINECOUNT > MEMO_LINECOUNT
	PRINTL 

PRINTL 反感の珠は欲情・恭順・屈服の珠を優先的に相殺します。
PRINTL その後に、恥情・苦痛・恐怖の珠を相殺します。
IF EXP_GAIN(58)
	SETCOLOR DEF_COLOR("イエロー")
	PRINTL ※媚薬の使用により、獲得する珠の量が減少しています※
	RESETCOLOR
ENDIF
WAIT


@GET_JUEL(ARG, ARG:1)
#FUNCTION
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET

SELECTCASE ARG
;Ｖ潤、Ａ潤、抑鬱、Ｐ潤の珠は存在しない
CASE 3, 12, 13, 16
	RETURNF 0
;反感・抑鬱を合計して反感の珠に換算
CASE 11
	LOCAL = CALC_JUEL(11, ARG:1) + CALC_JUEL(13, ARG:1)
CASEELSE
	LOCAL = CALC_JUEL(ARG, ARG:1)
ENDSELECT
RETURNF LOCAL

;JUEL:ARGをいくつ得るか計算する
@CALC_JUEL(ARG, ARG:1)
#FUNCTION

SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET

;パラメータレベルによる取得数
SELECTCASE PALAM:(ARG:1):ARG
;LV 0
CASE IS < 100
	LOCAL = 0
;LV1
CASE IS < 300
	LOCAL = 1
CASE IS < 500
	LOCAL = 2
;LV2
CASE IS < 1500
	LOCAL = 10
CASE IS < 3000
	LOCAL = 20
;LV3
CASE IS < 6000
	LOCAL = 100
CASE IS < 10000
	LOCAL = 200
;LV4
CASE IS < 30000
	LOCAL = 1000
;LV5
CASE IS < 60000
	LOCAL = 2000
;LV6
CASE IS < 100000
	LOCAL = 3000
;LV7
CASE IS < 150000
	LOCAL = 5000
;LV8
CASE IS < 250000
	LOCAL = 8000
;LV9
CASE IS < 500000
	LOCAL = 12000
;LV10
CASE IS < 1000000
	LOCAL = 16000
;LV11
CASE IS < 2500000
	LOCAL = 20000
;LV12
CASE IS < 5000000
	LOCAL = 25000
;LV13
CASE IS < 10000000
	LOCAL = 30000
;LV14
CASE IS < 50000000
	LOCAL = 35000
;LV15
CASE IS < 100000000
	LOCAL = 40000
CASEELSE
	LOCAL = 50000
ENDSELECT

;快ＣＶＡＢＭ絶頂ボーナス
SELECTCASE ARG
CASE 0
	LOCAL += EX:(ARG:1):Ｃ絶頂*1000 + EX:(ARG:1):尿道絶頂*1000 + JUEL:(ARG:1):20
CASE 1
	LOCAL += EX:(ARG:1):Ｖ絶頂*1000 + JUEL:(ARG:1):21
CASE 2
	LOCAL += EX:(ARG:1):Ａ絶頂*1000 + JUEL:(ARG:1):22
CASE 14
	LOCAL += EX:(ARG:1):Ｂ絶頂*1000 + JUEL:(ARG:1):23
CASE 15
	LOCAL += EX:(ARG:1):Ｍ絶頂*1000 + JUEL:(ARG:1):24
ENDSELECT

;媚薬によるペナルティ
SIF EXP_GAIN(58, ARG:1)
	LOCAL /= 1 + EXP_GAIN(58, ARG:1)

RETURNF LOCAL

