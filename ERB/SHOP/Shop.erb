;複数調教と大人数対応を合成。複数調教用キャラカードチェックを削除
;コマンドメニュー及び条件を変更
;｢@SELECT_TARGET｣を削除
;｢@SHOW_CHARADATA｣＝ステータス表示
;｢@CHARA_SALE｣＝キャラ売却
;｢@SALEITEM_CHECK｣｢@USE_ITEM｣＝アイテムの購入と使用
;-------------------------------------------------
;昼や夜の始まりに一度だけ起きるイベントはここで記述
;-------------------------------------------------
@EVENTSHOP
#PRI
#DIM LCOUNT
;安全危険日判定(誕生日依存でいってみる)
CALL SETFLAG, "安全危険日判定"

;朝
IF TIME == 0
	CALL YOBAI_ASAGAKE

	SELECTCASE CONFIG("調教終了時の服装")
	;終了時にお気に入りの服に着替えてもらうなら
	CASE 2
		FOR LCOUNT, 2, CHARANUM
			;昼用の衣装に着替えてもらう
			IF GETBIT(IS_FAVORITE_DRESS(LCOUNT), 5)
				CALL SET_FAVORITE_DRESS, LCOUNT, 5
				SIF CONFIG("パンツを夢魔の好きにさせる")
					CALL RAND_PANTU, LCOUNT
			ELSEIF GETBIT(IS_FAVORITE_DRESS(LCOUNT), 0)
				CALL SET_FAVORITE_DRESS, LCOUNT, 0
				SIF CONFIG("パンツを夢魔の好きにさせる")
					CALL RAND_PANTU, LCOUNT
			ENDIF
			;パンツの着用禁止
			SIF COND("身嗜み：ノーパン", LCOUNT)
				CALL SET_PANTIES, LCOUNT
		NEXT
	ENDSELECT
;夜
ELSE
	SELECTCASE CONFIG("調教終了時の服装")
	;終了時にお気に入りの服に着替えてもらうなら
	CASE 2
		FOR LCOUNT, 2, CHARANUM
			;夜用の衣装に着替えてもらう
			IF GETBIT(IS_FAVORITE_DRESS(LCOUNT), 6)
				CALL SET_FAVORITE_DRESS, LCOUNT, 6
				SIF CONFIG("パンツを夢魔の好きにさせる")
					CALL RAND_PANTU, LCOUNT
			ELSEIF GETBIT(IS_FAVORITE_DRESS(LCOUNT), 0)
				CALL SET_FAVORITE_DRESS, LCOUNT, 0
				SIF CONFIG("パンツを夢魔の好きにさせる")
					CALL RAND_PANTU, LCOUNT
			ENDIF
			;パンツの着用禁止
			SIF COND("身嗜み：ノーパン", LCOUNT)
				CALL SET_PANTIES, LCOUNT
		NEXT
	ENDSELECT
ENDIF

;書類仕事のフラグ
FLAG:12 = 0
;一言イベントの発言フラグリセット
CALL RESET_SEXUHARAFLAG

;極短時間の記録用のCEVENT消去
FOR LCOUNT, 1, CHARANUM
	CALL DEL_CEVENT_GROUP, "一言イベント短期", LCOUNT
NEXT

DRAWLINE
DRAWLINE

;-------------------------------------------------
;まだ調教していないキャラへのフラグ設定
;-------------------------------------------------
@CHECK_FLAG_MUMA
#DIM LCOUNT

FOR LCOUNT, 1, CHARANUM
	;服のずれなどを直す
	CALL RESETFLAG_CLO, LCOUNT
	CALL SETFLAG, "ペニス詳細設定"
	CALL SETFLAG, "属性", LCOUNT

	;ユニークキャラの判定
	SIF COND("ユニーク", LCOUNT)
		FLAG:(1000 + NO:LCOUNT) = 1
	;オトコなら胎内の精液を消去。
	IF TALENT:LCOUNT:男性
		BASE:LCOUNT:子宫内精子量 = 0
		BASE:LCOUNT:子宫内精液量 = 0
	ENDIF

	;キャラ独自の識別番号を設置
	CALL SETFLAG, "识别号码", LCOUNT

	;売却価格の計算
	CALL SETFLAG, "売却価格", LCOUNT

	;既に調教した経験があるならば以下は判定済み
	SIF EXP:LCOUNT:调教次数
		CONTINUE

	;男、女の判定
	SELECTCASE PENIS(LCOUNT)
	CASE 0
		TALENT:LCOUNT:女性 = 1
	CASEELSE
		TALENT:LCOUNT:女性 = 0
	ENDSELECT

	;スリーサイズの判定や変化
	CALL SET_3SIZE, LCOUNT
	;特技LV
	CALL GET_EXABL, LCOUNT
	CALL GET_EXTALENT, LCOUNT
	;衣装の設定
	IF CHECK_CLO("全裸", LCOUNT)
		CALL COORDINATE_MUMA, LCOUNT
		CALL MEMO_FAVORITE_DRESS, LCOUNT
	ENDIF
	CALL SETFLAG, "陰毛と腋毛", LCOUNT
	CALL SETFLAG, "乳首の詳細設定", LCOUNT
	CALL CALC, "好きな体位", LCOUNT
	;誕生日の設定
	SIF BASE:LCOUNT:生日 == 0
		BASE:LCOUNT:生日 = RAND:120 + 1
	;相性の設定
	SIF RELATION:LCOUNT:(NO:LCOUNT) < 120
		RELATION:LCOUNT:(NO:LCOUNT) = 120
	SIF BASE:LCOUNT:排泄物 == 0
		BASE:LCOUNT:排泄物 = RAND:4 + RAND:4
NEXT

@SHOW_SHOP
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM NEXT_LINE
#DIM MEMO_TARGET
#DIM MEMO_ASSI
#DIM ACTOR
#DIMS MENU_SHOP

;-------------------------------------------------
;バージョンアップ
;-------------------------------------------------
CALL VERUP_ERASQN, TARGET


REDRAW 0

;実行値の表示/非表示(0で非表示、1で表示)
FLAG:8 = 1
;TFLAGをリセット
VARSET TFLAG
;各キャラの行動等をリセット(bit)
;0夜に妊婦と過ごした, 1喫茶店出演, 2一晩開放実行, 3同衾イベント, 4日常イベント出演済み, 5乱交参加, 6喫茶店挨拶
CVARSET CFLAG, 95, 0
;助手画面をデフォに戻す
FLAG:45 = 0
;能力表示画面の３サイズ表示を初期化
FLAG:46 = 0
;調教対象切り替え画面の設定を初期化
FLAG:47 = 0
;調教中フラグなどなどOFF
FLAG:99 = 0
;非表示コマンドの表示状態をリセット
CLEARBIT FLAG:210, 21
;キャラ変更可能フラグリセット
FLAG:253 = 0
;PLAYERをMASTERに戻す（念のため）
MASTER = 1
PLAYER = MASTER
;複数キャラを使うイベント用の対象フラグリセット
VARSET TARGET, 0, 1, 4
;TARGETとASSIが-1となることを禁止
TARGET = MAX(TARGET, 0)
ASSI = MAX(ASSI, 0)
;レベルリミッターチェック
CALL SETFLAG, "等级限制器"

;-------------------------------------------------
;幾つかのフラグをここで設定（主に新規キャラ参入のときに働く）
;-------------------------------------------------
CALL CHECK_FLAG_MUMA

;精力とストックのセット
CALL SET_COUNTER_BASE
;調教コマンド実行前のパラメータの記録
CALL SAVE_PALAM_BEFORECOM

;-------------------------------------------------
;さぁ温泉旅行！
;-------------------------------------------------
IF FLAG:60 && DAYEV("土曜", "ゲーム内") && TEXTS("時間") == "昼"
	MEMO_TARGET = TARGET
	MEMO_ASSI = ASSI
	CALL EVENT_TRAVEL
	;昼夜のフラグ変動
	FOR LOCAL, 0, 2
		FOR LCOUNT, 1, 5
			CALL EVENTTURNEND_FLAG, LCOUNT
		NEXT

		TIME = (TIME + 1)%2
	NEXT
	;温泉フラグリセット
	VARSET FLAG, 0, 60, 70
	CVARSET CFLAG, 70, 0
	DAY += 1
	CALL SETFLAG, "安全危険日判定"
	TARGET = MEMO_TARGET
	ASSI = MEMO_ASSI
	RESTART
ENDIF
;-------------------------------------------------
;SHOP画面
;-------------------------------------------------
;この値をもとにCLEARLINEを行う
FLAG:2 = LINECOUNT

PRINTL
DRAWLINE

;ここから季節、年表示部分
FLAG:871 = (DAY - 1)%120 + 1
FLAG:872 = (DAY+119)/120
FLAG:873 = (FLAG:871 - 1) / 30

PRINTFORM 第{DAY}日(第{FLAG:872}年又{FLAG:871}日) %TEXTS("日付表示")% 金钱：%MONEY_C(MONEY)%
SIF FLAG:40
	PRINTFORM  [%NAMES("食事", FLAG:40)%]%BL(1)%

SIF ITEM:精液储存量
	PRINTFORM (魔法瓶的精液：{ITEM:精液储存量})
PRINTL

IF TEXTS("日付対応イベント") != ""
	SETCOLOR DEF_COLOR("黄色")
	PRINTFORM %TEXTS("日付対応イベント")%
	;クリスマスツリーチェック
	IF DAYEV("クリスマス期間") && NUM("クリスマスツリー")
		SETCOLOR DEF_COLOR("ハートピンク")
		FOR LCOUNT, 0, NUM("クリスマスツリー")
			PRINTFORM %UNICODE(0xD83C)%%UNICODE(0xDF84)%
		NEXT
	ENDIF

	RESETCOLOR
	PRINTL
ENDIF

IF TARGET
	PRINTFORM %NAME:TARGET% 调教中%BL(1)%
	CALL TEXT, "生理周期", TARGET
	PRINT 体力
	BASE:体力 = MAX(BASE:体力, 0)
	CALL PRINT_BAR, "減少ゲージ", BASE:体力, MAXBASE:体力, 5
	PRINTFORML ({BASE:体力}/{MAXBASE:体力}) 好感度：{CFLAG:2} 侍奉快乐：{EXP:侍奉快乐经验}
	;着せ替え表示
	CALL PRINT_DRESS, TARGET, "短縮表示"
	PRINTL
ENDIF
IF ASSI
	PRINTFORM %TEXTS("助手の名称")%：%NAME:ASSI%
	CALL TEXT, "生理周期", ASSI
	CALL ASSI_LIST_ABL, ASSI
	PRINTFORML
	;着せ替え表示
	CALL PRINT_DRESS, ASSI, "短縮表示"
	PRINTL
	;指示されているならそれも表示
	IF ABL:ASSI:助手 && CFLAG:ASSI:35
		PRINT 指示：
		CALL ASSI_LIST_INDICATE, ASSI
		PRINTL
	ENDIF
ENDIF

PRINTFORM 主人：%NAME:MASTER%
IF CONFIG("等级限制器")
	SETCOLOR DEF_COLOR("黄色")
	IF CONFIG("等级限制器：－10ＬＶ")
		PRINTFORM (对象的LV-10)
	ELSEIF CONFIG("等级限制器：－５ＬＶ")
		PRINTFORM (对象的LV-５)
	ELSEIF CONFIG("等级限制器：相手に合わせる")
		PRINTFORM (与对象的LV相近)
	ELSEIF CONFIG("等级限制器：＋５ＬＶ")
		PRINTFORM (对象的LV+５)
	ELSEIF CONFIG("等级限制器：＋10ＬＶ")
		PRINTFORM (对象的LV+10)
	ELSE
		PRINTFORM (将等级限制为：{CONFIG("等级限制器")})
	ENDIF
	RESETCOLOR
ENDIF
PRINTFORM  精力{BASE:MASTER:精力}/{MAXBASE:MASTER:精力}
SIF MAXBASE:MASTER:STOCK
	PRINTFORM ×({BASE:MASTER:STOCK}/{MAXBASE:MASTER:STOCK})
PRINT
CALL TEXT, "生理周期", MASTER
CALL ASSI_LIST_ABL, MASTER

PRINTFORML

;ここでのアイテム販売は中止
;CALL SALEITEM_CHECK
;0～99番のITEMSALESを1にする
VARSET ITEMSALES, 1, 0, 100

DRAWLINE

VARSET MENU_SHOP

SIF TARGET == 0
	MENU_SHOP = %MENU_SHOP%灰色/
IF TARGET && GETBIT(CFLAG:201, 4)
	MENU_SHOP = %MENU_SHOP%[100] 前去会面/
ELSE
	MENU_SHOP = %MENU_SHOP%[100] 进行调教/
ENDIF

MENU_SHOP = %MENU_SHOP%[101] 查看能力/

SIF TARGET == 0 || CONFIG("調教相手変更不可")
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[102] 休息/

SIF ASSI == 0 || CONFIG("調教相手変更不可")
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[108] 交换%TEXTS("助手の名称")%和对象/

SIF CHARA_NUM("助手可能") == 0 || CONFIG("調教相手変更不可")
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[109] 变更%TEXTS("助手の名称")%/

SIF CHARA_NUM("控えキャラ") == 0 || CONFIG("調教相手変更不可")
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[110] 变更调教对象/

IF CHARANUM >= 100 || CONFIG("調教相手変更不可")
	MENU_SHOP = %MENU_SHOP%灰色/
ELSEIF CHARANUM <= 2
	MENU_SHOP = %MENU_SHOP%ハートピンク/
ENDIF
MENU_SHOP = %MENU_SHOP%[111] 梦魔召唤/

SIF CHARA_NUM("売却可能") == 0 || CONFIG("調教相手変更不可")
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[112] 梦魔出售/

SIF CHARANUM <= 2
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[113] 确认着装/

MENU_SHOP = %MENU_SHOP%水色/[114] 呼叫御用商人/

SIF NUM("総衣装数") == NUM("所持衣装")
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%水色/[115] 去高级服饰店/

MENU_SHOP = %MENU_SHOP%水色/[116] 去秘法商店/

MENU_SHOP = %MENU_SHOP%水色/[117] 去饰品屋/

SIF FLAG:40
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%水色/[118] 去酒馆/

SIF CHARA_NUM("一晩開放可能") == 0
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[120] 外出许可管理/

MENU_SHOP = %MENU_SHOP%[121] 外出探索/

IF TALENT:MASTER:调合知识  || (ASSI && TALENT:ASSI:调合知识)
	MENU_SHOP = %MENU_SHOP%[122] 素材调合/
ELSE
	MENU_SHOP = %MENU_SHOP%灰色/[122] 素材调合/
ENDIF

SIF FLAG:12 || CHARA_NUM("書類仕事") == 0
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[123] 文书工作/

SIF ASSI == 0 || CEVENT("助手で性処理：おあずけ", ASSI) || CONFIG("調教相手変更不可")
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[124] %TEXTS("助手の名称")%性欲处理/

SIF TARGET == 0 || COND("温泉旅行可能") == 0
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[125] 预约旅行（周五夜）/

SIF FLAG:208
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[131] 偏爱＆弱点设定/

SIF NUM("所持アイテム") + NUM("所持衣装") == 0
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[140] 道具＆服装一览/
SIF NUM("売却可能アイテム") == 0
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[141] 出售道具/

;SIF TARGET <= 0
;	MENU_SHOP = %MENU_SHOP%灰色/
;MENU_SHOP = %MENU_SHOP%[142] 数据分析/

IF NUM("所持店舗")
	MENU_SHOP = %MENU_SHOP%[160] 咖啡店的准备/
ELSE
	SIF NUM("運営可能店舗") == 0
		MENU_SHOP = %MENU_SHOP%灰色/
	MENU_SHOP = %MENU_SHOP%[160] 咖啡店开张/
ENDIF

MENU_SHOP = %MENU_SHOP%改行/[200] 保存/

MENU_SHOP = %MENU_SHOP%[300] 读取/

SIF ITEM:化蝶之梦 == 100
	MENU_SHOP = %MENU_SHOP%[330] 从梦中醒来/

SIF ITEM:等级限制器
	MENU_SHOP = %MENU_SHOP%[333] 等级限制器/

MENU_SHOP = %MENU_SHOP%改行/[555] 系统设定/

;エンディング突入(胡蝶の夢や温泉旅行企画中は駄目)
SIF COND("エンディング選択可能")
	MENU_SHOP = %MENU_SHOP%[600] 迎接ＥＮＤ/
SIF NUM("ＥＮＤ閲覧済")
	MENU_SHOP = %MENU_SHOP%[650] 回想特殊END/

SIF TARGET && COND("子孫") && (NAME:TARGET == "娘" || NAME:TARGET == "息子")
	MENU_SHOP = %MENU_SHOP%改行/[700] 给%NAME:TARGET%取名字/


VARSET LOCALS
SPLIT MENU_SHOP, "/", LOCALS


NEXT_LINE = 0

;SHOP画面の表示
FOR LCOUNT, 0, 100
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASE "灰色", "水色", "ハートピンク"
		SETCOLOR DEF_COLOR(LOCALS:LCOUNT)
		CONTINUE
	CASE "改行"
		IF NEXT_LINE%3
			NEXT_LINE = 0
			PRINTL
		ENDIF
		CONTINUE
	CASE "強制改行"
		NEXT_LINE = 0
		PRINTL
		CONTINUE
	ENDSELECT

	PRINTFORM %TEXT_LJ(LOCALS:LCOUNT, 35)%

	RESETCOLOR

	NEXT_LINE += 1
	SIF NEXT_LINE%3 == 0
		PRINTL
NEXT
SIF NEXT_LINE%3
	PRINTL

DRAWLINE

VARSET LOCALS
;日付イベントがあるキャラを示唆する試み
SELECTCASE TEXTS("日付対応イベント")
;日付固有イベントフラグはイベントが設定されていない日に全消去
CASE ""
	FOR LCOUNT, 1, CHARANUM
		CALL DEL_CEVENT_GROUP, "日付", LCOUNT
	NEXT
CASEELSE
	;こっちは主人のフラグは見ない
	FOR LCOUNT, 2, CHARANUM
		SIF CDAYEV(TEXTS("日付対応イベント"), LCOUNT)
			LOCALS = %LOCALS%[{LCOUNT, 2}]%CALLNAME:LCOUNT%和
	NEXT
ENDSELECT

IF LOCALS != ""
	PRINTL
	SETCOLOR DEF_COLOR("黄色")
	PRINTFORML %SUBSTRING(LOCALS, 0, STRLENS(LOCALS) - 2)%不知道为什么躁动不安……
	PRINTFORML 看来今天注定不会太平静
	RESETCOLOR
ENDIF

MEMO_LINECOUNT = LINECOUNT

;一言イベントなど
IF CHARANUM >= 3 && COND("一言イベント中") == 0
	ACTOR = RANDF(CHARANUM - 2) + 2

	PRINTL
	;台詞有る場合に、台詞の名前表示をする？
	SIF CONFIG("台詞の名前非表示") == 0
		PRINTFORML - %CALLNAME:ACTOR% -

	MEMO_LINECOUNT = LINECOUNT

	TRYCALLFORM GUEST_KOJO_K{NO:ACTOR}_{CFLAG:ACTOR:209}, "一言イベント", ACTOR

	;台詞が無いなら辻褄あわせ
	IF LINECOUNT == MEMO_LINECOUNT
		CLEARLINE 1
		SIF CONFIG("台詞の名前非表示") == 0
			CLEARLINE 1
		MEMO_LINECOUNT = LINECOUNT
	ELSEIF LINECOUNT > MEMO_LINECOUNT
		FLAG:23 = ACTOR
		;一言イベント中フラグ
		SETBIT FLAG:23, 10
		CALL INPUT_SEXUHARA, ACTOR
	ENDIF

	IF COND("一言イベント中") == 0 && LINECOUNT == MEMO_LINECOUNT && RAND:3 == 0
		IF COND("温泉旅行可能", ACTOR)
			PRINTL
			PRINTFORML （感受到%NAME:ACTOR%火热的视线…。一起去温泉玩玩吧？）
		ELSEIF ACTOR != TARGET && COND("欲求不满", ACTOR)
			PRINTL
			PRINTFORML （感受到%NAME:ACTOR%火热的视线…。也该抽空陪陪她了）
		ENDIF
	ENDIF
ENDIF

SIF LINECOUNT == MEMO_LINECOUNT
	PRINTL


;-------------------------------------------------
;SHOP画面入力の0～99は@EVENTBUYへ、100以上は@USERSHOPへと行く
;ただし、@EVENTBUYではBOUGHTにRESULTが代入され、RESULT自体は0になっている
;-------------------------------------------------
@EVENTBUY
#DIM CHOICE
;システムで自動的に行われている購入処理を否定
ITEM:BOUGHT -= 1
MONEY += ITEMPRICE:BOUGHT

CHOICE = BOUGHT

SIF COND("一言イベント中")
	CALL EVENTBUY_SEXUHARA, CHOICE

;SHOP表示をクリア
CLEARLINE LINECOUNT - FLAG:2
;一言イベント中フラグもクリア
CLEARBIT FLAG:23, 10

RETURN 0

@USERSHOP
#DIM CHOICE
#DIM MEMO_TARGET
#DIM MEMO_ASSI

CHOICE = RESULT
TFLAG:102 = RESULT

SIF COND("一言イベント中")
	CALL USERSHOP_SEXUHARA, CHOICE

;[100]調教する
IF CHOICE == 100
	IF TARGET
		REDRAW 1
		BEGIN TRAIN
		RETURN 1
	ELSE
		PRINTW 目前没有指定调教对象
	ENDIF
;[101]能力の表示
ELSEIF CHOICE == 101
	;キャラ変更可能フラグ
	SIF CONFIG("調教相手変更不可")
		FLAG:253 = 1
	;1ページ目を表示
	CALL SHOW_CHARADATA, 0, 0
;[102]休憩
ELSEIF CHOICE == 102
	IF TARGET && CONFIG("調教相手変更不可") == 0
		REDRAW 1
		;休憩フラグを立てる
		FLAG:0 = 1
		TRYCALL EVENT_REST_PRE
		BEGIN TURNEND
		RETURN 1
	ELSEIF CONFIG("調教相手変更不可")
		PRINTW 现在还不可以休息哦
	ELSEIF TARGET == 0
		PRINTW 目前没有指定调教对象
	ENDIF
;ELSEIF CHOICE == 103
;	CALL SHOW_CHARA_ANALYSIS, 0, 0
;[108]助手を相手する
ELSEIF CHOICE == 108
	IF ASSI && CONFIG("調教相手変更不可") == 0
		LOCAL = ASSI
		ASSI = 0
		SIF TARGET && COND("助手可能")
			ASSI = TARGET
		TARGET = LOCAL
	ELSEIF ASSI && CONFIG("調教相手変更不可")
		PRINTFORMW 目前无法将%TEXTS("助手の名称")%和对象进行交换
	ELSEIF ASSI == 0
		PRINTFORMW 还没有指定%TEXTS("助手の名称")%呢
	ENDIF
;[109]助手変更
ELSEIF CHOICE == 109
	MEMO_ASSI = ASSI
	IF CHARA_NUM("助手可能") && CONFIG("調教相手変更不可") == 0
		;キャラ変更可能フラグ
		FLAG:253 = 1
		;1ページ目を表示
		CALL SELECT_ASSI, 0, 0
	ELSEIF CONFIG("調教相手変更不可")
		PRINTFORMW 目前无法将%TEXTS("助手の名称")%和对象进行交换
	ELSEIF CHARA_NUM("助手可能") == 0
		PRINTFORMW 还没有指定%TEXTS("助手の名称")%呢
	ENDIF
	IF ASSI && ASSI != MEMO_ASSI
		TRYCALLFORM GUEST_KOJO_K{NO:ASSI}_{CFLAG:ASSI:209}, "助手に抜擢", ASSI
		SIF EXP:ASSI:助手经验 == 0
			EXP:ASSI:助手经验 += 1
	ENDIF
;[110]調教対象切り替え
ELSEIF CHOICE == 110
	MEMO_TARGET = TARGET
	IF CHARA_NUM("控えキャラ") && CONFIG("調教相手変更不可") == 0
		;キャラ変更可能フラグ
		FLAG:253 = 1
		;1ページ目を表示
		CALL CHANGE_TARGET, 0, 0
	ENDIF
	;調教対象を変えた＆一度は相手をしたことがある
	SIF TARGET && TARGET != MEMO_TARGET && EXP:调教次数
		TRYCALLFORM GUEST_KOJO_K{NO:TARGET}_{CFLAG:209}, "調教相手に選ばれた", TARGET
;[111]夢魔召喚
ELSEIF CHOICE == 111
	IF CHARANUM < 100 && CONFIG("調教相手変更不可") == 0
		CALL CHARA_BUY
	ELSEIF CONFIG("調教相手変更不可")
		PRINTW 现在不是召唤的时候哟
	ELSEIF CHARANUM >= 100
		PRINTW 大厅已经挤不下人了！
	ENDIF
;[112]夢魔売却
ELSEIF CHOICE == 112
	IF CHARA_NUM("売却可能") && CONFIG("調教相手変更不可") == 0
		;売却価格のCFLAGを前もって計算。最高価格をMONEY:1、最高評価をMONEY:3とする
		MONEY:1 = 0
		MONEY:3 = 0
		FOR LOCAL, 2, CHARANUM
			CALL SETFLAG, "売却価格", LOCAL
			MONEY:1 = MAX(MONEY:1, CFLAG:LOCAL:40)
			MONEY:3 = MAX(MONEY:3, CFLAG:LOCAL:44)
		NEXT
		;歴代最高額の更新
		MONEY:2 = MAX(MONEY:1, MONEY:2)
		MONEY:4 = MAX(MONEY:3, MONEY:4)
		CALL CHARA_SALE, 0, 0
	ELSEIF CONFIG("調教相手変更不可")
		PRINTW 现在还不是卖出的时候哟
	ELSEIF CHARA_NUM("売却可能") == 0
		PRINTW 没有可以出售的梦魔
	ENDIF
;[113]コスチューム確認
ELSEIF CHOICE == 113
	IF CHARANUM > 2
		;1ページ目を表示
		CALL COSPLAY_LIST, 0, 0
	ENDIF
;[114]御用商人を呼ぶ
ELSEIF CHOICE == 114
	CALL EVENT_SHOP
;[115]高級服飾店『クヴァラットミルク』
ELSEIF CHOICE == 115
	IF NUM("総衣装数") > NUM("所持衣装")
		CALL COSPLAY_SHOP
	ELSE
		PRINTW 看板上挂着『ＳＯＬＤ ＯＵＴ』的标牌…
	ENDIF
;[116]アルカナショップ『魔女の家』
ELSEIF CHOICE == 116
	CALL ARCANA_SHOP
;[117]アクセサリショップ『おぼろげ屋』
ELSEIF CHOICE == 117
	CALL ACCESSORY_SHOP
;[118]酒場『一角獣の角』
ELSEIF CHOICE == 118
	IF FLAG:40 == 0
		CALL EAT_LUNCH
	ELSE
		PRINTFORMW 你已经吃不下了
	ENDIF
;[120]外出許可を与える
ELSEIF CHOICE == 120
	IF CHARA_NUM("一晩開放可能")
		CALL SET_CHARA_NIGHTWALKER, 0, 0

		;DRAWLINE
		;夢魔イベント
		;CALL NIGHTWALKER
		;BEGIN TURNEND
		;RETURN 1
	ELSE
		PRINTL 现在没人想要外出许可or没人达到可以外出的条件
		PRINTL 性行为上瘾，且[好色]or[淫乱]or露出癖等级高的角色会想要外出
		PRINTW 你没法将外出许可批给顺从和屈服刻印LV高的角色
	ENDIF
;[121]探索に出かける
ELSEIF CHOICE == 121
	REDRAW 1
	CALL EXPLORER, 0, 0
;[122]素材の調合
ELSEIF CHOICE == 122
	IF TALENT:MASTER:调合知识 || (ASSI && TALENT:ASSI:调合知识)
		CALL COMPOUND_ITEM
	ELSE
		PRINTFORMW MASTER或%TEXTS("助手の名称")%需要拥有[調合知識]才可以
	ENDIF
;[123]書類仕事をする
ELSEIF CHOICE == 123
	IF CHARA_NUM("書類仕事")
		IF FLAG:12 == 0
			CALL EVENT_DESKWORK
		ELSE
 			PRINTW 今天的工作已经做完了
		ENDIF
	ELSE
		PRINTL 你没有那么多需要整理的文件
		PRINTW （需要有经过相关训练的角色才能触发事件）
	ENDIF
;[124]助手に性欲処理を頼む
ELSEIF CHOICE == 124
	IF ASSI && CONFIG("調教相手変更不可") == 0
		TRYCALL PLAY_WITH_ASSI
	ELSEIF ASSI && CONFIG("調教相手変更不可")
		PRINTFORMW 你现在并不是很想搭理%TEXTS("助手の名称")%
	ELSEIF ASSI == 0
		PRINTFORMW 还没有指定%TEXTS("助手の名称")%呢
	ENDIF
;[125]土曜日に温泉旅行を予約する
ELSEIF CHOICE == 125
	IF TARGET && COND("温泉旅行可能")
		CALL EVENT_TRAVEL_SELECT
	ELSE
		PRINTL 想预订温泉旅行，就得在周五晚上金曜夜准备好$3,000，
		PRINTL 而且将带有特殊素质[恋慕]或[淫乱]的角色设成调教对象。
		PRINTW 但是，如果调教对象是[处女]的话，就必须带有特殊素质[相思相愛]才可以
	ENDIF
;[131]お気に入りや弱点等の設定
ELSEIF CHOICE == 131
	IF FLAG:208 == 0
		;設定の記録
		TFLAG:0 = TALENT:MASTER:弱点
		TFLAG:1 = TALENT:MASTER:性癖
		FOR LOCAL, 2, CHARANUM
			TFLAG:LOCAL = TALENT:LOCAL:偏爱
		NEXT
		CALL SET_CHARA_LOVE, 0, 0
	ELSE
		PRINTFORMW 还要再经过{FLAG:208}天，才能再次变更偏爱设定
	ENDIF
;[140]アイテム＆衣装一覧
ELSEIF CHOICE == 140
	IF NUM("所持アイテム") + NUM("所持衣装")
		CALL SHOW_ITEM_HAVE
	ENDIF
;[141]アイテム売却
ELSEIF CHOICE == 141
	IF NUM("売却可能アイテム")
		CALL SELL_ITEM
	ENDIF
;[142]アナライズ（一応残しておいてみる）
ELSEIF CHOICE == 142
	IF TARGET
		CALL ANALYSIS
	ELSE
		PRINTW 请从调教对象中选择要分析的对象
	ENDIF
;[160]喫茶店の準備
ELSEIF CHOICE == 160
	IF NUM("運営可能店舗") || NUM("所持店舗")
		CALL SHOP_EX
	ELSE
		PRINTFORML 可以作为咖啡店服务生的角色数量不足
		PRINTFORML （基本条件是：外表看起来需要像是女孩子，不能是[恋慕]角色且顺从等级足够高）
		PRINTFORML 每个店面需要至少4人才能正常营业，目前还需要{4 - CHARA_NUM("バイト可能")}人
		PRINTFORMW 除此以外还需要$10,000作为开店成本
	ENDIF
ELSEIF CHOICE == 200
	CALL SAVEGAME_EX
ELSEIF CHOICE == 300
	CALL LOADGAME_EX
;[330]夢から覚める
ELSEIF CHOICE == 330 && ITEM:化蝶之梦 == 100
	PRINTFORML 化蝶之梦尾声将至
	PRINTFORML ……真的要从梦里醒来吗？
	PRINTFORML [ 0] 好的
	PRINTFORML [ 1] 不要
	CALL INPUT_SELECT, 2
	SIF RESULT == 0
		CALL EVENT_KOTYOUNOYUME, "夢の終わり"
ELSEIF CHOICE == 333 && ITEM:等级限制器
	CALL LV_LIMITTER
ELSEIF CHOICE == 555
	CALL SET_CONFIGURE
ELSEIF CHOICE == 600
	;ノーマルモードでのエンディング突入
	IF COND("エンディング選択可能")
		REDRAW 1
		IF MONEY < NUM("目標金額")
			PRINTL 距离目标金额还有一段距离……
		ELSEIF CALCF("最愛の相手")
			PRINTPLAINFORM %CALLNAME:MASTER%最爱的对象是……[{CALCF("最愛の相手"), 2}]%CALLNAME:CALCF("最愛の相手")%です
			CALL CHECK_SINGLE_ENDING
			IF RESULT
				CALL PRINT_STR, "ハートピンク_（拥有特殊ＥＮＤ）_L_"
			ELSE
				PRINTL
			ENDIF
		ENDIF
		PRINTL 真的要迎接ENDING？
		PRINTL [ 0] 好的
		PRINTL [ 1] 不要
		CALL INPUT_SELECT, 2
		SIF RESULT
			RETURN 0
		CALL ENDING_CHECK
	ENDIF
ELSEIF CHOICE == 650 && NUM("ＥＮＤ閲覧済")
	MEMO_TARGET = TARGET
	CALL SHOW_CHARA_END
	TARGET = MEMO_TARGET
ELSEIF CHOICE == 700 && TARGET && COND("子孫") && (NAME:TARGET == "娘" || NAME:TARGET == "息子")
	CALL GIVE_NAME

;上位夢魔召喚
ELSEIF CHOICE == 666 && CHARANUM < 100
	IF CONFIG("調教相手変更不可") == 0
		FLAG:666 = 1
		IF CONFIG("召喚事故") == 0
			FLAG:221 = 1
			CALL CHARA_BUY
			FLAG:221 = 0
		ELSE
			CALL CHARA_BUY
		ENDIF
	ENDIF
	FLAG:666 = 0
ELSEIF CHOICE == 777
	CALL NAME_CONVERT, 0, 0
ELSEIF CHOICE == 888
	SIF TARGET == 0
		RETURN 0
	;可以执行命令の確認
	CALL CHECK_COM_ABLE, TARGET
	CALL EVENT_RANKOU_SET, "夜這い乱交"
	IF CSTR:夜袭 != ""
		CSTR:乱交 = %CSTR:夜袭%
		CALL EVENT_RANKOU, "夜這い乱交"
	ENDIF
ELSEIF CHOICE == 88224646
	MEMO_TARGET = TARGET

	SIF TARGET <= 0
		TARGET = 1
	TRYCALL DEBUG_MODE

	TARGET = MEMO_TARGET
	;3サイズと身長
	FOR LOCAL, 1, CHARANUM
		CALL SET_3SIZE, LOCAL
	NEXT
ENDIF

RETURN 0

@EVENT_SHOP
REDRAW 0

PRINTL
PRINTFORML 「来看看我这里的好东西吧」
CALL EVENT_SHOP_BUY

@EVENT_SHOP_BUY
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
PRINTL
PRINTFORML 要买些什么呢？　(剩余 %MONEY_C(MONEY)%)
CALL SALEITEM_CHECK
DRAWLINE
PRINTFORML [100] 离开店铺

CALL INPUT_SELECT, 100, 100

SELECTCASE RESULT
CASE 100
	PRINTFORMW 「期待您再次上门光临哦」
	REDRAW 1
	RETURN 0
CASEELSE
	;売ってない番号を選択したらやり直し
	IF ITEMSALES:RESULT == 0
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
	ELSE
		BOUGHT = RESULT
		CALL NEW_EVENTBUY
	ENDIF
ENDSELECT

RESTART

@SHOW_ITEM_HAVE
#DIM NEXT_LINE
#DIM LCOUNT

PRINTL □ 所持物品一览 □---------------------------------------------------------

NEXT_LINE = 0
FOR LCOUNT, 0, 30
	SIF ITEM:LCOUNT == 0
		CONTINUE
	SIF NEXT_LINE == 0
		PRINTL □ 调教道具 □-----------------------------------------------------------------
	SELECTCASE ITEM:LCOUNT
	CASE 1
		PRINTFORM %ITEMNAME:LCOUNT%　
	CASEELSE
		PRINTFORM %ITEMNAME:LCOUNT%({ITEM:LCOUNT})　
	ENDSELECT
	NEXT_LINE += 1
NEXT
IF NEXT_LINE
	PRINTL
	PRINTL
ENDIF

NEXT_LINE = 0
FOR LCOUNT, 30, 50
	SIF ITEM:LCOUNT == 0
		CONTINUE
	SIF NEXT_LINE == 0
		PRINTL □ 消耗品＆薰香 □-------------------------------------------------------
	SELECTCASE ITEM:LCOUNT
	CASE 1
		PRINTFORM %ITEMNAME:LCOUNT%　
	CASEELSE
		PRINTFORM %ITEMNAME:LCOUNT%({ITEM:LCOUNT})　
	ENDSELECT
	NEXT_LINE += 1
NEXT
IF NEXT_LINE
	PRINTL
	PRINTL
ENDIF


NEXT_LINE = 0
FOR LCOUNT, 90, 100
	SIF ITEM:LCOUNT == 0
		CONTINUE
	SIF NEXT_LINE == 0
		PRINTL □ 特殊物品 □---------------------------------------------------------------
	SELECTCASE ITEM:LCOUNT
	CASE 1
		PRINTFORM %ITEMNAME:LCOUNT%　
	CASEELSE
		PRINTFORM %ITEMNAME:LCOUNT%({ITEM:LCOUNT})　
	ENDSELECT
	NEXT_LINE += 1
NEXT
IF NEXT_LINE
	PRINTL
	PRINTL
ENDIF


NEXT_LINE = 0
FOR LCOUNT, 300, 320
	SIF ITEM:LCOUNT == 0
		CONTINUE
	SIF NEXT_LINE == 0
		PRINTL □ 收藏品 □-------------------------------------------------------------------
	SELECTCASE ITEM:LCOUNT
	CASE 1
		PRINTFORM %ITEMNAME:LCOUNT%　
	CASEELSE
		PRINTFORM %ITEMNAME:LCOUNT%({ITEM:LCOUNT})　
	ENDSELECT
	NEXT_LINE += 1
NEXT
IF NEXT_LINE
	PRINTL
	PRINTL
ENDIF

IF ITEM:淫魔水滴
	PRINTL □ 调合材料 □-----------------------------------------------------------------
	PRINTFORML 淫魔の雫({ITEM:淫魔水滴})
	PRINTL
ENDIF

NEXT_LINE = 0
FOR LCOUNT, 500, 600
	SIF ITEM:LCOUNT == 0
		CONTINUE
	SIF NEXT_LINE == 0
		PRINTL □ COS服 □-------------------------------------------------------------
	PRINTFORM %TEXT_LJ(ITEMNAME:LCOUNT, 20)%
	NEXT_LINE += 1
	SIF NEXT_LINE%4 == 0
		PRINTFORML
NEXT

SIF NEXT_LINE%4
	PRINTFORML

DRAWLINE
WAIT


@COMPOUND_ITEM
#DIM LCOUNT
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
PRINTL
DRAWLINE
PRINTFORML 淫魔水滴是调合物品和药品的重要原料　(持有淫魔水滴：{ITEM:淫魔水滴}个)

VARSET LOCAL
;調合アイテムの実際に使う相手がいるかなどを判定
;元気になる薬
SIF FIND_USE_ITEM("元气之药")
	LOCAL:0 = 1
;ドロップ
FOR LCOUNT, 1, 5
	SIF FIND_USE_ITEM(ITEMNAME:(LCOUNT + 54) )
		LOCAL:LCOUNT = 1
NEXT
;330番台のアイテム
FOR LCOUNT, 5, 16
	SIF FIND_USE_ITEM(ITEMNAME:(LCOUNT + 325) )
		LOCAL:LCOUNT = 1
NEXT
;淫紋
SIF FIND_USE_ITEM("淫紋刻印可能")
	LOCAL:30 = 1


SIF ITEM:乳香 < 99
	LOCAL:40 = 1
SIF ITEM:月之糖果 < 99
	LOCAL:50 = 1

;淫魔の雫は足りてる？
SELECTCASE ITEM:淫魔水滴
CASE IS < 3
	VARSET LOCAL
CASE IS < 10
	FOR LCOUNT, 0, 5
		LOCAL:LCOUNT = 0
	NEXT
	LOCAL:30 = 0
	LOCAL:40 = 0
	LOCAL:50 = 0
CASE IS < 30
	LOCAL:30 = 0
ENDSELECT

;淫魔の雫を買い足す？
SIF ITEM:淫魔水滴 < 999 && MONEY >= ITEMPRICE:淫魔水滴
	LOCAL:90 = 1

PRINTFORML  [\@ LOCAL:0 ? {0, 2} # × \@] %TEXT_LJ("扶她之药", 16)% 10个　变身成扶她or解除变身
PRINTFORML  [\@ LOCAL:1 ? {1, 2} # × \@] %TEXT_LJ("青色药丸", 16)% 10个　变成母乳体质
PRINTFORML  [\@ LOCAL:2 ? {2, 2} # × \@] %TEXT_LJ("赤色药丸", 16)% 10个　修复处女膜or失去母乳体质
PRINTFORML  [\@ LOCAL:3 ? {3, 2} # × \@] %TEXT_LJ("黑色药丸", 16)% 10个　反转性别
PRINTFORML  [\@ LOCAL:4 ? {4, 2} # × \@] %TEXT_LJ("白色药丸", 16)% 10个　治疗漏尿癖或体内的损伤
PRINTFORML  [\@ LOCAL:5 ? {5, 2} # × \@] %TEXT_LJ("巨根药", 16)% ３个　无需多说，变大就是了！
PRINTFORML  [\@ LOCAL:6 ? {6, 2} # × \@] %TEXT_LJ("缩根药", 16)% ３个　让阴茎变小
PRINTFORML  [\@ LOCAL:7 ? {7, 2} # × \@] %TEXT_LJ("剥皮药", 16)% ３个　降低包茎程度
PRINTFORML  [\@ LOCAL:8 ? {8, 2} # × \@] %TEXT_LJ("伸皮药", 16)% ３个　增加包茎程度
PRINTFORML  [\@ LOCAL:9 ? {9, 2} # × \@] %TEXT_LJ("丰胸药", 16)% ３个　让胸部变大
PRINTFORML  [\@ LOCAL:10 ? {10, 2} # × \@] %TEXT_LJ("贫乳药", 16)% ３个　让胸部变小
PRINTFORML  [\@ LOCAL:11 ? {11, 2} # × \@] %TEXT_LJ("巨人之药", 16)% ３个　让身体变大
PRINTFORML  [\@ LOCAL:12 ? {12, 2} # × \@] %TEXT_LJ("小人之药", 16)% ３个　让身体变小
PRINTFORML  [\@ LOCAL:13 ? {13, 2} # × \@] %TEXT_LJ("虹色之药", 16)% ３个　改变肤色
PRINTFORML  [\@ LOCAL:14 ? {14, 2} # × \@] %TEXT_LJ("桃尻药", 16)% ３个　让屁股变大
PRINTFORML  [\@ LOCAL:15 ? {15, 2} # × \@] %TEXT_LJ("小尻药", 16)% ３个　让屁股变小

PRINTFORML  [\@ LOCAL:30 ? {30, 2} # × \@] %TEXT_LJ("淫纹", 16)% 30个　增强色欲的纹章

PRINTFORML  [\@ LOCAL:40 ? {40, 2} # × \@] %TEXT_LJ("乳香", 16)% 10个　气味甘甜高贵的薰香（持有：{ITEM:乳香}个）
PRINTFORML  [\@ LOCAL:50 ? {50, 2} # × \@] %TEXT_LJ("月之糖果", 16)% 10个　梦魔们最喜欢的零食（持有：{ITEM:月之糖果}个）

PRINTFORML  [\@ LOCAL:90 ? {90, 2} # × \@] 购买淫魔水滴%MONEY_C(ITEMPRICE:淫魔水滴)%（金钱：%MONEY_C(MONEY)%）

PRINTFORML [100] 返回上层菜单

CALL INPUT_SELECT, 16, 30, 40, 50, 90, 100

IF RESULT != 100 && LOCAL:RESULT == 0
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

SELECTCASE RESULT
CASE 100
	RETURN 0
CASE 0
	BOUGHT = 51
	CALL USE_ITEM, "調合"
CASE 1 TO 4
	BOUGHT = RESULT + 54
	CALL USE_ITEM, "調合"
CASE 5 TO 29
	BOUGHT = RESULT + 325
	CALL USE_ITEM, "調合"
CASE 30
	BOUGHT = 350
	CALL USE_ITEM, "調合"
CASE 40
	BOUGHT = 49
	PRINTFORMW 调合乳香
	ITEM:乳香 += 1
	ITEM:淫魔水滴 -= 10
CASE 50
	BOUGHT = 99
	PRINTFORMW 调合月之糖果
	ITEM:月之糖果 += 1
	ITEM:淫魔水滴 -= 10
CASE 90
	ITEM:淫魔水滴 += 1
	MONEY -= ITEMPRICE:淫魔水滴
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
ENDSELECT

RESTART

@SELL_ITEM, ARGS
#DIM LCOUNT
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT

PRINTL
DRAWLINE
PRINTFORML 看起来好像没有可以卖掉的东西呢　金钱：%MONEY_C(MONEY)%
DRAWLINE
VARSET LOCAL
IF FLAG:259
	;調教アイテム
	FOR LCOUNT, 0, 30
		SIF ITEM:LCOUNT <= 0
			CONTINUE
		PRINTFORML  [{LCOUNT, 2}] %TEXT_LJ(ITEMNAME:LCOUNT, 23)%×{ITEM:LCOUNT, 3} (%MONEY_C(ITEMPRICE:LCOUNT / 2)%)
		LOCAL:LCOUNT = 1
	NEXT
ELSE
	;消耗アイテム
	FOR LCOUNT, 30, 100
		SIF ITEM:LCOUNT <= 0
			CONTINUE
		SELECTCASE LCOUNT
		CASE 30 TO 49, 96, 99
			PRINTFORML  [{LCOUNT, 2}] %TEXT_LJ(ITEMNAME:LCOUNT, 23)%×{ITEM:LCOUNT, 3} (%MONEY_C(ITEMPRICE:LCOUNT / 2)%)
			LOCAL:LCOUNT = 1
		ENDSELECT
	NEXT
	;収集専用アイテム
	FOR LCOUNT, 300, 330
		SIF ITEM:LCOUNT <= 0
			CONTINUE
		PRINTFORML [{LCOUNT, 3}] %TEXT_LJ(ITEMNAME:LCOUNT, 23)%×{ITEM:LCOUNT, 3} (%MONEY_C(ITEMPRICE:LCOUNT / 2)%)
		LOCAL:LCOUNT = 1
	NEXT
ENDIF
PRINTFORML [900] \@ FLAG:259 ? 显示消耗品之类的东西 # 出售调教道具之类的东西 \@
PRINTFORML [999] 返回上层菜单
SIF ARGS != ""
	PRINTFORML %ARGS%

$INPUT_LOOP
INPUT

IF RESULT == 999
	PRINTFORMW 如果你有什么有趣的东西再联系我吧
	FLAG:259 = 0
	RETURN 0
ELSEIF RESULT == 900
	INVERTBIT FLAG:259, 0
ELSEIF RESULT >= 0 && RESULT < 999 && ITEM:RESULT && LOCAL:RESULT
	ARGS = 卖掉了%ITEMNAME:RESULT%，获得了%MONEY_C(ITEMPRICE:RESULT / 2)%
	ITEM:RESULT -= 1
	MONEY += ITEMPRICE:RESULT / 2
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@COSPLAY_SHOP
DRAWLINE
PRINTL  □ 高级服饰店『卡瓦拉特牛奶』 □
DRAWLINE
PRINTFORML 「欢迎光临～
PRINTFORML 　本店有很多来自世界各地的高级服饰。
PRINTFORMW 　特价仅限今天，每套只要%MONEY_C(ITEMPRICE:女教师)%～」
CALL COSPLAY_SHOP_BUY

@COSPLAY_SHOP_BUY
#DIM LCOUNT

PRINTL
PRINTFORML 要买些什么呢？　(剩余 %MONEY_C(MONEY)%)

VARSET LOCAL
FOR LCOUNT, 500, 600
	SIF ITEM:LCOUNT
		CONTINUE

	IF STRLENS(ITEMNAME:LCOUNT)
		PRINTFORML  [{LCOUNT - 500,2}] %ITEMNAME:LCOUNT%
		LOCAL:(LCOUNT - 500) = 1
	ENDIF
NEXT


PRINTFORML [100] 离开店铺
$INPUT_LOOP
INPUT
IF RESULT == 100
	PRINTW 「期待您再次上门光临哦」
	RETURN 0
ELSEIF RESULT >= 0 && RESULT <= 99 && ITEM:(RESULT + 500) == 0 && LOCAL:RESULT
	IF MONEY >= ITEMPRICE:(RESULT + 500)
		MONEY -= ITEMPRICE:(RESULT + 500)
		PRINTFORML ＜购入了%ITEMNAME:(RESULT + 500)%＞
		ITEM:(RESULT + 500) = 1
	ELSE
		PRINTL 「对不起，你手上的钱好像不太够呢」
		PRINTW 「期待您再次上门光临哦」
		RETURN 0
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

IF MONEY >= ITEMPRICE:女教师 && NUM("総衣装数") > NUM("所持衣装")
	PRINTW 「还要买其他服装吗？」
	RESTART
ELSE
	PRINTW 「期待您再次上门光临哦」
	RETURN 0
ENDIF

@ARCANA_SHOP
DRAWLINE
PRINTL  □ 秘法店『魔女之家』 □
DRAWLINE
;土曜の夜は魔女のお祭り
IF DAYEV("土曜") && TEXTS("時間") == "夜"
	PRINTFORML 「哦呀～欢迎光临。
	PRINTFORML 　我这里的魔导书都分便宜哦，是超有良心的好店。
	PRINTFORMW 　……嘛，你说这里的书都不太容易读懂？ 库～库～库～」
	PRINTFORML
	PRINTFORML  如果拥有[魔法使]素质，可以在这里以金钱为代价习得秘法
	PRINTFORML  不过，MASTER以外的角色如果要学习秘法，还需要为每次学习准备1,000个习得宝珠
	CALL PRINT_COLORTEXT, " 不同颜色", DEF_COLOR("水色")
	PRINTFORML 是中级以上的魔导书。必须具有[聪慧]或[上位梦魔]才可以学习
	PRINTFORMW （不过话说回来，这里面好像没有关于STOCK术和束缚术的书……）
	CALL ARCANA_SHOP_BUY, "中級"
	PRINTFORMW 「要再来哦」
ELSE
	PRINTFORML 「啊啦～欢迎光临。
	PRINTFORML 　我这里的魔导书既便宜又好懂哦，是超有良心的好店。
	PRINTFORMW 　……但是，如果是脑袋不太好的孩子，可能还是会读不懂哦？」
	PRINTFORML
	PRINTFORML  如果拥有[魔法使]素质，可以在这里以金钱为代价习得秘法
	PRINTFORML  不过，MASTER以外的角色如果要学习秘法，还需要为每次学习准备1,000个习得宝珠
	CALL PRINT_COLORTEXT, " 不同颜色", DEF_COLOR("水色")
	PRINTFORML 是中级以上的魔导书。必须具有[聪慧]或[上位梦魔]才可以学习
	PRINTFORMW （不过话说回来，这里面好像没有关于STOCK术和束缚术的书……）
	CALL ARCANA_SHOP_BUY, "中級"
	PRINTFORMW 「要再来哦」
ENDIF


@ARCANA_SHOP_BUY, ARGS
#DIM LCOUNT
#DIM CHOICE
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT
REDRAW 0

VARSET ITEMSALES, 0, 600, 700
VARSET LOCAL

;売られてるアルカナの判定
FOR LCOUNT, 600, 600 + NUM("存在するアルカナ")
	SIF MONEY < ITEMPRICE:LCOUNT
		CONTINUE

	FOR LOCAL, 1, CHARANUM
		SIF TALENT:LOCAL:魔法使 != 1
			CONTINUE

		SELECTCASE LOCAL
		CASE MASTER
			SELECTCASE ITEMNAME:LCOUNT
			CASE "STOCK", "束缚术"
				CONTINUE
			ENDSELECT
		CASEELSE
			SIF JUEL:LOCAL:习得 < 1000
				CONTINUE
		ENDSELECT

		SELECTCASE ITEMNAME:LCOUNT
		CASE "STOCK", "倍化术", "解弱化术", "束缚术", "全强术", "大根术", "快感术"
			SIF TALENT:LOCAL:聪慧 == 0 && TALENT:LOCAL:上位梦魔 == 0
				CONTINUE
		ENDSELECT

		SIF TALENT:LOCAL:(ITEMNAME:LCOUNT)
			CONTINUE

		ITEMSALES:LCOUNT = 1
		BREAK
	NEXT
NEXT

PRINTL
PRINTFORML 要买些什么呢？　(金钱：%MONEY_C(MONEY)%)
FOR LCOUNT, 0, NUM("存在するアルカナ")
	LOCAL = LCOUNT + 600

	SELECTCASE ITEMNAME:LOCAL
	CASE "束缚术"
		ITEMSALES:LOCAL = 0
		CONTINUE
	CASE "STOCK", "倍化术", "解弱化术", "全强术", "大根术", "快感术"
		SELECTCASE ARGS
		CASE "初級"
			ITEMSALES:LOCAL = 0
			CONTINUE
		CASEELSE
			SETCOLOR DEF_COLOR("水色")
		ENDSELECT
	ENDSELECT

	PRINTFORML  [\@ ITEMSALES:LOCAL ? {LCOUNT, 2} # × \@] %TEXT_LJ(ITEMNAME:LOCAL, 12)% %TEXT_RJ(MONEY_C(ITEMPRICE:LOCAL), 7)% %TEXTS("アルカナ解説", LCOUNT + 230)%
	RESETCOLOR
NEXT

PRINTFORML [100] 离开店铺

CALL INPUT_SELECT, NUM("存在するアルカナ"), 100

SELECTCASE RESULT
CASE 100
	REDRAW 1
	RETURN 0
CASEELSE
	IF RESULT >= 0 && RESULT < NUM("存在するアルカナ") && ITEMSALES:(RESULT + 600)
		BOUGHT = RESULT + 600
		PRINTFORML 由谁来学习%ITEMNAME:BOUGHT%呢？　(购买后剩余：%MONEY_C(MONEY - ITEMPRICE:BOUGHT)%)

		VARSET LOCAL
		FOR LCOUNT, 1, CHARANUM
			SIF TALENT:LCOUNT:魔法使 != 1
				CONTINUE

			SELECTCASE LCOUNT
			CASE MASTER
				SELECTCASE ITEMNAME:BOUGHT
				CASE "STOCK", "束缚术"
					CONTINUE
				ENDSELECT
			CASEELSE
				SIF JUEL:LCOUNT:习得 < 1000
					CONTINUE
			ENDSELECT

			SELECTCASE ITEMNAME:BOUGHT
			CASE "STOCK", "倍化术", "解弱化术", "束缚术", "全强术", "大根术", "快感术"
				SIF TALENT:LCOUNT:聪慧 == 0 && TALENT:LCOUNT:上位梦魔 == 0
					CONTINUE
			ENDSELECT

			SIF TALENT:LCOUNT:(ITEMNAME:BOUGHT)
				CONTINUE

			CALL PRINT_NUM_NAME, "体力", LCOUNT
			LOCAL:LCOUNT = 1
		NEXT
		PRINTL  [100] 返回上层菜单

		CALL INPUT_SELECT, CHARANUM, 100
		CHOICE = RESULT

		SELECTCASE CHOICE
		CASE 100
		CASE IS < CHARANUM
			IF LOCAL:CHOICE
				LOCALS =
				;覚えているアルカナの表示
				FOR LCOUNT, 230, 230 + NUM("存在するアルカナ")
					SIF TALENT:CHOICE:LCOUNT
						LOCALS = %LOCALS%[%TALENTNAME:LCOUNT%]
				NEXT
				IF LOCALS != ""
					PRINTFORML %CALLNAME:CHOICE%已经学会了%LOCALS%
					PRINTFORML 要让她学习[%ITEMNAME:BOUGHT%]吗？\@ CHOICE != MASTER ? （习得宝珠：{JUEL:CHOICE:习得}） # \@
				ELSE
					PRINTFORML %CALLNAME:CHOICE%还没学习过秘法
					PRINTFORML 要让她学习[%ITEMNAME:BOUGHT%]吗？\@ CHOICE != MASTER ? （习得宝珠：{JUEL:CHOICE:习得}） # \@
				ENDIF

				PRINTFORML [ 0] 好的
				PRINTFORML [ 1] 我再考虑一下
				CALL INPUT_SELECT, 2
				SELECTCASE RESULT
				CASE 1
					RESTART
				ENDSELECT

				PRINTFORML ＜%CALLNAME:CHOICE%学会了[%ITEMNAME:BOUGHT%]＞
				TALENT:CHOICE:(ITEMNAME:BOUGHT) = 1
				IF CHOICE != MASTER
					PRINTFORMW 习得宝珠-1000
					JUEL:CHOICE:习得 -= 1000
				ENDIF
				MONEY -= ITEMPRICE:BOUGHT
			ENDIF
		ENDSELECT
	ELSE
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
	ENDIF
ENDSELECT
RESTART

;-------------------------------------------------
;ARGSを使う事が出来るキャラが一人でもいたら１を返す。いない場合には０
;-------------------------------------------------
@FIND_USE_ITEM(ARGS)
#FUNCTION

SIF TARGET && COND(ARGS)
	RETURNF 1
SIF COND(ARGS, MASTER) || FIND_COND(ARGS)
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;アイテム出現条件
;ARGSに"表示なし"とすればアイテム一覧は出さない
;-------------------------------------------------
@SALEITEM_CHECK, ARGS
#DIM LCOUNT
#DIM NEXT_LINE

CALL SETFLAG, "アイテムフラグ調整"

ITEMSALES:触手使役术 = 0

ITEMSALES:肛珠 = 0
SIF ITEM:肛用按摩棒 && ITEM:肛珠 == 0
	ITEMSALES:肛珠 = 1
;魔法瓶
ITEMSALES:魔法瓶 = 0
SIF FLAG:91 && ITEM:魔法瓶 == 0
	ITEMSALES:魔法瓶 = 1

ITEMSALES:排卵诱发剂 = 0
ITEMSALES:肌肉松弛剂 = 0

;50番以降は、初期状態では売られていない
VARSET ITEMSALES, 0, 50, 100


;主人か助手が『調合知識』を持っている
IF TALENT:MASTER:调合知识 || (ASSI && TALENT:ASSI:调合知识)
	SIF FIND_USE_ITEM("元气之药")
		ITEMSALES:元气之药 = 1
	SIF FIND_USE_ITEM("扶她之药")
		ITEMSALES:扶她之药 = 1
ENDIF

;主人か助手が『禁断の知識』を持っている
IF TALENT:MASTER:禁忌知识 || (ASSI && TALENT:ASSI:禁忌知识)
	SIF ITEM:肌肉松弛剂 < 99
		ITEMSALES:肌肉松弛剂 = 1
	;触手使役術
	SIF ITEM:触手使役术 == 0
		ITEMSALES:触手使役术 = 1

	;ドロップ
	FOR LCOUNT, 55, 59
		SIF FIND_USE_ITEM(ITEMNAME:LCOUNT)
			ITEMSALES:LCOUNT = 1
	NEXT

	FOR LCOUNT, 1, CHARANUM
		IF TALENT:LCOUNT:誓约 == 0 && TALENT:LCOUNT:相思相爱 == 0
			ITEMSALES:誓约卷轴 = 1
			BREAK
		ENDIF
	NEXT
	FOR LCOUNT, 1, CHARANUM
		IF COND("成長性", LCOUNT)
			ITEMSALES:扭曲时钟 = 1
			BREAK
		ENDIF
	NEXT
	FOR LCOUNT, 1, CHARANUM
		;温泉予約中はダメ
		SIF FLAG:60
			BREAK
		IF ITEM:化蝶之梦 == 0 && COND("妊娠イベント中", LCOUNT) == 0 && MARK:LCOUNT:排斥刻印 == 0 && ABL:LCOUNT:服从 >= 4
			ITEMSALES:化蝶之梦 = 1
			BREAK
		ENDIF
	NEXT
	SIF ITEM:等级限制器 == 0
		ITEMSALES:等级限制器 = 1
	SIF CHARANUM >= 3
		ITEMSALES:记忆消除药 = 1
ENDIF


;素質系アイテムの処理
SIF ABL:MASTER:技巧 < 3
	ITEMSALES:☆技巧Lv = 1
SIF TALENT:MASTER:高潮管理 == 0
	ITEMSALES:☆高潮管理 = 1
SIF TALENT:MASTER:无视污臭 == 0
	ITEMSALES:☆无视脏污 = 1
SIF TALENT:MASTER:禁忌知识 == 0
	ITEMSALES:☆禁忌知识 = 1
;☆調合大全。賢くて、調合知識を持っていないものがいる。習得の珠も5万必要
FOR LCOUNT, 1, CHARANUM
	SIF TALENT:LCOUNT:聪慧 == 0 || TALENT:LCOUNT:调合知识 || JUEL:LCOUNT:习得 < ITEMPRICE:65
		CONTINUE
	ITEMSALES:☆调合大全 = 1
	BREAK
NEXT
;☆調教知識。賢いorＭＡＳＴＥＲ。習得の珠も1万必要
FOR LCOUNT, 1, CHARANUM
	IF LCOUNT == MASTER
		SIF FLAG:240
			CONTINUE
	ELSEIF TALENT:LCOUNT:聪慧 == 0 || JUEL:LCOUNT:习得 < ITEMPRICE:66
		CONTINUE
	ENDIF
	ITEMSALES:☆调教知识 = 1
	BREAK
NEXT

;インのモウサポートセンター
IF CONFIG("陰毛と腋毛")
	FOR LCOUNT, 1, CHARANUM
		IF COND("阴毛", LCOUNT) && COND("阴毛", LCOUNT) < 7
			ITEMSALES:生毛剂 = 1
			BREAK
		ENDIF
	NEXT
	FOR LCOUNT, 1, CHARANUM
		IF COND("阴毛", LCOUNT) >= 4
			ITEMSALES:脱毛剂 = 1
			BREAK
		ENDIF
	NEXT
ENDIF

;妊娠パッチをONにしている
IF CONFIG("妊娠パッチ")
	ITEMSALES:排卵诱发剂 = 1
	;避妊薬を使う意味のあるキャラがいるか
	FOR LCOUNT, 1, CHARANUM
		;オトコor妊娠中or避妊中or人形
		SIF TALENT:LCOUNT:男性 || TALENT:LCOUNT:怀孕 || TALENT:LCOUNT:魔导人偶 || COND("着床確認", LCOUNT)
			CONTINUE

		ITEMSALES:紧急避孕药 = 1

		SIF TALENT:LCOUNT:处女 == 0
			ITEMSALES:避孕护符 = 1

		SIF TALENT:LCOUNT:避孕护符 == 0
			ITEMSALES:避孕药 = 1
	NEXT
ENDIF

;複数指輪が渡せる設定orまだ渡していない
IF CONFIG("相思相愛：人数制限無し") || TALENT:MASTER:相思相爱 == 0
	FOR LCOUNT, 1, CHARANUM
		SIF TALENT:LCOUNT:恋慕 == 0 || TALENT:LCOUNT:相思相爱
			CONTINUE
		ITEMSALES:誓约指环 = 1
		BREAK
	NEXT
ENDIF

;淫魔の雫は999個まで持てる
SIF ITEM:淫魔水滴 < 999
	ITEMSALES:淫魔水滴 = 1

;非売品
ITEMSALES:乳香 = 0
ITEMSALES:砥砺思维之石 = 0
ITEMSALES:月之糖果 = 0

FOR LCOUNT, 30, 50
	IF ITEM:LCOUNT >= 99
		ITEM:LCOUNT = 99
		ITEMSALES:LCOUNT = 0
	ENDIF
NEXT

SIF ARGS == "表示なし"
	RETURN 0

DRAWLINE
;0-99に自動的に売り物が表示される。
;PRINT_SHOPITEM
NEXT_LINE = 0
FOR LCOUNT, 0, 100
	SIF ITEMSALES:LCOUNT == 0 || ITEMNAME:LCOUNT == ""
		CONTINUE

	PRINTFORMLC  [{LCOUNT, 2}] %TEXT_LJ(ITEMNAME:LCOUNT, 14)%%TEXT_RJ(MONEY_C(ITEMPRICE:LCOUNT), MIN(21 - STRLENS(ITEMNAME:LCOUNT), 7) )%

	NEXT_LINE += 1
	SIF NEXT_LINE%3 == 0
		PRINTL
NEXT
SIF NEXT_LINE%3
	PRINTL

;-------------------------------------------------
;アイテム購入
;-------------------------------------------------
@NEW_EVENTBUY
;調教アイテム
SELECTCASE BOUGHT
CASE 0 TO 29, 97

	;お金がなければ買えない
	IF MONEY < ITEMPRICE:BOUGHT
		RETURN 0
	ENDIF

	PRINTFORMW ＜%ITEMNAME:BOUGHT%入手了＞
	ITEM:BOUGHT += 1
	MONEY -= ITEMPRICE:BOUGHT

	;補足があるならここで
	SELECTCASE BOUGHT
	;尿道責めキット
	CASE 24
		PRINTFORML □ 补充 □---------------------------------------------------------------------
		PRINTFORM 尿道扩张需要
		CALL PRINT_COLORTEXT, "[调教知识(性器扩张)]", DEF_COLOR("黄色")
		PRINTFORM 或
		CALL PRINT_COLORTEXT, "[扩张适应]", DEF_COLOR("黄色")
		PRINTFORML 之类的素质
		PRINTFORM 虽然素质
		CALL PRINT_COLORTEXT, "[性变态]", DEF_COLOR("黄色")
		PRINTFORML 不能降低扩张难度，但可以缓解因为扩张所致的恐惧等负面情绪上升趋势
		PRINTFORML
		PRINTFORML 尿道的快感跟Ｃ感度有依存关系……，初期疼痛的感觉会超过快乐的感觉
		PRINTFORML 随着经验的累积，痛感会慢慢减弱，所以要慢慢累积尿道调教经验
		PRINTFORM 通过累积尿道的绝顶体验，就可以习得
		CALL PRINT_COLORTEXT, "[尿道性感]", DEF_COLOR("黄色")
		PRINTFORML 但如果缺少
		CALL PRINT_COLORTEXT, "[尿道性感]", DEF_COLOR("黄色")
		PRINTFORMW 调教将会非常痛苦，建议尽早取得这项素质
	ENDSELECT
;ローションなど、消費アイテム。それに加えて淫魔の雫も
CASE 30 TO 49, 78
	CALL BUY_PLURAL

;素質アイテム【技巧Lv】
CASE 60
	VARSET LOCAL
	PRINTL  [0] 提升技巧Lv
	IF ABL:MASTER:技巧 > 1
		PRINTL  [1] 降低技巧Lv(免费)
		LOCAL:1 = 1
	ENDIF
	PRINTL [100] 返回上层菜单
	CALL INPUT_SELECT, 1, LOCAL:1, 100
	SELECTCASE RESULT
	CASE 100

	CASE 0
		IF MONEY < ITEMPRICE:BOUGHT
			PRINTFORMW 金钱不足
			RETURN 0
		ENDIF
		MONEY -= ITEMPRICE:BOUGHT
		ABL:MASTER:技巧 += 1
		PRINTFORMW ＜%NAME:MASTER%的技巧Lv提升了1级　现在的技巧Lv：{ABL:MASTER:技巧}＞
	CASE 1
		ABL:MASTER:技巧 -= 1
		PRINTFORMW ＜%NAME:MASTER%の的技巧Lv下降了1级　现在的技巧Lv：{ABL:MASTER:技巧}＞
	ENDSELECT

;MASTERが素質を覚えるアイテム
CASE 61 TO 64
	;お金がなければ買えない
	IF MONEY < ITEMPRICE:BOUGHT
		RETURN 0
	ENDIF
	SELECTCASE ITEMNAME:BOUGHT
	CASE "☆高潮管理"
		PRINTFORMW ＜%NAME:MASTER%取得了[高潮管理]＞
		TALENT:MASTER:高潮管理 = 1
	CASE "☆无视脏污"
		PRINTFORMW ＜%NAME:MASTER%取得了[无视脏污]＞
		TALENT:MASTER:无视污臭 = 1
	;サドは現在封印中
	CASE "☆サド"
		PRINTFORMW ＜%NAME:MASTER%取得了[抖Ｓ]＞
		TALENT:MASTER:抖Ｓ = 1
	CASE "☆禁忌知识"
		PRINTFORMW ＜%NAME:MASTER%取得了[禁忌知识]＞
		TALENT:MASTER:禁忌知识 = 1
	ENDSELECT
	MONEY -= ITEMPRICE:BOUGHT

;使用アイテム、素質アイテム
CASE 50 TO 91, 94, 95, 98
	CALL USE_ITEM
ENDSELECT


;-------------------------------------------------
;複数個所持可能なアイテムについて、同時購入個数を選択
;-------------------------------------------------
@BUY_PLURAL
#DIM MEMO

;買う前の所持数を記録
MEMO = ITEM:BOUGHT

DRAWLINE
CALL SHOW_BUY_ITEM, BOUGHT

;１０個以上買った？
IF ITEM:BOUGHT - MEMO >= 10
	LOCAL = (ITEM:BOUGHT - MEMO)/10
	PRINTFORMW 道具屋的主人看你买了这么多，于是决定免费赠送给你{LOCAL}个
	ITEM:BOUGHT += LOCAL
ENDIF
PRINTL

;-------------------------------------------------
;所持金に対応して、ITEM:ARGを何個買うかの選択肢を表示する関数
;-------------------------------------------------
@SHOW_BUY_ITEM, ARG
#DIM LCOUNT
#DIM MEMO_MAX

;買える最大個数
MEMO_MAX = MIN(99 - ITEM:ARG, MONEY/ITEMPRICE:ARG)
;淫魔の雫
SIF ARG == 78
	MEMO_MAX = MIN(999 - ITEM:ARG, MONEY/ITEMPRICE:ARG)

PRINTFORML 你想要买多少个%ITEMNAME:ARG%？　(持有：{ITEM:ARG}　金钱：%MONEY_C(MONEY)%)
PRINTFORML （可以直接输入购买数量。输入[  0]或[999]返回上层菜单）

FOR LCOUNT, 1, MEMO_MAX+1
	SIF MONEY < ITEMPRICE:ARG * LCOUNT
		BREAK
	SELECTCASE LCOUNT
	CASE IS <= 5, 10, 20, 50, MEMO_MAX
		PRINTFORML  [{LCOUNT, 2}] {LCOUNT, 2}个　(%MONEY_C(ITEMPRICE:ARG * LCOUNT)%)
	ENDSELECT
NEXT
PRINTFORML [999] 返回上层菜单

$INPUT_LOOP
INPUT

IF RESULT == 0 || RESULT == 999
	RETURN 0
ELSEIF ITEMPRICE:ARG * RESULT > MONEY
	PRINTL 金钱不足
	GOTO INPUT_LOOP
ELSEIF RESULT > MEMO_MAX
	PRINTL 你已经放不下那么多了
	GOTO INPUT_LOOP
ELSEIF RESULT >= 1 && RESULT <= MEMO_MAX
	PRINTFORMW ＜购买了 \@ RESULT > 1 ? {RESULT}个  # \@ %ITEMNAME:ARG%＞
ELSE
	GOTO INPUT_LOOP
ENDIF

ITEM:ARG += RESULT
MONEY -= ITEMPRICE:ARG * RESULT

RETURN RESULT

;-------------------------------------------------
;アイテム使用。ARGS == "調合"だと淫魔の雫が通貨扱いになる
;-------------------------------------------------
@USE_ITEM, ARGS
#DIM LCOUNT
#DIM CHOICE, 2
#DIM MEMO, 10

DRAWLINE
;アイテムの効果を表示する。
SELECTCASE BOUGHT
CASE 65
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%)：习得调合知识（要求：聪慧＆习得宝珠{ITEMPRICE:BOUGHT}）
CASE 66
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%)：习得各种调教知识
	PRINTFORML 在本周内不能对MASTER进行变更\@ FLAG:240 ? (还剩{FLAG:240}天) # \@，
	PRINTFORML 变更其他角色，需要具备聪慧＆习得宝珠{ITEMPRICE:BOUGHT}
CASE 70
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%)：可以促进\@ CONFIG("阴毛") ? 和%TEXTS("腋")% # \@ 的毛的生长
CASE 71
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%)：可以脱掉\@ CONFIG("阴毛") ? 和%TEXTS("腋")% # \@ 的毛
CASE 72
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%)：事前饮用就可以避孕。效果持续10天
CASE 73
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%)：紧急避孕药。可以防止最近的性行为导致的怀孕。
CASE 74
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%)：附有避孕魔法的Ｔ字形小护符
	SETCOLOR DEF_COLOR("ハートピンク")
	PRINTFORML ※需要装备到子宫中，只要装上就会一直有效※
	RESETCOLOR
	PRINTFORML 选择已经装备的角色、就可以破坏避孕护符
CASE 90
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%)：戴在手上她就是你的人了
CASE 91
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%)：爱的宣言
	CALL PRINT_STR, "H_ _ハートピンク_粉红色名字_的角色带有特殊ＥＮＤ _L_"
CASE 94
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%)：用魔法操控特定的时间和空间(孩子们会一口气长大)
CASE 95
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%)：带有奇特气息的书。两册一套。
	PRINTFORML 可以交换两个持有者的身体
CASE 98
	PRINTFORML %ITEMNAME:BOUGHT%(%MONEY_C(ITEMPRICE:BOUGHT)%)：消除所有关于调教的记忆
;以下は調合系
CASE 50
	PRINTFORML %ITEMNAME:BOUGHT%：完全恢复体力和精力（连带STOCK也是！）
CASE 51
	PRINTFORML %ITEMNAME:BOUGHT%：将女孩子变成扶她，或者变回来
CASE 55
	PRINTFORML %ITEMNAME:BOUGHT%：获得母乳体质、治疗乳头凹陷
CASE 56
	PRINTFORML %ITEMNAME:BOUGHT%：修复处女膜并去除母乳体质
CASE 57
	PRINTFORML %ITEMNAME:BOUGHT%：转换性别
CASE 58
	PRINTFORML %ITEMNAME:BOUGHT%：治疗漏尿癖和内伤
ENDSELECT

SELECTCASE BOUGHT
CASE 72, 95
	PRINTFORML 要给谁使用呢？　(金钱：%MONEY_C(MONEY)%)
;淫紋
CASE 350
	PRINTFORML 用魔力之笔在下腹纹上带有情欲的文章
	PRINTFORML (等级上限LV4。随着LV上升，子宫就会渴求更多愉悦)
	PRINTFORML 要给谁纹上呢？　(持有淫魔水滴：{ITEM:淫魔水滴}个)
CASE 55 TO 59
	SELECTCASE ARGS
	CASE "調合"
		PRINTFORML 要给谁服用呢？　(持有淫魔水滴：{ITEM:淫魔水滴}个)
	CASEELSE
		PRINTFORML 要给谁服用呢？　(所持金：%MONEY_C(MONEY)%)
	ENDSELECT
;調合薬
CASE 330 TO 340
	PRINTFORML 要在谁身上使用%ITEMNAME:BOUGHT%呢？　(持有淫魔水滴：{ITEM:淫魔水滴}个)
CASEELSE
	SELECTCASE ARGS
	CASE "調合"
		PRINTFORML 要用在谁身上呢？　(持有淫魔水滴：{ITEM:淫魔水滴}个)
	CASEELSE
		PRINTFORML 要用在谁身上呢？　(所持金：%MONEY_C(MONEY)%)
	ENDSELECT
ENDSELECT

VARSET LOCAL
FOR LCOUNT, 1, CHARANUM
	LOCAL:LCOUNT = 0

	SELECTCASE BOUGHT
	CASE 65
		SIF TALENT:LCOUNT:聪慧 == 0 || TALENT:LCOUNT:调合知识 || JUEL:LCOUNT:习得 < ITEMPRICE:BOUGHT
			CONTINUE
	CASE 66
		IF LCOUNT == MASTER
			SIF FLAG:240
				CONTINUE
		ELSEIF TALENT:LCOUNT:聪慧 == 0 || JUEL:LCOUNT:习得 < ITEMPRICE:BOUGHT
			CONTINUE
		ENDIF
	CASE 73
		SIF TALENT:LCOUNT:男性 || TALENT:LCOUNT:怀孕 || TALENT:LCOUNT:魔导人偶 || COND("着床確認", LCOUNT)
			CONTINUE
	CASE 85
		SIF LCOUNT == MASTER
			CONTINUE
	CASE 90
		SIF LCOUNT == MASTER || TALENT:LCOUNT:誓约 || TALENT:LCOUNT:相思相爱
			CONTINUE
	CASE 91
		SIF LCOUNT == MASTER || TALENT:LCOUNT:恋慕 == 0 || TALENT:LCOUNT:相思相爱
			CONTINUE
	CASE 94
		SIF !COND("成長性", LCOUNT)
			CONTINUE
	;胡蝶の夢
	CASE 95
		SIF LCOUNT > MASTER && (COND("妊娠イベント中", LCOUNT) || MARK:LCOUNT:排斥刻印 || ABL:LCOUNT:服从 < 4)
			CONTINUE
	;MASTERが記憶を失ってどうする…経験を失うのは面白いかも？
	CASE 98
		SIF LCOUNT == MASTER
			CONTINUE
	ENDSELECT

	;登録番号・名前・体力・その他ステータスをまとめて表示
	SELECTCASE BOUGHT
	CASE 50
		SIF COND(ITEMNAME:BOUGHT, LCOUNT) == 0
			CONTINUE
		CALL PRINT_NUM_NAME, "体力と精力", LCOUNT
	CASE 51
		SIF TALENT:LCOUNT:男性
			CONTINUE
		CALL PRINT_NUM_NAME, "扶她之药", LCOUNT
	CASE 55 TO 59
		SIF COND(ITEMNAME:BOUGHT, LCOUNT) == 0
			CONTINUE
		CALL PRINT_NUM_NAME, ITEMNAME:BOUGHT, LCOUNT
	CASE 66
		CALL PRINT_NUM_NAME, "調教知識", LCOUNT
	CASE 70
		SIF (COND("阴毛", LCOUNT) == 0 || COND("阴毛", LCOUNT) >= 7) && (COND("腋毛", LCOUNT) == 0 || COND("腋毛", LCOUNT) >= 7)
			CONTINUE
		CALL PRINT_NUM_NAME, "陰毛＆腋毛", LCOUNT
	CASE 71
		SIF COND("阴毛", LCOUNT) <= 2 && COND("腋毛", LCOUNT) <= 2
			CONTINUE
		CALL PRINT_NUM_NAME, "陰毛＆腋毛", LCOUNT
	CASE 72
		SIF TALENT:LCOUNT:男性 || TALENT:LCOUNT:怀孕 || TALENT:LCOUNT:魔导人偶 || TALENT:LCOUNT:避孕护符 || COND("着床確認", LCOUNT)
			CONTINUE
		CALL PRINT_NUM_NAME, "避妊措置", LCOUNT
	CASE 74
		SIF TALENT:LCOUNT:男性 || TALENT:LCOUNT:怀孕 || TALENT:LCOUNT:魔导人偶 || TALENT:LCOUNT:处女 || COND("着床確認", LCOUNT)
			CONTINUE
		CALL PRINT_NUM_NAME, "避妊措置", LCOUNT
	CASE 91
		CALL PRINT_NUM_NAME, "誓约指环", LCOUNT
	CASE 330 TO 333
		SIF PENIS(LCOUNT) == 0
			CONTINUE
		CALL PRINT_NUM_NAME, ITEMNAME:BOUGHT, LCOUNT
	CASE 334, 335, 339, 340
		SIF TALENT:LCOUNT:男性
			CONTINUE
		CALL PRINT_NUM_NAME, ITEMNAME:BOUGHT, LCOUNT
	CASE 330 TO 359
		CALL PRINT_NUM_NAME, ITEMNAME:BOUGHT, LCOUNT
	CASEELSE
		CALL PRINT_NUM_NAME, "体力", LCOUNT
	ENDSELECT
	LOCAL:LCOUNT = 1
NEXT
PRINTL  [100] 返回上层菜单

;特殊な選択肢
SELECTCASE BOUGHT
CASE 72
	MEMO = 0
	FOR LCOUNT, 1, CHARANUM
		SIF LOCAL:LCOUNT == 0 || COND("避妊中", LCOUNT)
			CONTINUE
		MEMO += 1
	NEXT
	IF MEMO >= 2
		PRINTFORML  [200] 给全部有怀孕风险的人购买
		LOCAL:200 = 1
	ENDIF
ENDSELECT

$INPUT_LOOP
INPUT

IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 200 && LOCAL:200

ELSEIF RESULT < MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF LOCAL:RESULT == 0
	GOTO INPUT_LOOP
ENDIF

;選択キャラをCHOICEに保存
CHOICE = RESULT


;お会計はSELECTCASEの終了後なので、RESTARTさせる場合にはその前に代金を頂くこと
SELECTCASE BOUGHT
;元気になる薬
CASE 50
	PRINTFORMW ＜%NAME:CHOICE%的体力和精力已完全恢复＞

	BASE:CHOICE:体力 = MAX(BASE:CHOICE:体力, MAXBASE:CHOICE:体力)
	BASE:CHOICE:精力 = MAX(BASE:CHOICE:精力, MAXBASE:CHOICE:精力)
	BASE:CHOICE:STOCK = MAX(BASE:CHOICE:STOCK, MAXBASE:CHOICE:STOCK)

	MONEY -= ITEMPRICE:BOUGHT

	SIF FIND_USE_ITEM(ITEMNAME:BOUGHT)
		RESTART
	RETURN 0
;ふたなり化
CASE 51
	IF TALENT:CHOICE:扶她 == 0
		PRINTFORMW ＜%NAME:CHOICE%变成扶她了＞
		TALENT:CHOICE:女性 = 0
		TALENT:CHOICE:扶她 = 2
		CALL EXP50_CHECK, CHOICE
	ELSE
		PRINTFORMW ＜%NAME:CHOICE%从扶她变回原样了＞
		TALENT:CHOICE:女性 = 1
		TALENT:CHOICE:扶她 = 0
	ENDIF
	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "扶她之药"
	SWAP TARGET, CHOICE

	SELECTCASE ARGS
	CASE "調合"
		ITEM:淫魔水滴 -= 10
	CASEELSE
		MONEY -= ITEMPRICE:BOUGHT
	ENDSELECT

	RETURN 0
;母乳体質にするor陥没乳首を治す
CASE 55
	IF TALENT:CHOICE:母乳体质 == 0
		PRINTL  [ 0] 获得母乳体质
	ELSE
		CALL PRINT_STR, "灰色_ [×] 获得母乳体质_L"
	ENDIF
	IF COND("陥没乳首", CHOICE)
		PRINTL  [ 1] 治疗乳头凹陷
	ELSE
		CALL PRINT_STR, "灰色_ [×] 治疗乳头凹陷_L"
	ENDIF
	PRINTFORML [100] 返回上层菜单

	CALL INPUT_SELECT, 2, 100

	IF RESULT == 100
		RETURN 0
	ELSEIF RESULT == 0 && TALENT:CHOICE:母乳体质 == 0
		PRINTFORMW ＜%NAME:CHOICE%は母乳が出るようになった＞
		TALENT:CHOICE:母乳体质 = 2
	ELSEIF RESULT == 1 && COND("陥没乳首", CHOICE)
		PRINTFORMW ＜%NAME:CHOICE%の陥没乳首が治った＞
		SETBIT TALENT:CHOICE:乳头, 10
		CLEARBIT TALENT:CHOICE:乳头, 11
		CLEARBIT TALENT:CHOICE:乳头, 12
	ENDIF

	SELECTCASE ARGS
	CASE "調合"
		ITEM:淫魔水滴 -= 10
	CASEELSE
		MONEY -= ITEMPRICE:BOUGHT
	ENDSELECT

	SIF FIND_USE_ITEM(ITEMNAME:BOUGHT)
		RESTART
	RETURN 0

;処女膜再生or母乳体質喪失
CASE 56
	IF TALENT:CHOICE:处女 == 0
		PRINTL  [ 0] 修复处女膜
	ELSE
		CALL PRINT_STR, "灰色_ [×] 修复处女膜_L"
	ENDIF
	IF TALENT:CHOICE:母乳体质
		PRINTL  [ 1] 治疗母乳体质
	ELSE
		CALL PRINT_STR, "灰色_ [×] 治疗母乳体质_L"
	ENDIF
	PRINTFORML [100] 返回上层菜单

	CALL INPUT_SELECT, 2, 100

	IF RESULT == 100
		RETURN 0
	ELSEIF RESULT == 0 && TALENT:CHOICE:处女 == 0
		PRINTFORMW ＜%NAME:CHOICE%的处女膜被修复了＞
		TALENT:CHOICE:处女 = 2
	ELSEIF RESULT == 1 && TALENT:CHOICE:母乳体质
		PRINTFORMW ＜%NAME:CHOICE%的母乳体质被治好了＞
		TALENT:CHOICE:母乳体质 = 0
	ENDIF

	SELECTCASE ARGS
	CASE "調合"
		ITEM:淫魔水滴 -= 10
	CASEELSE
		MONEY -= ITEMPRICE:BOUGHT
	ENDSELECT

	SIF FIND_USE_ITEM(ITEMNAME:BOUGHT)
		RESTART
	RETURN 0
;性転換
CASE 57
	CALL CHANGE_SEX, CHOICE
	PRINTFORM ＜%NAME:CHOICE%现在
	PRINTFORM \@ CFLAG:CHOICE:4 ? 变成了 # 变回了 \@
	IF TALENT:CHOICE:扶她
		PRINTFORMW 扶她＞
	ELSEIF TALENT:CHOICE:女性
		PRINTFORMW 女性＞
	ELSE
		PRINTFORMW 男性＞
	ENDIF

; original edition description
;	IF TALENT:CHOICE:扶她
;		PRINTFORM ＜%NAME:CHOICE%はふたなり
;	ELSEIF TALENT:CHOICE:女性
;		PRINTFORM ＜%NAME:CHOICE%はオンナ
;	ELSE
;		PRINTFORM ＜%NAME:CHOICE%はオトコ
;	ENDIF
;	PRINTFORMW に\@ CFLAG:CHOICE:4 ? なりました # 戻りました \@＞

	IF TALENT:CHOICE:男性
		PRINTL
		PRINTFORML ……要干脆让%CALLNAME:CHOICE%变成伪娘吗？
		PRINTFORML [ 0] 变成伪娘吧 \@ TALENT:CHOICE:高挑体型 ? (身高变矮了) # \@
		PRINTFORML [ 1] 才不要
		CALL INPUT_SELECT, 2
		IF RESULT == 0
			TALENT:CHOICE:伪娘 = 1
			SIF TALENT:CHOICE:高挑体型
				CALL CALC, "普通体型化", CHOICE
			PRINTFORMW ＜%CALLNAME:CHOICE%变成了伪娘＞
		ENDIF
		PRINTL
	ENDIF
	CALL SET_3SIZE, CHOICE

	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "黑色药丸"
	SWAP TARGET, CHOICE

	SELECTCASE ARGS
	CASE "調合"
		ITEM:淫魔水滴 -= 10
	CASEELSE
		MONEY -= ITEMPRICE:BOUGHT
	ENDSELECT

	RETURN 0
;治療
CASE 58
	PRINTFORM ＜治好了%NAME:CHOICE%的
	IF TALENT:CHOICE:漏尿癖 > 0
		PRINTFORM %TALENTNAME:57%
		TALENT:CHOICE:漏尿癖 -= 1
		;次になるのは、50回お漏らしを追加でした時とする
		CFLAG:CHOICE:34 = EXP:CHOICE:漏尿经验 + 20
		SIF TALENT:CHOICE:Ｖ损伤 + TALENT:CHOICE:Ａ损伤 > 0
			PRINT 和
	ENDIF
	IF TALENT:CHOICE:Ｖ损伤 + TALENT:CHOICE:Ａ损伤 > 0
		TALENT:CHOICE:Ｖ损伤 = 0
		TALENT:CHOICE:Ａ损伤 = 0
		PRINTFORM 体内损伤
	ENDIF
	PRINTFORMW 被治好了＞
	CALL GET_EXABL2, CHOICE

	SELECTCASE ARGS
	CASE "調合"
		ITEM:淫魔水滴 -= 10
	CASEELSE
		MONEY -= ITEMPRICE:BOUGHT
	ENDSELECT

	RETURN 0
;調合知識
CASE 65
	PRINTFORMW ＜%NAME:CHOICE%学会了调合知识＞
	TALENT:CHOICE:调合知识 = 1
	JUEL:CHOICE:习得 -= ITEMPRICE:BOUGHT
CASE 66
	CHOICE:1 = TALENT:CHOICE:调教知识
	CALL SETFLAG, "調教知識の選択", CHOICE
	SIF TALENT:CHOICE:调教知识 == CHOICE:1
		RESTART
;陰毛を生やす
CASE 70
	IF COND("阴毛", CHOICE) == 0 || COND("阴毛", CHOICE) >= 7
		RESULT = 1
	ELSEIF COND("腋毛", CHOICE) == 0 || COND("腋毛", CHOICE) >= 7
		RESULT = 0
	ELSE
		PRINTFORML 阴毛和%TEXTS("腋")%毛，要促进哪一种毛生长呢？
		PRINTFORML [ 0] 阴毛
		PRINTFORML [ 1] %TEXTS("腋")%毛
		CALL INPUT_SELECT, 2
	ENDIF
	IF RESULT == 0
		PRINTFORMW ＜%NAME:CHOICE%的阴毛长回来了＞
		SELECTCASE COND("阴毛", CHOICE)
		CASE IS <= 5
			BASE:CHOICE:阴毛 = 600
		CASE 6
			BASE:CHOICE:阴毛 = 700
		ENDSELECT
	ELSE
		PRINTFORMW ＜%NAME:CHOICE%的%TEXTS("腋")%毛长回来了＞
		BASE:CHOICE:腋毛 = 700
	ENDIF
	SIF TALENT:CHOICE:魔导人偶
		PRINTFORMW 等一下，%NAME:CHOICE%是魔导人偶，所以无法长毛。而且她也不会自己长出毛来。
;陰毛を失くす
CASE 71
	IF COND("阴毛", CHOICE) <= 2
		RESULT = 1
	ELSEIF COND("腋毛", CHOICE) <= 2
		RESULT = 0
	ELSE
		PRINTFORML 要脱掉阴毛还是%TEXTS("腋")%毛呢？
		PRINTFORML [ 0] 阴毛
		PRINTFORML [ 1] %TEXTS("腋")%毛
		CALL INPUT_SELECT, 2
	ENDIF
	IF RESULT == 0
		PRINTFORMW ＜%NAME:CHOICE%的阴毛已经被脱掉了＞
		BASE:CHOICE:阴毛 = 200
		;魔導人形なら、パイパン化
		SIF TALENT:CHOICE:魔导人偶
			BASE:CHOICE:阴毛 = 100
	ELSE
		PRINTFORMW ＜%NAME:CHOICE%的%TEXTS("腋")%毛已经被脱掉了＞
		BASE:CHOICE:腋毛 = 200
		SIF TALENT:CHOICE:魔导人偶
			BASE:CHOICE:腋毛 = 100
	ENDIF
;避妊薬
CASE 72
	SELECTCASE CHOICE
	;全員に配る場合
	CASE 200
		MEMO = 0
		FOR LCOUNT, 1, CHARANUM
			SIF LOCAL:LCOUNT == 0 || COND("避妊中", LCOUNT)
				CONTINUE
			MEMO += 1
		NEXT
		PRINTFORML 还没避孕的有{MEMO}人，需要%MONEY_C(ITEMPRICE:BOUGHT * MEMO)%
		;お金が足りている
		IF MONEY >= ITEMPRICE:BOUGHT*MEMO
			PRINTFORML [ 0] 批量购买　(购买后金钱：%MONEY_C(MONEY - ITEMPRICE:BOUGHT*MEMO)%)
			PRINTFORML [ 1] 果然还是不买了
			CALL INPUT_SELECT, 2
			IF RESULT == 0
				MONEY -= ITEMPRICE:BOUGHT*MEMO
				FOR LCOUNT, 1, CHARANUM
					SIF LOCAL:LCOUNT == 0 || COND("避妊中", LCOUNT)
						CONTINUE
					TALENT:LCOUNT:避孕药服用 += 10
				NEXT
				PRINTFORMW ＜给大家都服用了避孕药＞
			ENDIF
		ELSE
			PRINTW …看起来不太够钱呢
		ENDIF
	CASEELSE
		TALENT:CHOICE:避孕药服用 += 10
		IF CHOICE == MASTER
			PRINTFORMW ＜%NAME:CHOICE%的避孕有效时间还有{TALENT:CHOICE:避孕药服用}天＞
		ELSE
			PRINTFORMW ＜%NAME:CHOICE%服用了避孕药。避孕有效时间还有{TALENT:CHOICE:避孕药服用}天＞
		ENDIF

		SWAP TARGET, CHOICE
		CALL SELF_KOJO, "避孕药"
		SWAP TARGET, CHOICE

		MONEY -= ITEMPRICE:BOUGHT
	ENDSELECT
	IF MONEY >= ITEMPRICE:BOUGHT
		RESTART
	ELSE
		RETURN 0
	ENDIF
;紧急避孕药
CASE 73
	CALL SETFLAG, "避妊", CHOICE
	PRINTFORMW ＜%NAME:CHOICE%的怀孕危机已解除＞

	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "紧急避孕药"
	SWAP TARGET, CHOICE
;避妊の護符
CASE 74
	;付けている場合には外す
	IF TALENT:CHOICE:避孕护符
		PRINTFORMW ＜通过扯动%NAME:CHOICE%子宫口垂着的小红绳，避孕护符被破坏了＞
		TALENT:CHOICE:避孕护符 = 0
	ELSE
		PRINTFORML ＜%NAME:CHOICE%成功将避孕护符装备到子宫里了＞
		PRINTFORMW 可以看到子宫口垂出破坏护符时需要用到的小红绳…
		;即座に避妊の効果が出る
		CALL SETFLAG, "避妊", CHOICE
		TALENT:CHOICE:避孕护符 = 1
		MONEY -= ITEMPRICE:BOUGHT
	ENDIF

	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "避孕护符"
	SWAP TARGET, CHOICE

	;避妊薬は回収
	IF TALENT:CHOICE:避孕药服用 > 0
		PRINTL
		PRINTFORML 卖掉了多余的避孕药。
		PRINTFORMW 金钱＋%MONEY_C(TALENT:CHOICE:避孕药服用*150)%
		MONEY += TALENT:CHOICE:避孕药服用*150
		TALENT:CHOICE:避孕药服用 = 0
	ENDIF

	IF MONEY >= ITEMPRICE:BOUGHT
		RESTART
	ELSE
		RETURN 0
	ENDIF
;誓約の巻物
CASE 90
	PRINTFORMW ＜%NAME:CHOICE%现在已经是你的人了＞
	TALENT:CHOICE:誓约 = 1

	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "誓约卷轴"
	SWAP TARGET, CHOICE
;誓約の指輪
CASE 91
	PRINTFORMW ＜给%NAME:CHOICE%戴上了戒指＞
	TALENT:CHOICE:誓约 = 0
	TALENT:CHOICE:相思相爱 = 1
	TALENT:MASTER:相思相爱 = 1

	;告白イベント
	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "誓约指环"
	SWAP TARGET, CHOICE

	SETCOLOR DEF_COLOR("イエロー")
	PRINTL
	PRINTFORMW 现在跟%CALLNAME:CHOICE%是相思相爱的一对了哦！
	RESETCOLOR
	;指輪をとっておく
	ITEM:誓约指环 += 1
;歪んだ時計
CASE 94
	PRINTFORML ＜%NAME:CHOICE%被拔苗助长了＞
	PRINTFORMW 只要继续积累经验就能成为大人了，从背影里透出全新的成长可能性……
	SETBIT CFLAG:CHOICE:484, 0
;胡蝶の夢（体の入れ換え）
CASE 95
	CALL KOTYOUNOYUME, CHOICE
	;胡蝶の夢を使わなかった
	SIF ITEM:化蝶之梦 == 0
		RETURN 0
;記憶消去薬
CASE 98
	PRINTFORMW ＜以前的调教，%NAME:CHOICE%已经完全想不起来了＞
	CALL MEMORY_LOST, CHOICE
;身体変化系の効果
CASE 330 TO 349
	SIF COND(ITEMNAME:BOUGHT, CHOICE) == 0
		RESTART

	;元の体型を記憶
	LOCALS = %CONDS("体型", CHOICE)%

	SELECTCASE BOUGHT
	CASE 330
		IF GETBIT(TALENT:CHOICE:阴茎, 4)
			SETBIT TALENT:CHOICE:阴茎, 5
			CLEARBIT TALENT:CHOICE:阴茎, 4
		ELSEIF GETBIT(TALENT:CHOICE:阴茎, 3)
			SETBIT TALENT:CHOICE:阴茎, 4
			CLEARBIT TALENT:CHOICE:阴茎, 3
		ELSEIF GETBIT(TALENT:CHOICE:阴茎, 2)
			SETBIT TALENT:CHOICE:阴茎, 3
			CLEARBIT TALENT:CHOICE:阴茎, 2
		ELSEIF GETBIT(TALENT:CHOICE:阴茎, 1)
			SETBIT TALENT:CHOICE:阴茎, 0
			CLEARBIT TALENT:CHOICE:阴茎, 1
		ELSEIF GETBIT(TALENT:CHOICE:阴茎, 0)
			SETBIT TALENT:CHOICE:阴茎, 2
			CLEARBIT TALENT:CHOICE:阴茎, 0
		ENDIF
		PRINTFORMW ＜%NAME:CHOICE%阴茎的大小变成了[%TALENT_NAME(2, TALENT:CHOICE:阴茎, CHOICE)%]＞
	CASE 331
		IF GETBIT(TALENT:CHOICE:阴茎, 5)
			SETBIT TALENT:CHOICE:阴茎, 4
			CLEARBIT TALENT:CHOICE:阴茎, 5
		ELSEIF GETBIT(TALENT:CHOICE:阴茎, 4)
			SETBIT TALENT:CHOICE:阴茎, 3
			CLEARBIT TALENT:CHOICE:阴茎, 4
		ELSEIF GETBIT(TALENT:CHOICE:阴茎, 3)
			SETBIT TALENT:CHOICE:阴茎, 2
			CLEARBIT TALENT:CHOICE:阴茎, 3
		ELSEIF GETBIT(TALENT:CHOICE:阴茎, 2)
			SETBIT TALENT:CHOICE:阴茎, 0
			CLEARBIT TALENT:CHOICE:阴茎, 2
		ELSEIF GETBIT(TALENT:CHOICE:阴茎, 0)
			SETBIT TALENT:CHOICE:阴茎, 1
			CLEARBIT TALENT:CHOICE:阴茎, 0
		ENDIF
		PRINTFORMW ＜%NAME:CHOICE%阴茎的大小变成了[%TALENT_NAME(2, TALENT:CHOICE:阴茎, CHOICE)%]＞
	CASE 332
		IF GETBIT(TALENT:CHOICE:阴茎, 12)
			SETBIT TALENT:CHOICE:阴茎, 11
			CLEARBIT TALENT:CHOICE:阴茎, 12
		ELSEIF GETBIT(TALENT:CHOICE:阴茎, 11)
			CLEARBIT TALENT:CHOICE:阴茎, 11
		ENDIF
		PRINTFORMW ＜%NAME:CHOICE%阴茎的状态变成了[%TALENT_NAME(2, TALENT:CHOICE:阴茎, CHOICE)%]＞
	CASE 333
		IF GETBIT(TALENT:CHOICE:阴茎, 11)
			SETBIT TALENT:CHOICE:阴茎, 12
			CLEARBIT TALENT:CHOICE:阴茎, 11
		ELSE
			SETBIT TALENT:CHOICE:阴茎, 11
		ENDIF
		PRINTFORMW ＜%NAME:CHOICE%阴茎的状态变成了[%TALENT_NAME(2, TALENT:CHOICE:阴茎, CHOICE)%]＞
	CASE 334
		IF TALENT:CHOICE:巨乳 >= 2
			PRINTFORMW ＜%NAME:CHOICE%的胸围已经达到超乳级了＞
			CALL CALC, "超乳化", CHOICE
		ELSEIF TALENT:CHOICE:巨乳
			PRINTFORMW ＜%NAME:CHOICE%的胸围已经达到爆乳级了＞
			CALL CALC, "爆乳化", CHOICE
		ELSEIF TALENT:CHOICE:贫乳
			PRINTFORMW ＜%NAME:CHOICE%的胸围已经变得平平无奇了＞
			CALL CALC, "並乳化", CHOICE
		ELSE
			PRINTFORMW ＜%NAME:CHOICE%的胸围已经达到巨乳级了＞
			CALL CALC, "巨乳化", CHOICE
		ENDIF
	CASE 335
		IF TALENT:CHOICE:巨乳 >= 3
			PRINTFORMW ＜%NAME:CHOICE%目前的胸围是[爆乳]＞
			CALL CALC, "爆乳化", CHOICE
		ELSEIF TALENT:CHOICE:巨乳 == 2
			PRINTFORMW ＜%NAME:CHOICE%目前的胸围是[爆乳]＞
			CALL CALC, "巨乳化", CHOICE
		ELSEIF TALENT:CHOICE:巨乳
			PRINTFORMW ＜%NAME:CHOICE%目前的胸围是平均大小＞
			CALL CALC, "並乳化", CHOICE
		ELSE
			PRINTFORMW ＜%NAME:CHOICE%目前的胸围是[贫乳]＞
			CALL CALC, "貧乳化", CHOICE
		ENDIF
	CASE 336
		IF TALENT:CHOICE:娇小体型
			PRINTFORMW ＜%NAME:CHOICE%失去了[娇小体型]＞
			CALL CALC, "普通体型化", CHOICE
		ELSE
			PRINTFORMW ＜%NAME:CHOICE%获得了[高挑]＞
			CALL CALC, "長身化", CHOICE
			SIF TALENT:CHOICE:伪娘
				PRINTFORMW ＜…按照常理来说，伪娘是不可能长得太高的＞
		ENDIF
	CASE 337
		IF TALENT:CHOICE:高挑体型
			PRINTFORMW ＜%NAME:CHOICE%失去了[高挑]＞
			CALL CALC, "普通体型化", CHOICE
		ELSE
			PRINTFORMW ＜%NAME:CHOICE%获得了[娇小体型]＞
			CALL CALC, "小柄化", CHOICE
		ENDIF
	CASE 338
		PRINTFORM 目前[{CHOICE, 2}]%CALLNAME:CHOICE%的肤色是
		IF TALENT:CHOICE:肤色
			PRINTFORML %CONDS("肌の色", CHOICE)%
		ELSE
			PRINTFORML 普通的肤色
		ENDIF
		PRINTFORML 要变成什么颜色呢？
		PRINTFORML  [ 0] 普通的肤色
		PRINTFORML  [ 1] 褐色
		PRINTFORML  [ 2] 白色
		PRINTFORML  [ 3] 青色
		PRINTFORML [100] 果然还是不改了
		CALL INPUT_SELECT, 4, 100
		SELECTCASE RESULT
		CASE 100
			RETURN 0
		CASE 0
			PRINTFORMW ＜%CALLNAME:CHOICE%的肤色变成了普通的[肤色]＞
		CASE 1
			PRINTFORMW ＜%CALLNAME:CHOICE%的肤色现在变成了健康的[褐色]＞
		CASE 2
			PRINTFORMW ＜%CALLNAME:CHOICE%的肤色变成了高洁的[白色]＞
		CASE 3
			PRINTFORMW ＜%CALLNAME:CHOICE%的肤色变成了罕见的[青色]＞
		ENDSELECT
		IF TALENT:CHOICE:肤色 == RESULT
			PRINTFORMW …原本就是这种肤色。素材已归还
			RETURN 0
		ELSE
			TALENT:CHOICE:肤色 = RESULT
		ENDIF
	CASE 339
		PRINTFORM ＜%NAME:CHOICE%的臀围从{BASE:CHOICE:臀围}cm增加了{BASE:CHOICE:身高*3/100}cm，
		BASE:CHOICE:臀围 += BASE:CHOICE:身高*3/100
		PRINTFORMW 变成了{BASE:CHOICE:臀围}cm＞
		IF TALENT:CHOICE:大臀 == 2
			CALL SETFLAG, "ヒップ設定", CHOICE
			SIF TALENT:CHOICE:大臀 != 2
				PRINTFORMW ＜%NAME:CHOICE%变成了[%CONDS("お尻", CHOICE)%]＞
		ELSEIF TALENT:CHOICE:大臀 == 1
			CALL SETFLAG, "ヒップ設定", CHOICE
			SIF TALENT:CHOICE:大臀 != 1
				PRINTFORMW ＜%NAME:CHOICE%变成了[%CONDS("お尻", CHOICE)%]＞
		ELSEIF TALENT:CHOICE:小臀
			CALL SETFLAG, "ヒップ設定", CHOICE
			SIF TALENT:CHOICE:小臀 == 0
				PRINTFORMW ＜%NAME:CHOICE%失去了[小尻]＞
		ELSEIF TALENT:CHOICE:大臀 == 0
			CALL SETFLAG, "ヒップ設定", CHOICE
			SIF TALENT:CHOICE:大臀 != 0
				PRINTFORMW ＜%NAME:CHOICE%变成了[%CONDS("お尻", CHOICE)%]＞
		ENDIF
	CASE 340
		PRINTFORM ＜%NAME:CHOICE%的臀围从{BASE:CHOICE:臀围}cm减少了{BASE:CHOICE:身高*3/100}cm，
		BASE:CHOICE:臀围 -= BASE:CHOICE:身高*3/100
		PRINTFORMW 变成了{BASE:CHOICE:臀围}cm＞
		IF TALENT:CHOICE:大臀 == 3
			CALL SETFLAG, "ヒップ設定", CHOICE
			SIF TALENT:CHOICE:大臀 != 3
				PRINTFORMW ＜%NAME:CHOICE%失去了[超尻]＞
		ELSEIF TALENT:CHOICE:大臀 == 2
			CALL SETFLAG, "ヒップ設定", CHOICE
			SIF TALENT:CHOICE:大臀 != 2
				PRINTFORMW ＜%NAME:CHOICE%失去了[爆尻]＞
		ELSEIF TALENT:CHOICE:大臀 == 1
			CALL SETFLAG, "ヒップ設定", CHOICE
			SIF TALENT:CHOICE:大臀 != 1
				PRINTFORMW ＜%NAME:CHOICE%失去了[巨尻]＞
		ELSEIF TALENT:CHOICE:小臀 == 0
			CALL SETFLAG, "ヒップ設定", CHOICE
			SIF TALENT:CHOICE:小臀
				PRINTFORMW ＜%NAME:CHOICE%变成了[%CONDS("お尻", CHOICE)%]＞
		ENDIF
	ENDSELECT
	;3サイズをチェック
	CALL SET_3SIZE, CHOICE

	IF LOCALS != CONDS("体型", CHOICE)
		PRINTFORM ＜随着变化，%NAME:CHOICE%的体型变成了
		SETCOLOR DEF_COLOR("黄色")
		PRINTFORM [%CONDS("体型", CHOICE)%]
		RESETCOLOR
		PRINTFORMW ＞
	ENDIF

	SWAP TARGET, CHOICE
	CALL SELF_KOJO, ITEMNAME:BOUGHT
	SWAP TARGET, CHOICE

	ITEM:淫魔水滴 -= ITEMPRICE:BOUGHT

	;所持雫が足りていて、使う対象が居るならもう一回キャラリスト表示
	IF ITEM:淫魔水滴 >= ITEMPRICE:BOUGHT
		FOR LCOUNT, 1, CHARANUM
			SIF COND(ITEMNAME:BOUGHT, LCOUNT) == 0
				CONTINUE

			RESTART
		NEXT
	ENDIF

	RETURN 0

;淫紋
CASE 350
	SIF COND("淫紋刻印可能", CHOICE) == 0
		RESTART

	;吸精体質の上昇判定などに使う
	MEMO = TALENT:CHOICE:吸精体质
	MEMO:1 = COND("子宮口弛緩度", CHOICE)

	SELECTCASE MARK:CHOICE:淫纹
	CASE 0
		PRINTFORMW 利用淫魔水滴，在%NAME:CHOICE%的下腹纹上了可爱的心型纹章
	CASE 1
		PRINTFORMW 在现有纹章的基础上，在心型的下方又加上了%NAME:CHOICE%子宫的象形图腾
	CASE 2
		PRINTFORMW %NAME:CHOICE%的下腹…子宫图腾的上方，又加入了隐喻卵巢和输卵管的图腾
	CASE 3
		PRINTFORMW 最后，在%NAME:CHOICE%淫靡的纹章下方，将子宫颈的图腾也描绘上去，这样一来淫纹就完整了
	CASE 4
		GOTO INPUT_LOOP
	ENDSELECT

	MARK:CHOICE:淫纹 += 1
	TALENT:CHOICE:吸精体质 = SET_EXTALENT("吸精体質", CHOICE)

	SWAP TARGET, CHOICE
	CALL SELF_KOJO, "淫纹"
	SWAP TARGET, CHOICE

	SETCOLOR DEF_COLOR("ハートピンク")
	PRINTFORMW ＜%NAME:CHOICE%获得了淫纹LV{MARK:CHOICE:淫纹}＞
	RESETCOLOR

	;素質の変化
	IF TALENT:CHOICE:不易湿
		TALENT:CHOICE:不易湿 = 0
		PRINTFORMW ＜%NAME:CHOICE%失去了[不易湿]＞
	ENDIF
	IF TALENT:CHOICE:Ｖ钝感 && MARK:CHOICE:淫纹 >= 4
		TALENT:CHOICE:Ｖ钝感 = 1
		PRINTFORMW ＜%NAME:CHOICE%失去了[Ｖ钝感]＞
	ENDIF

	IF TALENT:CHOICE:容易湿 == 0 && MARK:CHOICE:淫纹 >= 2
		TALENT:CHOICE:容易湿 = 1
		PRINTFORMW ＜%NAME:CHOICE%获得了[容易湿]＞
	ENDIF
	IF TALENT:CHOICE:子宫性感 == 0 && MARK:CHOICE:淫纹 >= 2
		TALENT:CHOICE:子宫性感 = 1
		PRINTFORMW ＜%NAME:CHOICE%获得了[子宫性感]＞
	ENDIF

	IF TALENT:CHOICE:吸精体质 > MEMO
		PRINTFORMW ＜%NAME:CHOICE%的吸精体质提升了LV{TALENT:CHOICE:吸精体质}＞
	ENDIF
	IF COND("子宮口弛緩度", CHOICE) > MEMO:1
		PRINTFORMW ＜%NAME:CHOICE%的子宫口变得更柔软了＞
	ENDIF

	ITEM:淫魔水滴 -= ITEMPRICE:BOUGHT

	;所持雫が足りていて、使う対象が居るならもう一回キャラリスト表示
	IF ITEM:淫魔水滴 >= ITEMPRICE:BOUGHT
		FOR LCOUNT, 1, CHARANUM
			SIF TALENT:LCOUNT:男性 || COND("妊娠イベント中", LCOUNT)
				CONTINUE
			RESTART
		NEXT
	ENDIF

	RETURN 0
ENDSELECT

;代金はここで支払う（一部例外あり）
MONEY -= ITEMPRICE:BOUGHT
