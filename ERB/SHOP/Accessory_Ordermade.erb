;eraSQ製作スレッド９  738氏 10/09/01 作成 	10/09/02 更新
;-----------------------------------------------------------------------------------------
@ACCESSORY_ORDERMADE_MENU
#DIM LCOUNT
#DIM NUM_ACCE
#DIM MEMO_PRICE

;まずは何個目かを判定
FOR LCOUNT, 1, 9
	IF FLAG:(300 + LCOUNT * 10) == 0
		NUM_ACCE = LCOUNT
		BREAK
	ENDIF
NEXT

PRINTL 
PRINTL 「我可以根据你的喜好定制符合你气质的饰品。
PRINTL 　虽然会有些贵，但绝对物有所值。
PRINTL 　那么，请先告诉我你的喜好吧？」
PRINTL （收费会根据定制难度而变化哦）
PRINTL  [ 0] 手镯　　　重视精神力　
PRINTL  [ 1] 袖饰　　　重视魅力　　
PRINTL  [ 2] 手套　　　重视技巧　　
PRINTL  [ 3] 绶带　　　重视精力　　
PRINTL  [ 4] 指环　　　平衡型
PRINTL [100] 返回上层菜单

CALL INPUT_SELECT, 5, 100

SIF RESULT == 100
	RETURN 0

FLAG:(300 + NUM_ACCE * 10) = RESULT * 10

PRINTL 
PRINTL 「那么，要用请问要用什么素材呢？」

SELECTCASE FLAG:(300 + NUM_ACCE * 10)
CASE 0
	PRINTL  [0]象牙　　　　　[1]青铜　　　　　[2]白银　　　　　[3]灵银
	CALL INPUT_SELECT, 4
CASE 10
	PRINTL  [0]珊瑚　　　　　[1]化石　　　　　[2]翡翠　　　　　[3]琥珀
	CALL INPUT_SELECT, 4
CASE 20
	PRINTL  [0]真丝　　　　　[1]锦缎　　　　　[2]天鹅绒
	CALL INPUT_SELECT, 3
CASE 30
	PRINTL  [0]种马　　　　　[1]雄鹿　　　　　[2]雄兔
	CALL INPUT_SELECT, 3
CASE 40
	PRINTL  [0]水晶　　　　　[1]钻石
	CALL INPUT_SELECT, 2
ENDSELECT
FLAG:(300 + NUM_ACCE * 10) += RESULT + 1

PRINTL 
PRINT 「最多只能附加
CALL PRINT_COLORTEXT, "５个", DEF_COLOR("黄色")
PRINTL 效果…请问你要附加什么效果呢？
PRINTL 　只不过，附加的效果越多，定制费就会越贵哦。
PRINT 　例如：１个效果只需要收费
CALL PRINT_COLORTEXT, "$5,000", DEF_COLOR("黄色")
PRINTL 。
PRINT 　但是，要附加２个及以上效果的话…费用就会变成
CALL PRINT_COLORTEXT, "效果数量×$10,000", DEF_COLOR("黄色")
PRINTL 。
PRINTW 　附加的效果越多，需要的定制费用就会越多。请慎重选择呢～」

$MAKE_ACCE

CALL SET_ACCESSORY_ABILITY, NUM_ACCE

PRINTL 「那么，请问是要定制成这样子吗？」

CALL PRINT_ACCESSORY, NUM_ACCE

VARSET LOCAL
FOR LCOUNT, 1, 6
	SIF FLAG:(300 + NUM_ACCE * 10 + LCOUNT) == 0
		BREAK
	LOCAL += 1
NEXT

;お代の計算
SELECTCASE LOCAL
CASE 0
	MEMO_PRICE = 0
CASE 1
	MEMO_PRICE = 5000
CASEELSE
	MEMO_PRICE = LOCAL*10000
ENDSELECT

PRINTFORM 「费用的话……跟之前说好的一样，
CALL PRINT_COLORTEXT, MONEY_C(MEMO_PRICE), DEF_COLOR("黄色")
PRINTFORML 这样没问题吧？」
PRINTL 

PRINTFORML  [ 0] 没问题　（金钱：%MONEY_C(MONEY)% … \@ MONEY >= MEMO_PRICE ? 预算内 # 超出预算！ \@）
PRINTFORML  [ 1] 让我再考虑一下
PRINTFORML [100] 离开店铺

$ORDER_BUY_ANS
INPUT

IF RESULT == 100
	VARSET FLAG, 0, 300 + NUM_ACCE * 10, 310 + NUM_ACCE * 10
	PRINTW 「总觉得…气氛有点冷啊」
ELSEIF RESULT == 0 && MONEY < MEMO_PRICE
	PRINTL 「钱好像不太够…」
	GOTO ORDER_BUY_ANS
ELSEIF RESULT == 0
	PRINTW 「谢谢惠顾」
	MONEY -= MEMO_PRICE
ELSEIF RESULT == 1
	GOTO MAKE_ACCE
ELSE
	GOTO ORDER_BUY_ANS
ENDIF

@SET_ACCESSORY_ABILITY, ARG
#DIM LCOUNT
#DIM MEMO_PRICE
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0

PRINTL  
VARSET LOCAL
FOR LCOUNT, 1, 6
	SIF FLAG:(300 + ARG * 10 + LCOUNT) == 0
		BREAK
	LOCAL += 1
NEXT

;お代の計算
SELECTCASE LOCAL
CASE 0
	MEMO_PRICE = 0
CASE 1
	MEMO_PRICE = 5000
CASEELSE
	MEMO_PRICE = LOCAL * 10000
ENDSELECT
;選択肢の存在チェック
;FOR LCOUNT, 1, 101
;	SELECTCASE LCOUNT
;	CASE 1 TO 6, 10 TO 14, 20 TO 24, 30 TO 33, 50 TO 52
;		SIF LOCAL <= 4
;			LOCAL:LCOUNT = 1
;	CASE 99, 100
;		SIF LOCAL
;			LOCAL:LCOUNT = 1
;	ENDSELECT
;NEXT

SELECTCASE LOCAL
CASE 0
	PRINTFORML 「要附加怎样的效果呢？」
CASEELSE
	PRINTFORML 「目前饰品看起来大概是这个样子」
	CALL PRINT_ACCESSORY, ARG

	PRINT 「大概需要

	CALL PRINT_COLORTEXT, MONEY_C(MEMO_PRICE), DEF_COLOR("黄色")

	SELECTCASE LOCAL
	CASE 5
		PRINTFORML 。 已经不能再附加更多效果了」
	CASEELSE
		PRINTFORML 。 定制成这样可以吗？」
	ENDSELECT
ENDSELECT
PRINTL 

SIF LOCAL >= 5
	SETCOLOR DEF_COLOR("灰色")

PRINTFORML  [ 1] %TEXT_RJ("ＥＰ＋３０％", 18)%　…　射精计量表上限增加30％
PRINTFORML  [ 2] %TEXT_RJ("精力＋３０％", 18)%　…　精力上限增加30％
PRINTFORML  [ 3] %TEXT_RJ("技巧＋３０％", 18)%　…　赋予对方的快感增加30％
PRINTFORML  [ 4] %TEXT_RJ("精神力＋３０％", 18)%　…　受到的快感 ＆ 恍惚机率等减少30％
PRINTFORML  [ 5] %TEXT_RJ("魅力＋３０％", 18)%　…　赋予对方的不快感减少30％ ＆ 好感度＋１
PRINTFORML  [ 6] %TEXT_RJ("ＳＴＯＣＫ增加", 18)%　…　ＳＴＯＣＫ增加一次（不可叠加）
PRINTFORML  [10] %TEXT_RJ("Ｃ强化＋５０％", 18)%　…　赋予对手的Ｃ快感增加50％
PRINTFORML  [11] %TEXT_RJ("Ｖ强化＋５０％", 18)%　…　赋予对手的Ｖ快感增加50％
PRINTFORML  [12] %TEXT_RJ("Ａ强化＋５０％", 18)%　…　赋予对手的Ａ快感增加50％
PRINTFORML  [13] %TEXT_RJ("Ｂ强化＋５０％", 18)%　…　赋予对手的Ｂ快感增加50％
PRINTFORML  [14] %TEXT_RJ("Ｍ强化＋５０％", 18)%　…　赋予对手的Ｍ快感增加50％
PRINTFORML  [20] %TEXT_RJ("Ｈ氛围＋３", 18)%　…　Ｈ氛围的增量＋３
PRINTFORML  [21] %TEXT_RJ("判定值＋７", 18)%　…　指令判定值＋７
PRINTFORML  [22] %TEXT_RJ("受到的快感＋２０％", 18)%　…　受到的快感增加20％
PRINTFORML  [23] %TEXT_RJ("逆推机率＋２０％", 18)%　…　逆推机率增加20％
PRINTFORML  [24] %TEXT_RJ("取得经验值＋２０％", 18)%　…　获得的经验值增加20％
PRINTFORML  [30] %TEXT_RJ("精强术常态化", 18)%　…　常态化发动精强术
PRINTFORML  [31] %TEXT_RJ("小根术常态化", 18)%　…　常态化发动小根术
PRINTFORML  [32] %TEXT_RJ("高敏术常态化", 18)%　…　常态化发动高敏术
PRINTFORML  [33] %TEXT_RJ("大根术常态化", 18)%　…　常态化发动大根术
PRINTFORML  [50] %TEXT_RJ("隐密＋２０％", 18)%　…　探索时，遇敌机率减少20％
PRINTFORML  [51] %TEXT_RJ("宝箱出现率＋２０％", 18)%　…　探索时，宝箱出现机率增加20％
PRINTFORML  [52] %TEXT_RJ("门扉发现率＋２０％", 18)%　…　探索时，通往下层的门扉发现率增加20％

RESETCOLOR
SIF LOCAL == 0
	SETCOLOR DEF_COLOR("灰色")

PRINTFORML  [99] 去除最后一项效果
PRINTFORML [100] 这样就好了

RESETCOLOR
$INPUT_LOOP
INPUT
;IF RESULT >= 1 && RESULT <= 100 && LOCAL:RESULT
IF RESULT >= 1 && RESULT <= 100
	SELECTCASE RESULT
	CASE 100
		REDRAW 1
		RETURN 1
	CASE 99
		;一番下の効果を消す
		FLAG:(300 + ARG * 10 + LOCAL) = 0
	CASE 1 TO 5
		FLAG:(300 + ARG * 10 + LOCAL + 1) = RESULT * 1000 + 30
	CASE 10 TO 14
		FLAG:(300 + ARG * 10 + LOCAL + 1) = RESULT * 1000 + 50
	CASE 20
		FLAG:(300 + ARG * 10 + LOCAL + 1) = RESULT * 1000 + 3
	CASE 21
		FLAG:(300 + ARG * 10 + LOCAL + 1) = RESULT * 1000 + 7
	CASE 22 TO 24, 50 TO 52
		FLAG:(300 + ARG * 10 + LOCAL + 1) = RESULT * 1000 + 20
	CASEELSE
		FLAG:(300 + ARG * 10 + LOCAL + 1) = RESULT * 1000
	ENDSELECT
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART
