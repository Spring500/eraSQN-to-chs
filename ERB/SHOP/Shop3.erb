@PROFILE_TEXT, ARG
#DIM LCOUNT

;キャラ召喚時に解説を入れる
PRINTFORML キャラ番号00\@ ARG < 10 ? 0 # \@{ARG}
PRINTFORML 【%NAME:TARGET%】ＬＶ:{ABL:ＬＶ}　体力:{MAXBASE:体力}　気力:{MAXBASE:气力}　精力:{MAXBASE:精力}
PRINTL
SELECTCASE ARG
CASE 1
	IF RAND:2 == 0
		PRINTL 夢に侵入し、快楽と引き換えに精を奪い取る夢魔の代表格
		PRINTL 豊満な肉体を持ち、夢魔らしく男にも女にも経験豊富で欲望に忠実である
		PRINTL …試しに調教するにも絞られてみるにもいい相手だろう
	ELSE
		PRINTL 人の夢の中に侵入し、快楽を与える代わりに精気を吸い取る夢魔
		PRINTL 総じて豊満な身体を持ち性欲も強いため調教自体は難しくないだろうが
		PRINTL もし調教する気ならば、まず自分自身が搾り取られないよう注意が必要だろう
	ENDIF
CASE 2
	IF RAND:2 == 0
		PRINTL 悪戯好きな小悪魔っ娘夢魔
		PRINTL 性格は子供っぽく、また幼さ故に手管は拙い
		PRINTL 駆け出しでもそれほど調教は難しくはないだろう
	ELSE
		PRINTL 小さな羽を持ち悪戯好きの小悪魔
		PRINTL 好奇心が強く性行為にも積極的ではあるが
		PRINTL まだ子供のような外見のせいか少々生意気で物を覚えるのは
		PRINTL 苦手のようなのでその点は気をつける必要がある
	ENDIF
CASE 3
	IF CFLAG:8 == 2
		PRINTL 血を吸って育ったバラの精霊
	ELSE
		PRINTL 森に生息する木の精霊
	ENDIF
	PRINTL 過ごした年月故か高い技巧と知性を持ち、日に当たると調子が良くなるようだ
	PRINTL 特に調教に不利な特徴もないため、堕としやすい相手といえるだろう
CASE 4
	IF RAND:2 == 0
		IF CFLAG:8 == 3
			PRINTL 妖精を統べる妖精の女王
		ELSE
			PRINTL 森に生息する小さな妖精
		ENDIF
		PRINTL 非常に小さいためそのままでは入れる事はできず、夢魔にしては珍しく純潔を保っている
		PRINTL 調教はそれほど難しくはないが、体力気力が極端に低い為、無茶は禁物
	ELSE
		PRINTL 小さく愛らしい姿をした妖精
		IF CFLAG:8 == 3
			PRINTL 妖精の中では珍しく、臆病さがなく気が強い
		ELSE
			PRINTL 陽気で好奇心が強いが同時にかなり臆病な性格をしている
		ENDIF
		PRINTL 人間と比べるとかなり小さな体躯をしており通常の方法では挿入することが出来ない
		PRINTFORML もし魔法を使うことが出来る%TEXTS("助手の名称")%がいるならなんとか出来るかもしれない
	ENDIF
CASE 5
	IF CFLAG:8 == 1
		PRINTL 気に入った男性を水辺に誘い込み溺れさせる水の精霊
	ELSE
		PRINTL 風に乗って飛び回る風の精霊
	ENDIF
	PRINTL 恋に恋する少女であるらしく、恋人の求めには積極的に応じてしまうようだ
	PRINTL …甘い恋人関係を楽しみながら開発するのもいいかもしれない
CASE 6
	IF CFLAG:8 == 2
		PRINTL 童話にも描かれた二足歩行の猫の妖精
	ELSEIF CFLAG:8 == 4
		PRINTL 旅人に謎をかけ、答えられなかった者を食べてしまう魔物
	ELSE
		PRINTL 二本の尻尾を持つ猫の魔物
	ENDIF
	PRINTL 猫なのでもちろん猫耳猫舌で語尾に「ニャ」がつく
	PRINTL 調教次第では逆に被虐の快感を教えられるかもしれないので注意が必要
CASE 7
	IF CFLAG:8 == 2
		PRINTL 地獄の番犬と称される魔物
	ELSE
		PRINTL 気性の荒い人狼
	ENDIF
	PRINTL その姿の通りケダモノのような激しい調教でもあまり壊れる事はないだろう
	IF TALENT:四足步行
		PRINTL 人語は喋れないが理解はできるようだ
	ELSE
		PRINTL 言葉遣いは荒っぽく未熟だが、人と同じように二本の足で立って行動できるようだ
	ENDIF
CASE 8
	IF CFLAG:8 == 2
		PRINTL 「泣き娘」と言われる死神の一種
	ELSE
		PRINTL 夢魔となってしまった清楚な少女
	ENDIF
	PRINTL 奥手で恥ずかしがり屋だが、献身的な魅力溢れる少女である
	PRINTL 最初は身持ちが硬く服を脱ぐにも抵抗を示すが、懐柔にそれほど時間はかからないはずだ
CASE 9
	IF CFLAG:8 == 2
		PRINTL 「泣き娘」と言われる死神の一種
	ELSE
		PRINTL 夢魔となってしまった初心な少女
	ENDIF
	PRINTL 処女で経験がほとんどなく、その希少性故に他のヴィレッジャに比べて人気が高い
	PRINTL …そのいじめてオーラは、あなたの加虐心を存分にくすぐる事だろう
CASE 10
	PRINTL 腰から下が大蛇である魔物の夢魔
	IF CFLAG:8 == 2
		PRINTL とはいえその気質は気品ある清楚な女性であるため、紳士的な対応をすべきかもしれない
	ELSE
		PRINTL 性的に経験豊富であるらしく、気に入れば女であっても気にしない性質のようだ
	ENDIF
	PRINTFORML %TEXTS("助手の名称")%にすればその器用な下半身を駆使した調教を行う事もできるようになるだろう
CASE 11
	IF CFLAG:8 == 2
		PRINTL 魔性の歌声を持つ鳥妖人
		PRINTL その歌声で、船乗りたちを誘惑していたと言う伝承があり、歌唱技能を持つ
		PRINTL 不思議な力を持つ彼女の歌声は、体力の低い者の調教に役立つかもしれない
	ELSE
		PRINTL 獰猛で狡猾な鳥妖人
		PRINTL 繁殖の時期に人間を巣に持ち帰って子作りする伝承があり、サドでプライドが高い
		PRINTL 手が羽根なので、手での奉仕は必然的にその鉤爪のついた足で責められる事になるだろう
	ENDIF
CASE 12
	IF CFLAG:8 == 2
		PRINTL 炎を身にまとう蜥蜴の魔物
	ELSE
		PRINTL 二足歩行する蜥蜴人の戦士
	ENDIF
	PRINTL 反抗的でプライドが高いが、実は素直になれないだけのようだ
	PRINTL 調教が進めばその変化にきっとグッとくる事だろう
CASE 13
	PRINTL 幾人もの人間を惑わせる歌声を持つ人魚
	PRINTL 彼女の歌には心身を安心させる効果があるが、最初の内は声を失ったような沈黙を保つ
	PRINTL 心を開かせればその声を聞く事ができ、きっと体力の低い者の調教に役立つだろう
CASE 14
	PRINTL インドにて、踊り子・天女・水と空の精霊と盛り沢山の肩書を持つ精霊
	PRINTL 誘惑の術に長けていて、事あるごとに誘いをかけてくる、いうなれば誘い攻めのエキスパート
	PRINTL 踊りも得意で特に腰使いには定評があるらしい。接合時、顔面騎乗時には十分注意しよう。
CASE 15
	PRINTL 魔女の呪いにより下半身に怪物となってしまった少女
	IF CFLAG:8 == 2
		PRINTL 怪物となった後はスキュラと共に海で人間を襲っていた
	ELSE
		PRINTL 怪物となった後はカリュブデスと共に海で人間を襲っていた
	ENDIF
	PRINTL そのせいか強気な態度を取りやすいが、元々経験の少ない少女であったため意外と素直である
	PRINTL 襲った人間は食べていたため性交経験はほとんどない。
	PRINTL 反対に自慰は触手すら使って行っていたためベテラン（？）の領域に達している。
	PRINTL 反撃の際には存分にそのテクニックを使ってくるため、触手快感に溺れないよう十分に注意。
;	PRINTFORML 逆に%TEXTS("助手の名称")%時には特定領域で大きな助けになる……かもわからない
;	PRINTL 魔物となったとはいえ元々は少女であっためか恥じらいも残っており、性格も献身的である
;	PRINTL また一人でいた為か自慰しやすいので調教は楽に行えるだろう
;	PRINT 弱点：胸　性格：強気　※：長編準拠
CASE 16
	IF CFLAG:8 == 2
		PRINTL 東の大国の王を誑かし国を傾けたとも言われる狐の魔物
	ELSEIF CFLAG:8 == 3
		PRINTL 強い魔力と九つの尻尾を持つ狐の魔物
	ELSE
		PRINTL 少々時代懸かった口調で喋る狐の魔物
	ENDIF
	PRINTL 技巧はそれほど高くはないが、献身的で従順なため奉仕に向く
	PRINTFORML 狐であるため、%TEXTS("助手の名称")%にするのも悪くないかもしれない
	SIF TALENT:娇小体型
		PRINTL …おや、ちょっと身体が小さい気が…？
CASE 17
	IF CFLAG:8 == 1
		PRINTL 冥府の女王に仕える夜の魔物で、膝から下がロバの足になっている美女
		PRINTL 男を誘惑し、悪夢を見せながら生き血を吸ったり、性交の後食べてしまうと言われている
		PRINTL 魔術に長けるが罵詈雑言に弱いためか少々腰が低く、ハードな調教も受け入れやすいようだ
	ELSE
		IF CFLAG:8 == 3
			PRINTL その名を呼ぶことすら恐れられると言われる女王様の中の女王様
		ELSE
			PRINTL プライドの高い女王様
		ENDIF
		PRINTL 女王であるが故にカリスマ性があり、責める事に関しては天性の才能がある
		PRINTL 鞭と首輪があればそのサディストとしての性質が表に出てくる事だろう
	ENDIF
CASE 18
	PRINTL 機織りの得意な女性が神の怒りを受けて生まれた蜘蛛の魔物
	PRINTL 技巧が高く、６本ある腕による反撃に調教は難航するかもしれない
	PRINTL 薬物に対する耐性を持つため、媚薬を使えない点にも注意
CASE 19
	PRINTL 高い魔力を持つ魔法使い
	PRINTL 生意気で少々体力気力が低いが、それほど気にするほどでもない
	PRINTFORML その知識と魔法の力を見込んで%TEXTS("助手の名称")%にする事も手であるだろう
CASE 20
	PRINTL 人間に祝福を与えるために舞い降りた天使
	SIF CFLAG:8 == 2
		PRINTL その中でも特に強い力を持つ天使の一人
	PRINTL 天使だからかその舌足らずな口調故か人気が高く、一緒にいるだけで癒される…かも
	PRINTL 大人しく献身的だが、媚薬を使えば枷が外れて乱れてしまうようだ
CASE 21
	PRINTL 傲慢によって天使の座を追われた、かつて神に愛された堕天使
	PRINTL エンジェルに似ているがその性質は正反対にプライドが高くサドで小悪魔
	PRINTL …既に堕ちている天使をより深いところに堕としてみるのもいいかもしれない
CASE 22
	PRINTL ニホンという国に存在するニンジャと言われる工作員、その女版
	PRINTL その手の訓練の経験があるのか、快楽の最中でも自分を見失うことはあまりないようだ
	PRINTL 経験は豊富であるが、少々調教はてこずるかもしれない
CASE 23
	PRINTL 時計を持ちチョッキを着た白ウサギ
	PRINTL 恥じらいが薄く快感にも素直に反応する
	PRINTL 性格も好奇心が強く楽観的なため調教にも積極的に応じてくれるだろう
CASE 24
	PRINTL かつて愛する人に徹底的に拒絶された娘
	PRINTL そういった過去のためかその瞳の中に光はなく、心がどこか壊れてしまっているようだ
	PRINTL 調教はそれほど難しくはないであろうが、商品として価値は薄いだろう
CASE 25
	PRINTL 人の生き血を啜る誇り高き吸血鬼
	PRINTL 処女の血を嗜好していたのか、乙女との性行為にあまり反発はないようだ
	PRINTL 吸血鬼であるが故に体力が高く回復も早い為、多少無茶しても問題はないだろう
CASE 26
	PRINTL 竜を使役し乗りこなす少女
	PRINTL 性格は強気で精神的にも気丈ではあるが根が素直なため献身的に尽くしてくれるだろう
	PRINTL 性的な経験は多くないようだが回復が早い為ハードな調教にも付き合える体力をもっている
CASE 27
	PRINTL どこか不思議な魅力を感じさせる女占い師
	PRINTL 占いによって弱点を的確に突いてくるが、経験は人並みのようだ
	PRINTL …彼女の予言は良く当たるが、本当にそれが予言なのかは甚だ疑問である
CASE 28
	PRINTL 日夜戦い続ける女兵士
	PRINTL 戦士としてプライドが高く気丈だが、代わりにどうやら頭を使う事が苦手なようだ
	PRINTL 痛みに強く回復も早いため、足りない経験をじっくり教え込むのもいいだろう
CASE 29
	PRINTL 意志を持った粘性の液体の魔物
	PRINTL その液体でできた豊満な体にローションを使う機会はあまりないだろう
	PRINTL 楽観的で好奇心旺盛なため、その体を使った奉仕に向いた性格のようだ
CASE 30
	IF CFLAG:8 == 1
		PRINTL
		PRINTL
		PRINTL
	ELSE
		PRINTL 純情な乙女にしか心を許さないと言われる一角獣
		PRINTL 額の角には薬効や魔力が宿ると言われ、毒に対する耐性や治癒の力を持つ
		PRINTL 彼女自身がおとなしく献身的な乙女なので、無理強いしなければ調教しやすいだろう
	ENDIF
CASE 31
	PRINTL 悪戯好きな小鬼の魔物
	PRINTL 精神的に幼く、サドというほどでもないが責める事が好きなようだ
	PRINTL 特にこれといった難点はないため、比較的楽に調教する事ができるだろう
CASE 32
	PRINTL
	PRINTL
	PRINTL
CASE 33
	IF CFLAG:8 == 4
		PRINTL 強欲の罪を司る宝箱を模した魔物
	ELSE
		PRINTL 宝箱に擬態した魔物
	ENDIF
	PRINTL 意外と体力が高く回復も早い為、じっくり従順に教え込むのも悪くないだろう
	PRINTL 宝箱という魅力により商品価値は高い…かもしれない
CASE 34
	PRINTL 夜の国から来た石竜(バジリスク)
	PRINTL 高いプライドを持つサドッ気の強い上位夢魔であり、メローナとかなり仲が良い
	PRINTL 自分と他人に明確な線を引いている節があり、堕とすのには大変な労力を要すだろう
CASE 35
	PRINTL 夜の国から来た火竜(サラマンダー)
	PRINTL 妙な魅力を持った上位夢魔であり、ストゥーナとかなり仲が良い
	PRINTL その飲み込みの速さにより、ストゥーナと比べて比較的落としやすいだろう
CASE 36
	PRINTL
	PRINTL
	PRINTL
CASE 37
	PRINTL 吟遊詩人の恋人
	PRINTL
	PRINTL
CASE 38
	PRINTL ハーピー三姉妹の長女
	PRINTL 姉妹で唯一頭が回る、性格は強気で傲慢で典型的なハーピー
	PRINTL 勇者に挑んでは負けてを繰り返している為か姉妹揃って回復が早い
CASE 39
	PRINTL ハーピー三姉妹の次女
	PRINTL 性格は生意気なところもあるが姉と同様強気で傲慢
	PRINTL 姉妹の仲で唯一魔法が使えるが別に賢くはないようだ
	PRINTL 姉妹揃って何度も挑んでは返り討ちにされているからか回復が早い
CASE 40
	PRINTL ハーピー三姉妹の三女
	PRINTL 下の姉同様頭は良くないが性格は姉に似ず明るく素直でやや幼い
	PRINTL 姉達とともに勇者に挑んでいるからか回復が早い
CASE 41
	PRINTL 正体不明の怪物
	PRINTL 強気な性格という以外詳しいことは不明
	PRINTL 性格に似合わずキスに弱いという一面もある
CASE 42
	PRINTL 正体不明の怪物
	PRINTL 何もかもが不明
	PRINTL 原典にはただ一言、『燻り狂えるバンダースナッチに近寄るべからず』とあるだけである
CASE 43
	PRINTL 一年中発情しているやけっぱちな鳥
	PRINTL 強気ではあるが発情している為、濡れやすく好色で快感にも素直
	PRINTL 調教は簡単に行うことが出来るだろうが、トリ頭のせいか物覚えは悪い
CASE 44
	PRINTL 王都グルッペンの高級娼館『ミランダ』の高級娼婦
	PRINTL 何故か夢魔にも劣らぬ性技術を持つ
	PRINTL 自称、キアズマの出身
	PRINTL それ以外の事は何も分かっていない謎の女性だが…
CASE 45
	PRINTL
	PRINTL
	PRINTL
CASE 46
	IF RAND:2
		PRINTL 魔導人形型式CR-18X
		PRINTL リスヴェグ魔導院が開発した魔力で動く自動人形。
		PRINTL 元々家事から夜伽までこなす万能型だが、
		PRINTL 夢魔の魔力を注がれたことで特に後者の機能が大幅に強化されている
	ELSE
		PRINTL リズヴェグ魔導院の女性型自動人形
		PRINTL 全体的に感度は低く人を感じさせるのに特化したタイプであり、嗜虐的な面も伺える
		PRINTL 従順ではあるが気ままな部分もあり、理想を形にするには多大な労力がかかる事だろう
	ENDIF
CASE 47
	PRINTL ルグブロード南の大迷路に住む魔女。
	PRINTL 年齢不詳。
	PRINTL 夢魔ではないが、迷路に迷い込んだ男から精を奪って若さと美貌を保っている。
	PRINTL 高い魔力をロクなことに使わないのはリスヴェグの魔術師に共通した性質らしい。
CASE 48
	PRINTL もっとも強力な魔力を持つ夢魔の一人。
	PRINTL エルダーサキュバスという種族に属する。
	PRINTL 夢魔に組織というものは存在しないが、目的が一致した夢魔の間では
	PRINTL 自然と彼女のような能力の高いものがリーダー的存在となる。
	SIF TALENT:娇小体型
		PRINTL …おや、ちょっと身体が小さい気が…？
CASE 49
	PRINTL
	PRINTL
	PRINTL
CASE 50
	PRINTL 夢魔のリーダーであるエストを姉に持つ少女
	PRINTL 子供らしく調教に不利な素質はあまりないが、アイテムなどをオモチャに変える力を持つ
	PRINTL 一度懐くと独占欲が強いためか、放っとくと酷い事になるかもしれないので注意が必要
CASE 51
	PRINTL
	PRINTL
	PRINTL
CASE 52
	PRINTL 夢魔に対抗できる(唯一のはずの)存在・魔王の欠片の一つにして勇者の相棒
	PRINTL そのため、言う事を聞かせづらく、また弱点らしい弱点を持たない
CASE 53
	PRINTL
	PRINTL
	PRINTL
CASE 54
	PRINTL グルッペン王立図書館に勤務する老史書アイゼクトの秘書
	PRINTL 秘書としてはとても優秀。がどこか抜けているところも
	PRINTL 夢見がちで禁断の愛故に…
CASE 55
	PRINTL 別の世界から迷い込んだ少女
	PRINTL 不思議な魅力を持つ大人しい少女だが好奇心は強い
	PRINTL 物覚えもよく素直なため調教に向いてはいるのだが、性経験がまったく無いので無理は禁物
	PRINTL
CASE 56
	PRINTL 老司書アイゼクトが若き頃に出会った面影の少女
	PRINTL 思わず庇護欲を掻き立てられるような、可憐で儚い雰囲気を持つ
	PRINTL 理想の乙女とも言える少女…彼女ほど調教しがいのある獲物はそうそういないだろう
CASE 57
	PRINTL 老司書アイゼクトが若き頃に出会った少女、の面影から形成された夢(ユメ)
	PRINTL アイゼクトの記憶の中の少女とは異なり、可憐でありながら妖艶な雰囲気に満ちている
	PRINTL 日記帳を紐解いていけば彼女を理解できるのだろうか…？
CASE 58
	PRINTL 終わらないティーパーティーを開いていた「三月兎」
	PRINTL 気丈で生意気その上に男嫌いで臭いにも敏感、確実に調教は困難を極めるだろう
	PRINTL そしてその狂気に故に一線を越えることを躊躇ってしまうだろうが
	PRINTL それを超えることが出来ればきっと彼女の世界を見ることが出来るだろう
CASE 59
	PRINTL 夢魔に犯され、夢魔になってしまった少女
	PRINTL 夢魔になって間もないため、性知識はほとんど無い
	PRINTL 最初の獲物が経験の無い少年であったため、挿入に至る前に果ててしまい
	PRINTL 夢魔でありながらいまだ処女である
CASE 60
	PRINTL ハチの特徴を持つデミヒューマン
	PRINTL 近代の創作物を起源とし、神話や伝承には登場しない
	PRINTL 作品によって性格が大きく異なり、
	PRINTL 人間に友好的で温和なものや攻撃的なものなど様々である
CASE 61
	PRINTL 主神オーディーンの従者。死と戦いの女神。
	PRINTL 鎧兜を身に着け馬で戦場を駆け回り、戦死した者をヴァルハラへと運ぶ役割を持つ。
	PRINTL ヴァルハラに召された戦士たちは、そこで彼女たちの歓待を受ける事ができる。
CASE 98
	IF TALENT:MASTER:男性
		PRINTL 夢の世界に存在する鏡写しの自分
		PRINTL 特にあなたと大きく違うのは、何も知らない生娘である事
		PRINTL もう一人の自分をあなた色に調教する……それは、とても甘美な事のように思えた
	ELSEIF TALENT:MASTER:梦魔 || TALENT:MASTER:上位梦魔
		PRINTL 夢の世界に迷い込んできた少女
		PRINTL 何も知らない処女ではある事以外これといった特徴はないが
		PRINTL どうやら夢の快楽を体験したがっているようだ
	ELSE
		PRINTL 夢の世界に存在するもう一人の自分
		PRINTL あなたとの違いは、なんの変哲もない生娘である事である
		PRINTL …ありえたかもしれない自分自身を快楽に溺れさせるのもいいかもしれない
	ENDIF
ENDSELECT
PRINTL
VARSET LOCALS
;個別ENDの有無を出力
FOR LCOUNT, 1, 10
	RESULT = 0
	TRYCALLFORM IS_SINGLE_ENDING_K{ARG}_{LCOUNT}
	SELECTCASE RESULT
	CASE 1
		LOCALS = %LOCALS%口上{LCOUNT}%BL(1)%
	CASE IS >= 2
		LOCALS:1 = 専用口上を持つキャラ以外では、
	ENDSELECT
NEXT
SIF LOCALS == ""
	RETURN 0
SETCOLOR DEF_COLOR("ハートピンク")
PRINTFORML %LOCALS:1%%LOCALS%に於いて個別ＥＮＤが用意されています
RESETCOLOR

;ARGには作成するキャラのNO、ARGSには補足
;ARGS == "主人"とすると、精力などが主人として呼び出した時のパラメーターになる
@PROFILE, ARG, ARGS
#DIM LCOUNT
#DIM LCOUNT2
#DIM MEMO_TARGET
#DIM MEMO_MASTER
#DIM MEMO, 4
;キャラメイクの時の固定素質
#DIMS TALENT_FIX
;キャラメイクの時の除外素質
#DIMS TALENT_RMV

ADDCHARA ARG

REDRAW 0

MEMO_TARGET = TARGET
MEMO:0 = A
MEMO:1 = C
MEMO:2 = D
MEMO:3 = E

TARGET = CHARANUM-1

;お遊びのクズノハ小柄化＆ワーウルフゆめおと化＆バンダースナッチは胸の大きさが決まってないぞ！
;V.ヴィレッジャはお嬢様もありだ！
;天使コンビはふたなりでもきっと許される！
;エスト様はちっちゃくても良いよね！
SELECTCASE ARG
CASE 7
	SIF RAND:2
		TALENT:四足步行 = 0
CASE 9
	SIF RAND:2
		TALENT:贫乳 = 1
CASE 16
	IF RAND:3 == 0
		TALENT:娇小体型 = 1
		IF RAND:4
			TALENT:贫乳 = 1
			TALENT:巨乳 = 0
		ENDIF
	ENDIF
CASE 20
	IF RAND:5 == 0
		TALENT:女性 = 0
		TALENT:扶她 = 1
		TALENT:童贞 = 1
		ABL:Ｃ感觉 = 2
	ENDIF
	SIF TALENT:男性 == 0 && RAND:5 == 0
		TALENT:处女 = 1
CASE 21
	IF RAND:5 == 0
		TALENT:女性 = 0
		TALENT:扶她 = 1
		ABL:Ｃ感觉 = 2
		EXP:Ｖ插入经验 = EXP:Ｖ性交经验
		EXP:腰技经验 *= 2
	ENDIF
CASE 42
	IF RAND:3 == 0
		TALENT:巨乳 = 1
	ELSEIF RAND:2
		TALENT:贫乳 = 1
	ENDIF
CASE 48
	IF RAND:5 == 0
		TALENT:高挑体型 = 0
		TALENT:娇小体型 = 1
		TALENT:贫乳 = 1
		TALENT:巨乳 = 0
	ENDIF
CASE 61
	;処女の戦乙女も、またいいものだ
	SIF TALENT:男性 == 0 && RAND:5 == 0
		TALENT:处女 = 1
ENDSELECT

IF COND("ユニーク") == 0
	;ユニークでないならおっぱいの大きさ変動ぐらい有っても良いじゃない
	IF RAND:5 == 0 && TALENT:男性 == 0
		IF TALENT:巨乳 >= 2
			SIF RAND:2
				TALENT:巨乳 -= 1
		ELSEIF TALENT:巨乳
			IF RAND:2
				TALENT:巨乳 = 2
			ELSE
				TALENT:巨乳 = 0
			ENDIF
		ELSEIF TALENT:贫乳
			SIF RAND:2
				TALENT:贫乳 = 0
		ELSE
			IF RAND:2
				TALENT:巨乳 = 1
			ELSE
				TALENT:贫乳 = 1
			ENDIF
		ENDIF
	ENDIF
	;肌の色がちょっとぐらい変わってても良いじゃない
	IF RAND:5 == 0
		SELECTCASE TALENT:肤色
		;普通→褐色、色白
		CASE 0
			IF RAND:2
				TALENT:肤色 = 1
			ELSE
				TALENT:肤色 = 2
			ENDIF
		;褐色→普通
		CASE 1
			TALENT:肤色 = 0
		;色白→普通
		CASE 2
			TALENT:肤色 = 0
		;青肌→褐色、色白
		CASE 3
			IF RAND:2
				TALENT:肤色 = 1
			ELSE
				TALENT:肤色 = 2
			ENDIF
		ENDSELECT
	ENDIF
ENDIF

;いくつかの先に固定処理しておいた方が良い素質を処理（経験などに絡むもの）
IF CEVENT("固定：ふたなり", MASTER)
	TALENT:扶她 = 1
	TALENT:男性 = 0
	TALENT:女性 = 0
ELSEIF CEVENT("除外：ふたなり", MASTER)
	TALENT:扶她 = 0
	TALENT:女性 = 1
ENDIF
IF CEVENT("固定：処女", MASTER) && TALENT:男性 == 0
	TALENT:处女 = 1
ELSEIF CEVENT("除外：処女", MASTER) && TALENT:男性 == 0
	TALENT:处女 = 0
ENDIF
IF CEVENT("固定：童貞", MASTER) && PENIS(TARGET)
	TALENT:童贞 = 1
ELSEIF CEVENT("除外：童貞", MASTER) && PENIS(TARGET)
	TALENT:童贞 = 0
ENDIF

CALL HIGH_RANKER, ARG, E
CALL SETFLAG, "识别号码", TARGET
CALL SET_NEWNAME
CALL RESET_EXP_GAIN, TARGET
CALL GET_EXABL, TARGET
CALL GET_EXTALENT, TARGET
CALL SET_3SIZE, TARGET
CALL SETFLAG, "陰毛と腋毛", TARGET
CALL SETFLAG, "乳首の詳細設定", TARGET


;キャラメイク時の素質固定or除外を行う
VARSET LOCALS
SPLIT TEXTS("固定可能な素質"), "/", LOCALS

FOR LCOUNT, 0, 100
	SIF LOCALS:LCOUNT == ""
		BREAK

	TALENT_FIX = 固定：%LOCALS:LCOUNT%
	TALENT_RMV = 除外：%LOCALS:LCOUNT%

	;固定素質
	IF CEVENT(TALENT_FIX, MASTER)
		SELECTCASE LOCALS:LCOUNT
		CASE "乳首肥大"
			SIF GETBIT(TALENT:乳头, 0) == 0
				CONTINUE
			CLEARBIT TALENT:乳头, 0
			SETBIT TALENT:乳头, 1
		CASE "陥没乳首"
			SIF GETBIT(TALENT:乳头, 10) == 0
				CONTINUE
			CLEARBIT TALENT:乳头, 10
			SETBIT TALENT:乳头, 11
		CASE "巨乳輪"
			SIF GETBIT(TALENT:乳头, 20) == 0
				CONTINUE
			CLEARBIT TALENT:乳头, 20
			SETBIT TALENT:乳头, 21
		CASE "長身"
			SIF TALENT:妖精
				CONTINUE
			CALL CALC, "長身化", TARGET
		CASE "小柄体型"
			SIF TALENT:妖精
				CONTINUE
			CALL CALC, "小柄化", TARGET
		CASE "貧乳"
			SIF TALENT:男性
				CONTINUE
			CALL CALC, "貧乳化", TARGET
		CASE "巨乳"
			SIF TALENT:男性
				CONTINUE
			CALL CALC, "巨乳化", TARGET
		CASE "爆乳"
			SIF TALENT:男性
				CONTINUE
			CALL CALC, "爆乳化", TARGET
		CASE "超乳"
			SIF TALENT:男性
				CONTINUE
			CALL CALC, "超乳化", TARGET
		CASE "小尻"
			SIF COND("小尻")
				CONTINUE

			TALENT:大臀 = 0
			FOR LCOUNT2, 0, 100
				SIF COND("小尻")
					BREAK
				BASE:臀围 -= BASE:身高*2/100
			NEXT
			TALENT:小臀 = 1
		CASE "巨尻"
			SIF COND("巨尻") == 1
				CONTINUE

			IF COND("巨尻") == 0
				TALENT:小臀 = 0
				FOR LCOUNT2, 0, 100
					SIF COND("巨尻")
						BREAK
					BASE:臀围 += BASE:身高*2/100
				NEXT
			ELSE
				FOR LCOUNT2, 0, 100
					SIF COND("巨尻") <= 1
						BREAK
					BASE:臀围 -= BASE:身高*2/100
				NEXT
			ENDIF
			TALENT:大臀 = 1
		CASE "爆尻"
			SIF COND("巨尻") == 2
				CONTINUE

			IF COND("巨尻") <= 1
				TALENT:小臀 = 0
				FOR LCOUNT2, 0, 100
					SIF COND("巨尻") >= 2
						BREAK
					BASE:臀围 += BASE:身高*2/100
				NEXT
			ELSE
				FOR LCOUNT2, 0, 100
					SIF COND("巨尻") <= 2
						BREAK
					BASE:臀围 -= BASE:身高*2/100
				NEXT
			ENDIF
			TALENT:大臀 = 2
		CASE "超尻"
			SIF COND("巨尻") == 3
				CONTINUE

			TALENT:小臀 = 0
			FOR LCOUNT2, 0, 100
				SIF COND("巨尻") == 3
					BREAK
				BASE:臀围 += BASE:身高*2/100
			NEXT
			TALENT:大臀 = 3
		CASE "褐色肌"
			TALENT:肤色 = 1
		CASE "色白"
			TALENT:肤色 = 2
		CASE "青肌"
			TALENT:肤色 = 3
		;オトコでないなら追加
		CASE "母乳体質", "子宮性感"
			SIF TALENT:男性 == 0
				TALENT:(LOCALS:LCOUNT) = 1
		;純粋追加
		CASE "腸液分泌体質", "嘔吐反射耐性", "拡張適正", "喉性感", "尿道性感", "においに弱い"
			TALENT:(LOCALS:LCOUNT) = 1
		;上で処理済み
		CASE "処女", "童貞", "ふたなり"
		ENDSELECT
	;除外素質
	ELSEIF CEVENT(TALENT_RMV, MASTER)
		SELECTCASE LOCALS:LCOUNT
		CASE "乳首肥大"
			SIF GETBITS(TALENT:乳头, 1, 9) == 0
				CONTINUE
			TALENT:乳头 = CLEARBITS(TALENT:乳头, 1, 9)
			SETBIT TALENT:乳头, 0
		CASE "陥没乳首"
			SIF GETBITS(TALENT:乳头, 11, 19) == 0
				CONTINUE
			TALENT:乳头 = CLEARBITS(TALENT:乳头, 11, 19)
			SETBIT TALENT:乳头, 10
		CASE "巨乳輪"
			SIF GETBITS(TALENT:乳头, 21, 29) == 0
				CONTINUE
			TALENT:乳头 = CLEARBITS(TALENT:乳头, 21, 29)
			SETBIT TALENT:乳头, 20
		CASE "長身"
			SIF TALENT:妖精 || TALENT:高挑体型 == 0
				CONTINUE
			CALL CALC, "普通体型化", TARGET
		CASE "小柄体型"
			SIF TALENT:妖精 || TALENT:娇小体型 == 0
				CONTINUE
			CALL CALC, "普通体型化", TARGET
		CASE "貧乳"
			SIF TALENT:男性 || TALENT:贫乳 == 0
				CONTINUE

			SELECTCASE RAND:10
			CASE 9
				CALL CALC, "超乳化", TARGET
			CASE 8
				CALL CALC, "爆乳化", TARGET
			CASE 7
				CALL CALC, "巨乳化", TARGET
			CASEELSE
				CALL CALC, "並乳化", TARGET
			ENDSELECT
		CASE "巨乳"
			SIF TALENT:男性 || TALENT:巨乳 != 1
				CONTINUE

			SELECTCASE RAND:10
			CASE 7 TO 9
				CALL CALC, "超乳化", TARGET
			CASE 4 TO 6
				CALL CALC, "爆乳化", TARGET
			CASE 0
				CALL CALC, "貧乳化", TARGET
			CASEELSE
				CALL CALC, "並乳化", TARGET
			ENDSELECT
		CASE "爆乳"
			SIF TALENT:男性 || TALENT:巨乳 != 2
				CONTINUE

			SELECTCASE RAND:10
			CASE 7 TO 9
				CALL CALC, "超乳化", TARGET
			CASE 4 TO 6
				CALL CALC, "巨乳化", TARGET
			CASE 0
				CALL CALC, "貧乳化", TARGET
			CASEELSE
				CALL CALC, "並乳化", TARGET
			ENDSELECT
		CASE "超乳"
			SIF TALENT:男性 || TALENT:巨乳 != 3
				CONTINUE

			SELECTCASE RAND:10
			CASE 7 TO 9
				CALL CALC, "爆乳化", TARGET
			CASE 4 TO 6
				CALL CALC, "巨乳化", TARGET
			CASE 0
				CALL CALC, "貧乳化", TARGET
			CASEELSE
				CALL CALC, "並乳化", TARGET
			ENDSELECT
		CASE "小尻"
			SIF COND("小尻") == 0
				CONTINUE

			TALENT:大臀 = 0
			FOR LCOUNT2, 0, 100
				SIF COND("小尻") == 0
					BREAK
				BASE:臀围 += BASE:身高*3/100
			NEXT
			CALL SETFLAG, "ヒップ設定", TARGET
		CASE "巨尻"
			SIF COND("巨尻") != 1
				CONTINUE

			IF RAND:2
				FOR LCOUNT2, 0, 100
					SIF COND("巨尻") != 1
						BREAK
					BASE:臀围 += BASE:身高*3/100
				NEXT
			ELSE
				FOR LCOUNT2, 0, 100
					SIF COND("巨尻") != 1
						BREAK
					BASE:臀围 -= BASE:身高*3/100
				NEXT
			ENDIF
			CALL SETFLAG, "ヒップ設定", TARGET
		CASE "爆尻"
			SIF COND("巨尻") != 2
				CONTINUE

			IF RAND:2
				FOR LCOUNT2, 0, 100
					SIF COND("巨尻") != 2
						BREAK
					BASE:臀围 += BASE:身高*3/100
				NEXT
			ELSE
				FOR LCOUNT2, 0, 100
					SIF COND("巨尻") != 2
						BREAK
					BASE:臀围 += BASE:身高*3/100
				NEXT
			ENDIF
			CALL SETFLAG, "ヒップ設定", TARGET
		CASE "超尻"
			SIF COND("巨尻") != 3
				CONTINUE

			FOR LCOUNT2, 0, 100
				SIF COND("巨尻") < 3
					BREAK
				BASE:臀围 -= BASE:身高*3/100
			NEXT
			CALL SETFLAG, "ヒップ設定", TARGET
		CASE "褐色肌"
			SIF	TALENT:肤色 == 1
				TALENT:肤色 = 0
		CASE "色白"
			SIF	TALENT:肤色 == 2
				TALENT:肤色 = 0
		CASE "青肌"
			SIF	TALENT:肤色 == 3
				TALENT:肤色 = 0
		;純粋除外
		CASE "腸液分泌体質", "嘔吐反射耐性", "拡張適正", "喉性感", "尿道性感", "においに弱い", "母乳体質", "子宮性感"
			SIF SET_EXTALENT(LOCALS:LCOUNT) == 0
				TALENT:(LOCALS:LCOUNT) = 0
		;上で処理済み
		CASE "処女", "童貞", "ふたなり"
		ENDSELECT
	ENDIF
NEXT

CALL SETFLAG, "属性", TARGET
CALL CALC, "好きな体位", TARGET
CALL SETFLAG, "ペニス詳細設定"
;衣装の設定
CALL COORDINATE_MUMA, TARGET
CALL MEMO_FAVORITE_DRESS, TARGET
;精力、ストックセット
IF ARGS == "主人"
	MEMO_MASTER = MASTER
	MASTER = TARGET
	CALL SET_COUNTER_BASE
	MASTER = MEMO_MASTER
ELSE
	CALL SET_COUNTER_BASE
ENDIF
;魔法使い持ちは、魔法を持っている可能性アリ
SIF TALENT:魔法使 == 1
	CALL GET_MAGIC, TARGET

PRINTL
CALL PRINT_PROFILE

TARGET = MEMO_TARGET
A = MEMO:0
C = MEMO:1
D = MEMO:2
E = MEMO:3

@PRINT_PROFILE
CALL NEW_PRINT_STATE, TARGET, "召喚"

PRINT 弱点：
IF TALENT:Ｍ敏感 && TALENT:Ｃ敏感 && TALENT:Ｖ敏感 && TALENT:Ａ敏感 && TALENT:Ｂ敏感
	PRINT 全て　
ELSEIF TALENT:Ｍ敏感 + TALENT:Ｃ敏感 + TALENT:Ｖ敏感 + TALENT:Ａ敏感 + TALENT:Ｂ敏感 + TALENT:弱精液 == 0
	PRINT 特になし　
ELSE
	LOCALS =
	SIF TALENT:Ｍ敏感
		LOCALS += "キス、"
	SIF TALENT:Ｃ敏感 || TALENT:Ｖ敏感
		LOCALS += "アソコ、"
	SIF TALENT:Ｂ敏感
		LOCALS += "胸、"
	SIF TALENT:Ａ敏感
		LOCALS += "お尻、"
	SIF COND("精飲に弱い")
		LOCALS += "精飲、"
	SIF COND("膣内射精に弱い")
		LOCALS += "膣内射精、"
	SIF COND("肛内射精に弱い")
		LOCALS += "肛内射精、"
	SIF COND("乳内射精に弱い")
		LOCALS += "乳内射精、"
	STRLENS LOCALS
	SUBSTRING LOCALS, 0, RESULT - 2
	PRINTFORM %RESULTS%　
ENDIF

PRINT 性格：
VARSET LOCALS
SIF TALENT:好色
	LOCALS = %LOCALS%%TALENTNAME:75%、
SIF TALENT:强硬
	LOCALS = %LOCALS%%TALENTNAME:18%、
SIF TALENT:清纯
	LOCALS = %LOCALS%%TALENTNAME:39%、
SIF TALENT:开朗
	LOCALS = %LOCALS%%TALENTNAME:25%、
SIF TALENT:坦率
	LOCALS = %LOCALS%%TALENTNAME:13%、
SIF TALENT:阴郁
	LOCALS = %LOCALS%%TALENTNAME:26%、
SIF TALENT:腼腆
	LOCALS = %LOCALS%%TALENTNAME:14%、
SIF TALENT:胆怯
	LOCALS = %LOCALS%%TALENTNAME:10%、
SIF TALENT:坏心眼
	LOCALS = %LOCALS%%TALENTNAME:19%、
SIF TALENT:抖Ｓ
	LOCALS = %LOCALS%%TALENTNAME:83%、
SIF TALENT:高傲
	LOCALS = %LOCALS%%TALENTNAME:15%、
SIF TALENT:自卑
	LOCALS = %LOCALS%%TALENTNAME:17%、
SIF TALENT:沉着
	LOCALS = %LOCALS%%TALENTNAME:20%、
SIF TALENT:狂气
	LOCALS = %LOCALS%%TALENTNAME:123%、
SIF TALENT:献身
	LOCALS = %LOCALS%%TALENTNAME:63%、
SIF TALENT:斯文
	LOCALS = %LOCALS%%TALENTNAME:46%、
SIF TALENT:粗野
	LOCALS = %LOCALS%%TALENTNAME:47%、
SIF TALENT:聪慧
	LOCALS = %LOCALS%%TALENTNAME:50%、
SIF TALENT:糊涂
	LOCALS = %LOCALS%%TALENTNAME:51%、

IF STRLENS(LOCALS)
	PRINTFORM %SUBSTRING(LOCALS, 0, STRLENS(LOCALS) - 2)%　
ELSE
	PRINT ？　
ENDIF

;難易度
CALL ANALYSIS_CALC
CALL SHOW_DIFFICULTY

VARSET SOURCE
VARSET CUP

CALL SETFLAG, "売却価格", TARGET
PRINT 　素質評価：
SELECTCASE CFLAG:42
CASE IS >= 10000
	PRINT ＳＳＳ
CASE IS >= 8000
	PRINT ＳＳ
CASE IS >= 6000
	PRINT Ｓ
CASE IS >= 4000
	PRINT Ａ
CASE IS >= 2000
	PRINT Ｂ
CASE IS >= 1000
	PRINT Ｃ
CASE IS >= 500
	PRINT Ｄ
CASEELSE
	PRINT Ｅ
ENDSELECT

;助手能力
;CALL SHOW_ABL_ASSI

@CHARA_MANUAL, ARG, ARGS
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
;解説、弱点、性格、難易度、助手能力表示
CALL PROFILE, ARG, ARGS
PRINTL
PRINTL
IF ARGS == "主人"
	PRINTFORML これでよろしいですか？
	PRINTFORML [0]はい　[1]いいえ　[2]リロード　[3]素質の固定と除外（設定後リロードします）
ELSE
	PRINTFORML 召喚しますか？
	PRINTFORML [0]はい　[1]いいえ　[2]再召喚　[3]素質の固定と除外（設定後、再召喚します）
ENDIF

CALL HIGH_RANKER_SHOW, ARG

SELECTCASE RESULT
CASE 0
	RETURN 1
CASE 1
	RETURN 0
CASE 3
	CALL FIX_TALENT
ENDSELECT

;表示されていたキャラを白紙に戻す
DELCHARA CHARANUM-1
CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART


;主人のCEVENTで固定素質と除外素質を記録する
@FIX_TALENT
#DIM MEMO_LINECOUNT
#DIM LCOUNT
#DIM NUM_TALENT
#DIM ABL_SAMON
#DIM CHOICE
#DIMS TALENT_FIX
#DIMS TALENT_RMV

REDRAW 0

;固定or除外できる総数
ABL_SAMON = 1 + (TALENT:MASTER:天才召唤师 > 0)*2 + (TALENT:MASTER:天灾召唤师 > 0) + (TALENT:MASTER:谜之魅力 > 0) + (TALENT:MASTER:禁忌知识 > 0)

MEMO_LINECOUNT = LINECOUNT
PRINTL
PRINTFORML 特定の素質の中から{ABL_SAMON}個だけ固定or除外して、召喚を操作することができます
PRINTFORML ただし、スライムの[拡張適正]など除外できないものも幾つかあります
CALL PRINT_STR, "(選べる数は_黄色_[召喚の天才]_で２つ、_暗黄色_[召喚の天災][謎の魅力][禁断の知識]_で１つ増えます)_L"
CALL PRINT_STR, "なお固定した素質は_緑_緑色_、除外した素質は_青_青色_で表示します_L"
CALL PRINT_STR, "それでは以下から選んでください。選択するごとに_緑_固定_→_青_除外_→指定なし と変化します_L"
PRINTL

VARSET LOCALS
SPLIT TEXTS("固定可能な素質"), "/", LOCALS

NUM_TALENT = 0
FOR LCOUNT, 0, 100
	SIF LOCALS:LCOUNT == ""
		BREAK

	TALENT_FIX = 固定：%LOCALS:LCOUNT%
	TALENT_RMV = 除外：%LOCALS:LCOUNT%

	SIF CEVENT(TALENT_FIX, MASTER)
		SETCOLOR DEF_COLOR("緑")
	SIF CEVENT(TALENT_RMV, MASTER)
		SETCOLOR DEF_COLOR("青")

	PRINTFORM  [{LCOUNT, 2}]%TEXT_LJ(LOCALS:LCOUNT, 14)%

	RESETCOLOR

	NUM_TALENT += 1
	SIF NUM_TALENT%4 == 0
		PRINTL
NEXT
SIF NUM_TALENT%4
	PRINTL
PRINTFORML [100]これで良い

CALL INPUT_SELECT, NUM_TALENT, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	RETURN 1
CASEELSE
	;まずは固定or除外or指定なし、の判定
	TALENT_FIX = 固定：%LOCALS:CHOICE%
	TALENT_RMV = 除外：%LOCALS:CHOICE%
	;固定→除外
	IF CEVENT(TALENT_FIX, MASTER)
		CALL DEL_CEVENT, TALENT_FIX, MASTER
		CALL SET_CEVENT, TALENT_RMV, MASTER
	;除外→指定なし
	ELSEIF CEVENT(TALENT_RMV, MASTER)
		CALL DEL_CEVENT, TALENT_RMV, MASTER
	;指定なし→固定
	ELSE
		;同居できない素質の固定を消す
		SELECTCASE LOCALS:CHOICE
		CASE "長身", "小柄体型"
			CALL DEL_CEVENT, "固定：長身", MASTER
			CALL DEL_CEVENT, "固定：小柄体型", MASTER
			CALL DEL_CEVENT, "除外：長身", MASTER
			CALL DEL_CEVENT, "除外：小柄体型", MASTER
		CASE "貧乳", "巨乳", "爆乳", "超乳"
			CALL DEL_CEVENT, "固定：貧乳", MASTER
			CALL DEL_CEVENT, "固定：巨乳", MASTER
			CALL DEL_CEVENT, "固定：爆乳", MASTER
			CALL DEL_CEVENT, "固定：超乳", MASTER
			CALL DEL_CEVENT, "除外：貧乳", MASTER
			CALL DEL_CEVENT, "除外：巨乳", MASTER
			CALL DEL_CEVENT, "除外：爆乳", MASTER
			CALL DEL_CEVENT, "除外：超乳", MASTER
		CASE "小尻", "巨尻", "爆尻", "超尻"
			CALL DEL_CEVENT, "固定：貧尻", MASTER
			CALL DEL_CEVENT, "固定：巨尻", MASTER
			CALL DEL_CEVENT, "固定：爆尻", MASTER
			CALL DEL_CEVENT, "固定：超尻", MASTER
			CALL DEL_CEVENT, "除外：貧尻", MASTER
			CALL DEL_CEVENT, "除外：巨尻", MASTER
			CALL DEL_CEVENT, "除外：爆尻", MASTER
			CALL DEL_CEVENT, "除外：超尻", MASTER
		CASE "褐色肌", "色白", "青肌"
			CALL DEL_CEVENT, "固定：褐色肌", MASTER
			CALL DEL_CEVENT, "固定：色白", MASTER
			CALL DEL_CEVENT, "固定：青肌", MASTER
			CALL DEL_CEVENT, "除外：褐色肌", MASTER
			CALL DEL_CEVENT, "除外：色白", MASTER
			CALL DEL_CEVENT, "除外：青肌", MASTER
		ENDSELECT

		;既に選択可能個数分選択している？
		IF STRCOUNTS(CSTR:MASTER:10, "固定：", "除外：") >= ABL_SAMON
			PRINTFORMW 選べる数をオーバーしています
		;指定なし→固定
		ELSE
			CALL SET_CEVENT, TALENT_FIX, MASTER
		ENDIF
	ENDIF
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@HIGH_RANKER_SHOW, ARG
;その種に上位夢魔がいるかチェック
IF IS_HIGH_RANKER(ARG)
	PRINTFORML ～同じ種族の夢魔のプロフィール閲覧はこちら～
ELSE
	CALL ONEINPUT_SELECT, 4
	RETURN RESULT
ENDIF

SELECTCASE ARG
CASE 1
	PRINTFORML [4] ナイトメア
	PRINTFORML [5] アスモデ
	PRINTFORML [6] リリス
	PRINTFORML [7] サキュバス

	CALL ONEINPUT_SELECT, 8
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 3
	CASE 6
		E = 4
	CASE 7
		E = 0
	ENDSELECT
CASE 2
	PRINTFORML [4] アルプ
	PRINTFORML [5] リリム
	PRINTFORML [6] インプ
	CALL ONEINPUT_SELECT, 7
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 4
	CASE 6
		E = 0
	ENDSELECT
CASE 3
	PRINTFORML [4] アルラウネ
	PRINTFORML [5] スクーグスロー
	PRINTFORML [6] ドライアド
	CALL ONEINPUT_SELECT, 7
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 3
	CASE 6
		E = 0
	ENDSELECT
CASE 4
	PRINTFORML [4] リャナンシー
	CALL PRINT_STR, "[5] ティターニア（_黄色_専用口上有_）_L_"
	PRINTFORML [6] ピクシー
	CALL ONEINPUT_SELECT, 7
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 3
	CASE 6
		E = 0
	ENDSELECT
CASE 5
	PRINTFORML [4] ウンディーネ
	PRINTFORML [5] シルフ
	PRINTFORML [6] エアリアル
	CALL ONEINPUT_SELECT, 7
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 0
	CASE 6
		E = 3
	ENDSELECT
CASE 6
	PRINTFORML [4] ケットシー
	PRINTFORML [5] スフィンクス
	PRINTFORML [6] ネコマタ
	;PRINTFORML [7] ブラックキャット
	CALL ONEINPUT_SELECT, 7
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 4
	;CASE 7
	;	E = 3
	CASE 6
		E = 0
	ENDSELECT
CASE 7
	PRINTFORML [4] ケルベロス
	PRINTFORML [5] ワーウルフ
	CALL ONEINPUT_SELECT, 6
	SELECTCASE RESULT
	CASE 4
		E = 3
	CASE 5
		E = 0
	ENDSELECT
CASE 8
	CALL PRINT_STR, "[4] バンシー（_黄色_専用口上_＆_ハートピンク_個別ＥＮＤ有_）_L_"
	PRINTFORML [5] アルバスタ
	PRINTFORML [6] ヴィレッジャ
	CALL ONEINPUT_SELECT, 7
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 4
	CASE 6
		E = 0
	ENDSELECT
CASE 10
	CALL PRINT_STR, "[4] メリジューヌ（_黄色_専用口上有_）_L_"
	PRINTFORML [5] エキドナ
	PRINTFORML [6] ラミア
	CALL ONEINPUT_SELECT, 7
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 4
	CASE 6
		E = 0
	ENDSELECT
CASE 11
	CALL PRINT_STR, "[4] セイレーン（_黄色_専用口上有_）_L_"
	PRINTFORML [5] モーリアン
	PRINTFORML [6] ハーピー
	CALL ONEINPUT_SELECT, 7
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 3
	CASE 6
		E = 0
	ENDSELECT
CASE 12
	PRINTFORML [4] サラマンダー
	PRINTFORML [5] ヴィーヴル
	PRINTFORML [6] リザードマン
	CALL ONEINPUT_SELECT, 7
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 4
	CASE 6
		E = 0
	ENDSELECT
CASE 13
	PRINTFORML [4] レヴィアタン
	PRINTFORML [5] マーメイド
	CALL ONEINPUT_SELECT, 6
	SELECTCASE RESULT
	CASE 4
		E = 3
	CASE 5
		E = 0
	ENDSELECT
CASE 14
	PRINTFORML [4] ダーキニー
	PRINTFORML [5] アプサラス
	CALL ONEINPUT_SELECT, 6
	SELECTCASE RESULT
	CASE 4
		E = 3
	CASE 5
		E = 0
	ENDSELECT
CASE 15
	PRINTFORML [4] カリュブディス
	PRINTFORML [5] スキュラ
	CALL ONEINPUT_SELECT, 6
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 0
	ENDSELECT
CASE 16
	PRINTFORML [4] ダッキ
	PRINTFORML [5] タマモ
	PRINTFORML [6] クズノハ
	CALL ONEINPUT_SELECT, 7
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 3
	CASE 6
		E = 0
	ENDSELECT
CASE 17
	CALL PRINT_STR, "[4] エンプーサ（_黄色_専用口上有_）_L_"
	PRINTFORML [5] マイェストレ
	PRINTFORML [6] クィーンメイブ
	CALL ONEINPUT_SELECT, 7
	SELECTCASE RESULT
	CASE 4
		E = 1
	CASE 5
		E = 3
	CASE 6
		E = 0
	ENDSELECT
CASE 18
	PRINTFORML [4] シェロブ
	PRINTFORML [5] ナシュジェアスダ
	PRINTFORML [6] アルケニー
	CALL ONEINPUT_SELECT, 7
	SELECTCASE RESULT
	CASE 4
		E = 3
	CASE 5
		E = 4
	CASE 6
		E = 0
	ENDSELECT
CASE 20
	PRINTFORML [4] ガブリエル
	PRINTFORML [5] エンジェル
	CALL ONEINPUT_SELECT, 6
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 0
	ENDSELECT
CASE 22
	PRINTFORML [4] ハツメ
	PRINTFORML [5] クノイチ
	CALL ONEINPUT_SELECT, 6
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 0
	ENDSELECT
CASE 25
	PRINTFORML [4] カーミラ
	PRINTFORML [5] ヴァンパイア
	CALL ONEINPUT_SELECT, 6
	SELECTCASE RESULT
	CASE 4
		E = 2
	CASE 5
		E = 0
	ENDSELECT
CASE 30
;	PRINTFORML [4] バイコーン
;	CALL ONEINPUT_SELECT, 5
;	SELECTCASE RESULT
;	CASE 4
;		E = 1
;	ENDSELECT
CASE 33
	PRINTFORML [4] マモン
	PRINTFORML [5] ミミック
	CALL ONEINPUT_SELECT, 6
	SELECTCASE RESULT
	CASE 4
		E = 4
	CASE 5
		E = 0
	ENDSELECT
ENDSELECT

RETURN RESULT


@IS_HIGH_RANKER(ARG)
#FUNCTION
SELECTCASE ARG
CASE 9, 19, 21, 23, 24, 26 TO 32, 34 TO 63, 94, 96 TO 98
	RETURNF 0
ENDSELECT

RETURNF 1

;ARGはキャラ番号、ARG:1にはEの値（主に経験数がE倍になる）が入っている
@HIGH_RANKER, ARG, ARG:1, ARGS
SELECTCASE ARG
;サキュバス系
CASE 1
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:技巧 += 1
	ELSEIF ARG:1 == 3
		CFLAG:8 = 3
		ABL:欲望 += 2
		ABL:技巧 += 2
		ABL:施虐倾向 += 3
		TALENT:性变态 = 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ELSEIF ARG:1 == 4
		CFLAG:8 = 4
		ABL:欲望 += 1
		ABL:技巧 += 3
		ABL:侍奉精神 += 2
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ENDIF
;インプ系
CASE 2
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:技巧 += 1
		TALENT:肤色 = 1
	ELSEIF ARG:1 == 4
		CFLAG:8 = 4
		ABL:欲望 += 1
		ABL:技巧 += 2
		ABL:侍奉精神 += 2
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ENDIF
;ドライアド系
CASE 3
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:欲望 += 1
		ABL:技巧 += 1
		ABL:精液上瘾 += 2
		TALENT:污臭钝感 = 1
		TALENT:无视污臭 = 1
		TALENT:肤色 = 0
	ELSEIF ARG:1 == 3
		CFLAG:8 = 3
		ABL:欲望 += 1
		ABL:技巧 += 2
		ABL:侍奉精神 += 2
		TALENT:阴郁 = 1
		TALENT:羞耻心强 = 1
		TALENT:不知羞耻 = 0
		TALENT:纯情 = 1
		TALENT:献身 = 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ENDIF
;ピクシー系
CASE 4
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:技巧 += 1
		TALENT:歌唱技能 = 1
	ELSEIF ARG:1 == 3
		CFLAG:8 = 3
		ABL:欲望 += 1
		ABL:技巧 += 2
		ABL:侍奉精神 += 1
		TALENT:胆怯 = 0
		TALENT:强硬 = 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ENDIF
;シルフ系
CASE 5
	IF ARG:1 == 2
		CFLAG:8 = 1
		ABL:技巧 += 1
		ABL:Ｃ感觉 += 1
		ABL:Ｂ感觉 = 0
		TALENT:自大 = 1
		TALENT:容易湿 = 1
		TALENT:Ｍ敏感 = 0
		TALENT:Ｃ敏感 = 1
		TALENT:Ｖ敏感 = 1
		TALENT:Ｂ敏感 = 0
	ELSEIF ARG:1 == 3
		CFLAG:8 = 3
		ABL:技巧 += 2
		ABL:Ｃ感觉 += 1
		ABL:侍奉精神 += 1
		EXP:乳交经验 += 15
		EXP:亲吻经验 += 10
	ENDIF
;ネコマタ系
CASE 6
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:欲望 += 1
		ABL:技巧 += 2
		TALENT:聪慧 = 1
	ELSEIF ARG:1 == 4
		CFLAG:8 = 4
		ABL:欲望 += 1
		ABL:技巧 += 3
		ABL:施虐倾向 += 2
		TALENT:坏心眼 = 1
		TALENT:开朗 = 0
		TALENT:聪慧 = 1
		TALENT:承认快感 = 0
		TALENT:小恶魔 = 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	;ELSEIF ARG:1 == 4
	;	CFLAG:8 = 3
	;	ABL:欲望 += 1
	;	ABL:技巧 += 2
	;	ABL:侍奉精神 += 1
	;	TALENT:胆怯 = 0
	;	TALENT:强硬 = 1
	;	TALENT:肤色 = 1
	;	TALENT:梦魔 = 0
	;	TALENT:上位梦魔 = 1
	ENDIF
;ワーウルフ系
CASE 7
	IF ARG:1 == 3
		CFLAG:8 = 3
		ABL:欲望 += 1
		ABL:技巧 += 2
		TALENT:肤色 = 1
	ENDIF
	IF TALENT:四足步行 == 0
		CFLAG:209 = 2
		ABL:服从 = 0
		TALENT:刚强 = 1
		TALENT:坦率 = 1
		TALENT:开朗 = 1
		TALENT:不重贞操 = 0
		TALENT:糊涂 = 0
		TALENT:高挑体型 = 1
		TALENT:月光浴 = 1
	ENDIF
;ヴィレッジャ系(Vヴィレからの派生だと処女)
CASE 8, 9
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:欲望 += 1
		ABL:技巧 += 1
		TALENT:好奇心 = 0
		TALENT:阴郁 = 1
		TALENT:清纯 = 0
	ELSEIF ARG:1 == 4
		CFLAG:8 = 4
		ABL:欲望 += 1
		ABL:技巧 += 3
		ABL:施虐倾向 += 2
		TALENT:坦率 = 0
		TALENT:坏心眼 = 1
		TALENT:好奇心 = 0
		TALENT:阴郁 = 1
		TALENT:清纯 = 0
		TALENT:献身 = 0
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ENDIF
;ラミア系
CASE 10
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:欲望 += 1
		ABL:技巧 += 1
		ABL:百合倾向 = 0
		TALENT:强硬 = 0
		TALENT:清纯 = 1
		TALENT:斯文 = 1
		TALENT:聪慧 = 1
		TALENT:好色 = 0
		TALENT:双性恋 = 0
		TALENT:肤色 = 0
	ELSEIF ARG:1 == 4
		CFLAG:8 = 4
		ABL:欲望 += 1
		ABL:技巧 += 3
		ABL:精液上瘾 += 1
		TALENT:坦率 = 0
		TALENT:好奇心 = 0
		TALENT:母乳体质 = 1
		TALENT:生产经历 = 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
		BASE:胎儿的父亲 = -4
	ENDIF
;ハーピー系
CASE 11
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:施虐倾向 = 1
		TALENT:高傲 = 0
		TALENT:自大 = 0
		TALENT:强硬 = 0
		TALENT:开朗 = 1
		TALENT:糊涂 = 1
		TALENT:好奇心 = 1
		TALENT:歌唱技能 = 1
		ABL:Ｖ感觉 = 1
		EXP:Ｖ经验 = 10
		EXP:绝顶经验 = 5
		EXP:Ｖ性交经验 = 5
		EXP:精液经验 = 15
		EXP:口交经验 = 15
		EXP:足交经验 = 10
		EXP:腰技经验 = 5
	ELSEIF ARG:1 == 3
		CFLAG:8 = 3
		ABL:欲望 += 1
		ABL:技巧 += 2
		ABL:施虐倾向 += 1
		TALENT:聪慧 = 1
	ENDIF
;リザードマン系
CASE 12
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:欲望 += 1
		ABL:技巧 += 1
		TALENT:鼓舞 = 1
	ELSEIF ARG:1 == 4
		CFLAG:8 = 4
		ABL:欲望 += 1
		ABL:技巧 += 3
		TALENT:魅惑 = 1
		TALENT:魅力 = 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ENDIF
;マーメイド系
CASE 13
	IF ARG:1 == 3
		CFLAG:8 = 3
		ABL:欲望 += 1
		ABL:技巧 += 2
		ABL:施虐倾向 += 2
		TALENT:坏心眼 = 1
		TALENT:聪慧 = 0
		TALENT:承认快感 = 0
		TALENT:嫉妒 = 1
		TALENT:魅力 = 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ENDIF
;アプラサス系
CASE 14
	IF ARG:1 == 3
		CFLAG:8 = 3
		ABL:欲望 += 1
		ABL:技巧 += 2
		ABL:侍奉精神 += 2
		TALENT:强硬 = 1
		TALENT:开朗 = 0
		TALENT:怪力 = 1
		TALENT:肤色 = 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ENDIF
;スキュラ系
CASE 15
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:欲望 += 2
		ABL:技巧 += 2
		TALENT:纯情 = 1
		TALENT:肤色 = 1
	ENDIF
;クズノハ系
CASE 16
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:欲望 += 2
		ABL:技巧 += 2
		ABL:施虐倾向 += 3
		TALENT:坦率 = 0
		TALENT:腼腆 = 0
		TALENT:保守 = 0
		TALENT:羞耻心强 = 0
		TALENT:献身 = 0
		TALENT:高傲 = 1
		TALENT:强硬 = 1
		TALENT:坏心眼 = 1
		TALENT:好色 = 1
		TALENT:魅惑 = 1
	ELSEIF ARG:1 == 3
		CFLAG:8 = 3
		ABL:欲望 += 2
		ABL:技巧 += 4
		ABL:施虐倾向 += 2
		TALENT:坦率 = 0
		TALENT:腼腆 = 0
		TALENT:羞耻心强 = 0
		TALENT:斯文 = 1
		TALENT:献身 = 0
		TALENT:高傲 = 1
		TALENT:强硬 = 1
		TALENT:坏心眼 = 1
		TALENT:好色 = 1
		TALENT:魅力 = 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ENDIF
;クィーンメイヴ系
CASE 17
	IF ARG:1 == 1
		CFLAG:8 = 1
		TALENT:叛逆 = 0
		TALENT:高傲 = 0
		TALENT:自卑 = 1
		TALENT:好奇心 = 1
		TALENT:喜欢被关注 = 1
		TALENT:不重贞操 = 0
		TALENT:耐痛 = 0
		TALENT:容易湿 = 0
		TALENT:聪慧 = 0
		TALENT:污臭钝感 = 0
		TALENT:性变态 = 1
		TALENT:抖Ｓ = 0
		TALENT:高挑体型 = 0
		TALENT:Ａ敏感 = 0
		TALENT:Ｂ敏感 = 1
		TALENT:鼓舞 = 0
		TALENT:尾巴 = 5
		TALENT:魔法使 = 1
		TALENT:兽足 = 5
		TALENT:肤色 = 1
		ABL:Ａ感觉 = 0
		ABL:侍奉精神 = 1
		ABL:施虐倾向 = 1
		EXP:Ａ刺激经验 = 0
		EXP:嗜虐快乐经验 = 0
		EXP:百合经验 = 0
	ELSEIF ARG:1 == 3
		CFLAG:8 = 3
		ABL:欲望 += 1
		ABL:技巧 += 2
		TALENT:性变态 = 1
		TALENT:肤色 = 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ENDIF
;アルケニー系
CASE 18
	IF ARG:1 == 3
		CFLAG:8 = 3
		ABL:欲望 += 1
		ABL:技巧 += 1
		ABL:施虐倾向 += 3
		TALENT:针术 = 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ELSEIF ARG:1 == 4
		CFLAG:8 = 4
		ABL:欲望 += 1
		ABL:技巧 += 1
		ABL:侍奉精神 += 1
		ABL:Ｂ感觉 += 1
		ABL:Ｍ感觉 += 1
		TALENT:针术 = 1
		TALENT:魅惑 = 1
		TALENT:Ｖ敏感 = 0
		TALENT:Ｂ敏感 = 1
		TALENT:鼓舞 = 1
		TALENT:肤色 = 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
		EXP:腰技经验 += 10
	ENDIF
;エンジェル系
CASE 20
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:技巧 += 1
		ABL:Ａ感觉 += 1
		ABL:侍奉精神 -= 1
		TALENT:献身 = 0
		EXP:Ａ经验 = 15
		EXP:侍奉快乐经验 = 10
	ENDIF
;クノイチ系
CASE 22
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:技巧 += 2
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ENDIF
;ヴァンパイア系
CASE 25
	IF ARG:1 == 2
		CFLAG:8 = 2
		ABL:欲望 += 2
		ABL:技巧 += 2
		ABL:百合倾向 += 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ENDIF
;ユニコーン系
CASE 30
	IF ARG:1 == 1
		CFLAG:8 = 1
		ABL:Ａ感觉 = ABL:Ｖ感觉
		ABL:Ｖ感觉 = 0
		TALENT:Ｖ敏感 = 0
		TALENT:Ａ敏感 = 1
		TALENT:好色 = 1
		TALENT:治愈 = 0
		TALENT:鼓舞 = 1
		TALENT:回复术 = 0
		TALENT:精强术 = 1
	ENDIF
;ミミック系
CASE 33
	IF ARG:1 == 4
		CFLAG:8 = 4
		ABL:欲望 += 2
		ABL:技巧 += 2
		ABL:精液上瘾 += 1
		TALENT:梦魔 = 0
		TALENT:上位梦魔 = 1
	ENDIF
CASE 44
	IF ARG:1 == 4
		CFLAG:8 = 4
		MAXBASE:体力 = 2500
		BASE:体力 = MAXBASE:体力
		MAXBASE:气力 = 2200
		BASE:气力 = MAXBASE:气力
		ABL:欲望 += 1
		ABL:技巧 = 8
		ABL:侍奉精神 += 3
		ABL:Ｃ感觉 += 2
		ABL:Ｖ感觉 += 2
		EXP:Ｖ经验 += 50
		EXP:自慰经验 += 5
		EXP:Ｖ性交经验 += 30
		EXP:精液经验 += 20
		EXP:口交经验 += 30
		EXP:手淫经验 += 30
		EXP:足交经验 += 10
		EXP:腰技经验 += 20
		EXP:乳交经验 += 15
		EXP:Ａ刺激经验 += 10
		EXP:舔阴经验 += 10
	ENDIF
ENDSELECT

;鈍感を得たり
IF COND("ユニーク") == 0 && RAND:3 == 0 && ARGS != "アナライズ"
	IF TALENT:Ｃ敏感 == 0 && RAND:10 == 0
		TALENT:Ｃ钝感 = 1
		ABL:Ｃ感觉 = MAX(ABL:Ｃ感觉 - 2, 0)
	ENDIF
	IF TALENT:Ｖ敏感 == 0 && RAND:10 == 0
		TALENT:Ｖ钝感 = 1
		ABL:Ｖ感觉 = MAX(ABL:Ｖ感觉 - 2, 0)
	ENDIF
	IF TALENT:Ａ敏感 == 0 && RAND:10 == 0
		TALENT:Ａ钝感 = 1
		ABL:Ａ感觉 = MAX(ABL:Ａ感觉 - 2, 0)
	ENDIF
	IF TALENT:Ｂ敏感 == 0 && RAND:10 == 0
		TALENT:Ｂ钝感 = 1
		ABL:Ｂ感觉 = MAX(ABL:Ｂ感觉 - 2, 0)
	ENDIF
	IF TALENT:Ｍ敏感 == 0 && RAND:10 == 0
		TALENT:Ｍ钝感 = 1
		ABL:Ｍ感觉 = MAX(ABL:Ｍ感觉 - 2, 0)
	ENDIF
ENDIF

;能力のMAXをチェックする
FOR LOCAL, 0, 20
	ABL:LOCAL = MIN(NUM("最大レベル", LOCAL), ABL:LOCAL)
NEXT

;原作のLVを参考に、経験を増やす
FOR LOCAL, 0, 100
	SIF LOCAL == 2 || LOCAL == 3 || LOCAL == 50
		CONTINUE

	SIF CFLAG:8
		EXP:LOCAL *= CFLAG:8
	;所持している経験の揺らぎ
	SIF EXP:LOCAL && ARGS != "アナライズ"
		EXP:LOCAL = MAX(MULTIPLY(EXP:LOCAL, 50 + (RAND:6 + RAND:6)*10), 1)
NEXT
EXP:Ｖ经验 = MAX(EXP:Ｖ经验, EXP:Ｖ性交经验, EXP:Ｖ扩张经验)
EXP:Ａ经验 = MAX(EXP:Ａ经验, EXP:Ａ性交经验, EXP:Ａ扩张经验)

;処女？
IF TALENT:处女
	TALENT:生产经历 = 0
	ABL:Ｖ感觉 = 0
	EXP:Ｖ经验 = 0
	EXP:Ｖ性交经验 = 0
	EXP:腰技经验 = 0
	EXP:Ｖ扩张经验 = 0
	BASE:胎儿的父亲 = 0
ELSEIF TALENT:男性 == 0
	SIF EXP:Ｖ经验 == 0
		EXP:Ｖ经验 = 1
ENDIF
;童貞？
IF TALENT:童贞
	EXP:Ｖ插入经验 = 0
ELSEIF PENIS(TARGET)
	SIF EXP:Ｖ插入经验 == 0
		EXP:Ｖ插入经验 = 1
ENDIF

;拡張適正はレア素質
SIF RAND:10 == 0
	TALENT:扩张补正 = 1

;拡張度の上限に応じて拡張度にぶれがある（上限側になりやすい）
IF CONFIG("ＶＡ詳細表示：手動ＬＶＵＰ")
	LOCAL = CALCF("Ｖ拡張の上限", TARGET)
	SIF LOCAL > ABL:Ｖ扩张
		ABL:Ｖ扩张 = LIMIT(RAND:LOCAL + RAND:2 + RAND:2, ABL:Ｖ扩张, LOCAL)

	LOCAL = CALCF("Ａ拡張の上限", TARGET)
	SIF LOCAL > ABL:Ａ扩张
		ABL:Ａ扩张 = LIMIT(RAND:LOCAL + RAND:2 + RAND:2, ABL:Ａ扩张, LOCAL)
ELSE
	ABL:Ｖ扩张 = CALCF("Ｖ拡張の上限", TARGET)
	ABL:Ａ扩张 = CALCF("Ａ拡張の上限", TARGET)
ENDIF

;わーい母乳が出るぞ！（レア素質）
SIF TALENT:男性 == 0 && TALENT:贫乳 == 0 && RAND:10 + (TALENT:巨乳 + 1)/2 >= 9
	TALENT:母乳体质 = 1

;精液に弱いのも良いですよね
IF TALENT:弱精液 == 0 && ABL:精液上瘾 > RAND:5
	IF EXP:口交经验 && RANDIF(1, EXP:Ｖ性交经验, EXP:Ａ性交经验)
		TALENT:弱精液 = 1p0
	ELSEIF EXP:Ｖ性交经验 && RANDIF(1, EXP:Ａ性交经验)
		TALENT:弱精液 = 1p1
	ELSEIF EXP:Ａ性交经验
		TALENT:弱精液 = 1p2
	ENDIF
ENDIF

;フェラ経験豊富だと嘔吐反射耐性が有るかも
SIF EXP:口交经验 + EXP:喉性交经验 >= MAX(50, RAND:300)
	TALENT:呕吐反射耐性 = 1
;喉性感
SIF TALENT:Ｍ敏感*3 + ABL:Ｍ感觉 + GET_EXP_LV(EXP:口交经验) + GET_EXP_LV(EXP:喉性交经验) >= RAND:20
	TALENT:喉性感 = 1
;子宮性感
SIF TALENT:男性 == 0 && TALENT:Ｖ敏感*3 + ABL:Ｖ感觉 + GET_EXP_LV(EXP:Ｖ经验) + GET_EXP_LV(EXP:Ｖ性交经验) >= RAND:20
	TALENT:子宫性感 = 1
;尿道性感
SIF TALENT:性变态 + RAND:10 >= 9
	TALENT:尿道性感 = 1
;においに弱い
IF (TALENT:兽耳 > 0)*3 + (TALENT:污臭敏感 - TALENT:污臭钝感)*2 + TALENT:盲信 + (ABL:欲望 + 1)/2 + RAND:10 >= 9
	TALENT:弱气味 = 1
	TALENT:污臭钝感 = 0
ENDIF

CALL SETFLAG, "属性", TARGET

@SHOW_DIFFICULTY
PRINT 難易度：
;難易度条件
LOCAL = 0
;恭順と欲情の倍率が低い
SIF CUP:恭顺 < 800 && CUP:欲情 < 800
	LOCAL += 1
;恭順+欲情、または屈服の倍率が著しく低い
SIF (CUP:恭顺 < 500 && CUP:欲情 < 500) || CUP:屈服 < 500
	LOCAL += 1
;反感の倍率が高い
SIF CUP:反感 > 1300
	LOCAL += 1
;抑鬱の値が著しく高い
SIF CUP:抑郁 > 10000
	LOCAL += 1
;弱点が(あるとして)Ａのみ
SIF TALENT:Ｃ敏感 + TALENT:Ｖ敏感 + TALENT:Ｂ敏感 + TALENT:Ｍ敏感 == 0
	LOCAL += 1
;実行値補正を算出
TFLAG:36 = 0
CALL COM_ORDER, 1
;実行値補正が殆ど無い
SIF RESULT <= 0
	LOCAL += 1
;体力か気力が低い
SIF MAXBASE:体力 <= 1500 || MAXBASE:气力 <= 1000
	LOCAL += 1
;技巧が高い
LOCAL += MAX(ABL:技巧 - 3, 0)
;気丈or一線越えない
SIF TALENT:刚强 || TALENT:底线明确
	LOCAL += 1
;抑圧
SIF TALENT:压抑
	LOCAL += 1
;快感の否定
SIF TALENT:否认快感
	LOCAL += 1
;妖精である
SIF TALENT:妖精
	LOCAL += 1
;処女で、貞操観念が高い
SIF TALENT:处女 && TALENT:看重贞操
	LOCAL += 1
;夢魔でない（人間、上位夢魔、魔王の欠片 は夢魔よりも難易度が高い）
SIF TALENT:梦魔 == 0
	LOCAL += 1
;エスト
SIF NO:TARGET == 48
	LOCAL += 2

;難易度算出
IF LOCAL <= 0
	PRINT Ｅ
ELSEIF LOCAL == 1
	PRINT Ｄ
ELSEIF LOCAL == 2
	PRINT Ｃ
ELSEIF LOCAL == 3
	PRINT Ｂ
ELSEIF LOCAL == 4
	PRINT Ａ
ELSEIF LOCAL == 5
	PRINT Ｓ
ELSE
	PRINT ＳＳ
ENDIF

LOCAL:1 = (ABL:ＬＶ - FLAG:255)/5

SIF CONFIG("ＥＡＳＹ")
	LOCAL:1 /= 2

IF LIMIT(LOCAL, 0, 6) != LIMIT(LOCAL + LOCAL:1, 0, 6)
	PRINTFORM \@ LOCAL:1 > 0 ? ＋ # － \@(LV差)＝

	LOCAL += LOCAL:1

	IF LOCAL <= 0
		PRINT Ｅ
	ELSEIF LOCAL == 1
		PRINT Ｄ
	ELSEIF LOCAL == 2
		PRINT Ｃ
	ELSEIF LOCAL == 3
		PRINT Ｂ
	ELSEIF LOCAL == 4
		PRINT Ａ
	ELSEIF LOCAL == 5
		PRINT Ｓ
	ELSE
		PRINT ＳＳ
	ENDIF
ENDIF

@SHOW_ABL_ASSI
PRINTFORM %TEXTS("助手の名称")%能力：
LOCAL = 0
;解放
SIF TALENT:奔放
	LOCAL += 2
;針さばき
SIF TALENT:针术
	LOCAL += 1
;調合知識
SIF TALENT:调合知识
	LOCAL += 2
;両刀
SIF TALENT:双性恋
	LOCAL += 1
;サド
SIF TALENT:抖Ｓ
	LOCAL += 1
;妖狐
SIF TALENT:妖狐
	LOCAL += 1
;魅惑
SIF TALENT:魅惑
	LOCAL += 1
;謎の魅力
SIF TALENT:谜之魅力
	LOCAL += 2
;治療
SIF TALENT:治愈
	LOCAL += 1
;ふたなり
SIF TALENT:扶她
	LOCAL += 1
;禁断の知識
SIF TALENT:禁忌知识
	LOCAL += 1
;魔法使い
SIF TALENT:魔法使
	LOCAL += 2
;ラミア
SIF TALENT:拉弥亚
	LOCAL += 1
;旧支配者の名残
SIF TALENT:旧支配者的末裔
	LOCAL += 1
;ドラグーン
SIF TALENT:龙骑兵
	LOCAL += 1
;スライム
SIF TALENT:史莱姆
	LOCAL += 1
;☆甘い言葉
SIF TALENT:甜言蜜语
	LOCAL += 2
;歌唱技能
SIF TALENT:歌唱技能
	LOCAL += 2
;上位夢魔
SIF TALENT:上位梦魔
	LOCAL += 1

IF LOCAL <= 0
	PRINT Ｅ
ELSEIF LOCAL == 1
	PRINT Ｄ
ELSEIF LOCAL == 2
	PRINT Ｃ
ELSEIF LOCAL == 3
	PRINT Ｂ
ELSEIF LOCAL == 4
	PRINT Ａ
ELSEIF LOCAL == 5
	PRINT Ｓ
ELSE
	PRINT ＳＳ
ENDIF

PRINT 　
