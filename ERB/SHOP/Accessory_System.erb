;アクセサリショップ『おぼろげ屋』
;FLAG:300番台を使用。下一桁によって用途を分ける。
;0	アクセサリの種類と名前
;1～5	効果
;8	特殊フラグ
;0bit 紋白蝶で使用
;9	レベル
@ACCESSORY_SHOP
DRAWLINE
PRINTL  □ 饰品店『胧屋』 □
DRAWLINE
PRINTL 「喔喔，欢迎光临。
PRINTL 　虽然敝店看起来什么都不卖。
PRINTW 　但还是很受大家欢迎哦。」
CALL ACCESSORY_SHOP_BUY

@ACCESSORY_SHOP_BUY
PRINTL
PRINTFORML 要买什么呢？　(金钱：%MONEY_C(MONEY)%)
SIF NUM("未鑑定のアクセサリ")
	PRINTFORML 还是说你想要鉴定饰品？（饰品鉴定收费：$100/次）
PRINTFORML  [ 0] %TEXT_LJ("朦胧手镯", 20)% 精神力重视（$2,000）\@ ITEM:朦胧手镯 ? 未鉴定：{ITEM:朦胧手镯, 2}个 # \@
PRINTFORML  [ 1] %TEXT_LJ("朦胧袖饰", 20)% 魅力重视　（$2,000）\@ ITEM:朦胧袖饰 ? 未鉴定：{ITEM:朦胧袖饰, 2}个 # \@
PRINTFORML  [ 2] %TEXT_LJ("朦胧手套", 20)% 技巧重视　（$2,000）\@ ITEM:朦胧手套 ? 未鉴定：{ITEM:朦胧手套, 2}个 # \@
PRINTFORML  [ 3] %TEXT_LJ("朦胧绶带", 20)% 精力重视　（$2,000）\@ ITEM:朦胧绶带 ? 未鉴定：{ITEM:朦胧绶带, 2}个 # \@
PRINTFORML  [ 4] %TEXT_LJ("朦胧指环", 20)% 效果数重视（$5,000）\@ ITEM:朦胧指环 ? 未鉴定：{ITEM:朦胧指环, 2}个 # \@
SIF ITEM:朦胧宝石
	PRINTFORML  [ 5] %TEXT_LJ("朦胧宝石", 20)% 　　　　　　　　　　\@ ITEM:朦胧宝石 ? 未鉴定：{ITEM:朦胧宝石, 2}个 # \@

PRINTFORML  [10] %TEXT_LJ("定做饰品", 20)% 　　　　　（$5,000～）
IF NUM("所持アクセサリ")
	PRINTFORML  [20] %TEXT_LJ("购买砥砺思维之石", 20)% ＬＶＵＰ！（$5,000）
	PRINTFORML  [\@ ITEM:砥砺思维之石 ? 80 # × \@] %TEXT_LJ("使用砥砺思维之石", 20)%（{ITEM:砥砺思维之石, 2}个）
	PRINTFORML  [\@ ITEM:灼烧思考之炎 ? 81 # × \@] %TEXT_LJ("使用灼烧思考之炎", 20)%（{ITEM:灼烧思考之炎, 2}个）
	PRINTFORML  [\@ ITEM:纹白蝶 && NUM("所持アクセサリ") >= 2 ? 82 # × \@] %TEXT_LJ("使用纹白蝶", 20)%（{ITEM:纹白蝶, 2}只）
	PRINTFORML  [90] %TEXT_LJ("装备饰品", 20)%（在查看MASTER能力时，也可以装卸/替换饰品）
ENDIF
PRINTFORML [100] 离开店铺

DRAWLINE

$INPUT_LOOP
INPUT

;アクセサリを限界まで持っていると購入できない
IF RESULT == 100
	CALL ACCESSORY_SET_TALENT
	PRINTW 「要再来哟」
	RETURN 0
;アクセサリに付加される効果を決定
ELSEIF RESULT >= 0 && RESULT <= 4
	;IF ACCESSORY_BUY()
		;鑑定
		IF ITEM:(320+RESULT)
			LOCAL = 100
		ELSE
			LOCAL = ITEMPRICE:(320+RESULT)
		ENDIF
		;所持金が足りないと購入できない
		IF MONEY < LOCAL
			PRINTW 「喂喂，你的钱好像不太够哦？」
		ELSE
			;鑑定なら未鑑定のアクセサリ数を減らす
			IF ITEM:(320+RESULT)
				CALL ACCESSORY_IDENTIFY, RESULT*10, "鑑定"
				IF RESULT != 999
					MONEY -= LOCAL
					ITEM:(320+RESULT) -= 1
				ENDIF
				PRINTFORML {RESULT}
			ELSE
				CALL ACCESSORY_IDENTIFY, RESULT*10
				IF RESULT != 999
					MONEY -= LOCAL
					ITEM:(320+RESULT) -= 1
				ENDIF
				PRINTFORML {RESULT}
			ENDIF
		ENDIF
	;ELSE
		;PRINTW 「もう持てないみたいだぜ」
	;ENDIF
;鑑定
ELSEIF RESULT == 5 && ITEM:朦胧宝石
	;IF ACCESSORY_BUY()
		;所持金が足りないと購入できない
		IF MONEY < 100
			PRINTW 「喂喂，你的钱好像不太够哦？」
		ELSE
			CALL ACCESSORY_IDENTIFY, RESULT*10, "鑑定"
			IF RESULT != 999
				MONEY -= 100
				;未鑑定のアクセサリ数を減らす
				ITEM:325 -= 1
			ENDIF
		ENDIF
	;ELSE
		;PRINTW 「もう持てないみたいだぜ」
	;ENDIF
ELSEIF RESULT == 10
	;IF ACCESSORY_BUY()
		CALL ACCESSORY_ORDERMADE_MENU
	;ELSE
		;PRINTW 「悪いが、もう持てないみたいだぜ」
	;ENDIF
ELSEIF RESULT == 20 && NUM("所持アクセサリ")
	IF MONEY < ITEMPRICE:砥砺思维之石
		PRINTW 「喂喂，你的钱好像不太够哦？」
	ELSE
		PRINTFORML 「要买几个呢？」
		CALL SHOW_BUY_ITEM, 96
		IF RESULT == 0
			PRINTW 「什么嘛，一个都不买吗」
		ELSE
			PRINTFORMW 「{RESULT}个就是%MONEY_C(ITEMPRICE:砥砺思维之石 * RESULT)%，谢谢回顾。请小心使用哦」
		ENDIF
	ENDIF
ELSEIF RESULT == 80 && ITEM:砥砺思维之石 && NUM("所持アクセサリ")
	CALL ACCESSORY_LVUP
ELSEIF RESULT == 81 && ITEM:灼烧思考之炎 && NUM("所持アクセサリ")
	CALL ACCESSORY_FIRE
ELSEIF RESULT == 82 && ITEM:纹白蝶 && NUM("所持アクセサリ") >= 2
	CALL ACCESSORY_PIERISRAPAE
ELSEIF RESULT == 90 && NUM("所持アクセサリ")
	CALL ACCESSORY_SELECT
ELSE
	GOTO INPUT_LOOP
ENDIF

RESTART

;-------------------------------------------------
;アクセサリーの種類と性能を記録する
;ARGには0から10刻みで値が代入されている
;ARGSには"鑑定"などが入る
;-------------------------------------------------
@ACCESSORY_IDENTIFY, ARG, ARGS
#DIM LCOUNT
;まずは何個目かを判定

IF ACCESSORY_COUNT() == 8
	PRINTFORML 你的饰品盒已经放满了。
	RETURN 999
ENDIF

FOR LCOUNT, 1, 9
	IF FLAG:(300 + LCOUNT*10) == 0
		LOCAL = LCOUNT
		BREAK
	ENDIF
NEXT

SELECTCASE ARGS
;拾ってきたものの場合ＬＶが高いことがある
CASE "鑑定"
	SELECTCASE RAND:10
	CASE 0 TO 5
	CASE 6
		FLAG:(300 + LOCAL*10 + 9) += 1
	CASE 7
		FLAG:(300 + LOCAL*10 + 9) += 2
	CASE 8
		FLAG:(300 + LOCAL*10 + 9) += 3 + RAND:4
	CASEELSE
		FLAG:(300 + LOCAL*10 + 9) += 4 + RAND:6
	ENDSELECT
ENDSELECT

;LOCAL個めのアクセサリの形状と修飾語を記録
SELECTCASE ARG
CASE 0
	LOCALS = 腕輪
	FLAG:(300 + LOCAL*10) = ARG + RAND:4 + 1
CASE 10
	LOCALS = 袖飾り
	FLAG:(300 + LOCAL*10) = ARG + RAND:4 + 1
CASE 20
	LOCALS = 手袋
	FLAG:(300 + LOCAL*10) = ARG + RAND:3 + 1
CASE 30
	LOCALS = 飾り帯
	FLAG:(300 + LOCAL*10) = ARG + RAND:3 + 1
;指輪は必ず２個以上のボーナスを持つ
CASE 40
	LOCALS = 指輪
	FLAG:(300 + LOCAL*10) = ARG + RAND:2 + 1
CASE 50
	LOCALS = 宝石
	FLAG:(300 + LOCAL*10) = ARG
	;ＬＶは必ず１
	FLAG:(300 + LOCAL*10 + 9) = 0
ENDSELECT

PRINTFORMW おぼろげな%LOCALS%の正体は……

;能力をセット
FOR LCOUNT, 1, 6
	;２個目以降の性能は低確率
	SELECTCASE FLAG:(300 + LOCAL*10)/10
	;竜石
	CASE 5
		BREAK
	;指輪
	CASE 4
		SIF LCOUNT >= 3 && RAND:(LCOUNT - 1)
			BREAK
	;それ以外
	CASEELSE
		SIF LCOUNT >= 2 && RAND:LCOUNT
			BREAK
	ENDSELECT

	FLAG:(300 + LOCAL*10 + LCOUNT) = ACCESSORY_ABL()
NEXT

;アクセサリの詳細表示
CALL PRINT_ACCESSORY, LOCAL

PRINTW だった！


;-------------------------------------------------
;ARG番目のアクセサリーを表示する
;-------------------------------------------------
@PRINT_ACCESSORY, ARG
#DIM LCOUNT
;ブレスレット	魅力+5 精神力+25		魅力+5 精神力+10
;カフス 	魅力+25 精神力+5		魅力+10 精神力+5
;ガントレット	魅力+15 精神力+5 技巧+15	魅力+5 精神力+5 技巧+5
;サッシュ	魅力+15 精神力+5 精力+15	魅力+5 精神力+5 精力+5
;指輪		全ステータス+5			全ステータス+5
;竜石		全ステータス+25			全ステータス+10

LOCAL = 300 + ARG*10
SIF FLAG:LOCAL == 0
	RETURN 0

PRINTFORM  [{ARG, 2}] %PRINT_ACCESSORY_NAME(FLAG:LOCAL)% ＬＶ{FLAG:(LOCAL + 9) + 1, 2}
IF FLAG:399 == ARG
	PRINT [
	CALL PRINT_COLORTEXT, "Ｅ", DEF_COLOR("黄色")
	PRINT ]
ENDIF
;紋白蝶での合成ベース
IF GETBIT(FLAG:(LOCAL + 8), 0)
	PRINT [
	CALL PRINT_COLORTEXT, "合成ベース", DEF_COLOR("黄色")
	PRINT ]
ENDIF
PRINTL

;アクセサリのＬＶ
LOCAL = FLAG:(300 + ARG*10 + 9)

SELECTCASE FLAG:(300 + ARG*10)/10
CASE 0
	PRINTFORML 　　　精神力＋{25 + LOCAL * 10}％　魅力＋{5 + LOCAL * 5}％
CASE 1
	PRINTFORML 　　　精神力＋{5 + LOCAL * 5}％　魅力＋{25 + LOCAL * 10}％
CASE 2
	PRINTFORML 　　　技巧＋{15 + LOCAL * 5}％　精神力＋{5 + LOCAL * 5}％　魅力＋{15 + LOCAL * 5}％
CASE 3
	PRINTFORML 　　　精力＋{15 + LOCAL * 5}％　精神力＋{5 + LOCAL * 5}％　魅力＋{15 + LOCAL * 5}％
CASE 4
	PRINTFORML 　　　技巧＆精力＆精神力＆魅力＋{5 + LOCAL * 5}％
CASE 5
	PRINTFORML 　　　技巧＆精力＆精神力＆魅力＋{25 + LOCAL * 10}％
ENDSELECT

SETCOLOR DEF_COLOR("黄色")
FOR LCOUNT, 1, 6
	SIF FLAG:(300 + ARG*10 + LCOUNT)
		PRINTFORML %BL(5)% %ACCESSORY_EFFECT(FLAG:(300 + ARG*10 + LCOUNT) )%
NEXT
RESETCOLOR


@ACCESSORY_LVUP
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0
DRAWLINE
PRINTL
PRINTFORML ＬＶの上限は30です。
PRINTFORML どのアクセサリに使用しますか？　(砥砺思维之石：{ITEM:砥砺思维之石}個)

;全てのアクセサリを羅列
FOR LOCAL, 1, 9
	CALL PRINT_ACCESSORY, LOCAL
NEXT

;思考砥ぎの石が無い
IF ITEM:砥砺思维之石 <= 0
	PRINTL
	PRINTW 砥砺思维之石が無くなったので戻ります
	CALL ACCESSORY_EQUIP
	REDRAW 1
	RETURN 0
ENDIF

PRINTL [100] もどる

$INPUT_LOOP
INPUT

IF RESULT == 100
	CALL ACCESSORY_EQUIP
	REDRAW 1
	RETURN 0
ELSEIF 1 <= RESULT && RESULT <= 8 && FLAG:(300 + RESULT*10)
	IF FLAG:(300 + RESULT*10 + 9) <= 28
		ITEM:砥砺思维之石 -= 1
		FLAG:(300 + RESULT*10 + 9) += 1
	ELSE
		PRINTW 既にＬＶがＭＡＸです。
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@ACCESSORY_FIRE
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT
REDRAW 0

DRAWLINE
PRINTL
PRINTFORM アクセサリの
CALL PRINT_COLORTEXT, "この色", DEF_COLOR("黄色")
PRINTFORML の効果を上から一つ削ります。
PRINTFORML どのアクセサリに使用しますか？　(灼烧思考之炎：{ITEM:灼烧思考之炎}個)

;全てのアクセサリを羅列
FOR LOCAL, 1, 9
	CALL PRINT_ACCESSORY, LOCAL
NEXT

;思考焼きの炎が無い
IF ITEM:灼烧思考之炎 <= 0
	PRINTL
	PRINTW 思考焼きの炎が無くなったので戻ります
	CALL ACCESSORY_EQUIP
	REDRAW 1
	RETURN 0
ENDIF

PRINTL [100] もどる

$INPUT_LOOP
INPUT

IF RESULT == 100
	CALL ACCESSORY_EQUIP
	REDRAW 1
	RETURN 0
ELSEIF 1 <= RESULT && RESULT <= 8 && FLAG:(300 + RESULT*10)
	IF FLAG:(300 + RESULT*10 + 1)
		ITEM:灼烧思考之炎 -= 1
		;効果を一個づつずらして詰める
		FLAG:(300 + RESULT*10 + 1) = 0
		FOR LOCAL, 1, 5
			SWAP FLAG:(300 + RESULT*10 + LOCAL), FLAG:(300 + RESULT*10 + LOCAL + 1)
		NEXT
	ELSE
		PRINTW 能力が無いので削れません。
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART


@ACCESSORY_PIERISRAPAE
#DIM LCOUNT, 2
#DIM CHOICE, 3
#DIM MEMO_LINECOUNT

VARSET CHOICE
MEMO_LINECOUNT = LINECOUNT
REDRAW 0

DRAWLINE
PRINTL
PRINTFORML 二つのアクセサリを一つにします。
PRINTFORML 以下から二つ選んでください。　(紋白蝶：{ITEM:纹白蝶}頭)
PRINTFORML （最初に選んだものがベースとなります）

;全てのアクセサリを羅列
FOR LCOUNT, 1, 9
	CALL PRINT_ACCESSORY, LCOUNT
NEXT

;アクセサリが一つしか無い
IF NUM("所持アクセサリ") <= 1
	PRINTL
	PRINTW アクセサリが一つしか無いので戻ります
	FOR LCOUNT, 1, 9
		;紋白蝶フラグ消去
		CLEARBIT FLAG:(300 + LCOUNT*10 + 8), 0
	NEXT
	CALL ACCESSORY_EQUIP
	REDRAW 1
	RETURN 0
ENDIF
PRINTL [100] もどる

$INPUT_LOOP
INPUT

IF RESULT == 100
	FOR LCOUNT, 1, 9
		;紋白蝶フラグ消去
		CLEARBIT FLAG:(300 + LCOUNT*10 + 8), 0
	NEXT
	CALL ACCESSORY_EQUIP
	REDRAW 1
	RETURN 0
ELSEIF 1 <= RESULT && RESULT <= 8 && FLAG:(300 + RESULT*10)
	CHOICE:2 = RESULT
	INVERTBIT FLAG:(300 + CHOICE:2*10 + 8), 0
	;合成開始？
	FOR LCOUNT, 1, 9
		SIF GETBIT(FLAG:(300 + LCOUNT*10 + 8), 0) == 0
			CONTINUE
		SIF LCOUNT != CHOICE:2
			CHOICE:1 = LCOUNT
	NEXT
	;２個選んでいるなら合成開始
	IF CHOICE:1 && CHOICE:2
		PRINTL
		CALL PRINT_ACCESSORY, CHOICE:1
		PRINTL に

		;紋白蝶フラグ消去
		CLEARBIT FLAG:(300 + CHOICE:2*10 + 8), 0
		CALL PRINT_ACCESSORY, CHOICE:2
		PRINTL を合成した結果、

		ITEM:纹白蝶 -= 1
		;ＬＶＵＰ
		FLAG:(300 + CHOICE:1*10 + 9) += (FLAG:(300 + CHOICE:2*10 + 9) + 1)/2
		SIF FLAG:(300 + CHOICE:1*10 + 9) > 9
			FLAG:(300 + CHOICE:1*10 + 9) = 9
		;効果を移動
		FOR LCOUNT, 1, 6
			SIF FLAG:(300 + CHOICE:1*10 + LCOUNT)
				CONTINUE
			FOR LCOUNT:1, 0, 5
				SIF LCOUNT + LCOUNT:1 > 5
					BREAK
				FLAG:(300 + CHOICE:1*10 + LCOUNT + LCOUNT:1) = FLAG:(300 + CHOICE:2*10 + 1 + LCOUNT:1)
			NEXT
			BREAK
		NEXT

		;紋白蝶フラグ消去
		CLEARBIT FLAG:(300 + CHOICE:1*10 + 8), 0

		CALL PRINT_ACCESSORY, CHOICE:1
		PRINTFORMW となりました。

		;合成されたアクセサリを無くす
		IF FLAG:399 == CHOICE:2
			FLAG:399 = CHOICE:1
			CALL ACCESSORY_EQUIP
		ELSEIF FLAG:399 > CHOICE:2
			FLAG:399 -= 1
		ENDIF
		;FLAGリセット
		VARSET FLAG, 0, 300 + CHOICE:2*10, 310 + CHOICE:2*10
		;番号をつめる
		FOR LCOUNT, 0, 8
			LOCAL = 300 + CHOICE:2*10 + LCOUNT*10

			SIF LOCAL >= 380
				BREAK

			FOR LCOUNT:1, 0, 10
				SWAP FLAG:(LOCAL + LCOUNT:1), FLAG:(LOCAL + 10 + LCOUNT:1)
			NEXT
		NEXT
	ELSE
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		RESTART
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

RESTART


@ACCESSORY_SELECT
#DIM MEMO_LINECOUNT
;アクセサリを全部捨てた
SIF NUM("所持アクセサリ") == 0
	RETURN 0

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

DRAWLINE
PRINTL
PRINTL どのアクセサリを装備しますか？
PRINTL 既に装備しているものを選択すると外します。

;全てのアクセサリを羅列
FOR LOCAL, 1, 9
	CALL PRINT_ACCESSORY, LOCAL
NEXT

PRINTFORML  [\@ ITEM:砥砺思维之石 ? 80 # × \@] 砥砺思维之石を使う（{ITEM:砥砺思维之石, 2}個）
PRINTFORML  [\@ ITEM:灼烧思考之炎 ? 81 # × \@] 灼烧思考之炎を使う（{ITEM:灼烧思考之炎, 2}個）
PRINTFORML  [\@ ITEM:纹白蝶 && NUM("所持アクセサリ") >= 2 ? 82 # × \@] 紋白蝶を使う　　　（{ITEM:纹白蝶, 2}頭）
PRINTL  [98] アクセサリを捨てる
PRINTL  [99] アクセサリの効果一覧表を見る
PRINTL [100] もどる

$INPUT_LOOP
INPUT

IF RESULT == 100
	CALL ACCESSORY_SET_TALENT
	REDRAW 1
	RETURN 0
ELSEIF 1 <= RESULT && RESULT <= 8 && FLAG:(300 + RESULT*10)
	IF FLAG:399 == RESULT
		CALL ACCESSORY_EQUIP, "外す"
	ELSE
		FLAG:399 = RESULT
		CALL ACCESSORY_EQUIP
	ENDIF
ELSEIF RESULT == 80 && ITEM:砥砺思维之石
	CALL ACCESSORY_LVUP
ELSEIF RESULT == 81 && ITEM:灼烧思考之炎
	CALL ACCESSORY_FIRE
ELSEIF RESULT == 82 && ITEM:纹白蝶 && NUM("所持アクセサリ") >= 2
	CALL ACCESSORY_PIERISRAPAE
ELSEIF RESULT == 98
	CALL ACCESSORY_TRASH
ELSEIF RESULT == 99
	CALL ACCESSORY_HELP
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART


@ACCESSORY_TRASH
#DIM LCOUNT, 2
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT
REDRAW 0

DRAWLINE
PRINTL
PRINTL どのアクセサリを捨てますか？

;全てのアクセサリを羅列
FOR LOCAL, 1, 9
	CALL PRINT_ACCESSORY, LOCAL
NEXT

PRINTL [100] もどる

$INPUT_LOOP
INPUT

IF RESULT == 100
	CALL ACCESSORY_SET_TALENT
	REDRAW 1
	RETURN 0
ELSEIF 1 <= RESULT && RESULT <= 8 && FLAG:(300 + RESULT*10)
	;装備しているものを捨てたなら
	IF FLAG:399 == RESULT
		CALL ACCESSORY_EQUIP, "外す"
	ELSEIF FLAG:399 > RESULT
		FLAG:399 -= 1
	ENDIF

	;捨てるもののFLAGリセット
	VARSET FLAG, 0, 300 + RESULT*10, 310 + RESULT*10
	;番号をつめる
	FOR LCOUNT, 0, 8
		LOCAL = 300 + RESULT*10 + LCOUNT*10

		SIF LOCAL >= 380
			BREAK

		FOR LCOUNT:1, 0, 10
			SWAP FLAG:(LOCAL + LCOUNT:1), FLAG:(LOCAL + 10 + LCOUNT:1)
		NEXT
	NEXT
	;アクセサリを全部捨てた
	IF NUM("所持アクセサリ") == 0
		REDRAW 1
		RETURN 0
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@ACCESSORY_HELP
PRINTL ～アクセサリ付加効果一覧～
PRINTL
PRINTL 　ＥＰ　＋○％　　　　射精ゲージのＭＡＸが○％増加
PRINTL 　精力　＋○％　　　　精力のＭＡＸが○％増加
PRINTL 　技巧　＋○％　　　　与える快感等が○％増加
PRINTL 　精神力＋○％　　　　受ける快感＆恍惚率等が○％減少
PRINTL 　魅力　＋○％　　　　相手に与える不快感が○％減少＆20%以上なら好感度＋１
PRINTL 　ストック増加　　　　ストックを１本増加（重複しません）
PRINTL 　(部位)強化＋○％　　対応する場所に与える快感が○％増加
PRINTL 　ムード＋○　　　　　ムードの上昇が○増加
PRINTL 　実行値＋○　　　　　コマンド実行値が○増加
PRINTL 　受ける快感＋○％　　受ける快感等が○％増加
PRINTL 　押し倒され＋○％　　押し倒される確率が○％増加
PRINTL 　取得経験値＋○％　　装備者が獲得する経験値が○％増加
PRINTL 　常に(状態名)　　　　常に対応した状態のままになる
PRINTL 　隠密＋○％　　　　　探索において、敵に出会う確率が○％減少
PRINTL 　宝箱出現率＋○％　　探索において、宝箱を見つける確率が○％増加
PRINTL 　ドア発見率＋○％　　探索において、ドアを見つける確率が○％増加
WAIT


@ACCESSORY_EQUIP, ARGS
;ARGS == "外す" なら装備をはずす処理
SELECTCASE ARGS
CASE "外す"
	FLAG:399 = 0
	VARSET FLAG, 0, 300, 310
CASEELSE
	FOR LOCAL, 0, 10
		FLAG:(300 + LOCAL) = FLAG:(300 + FLAG:399*10 + LOCAL)
	NEXT
ENDSELECT

@ACCESSORY_SET_TALENT
#DIM LCOUNT
;ブレスレット	魅力+5 精神力+25		魅力+5 精神力+10
;カフス 	魅力+25 精神力+5		魅力+10 精神力+5
;ガントレット	魅力+15 精神力+5 技巧+15	魅力+5 精神力+5 技巧+5
;サッシュ	魅力+15 精神力+5 精力+15	魅力+5 精神力+5 精力+5
;指輪		全ステータス+5			全ステータス+5
;竜石		全ステータス+25			全ステータス+10

VARSET TALENT:MASTER:0, 0, 500, 600

IF FLAG:399 == 0
	CALL ACCESSORY_EQUIP, "外す"
	RETURN 0
ELSE
	CALL ACCESSORY_EQUIP
ENDIF

FOR LCOUNT, 1, 6
	LOCAL = 300 + LCOUNT

	SIF FLAG:LOCAL/1000 == 0
		CONTINUE

	TALENT:MASTER:(500 + FLAG:LOCAL/1000 ) += MAX(FLAG:LOCAL%1000, 1)
NEXT

;常にミニマムと常にエンラジで相殺
IF TALENT:MASTER:小根术常态化 && TALENT:MASTER:大根术常态化
	TALENT:MASTER:小根术常态化 = 0
	TALENT:MASTER:大根术常态化 = 0
ENDIF

SELECTCASE (FLAG:300 / 10)
CASE 0
	TALENT:MASTER:精神力强化 += 25 + FLAG:309 * 10
	TALENT:MASTER:魅力强化 += 5 + FLAG:309 * 5
CASE 1
	TALENT:MASTER:精神力强化 += 5 + FLAG:309 * 5
	TALENT:MASTER:魅力强化 += 25 + FLAG:309 * 10
CASE 2
	TALENT:MASTER:技巧强化 += 15 + FLAG:309 * 5
	TALENT:MASTER:精神力强化 += 5 + FLAG:309 * 5
	TALENT:MASTER:魅力强化 += 15 + FLAG:309 * 5
CASE 3
	TALENT:MASTER:精力强化 += 15 + FLAG:309 * 5
	TALENT:MASTER:精神力强化 += 5 + FLAG:309 * 5
	TALENT:MASTER:魅力强化 += 15 + FLAG:309 * 5
CASE 4
	TALENT:MASTER:精力强化 += 5 + FLAG:309 * 5
	TALENT:MASTER:技巧强化 += 5 + FLAG:309 * 5
	TALENT:MASTER:精神力强化 += 5 + FLAG:309 * 5
	TALENT:MASTER:魅力强化 += 5 + FLAG:309 * 5
CASE 5
	TALENT:MASTER:精力强化 += 25 + FLAG:309 * 10
	TALENT:MASTER:技巧强化 += 25 + FLAG:309 * 10
	TALENT:MASTER:精神力强化 += 25 + FLAG:309 * 10
	TALENT:MASTER:魅力强化 += 25 + FLAG:309 * 10
ENDSELECT


@PRINT_ACCESSORY_NAME(ARG)
#FUNCTIONS
SELECTCASE ARG/10
CASE 0
	SELECTCASE ARG%10
	CASE 1
		LOCALS = 象牙のブレスレット
	CASE 2
		LOCALS = 青銅のブレスレット
	CASE 3
		LOCALS = 白銀のブレスレット
	CASE 4
		LOCALS = 霊銀のブレスレット
	ENDSELECT
CASE 1
	SELECTCASE ARG%10
	CASE 1
		LOCALS = コーラルカフス
	CASE 2
		LOCALS = フォシルカフス
	CASE 3
		LOCALS = ジェイドカフス
	CASE 4
		LOCALS = アンバーカフス
	ENDSELECT
CASE 2
	SELECTCASE ARG%10
	CASE 1
		LOCALS = シルクガントレット
	CASE 2
		LOCALS = サテンガントレット
	CASE 3
		LOCALS = ビロードガントレット
	ENDSELECT
CASE 3
	SELECTCASE ARG%10
	CASE 1
		LOCALS = 牡馬のサッシュ
	CASE 2
		LOCALS = 牡鹿のサッシュ
	CASE 3
		LOCALS = 雄兎のサッシュ
	ENDSELECT
CASE 4
	SELECTCASE ARG%10
	CASE 1
		LOCALS = クリスタルリング
	CASE 2
		LOCALS = ダイアモンドリング
	ENDSELECT
CASE 5
	LOCALS = 竜石
ENDSELECT

RETURNF LOCALS


@ACCESSORY_ABL()
#FUNCTION
;LOCALに XXXを効果の種類、AAAを効力として、XXXAAA の形で数値を入れる

SELECTCASE RAND:100
;殆どの場合はステータス変化(種類は精力,技巧,精神力,魅力)
CASE IS <= 50
	;魅力
	IF RAND:5 == 0
		LOCAL = 5000
	;精神力
	ELSEIF RAND:4 == 0
		LOCAL = 4000
	;技巧
	ELSEIF RAND:3 == 0
		LOCAL = 3000
	;精力
	ELSEIF RAND:2
		LOCAL = 2000
	;ＥＰ
	ELSE
		LOCAL = 1000
	ENDIF

	SELECTCASE RAND:10
	CASE IS < 2
		LOCAL += 10
	CASE IS < 4
		LOCAL += 20
	CASE IS < 6
		LOCAL += 30
	CASE IS < 8
		LOCAL += 40
	CASEELSE
		LOCAL += 50
	ENDSELECT
;特殊効果
CASE IS <= 79
	SELECTCASE RAND:11
	;○○強化 該当部位をせめる場合に与える快感が強くなる 種類はキッス,バスト,ヒップ,ヴァッジ
	CASE 0 TO 2
		;Ｃ
		IF RAND:5 == 0
			LOCAL = 10000
		;Ｖ
		ELSEIF RAND:4 == 0
			LOCAL = 11000
		;Ａ
		ELSEIF RAND:3 == 0
			LOCAL = 12000
		;Ｂ
		ELSEIF RAND:2
			LOCAL = 13000
		;Ｍ
		ELSE
			LOCAL = 14000
		ENDIF
		LOCAL += 50
	;実行値アップ+○ 種類は+4,+7,+10
	CASE 3
		LOCAL = 21000
		SELECTCASE RAND:10
		CASE IS <= 6
			LOCAL += 4
		CASE IS <= 8
			LOCAL += 7
		CASEELSE
			LOCAL += 10
		ENDSELECT
	;ムードアップ+○ ムードアップ+○
	CASE 4
		LOCAL = 20000
		SELECTCASE RAND:10
		CASE IS <= 6
			LOCAL += 1
		CASE IS <= 8
			LOCAL += 2
		CASEELSE
			LOCAL += 3
		ENDSELECT
	;押し倒され率+○％
	CASE 5
		LOCAL = 23000
		SELECTCASE RAND:10
		CASE IS <= 6
			LOCAL += 10
		CASE IS <= 8
			LOCAL += 20
		CASEELSE
			LOCAL += 30
		ENDSELECT
	;受ける快感+○％ 種類は+10,+20,+50
	CASE 6
		LOCAL = 22000
		SELECTCASE RAND:10
		CASE IS <= 6
			LOCAL += 10
		CASE IS <= 8
			LOCAL += 20
		CASEELSE
			LOCAL += 50
		ENDSELECT
	;○○に強い その種族に強くなる 種類は少女,妖精,悪魔,ケモノ,トカゲ

	;常にアルカナ
	CASE 7, 8
		;常にエンラジ
		IF RAND:4 == 0
			LOCAL = 33000
		;常にセンシブ
		ELSEIF RAND:3 == 0
			LOCAL = 32000
		;常にミニマム ステート異常ミニマムになる
		ELSEIF RAND:2
			LOCAL = 31000
		;常にウルズグン
		ELSE
			LOCAL = 30000
		ENDIF
	;経験値増加
	CASE 9
		LOCAL = 24000
		SELECTCASE RAND:10
		CASE IS <= 6
			LOCAL += 20
		CASE IS <= 8
			LOCAL += 50
		CASEELSE
			LOCAL += 100
		ENDSELECT
	;ストック増加
	CASEELSE
		LOCAL = 6000
	ENDSELECT
;主に探索で効くもの
CASEELSE
	;隠密+○ 敵に出会う確率軽減。
	IF RAND:3 == 0
		LOCAL = 50000
	;宝箱出現率アップ
	ELSEIF RAND:2
		LOCAL = 51000
	;ドア発見率アップ
	ELSE
		LOCAL = 52000
	ENDIF
	IF RAND:4 == 0
		LOCAL += 20
	ELSE
		LOCAL += 40
	ENDIF
ENDSELECT
RETURNF LOCAL

@ACCESSORY_EFFECT(ARG)
#FUNCTIONS

SELECTCASE ARG/1000
CASE 1
	LOCALS = ＥＰ＋{ARG%1000}％
CASE 2
	LOCALS = 精力＋{ARG%1000}％
CASE 3
	LOCALS = 技巧＋{ARG%1000}％
CASE 4
	LOCALS = 精神力＋{ARG%1000}％
CASE 5
	LOCALS = 魅力＋{ARG%1000}％
CASE 6
	LOCALS = ストック増加

CASE 10
	LOCALS = Ｃ強化＋{ARG%1000}％
CASE 11
	LOCALS = Ｖ強化＋{ARG%1000}％
CASE 12
	LOCALS = Ａ強化＋{ARG%1000}％
CASE 13
	LOCALS = Ｂ強化＋{ARG%1000}％
CASE 14
	LOCALS = Ｍ強化＋{ARG%1000}％

CASE 20
	LOCALS = ムード＋{ARG%1000}
CASE 21
	LOCALS = 実行値＋{ARG%1000}
CASE 22
	LOCALS = 受ける快感＋{ARG%1000}％
CASE 23
	LOCALS = 押し倒され＋{ARG%1000}％
CASE 24
	LOCALS = 取得経験値＋{ARG%1000}％

CASE 30
	LOCALS = 常にウルズグン
CASE 31
	LOCALS = 常にミニマム
CASE 32
	LOCALS = 常にセンシブ
CASE 33
	LOCALS = 常にエンラジ

CASE 50
	LOCALS = 隠密＋{ARG%1000}％
CASE 51
	LOCALS = 宝箱出現率＋{ARG%1000}％
CASE 52
	LOCALS = ドア発見率＋{ARG%1000}％
ENDSELECT

RETURNF LOCALS


;アクセサリをまだ持てるか判定
@ACCESSORY_BUY()
#FUNCTION
SIF FLAG:380 == 0
	RETURNF 1
RETURNF 0

@ACCESSORY_COUNT
#FUNCTION
#DIM LCOUNT
#DIM ACCESSORY_NUM

VARSET ACCESSORY_NUM

FOR LCOUNT, 1, 9
	IF FLAG:(300 + LCOUNT*10) != 0
		ACCESSORY_NUM += 1
	ENDIF
NEXT

RETURNF ACCESSORY_NUM