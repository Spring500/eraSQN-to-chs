@SHOW_CHARA_ANALYSIS, P, M
CALL SHOW_CHARA_LIST_PRE, "アナライズ"
CALL SHOW_CHARA_LIST, "アナライズ", P, M

$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 2000 && CONFIG("世代別表示")
	M = 0
	P = 0
	RESTART
ELSEIF RESULT == 2001 && CONFIG("世代別表示")
	M += 1
	P = 0
	RESTART
ELSEIF RESULT == 2002 && CONFIG("世代別表示")
	M -= 1
	P = 0
	RESTART
ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != RESULT - 1001
	P = RESULT - 1001
	RESTART
ELSEIF RESULT < MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

LOCAL = TARGET
TARGET = RESULT

CALL ANALYSIS

TARGET = LOCAL

RESTART


@ANALYSIS
#DIM LCOUNT
#DIM MEMO_SOURCE, 20
#DIM MEMO_CUP, 20
#DIM MEMO_PALAM, 20
#DIM MEMO_MOOD

MEMO_MOOD = TFLAG:33
FOR LCOUNT, 0, 20
	MEMO_SOURCE:LCOUNT = SOURCE:LCOUNT
	MEMO_CUP:LCOUNT = CUP:LCOUNT
	MEMO_PALAM:LCOUNT = PALAM:LCOUNT
NEXT

CALL ANALYSIS_CALC
PRINTFORML ～%NAME:TARGET%の識別結果～

;-------------------------------------------------
;結果表示
;-------------------------------------------------
CALL SHOW_UP

TCVAR:27 = 0
CALL COM_ORDER, 1

LOCAL = RESULT

IF EXP:调教次数 == 0
	CALL SHOW_DIFFICULTY
	PRINTL 
ENDIF

PRINTFORML 調教実行値補正:\@ LOCAL ? {LOCAL} # なし \@

CALL ANALYSIS_CALC_SB

TFLAG:33 = MEMO_MOOD
FOR LCOUNT, 0, 20
	SOURCE:LCOUNT = MEMO_SOURCE:LCOUNT
	CUP:LCOUNT = MEMO_CUP:LCOUNT
	PALAM:LCOUNT = MEMO_PALAM:LCOUNT
NEXT

FLAG:8 = 1

WAIT

;パワーバランス
@ANALYSIS_CALC_SB, ARG
#DIM LCOUNT

VARSET LOCAL
LOCAL:400 = BASE:PLAYER:绝顶
LOCAL:500 = SELECTCOM

;ハダカにするので、衣装を記録
FOR LCOUNT, 40, 50
	LOCAL:LCOUNT = EQUIP:LCOUNT
	LOCALS:LCOUNT = %CSTR:LCOUNT%
NEXT

CALL SETFLAG, "ハダカにする", TARGET

FLAG:8 = 0

CALL ANALYSIS_CALC_SB_MAIN, ARG

BASE:PLAYER:绝顶 = LOCAL:400
SELECTCOM = LOCAL:500
;服装を戻す
FOR LCOUNT, 40, 50
	EQUIP:LCOUNT = LOCAL:LCOUNT
	CSTR:LCOUNT = %LOCALS:LCOUNT%
NEXT

@ANALYSIS_CALC_SB_MAIN, ARG
#DIM LCOUNT
VARSET LOCAL
;愛撫から奉仕までを見る
FOR LCOUNT, 0, 10
	;ＣＶＡＢＭ快感系などのコマンド抜粋
	SELECTCASE LCOUNT
	CASE 0
		SELECTCOM = 1
	CASE 1
		SELECTCOM = 5
	CASE 2
		SELECTCOM = 6
	CASE 3
		SELECTCOM = 60
	CASE 4
		SELECTCOM = 71
	CASE 5
		SELECTCOM = 90
	CASE 6
		SELECTCOM = 91
	CASE 7
		SELECTCOM = 92
	CASE 8
		SELECTCOM = 95
	CASE 9
		SELECTCOM = 96
	ENDSELECT

	TRYCALLFORM COM_ABLE{SELECTCOM}
	SIF RESULT == 0
		CONTINUE

	VARSET PALAM
	VARSET SOURCE
	VARSET CUP
	VARSET TEQUIP

	BASE:PLAYER:绝顶 = 0
	;条件はかなり良いもので試算
	PALAM:Ｖ润 = 30000
	PALAM:Ａ润 = 30000
	PALAM:恭顺 = 30000
	PALAM:欲情 = 30000
	TFLAG:33 = 160

	TRYCALLFORM SOURCE_COM{SELECTCOM}, 100
	CALL SAMEN_CHECK_PLAYER

	CALL CALC_SOURCE

	;LOCAL:1が感度の100以下切捨て、LOCAL:2が攻撃力の100以下切捨て
	LOCAL:1 = MAX(LOCAL:1, CUP:快Ｃ, CUP:快Ｖ, CUP:快Ａ, CUP:快Ｂ, CUP:快Ｍ) / 100 * 100
	LOCAL:2 = MAX(LOCAL:2, BASE:PLAYER:绝顶) / 100 * 100
NEXT

VARSET TFLAG
VARSET PALAM

IF ARG
	;敏感度
	SELECTCASE LOCAL:1
	;CASE IS >= 120000
	;	SETCOLOR DEF_COLOR("ショッキングピンク")
	CASE IS >= 80000
		SETCOLOR DEF_COLOR("ピンク")
	CASE IS >= 40000
		SETCOLOR DEF_COLOR("ハートピンク")
	CASE IS >= 20000
		SETCOLOR DEF_COLOR("黄色")
	ENDSELECT
	PRINTFORM {LOCAL:1, 8} 
	RESETCOLOR

	;攻撃力
	SELECTCASE LOCAL:2
	;CASE IS >= MAXBASE:MASTER:绝顶 * 4
	;	SETCOLOR DEF_COLOR("ショッキングピンク")
	CASE IS >= MAXBASE:MASTER:绝顶 * 3
		SETCOLOR DEF_COLOR("ピンク")
	CASE IS >= MAXBASE:MASTER:绝顶 * 2
		SETCOLOR DEF_COLOR("ハートピンク")
	CASE IS >= MAXBASE:MASTER:绝顶
		SETCOLOR DEF_COLOR("黄色")
	ENDSELECT
	PRINTFORM {LOCAL:2, 8} 
	RESETCOLOR
ELSE
	PRINTFORML 敏感度:{LOCAL:1, 7}
	PRINTFORML 攻撃力:{LOCAL:2, 7}
ENDIF

;LOCAL:3は優位度。※優位度は、%NAME:MASTER%と%NAME:TARGET%の技量比。50未満ならば%NAME:MASTER%が優勢
;お互いが弱すぎる時
IF LOCAL:1 < 1000 && LOCAL:2 < 1000
	LOCAL:3 = 50
ELSE
	;MAXBASE:快感容许值（女） = MIN(ABL:欲望*5000+ABL:ＬＶ*2000+TALENT:上位梦魔*5000+(JUEL:5+JUEL:0+JUEL:1+JUEL:2+JUEL:14+JUEL:15)/200, 120000)
	;LOCAL:4 = LOCAL:1 * 10000 / MAXBASE:快感容许值（女）
	;LOCAL:5 = LOCAL:2 * 10000 / MAXBASE:PLAYER:绝顶
	;LOCAL:3 = LOCAL:4 * 100 / (LOCAL:4 + LOCAL:5)
	;LOCAL:3 = LOCAL:1 * 100 / (LOCAL:1 + LOCAL:2 * 10000 / MAXBASE:PLAYER:绝顶)
	LOCAL:3 = 100 - (LOCAL:1 * 100 / (LOCAL:1 + LOCAL:2 * 10000 / MAXBASE:PLAYER:绝顶) )
ENDIF

SIF ARG == 0
	PRINTFORM 手強さ:
PRINTFORM {LOCAL:3, 3}/100 … 
SELECTCASE LOCAL:3
CASE IS <= 20
	SETCOLOR DEF_COLOR("ハートピンク")
	PRINTFORM 楽勝
CASE IS <= 40
	SETCOLOR DEF_COLOR("黄色")
	PRINTFORM 優勢
CASE IS <= 60
	PRINTFORM 互角
CASE IS <= 80
	SETCOLOR DEF_COLOR("水色")
	PRINTFORM 劣勢
CASEELSE
	SETCOLOR DEF_COLOR("青")
	PRINTFORM 無理
ENDSELECT
RESETCOLOR
PRINTL 



@ANALYSIS_CALC
#DIM LCOUNT, 2
#DIM MEMO_TARGET
MEMO_TARGET = TARGET

;雛形キャラで数値の基本値算出

ADDCHARA 98
TARGET = CHARANUM - 1
BASE:精力 = 10
ABL:ＬＶ = 1
ABL:Ｃ感觉 = 1
ABL:Ｖ感觉 = 1
ABL:Ａ感觉 = 1
ABL:Ｂ感觉 = 1
ABL:Ｍ感觉 = 1
TALENT:处女 = 0
TALENT:女性 = 1

VARSET LOCAL

CALL ANALYSIS_CALC_MAIN, "ＬＶ技巧補正有り"
FOR LCOUNT, 0, 16
	LOCAL:LCOUNT = MAX(CUP:LCOUNT, 1)
	;PRINTFORML %PALAMNAME:LCOUNT% {LOCAL:LCOUNT}
NEXT
;表示されていたキャラを白紙に戻す
DELCHARA CHARANUM-1

TARGET = MEMO_TARGET
CALL ANALYSIS_CALC_MAIN, "ＬＶ技巧補正有り"
;-------------------------------------------------
;結果の計算
;-------------------------------------------------
FOR LCOUNT, 0, 16
	;PRINTFORM %PALAMNAME:LCOUNT% {CUP:LCOUNT}
	CUP:LCOUNT = CUP:LCOUNT * 1000 / LOCAL:LCOUNT
	;PRINTFORML →{CUP:LCOUNT}
NEXT


@ANALYSIS_CALC_MAIN, ARGS
SELECTCOM = -1
PREVCOM = -1

;ARGS == "ＬＶ技巧補正有り"のときのみ機能
LOCAL:2 = ABL:PLAYER:技巧
LOCAL:90 = ABL:PLAYER:ＬＶ

SELECTCASE ARGS
CASE "ＬＶ技巧補正有り"
	ABL:PLAYER:技巧 = ABL:技巧
	ABL:PLAYER:ＬＶ = ABL:ＬＶ
ENDSELECT

TFLAG:33 = 160
VARSET PALAM
CALL SETFLAG, "調教外潤滑追加", 30000
PALAM:恭顺 = 30000
PALAM:欲情 = 30000
VARSET SOURCE, 1000
;ＣＶＡＢＭ感覚等はSOURCEの基本値が多いという仕組みなので、ここで代入しておく
SOURCE:快Ｃ = CALCF("Ｃ刺激", 0)
SOURCE:快Ｖ = CALCF("Ｖ刺激", 0)
SOURCE:快Ａ = CALCF("Ａ刺激", 0)
SOURCE:快Ｂ = CALCF("Ｂ刺激", 0)
SOURCE:快Ｍ = CALCF("Ｍ刺激", 0)
VARSET CUP

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
CALL CALC_SOURCE

TFLAG:33 = 0
VARSET PALAM

ABL:PLAYER:技巧 = LOCAL:2
ABL:PLAYER:ＬＶ = LOCAL:90



@SHOW_UP
#DIM LCOUNT
FOR LCOUNT, 0, 16
	SELECTCASE LCOUNT
	CASE 0 TO 2
		LOCAL = LCOUNT
	CASE 3 TO 4
		LOCAL = LCOUNT + 11
	CASEELSE
		LOCAL = LCOUNT - 2
	ENDSELECT

	SIF CUP:LOCAL / 100 == 10
		CONTINUE

	SELECTCASE LCOUNT
	CASE 0 TO 4
		SELECTCASE CUP:LOCAL/1000
		CASE IS >= 50
			SETCOLOR DEF_COLOR("ショッキングピンク")
		CASE IS >= 20
			SETCOLOR DEF_COLOR("ピンク")
		CASE IS >= 10
			SETCOLOR DEF_COLOR("ハートピンク")
		CASE IS >= 5
			SETCOLOR DEF_COLOR("黄色")
		CASE 0
			SETCOLOR DEF_COLOR("青")
		ENDSELECT
	CASE 5 TO 11
		SELECTCASE CUP:LOCAL/1000
		CASE IS >= 6
			SETCOLOR DEF_COLOR("ピンク")
		CASE IS >= 4
			SETCOLOR DEF_COLOR("ハートピンク")
		CASE IS >= 2
			SETCOLOR DEF_COLOR("黄色")
		CASE 0
			SETCOLOR DEF_COLOR("青")
		ENDSELECT
	CASEELSE
		SELECTCASE CUP:LOCAL/1000
		CASE IS >= 2
			SETCOLOR DEF_COLOR("青")
		CASE 0
			SETCOLOR DEF_COLOR("ハートピンク")
		ENDSELECT
	ENDSELECT

	;PRINTFORML %PALAMNAME:LOCAL%…{CUP:LOCAL/1000,4}.{(CUP:LOCAL%1000)/100}倍
	PRINTFORM %PALAMNAME:LOCAL%…{CUP:LOCAL/1000,4}.
	SIF (CUP:LOCAL%1000)/10 < 10
		PRINT 0
	PRINTFORML {(CUP:LOCAL%1000)/10}倍

	RESETCOLOR
NEXT
