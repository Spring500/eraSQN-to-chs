;-------------------------------------------------
;キャラを羅列する表示はここで一括管理。種類はARGSで区別する。
;LOCAL:LCOUNTでLOCAL番のキャラが条件クリアかどうかを管理する。
;-------------------------------------------------
@SHOW_CHARA_LIST, ARGS, ARG, ARG:1
#DIM LCOUNT

IF ARGS != "コス確認" && CONFIG("世代別表示")
	IF ARG:1 == 0
		PRINTL [2001]显示孩子们(第一世代)
	ELSEIF ARG:1 == 1
		PRINTL [2000]显示妻子们
		PRINTL [2001]显示下一个世代的孩子
	ELSEIF ARG:1 > 1
		PRINTL [2000]显示妻子们
		PRINTL [2001]显示下一个世代的孩子
		PRINTL [2002]显示上一个世代的孩子
	ENDIF
ENDIF

VARSET LOCAL
FOR LCOUNT, 1, CHARANUM
	IF ARGS != "コス確認" && CONFIG("世代別表示")
		IF ARG:1 == 0
			SIF BASE:LCOUNT:世代
				CONTINUE
		ELSE
			SIF BASE:LCOUNT:世代 != ARG:1
				CONTINUE
		ENDIF
	ENDIF
	SELECTCASE ARGS
	CASE "助手変更"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "調教対象変更"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "売却"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "コス確認"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "探索"
		SIF LCOUNT > MASTER && COND("助手可能", LCOUNT) == 0
			CONTINUE
	CASE "お気に入り"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "喫茶店"
		SIF COND("バイト可能", LCOUNT) == 0
			CONTINUE
	CASE "アナライズ"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "一晩開放許可"
		SIF COND("一晩開放可能", LCOUNT) == 0
			CONTINUE
	ENDSELECT
	LOCAL:LCOUNT = 1
NEXT

Z = 0
DRAWLINE
FOR LCOUNT, 1, CHARANUM
	SIF LOCAL:LCOUNT == 0
		CONTINUE
	IF LCOUNT > MASTER
		Z += 1
		SIF (Z-1 < CONFIG("表示人数")*ARG || Z > CONFIG("表示人数")*(ARG+1)) && CONFIG("表示人数")
			CONTINUE
	ENDIF

	SELECTCASE ARGS
	CASE "能力表示"
		IF LCOUNT == MASTER
			IF CONFIG("能力表示で３サイズ閲覧") == 0 || TALENT:MASTER:オトコ
				PRINTFORM 　[{MASTER, 2}]
				IF STATE("悪酔い", MASTER)
					SETCOLOR DEF_COLOR("青")
				ELSEIF STATE("ドランク", MASTER)
					SETCOLOR DEF_COLOR("ハートピンク")
				ENDIF
				PRINTFORM %TEXT_LJ(NAME:MASTER, 16)%
				RESETCOLOR
			ELSE
				PRINTFORM %BL(22)%
			ENDIF
			IF FLAG:31
				PRINTFORM  [999]攻略履历%BL(4)%
			ELSE
				PRINTFORM  %BL(16)%
			ENDIF
			IF CONFIG("３サイズ：表示")
				PRINTFORM [200]\@ CONFIG("能力表示で３サイズ閲覧") ? 显示体力等数据 # 显示三围 \@ %BL(12)%
			ELSE
				PRINTFORM %BL(28)%
			ENDIF
			SIF CONFIG("能力表示で３サイズ閲覧") == 0 && TALENT:MASTER:オトコ == 0
				CALL PRINT_N, LCOUNT
			PRINTL
			IF CONFIG("能力表示で３サイズ閲覧") == 0 || TALENT:MASTER:オトコ
				DRAWLINE
				CONTINUE
			ENDIF
		ENDIF
		IF CONFIG("能力表示で３サイズ閲覧")
			CALL PRINT_NUM_NAME, "３サイズ閲覧", LCOUNT
		ELSE
			CALL PRINT_NUM_NAME, "能力表示", LCOUNT
		ENDIF
	CASE "調教対象変更"
		IF CONFIG("調教対象変更でアナライズ")
			CALL PRINT_NUM_NAME, "アナライズ", LCOUNT
		ELSE
			CALL PRINT_NUM_NAME, "調教対象変更", LCOUNT
		ENDIF
	CASE "お気に入り", "名前変更"
		CALL PRINT_NUM_NAME, "体力", LCOUNT
	CASEELSE
		CALL PRINT_NUM_NAME, ARGS, LCOUNT
	ENDSELECT
NEXT

;選択肢の表示
IF CONFIG("表示人数") && Z > CONFIG("表示人数")
	IF ARG
		PRINTFORM [{1000 + ARG}]上一页　
	ELSE
		PRINTPLAINFORM [----]上一页　
	ENDIF
ENDIF

SIF ARGS != "コス確認" || (CONFIG("表示人数") && Z > CONFIG("表示人数"))
	PRINTFORM  [100]返回上层菜单

SELECTCASE ARGS
CASE "喫茶店"
	SIF CHARA_NUM("バイト可能") >= 2
		PRINTFORM  [200]全员行动指示
CASE "一晩開放許可"
	SIF CHARA_NUM("一晩開放可能") >= 2
		PRINTFORM  [200]全员行动指示
CASE "能力表示"
	SIF CHARA_NUM("助手可能") && CONFIG("調教相手変更不可") == 0
		PRINTFORM  [109]更换助手
	SIF CHARA_NUM("控えキャラ") && CONFIG("調教相手変更不可") == 0
		PRINTFORM  [110]更换调教对象
CASE "助手変更"
	IF CONFIG("探索同行者選択中") == 0
		PRINTFORM  [101]角色能力状态
		SIF CHARA_NUM("控えキャラ") && CONFIG("調教相手変更不可") == 0
			PRINTFORM  [110]更换调教对象
	ENDIF
CASE "調教対象変更"
	PRINTFORM  [101]角色能力状态
	SIF CHARA_NUM("助手可能") && CONFIG("調教相手変更不可") == 0
	  ; 未翻译
		PRINTFORM  [109]%TEXTS("助手の名称")%变更
ENDSELECT

SIF CONFIG("表示人数") && Z > CONFIG("表示人数") * (ARG + 1)
	PRINTFORM 　[{1000 + ARG + 2}]下一页

PRINTL

;全ページ数
IF CONFIG("表示人数")
	Q = (Z / CONFIG("表示人数")) + 1
ELSE
	Q = 1
ENDIF

@SHOW_CHARA_LIST_PRE, ARGS
PRINTL
SELECTCASE ARGS
CASE "喫茶店", "探索", "乳首選択"
CASEELSE
	SIF TARGET || (ASSI && TARGET <= 0)
		PRINTFORM 目前
	SIF TARGET
		PRINTFORM 的调教对象是[{TARGET}]%NAME:TARGET%，
	IF ASSI
		PRINTFORM %TEXTS("助手の名称")%是[{ASSI}]%NAME:ASSI%
	ELSEIF TARGET
		PRINTFORM 没有配置%TEXTS("助手の名称")%
	ENDIF
	SIF ARGS == "売却"
		PRINTFORM  (所持金:%MONEY_C(MONEY)%)
	PRINTL
ENDSELECT

SELECTCASE ARGS
CASE "能力表示"
	PRINTL 想要查看谁的能力呢？
	IF CHARA_NUM("ドランク")
		PRINT 顺带一提，
		CALL PRINT_COLORTEXT, "青色表示的角色正处于烂醉状态", DEF_COLOR("青")
		PRINT ，
		CALL PRINT_COLORTEXT, "粉色表示的角色正处于醉酒状态", DEF_COLOR("ハートピンク")
		PRINTL 。
	ENDIF
	PRINTL 通过点击能力显示画面的各个项目(□ 素质 □ 等)可以将其折叠起来
CASE "助手変更"
	IF CONFIG("助手への指示")
	  ; 未翻译，没找到在那里出现，先抄旧汉化版
		; PRINTL []の数だけ指示を与えることが出来ます。
		PRINTL 有多少个[　]就可以下达多少个指示。
		PRINTL 要给谁变更指示？
	ELSE
		PRINTFORML 要选谁作为%TEXTS("助手の名称")%呢？（要解除助手请点击已选择的%TEXTS("助手の名称")%）
		IF CONFIG("助手の探索能力表示")
			PRINTFORML 点击%TEXTS("助手の名称")%的能力(如「财宝发现」之类的)就可以显示该能力的说明
		ELSE
			PRINTFORML 点击%TEXTS("助手の名称")%的能力(如「调合」之类的)就可以显示该能力的说明
			PRINTFORML （点击「魔法使」或「秘法」就可以显示已习得的秘法）
		ENDIF
	ENDIF
	DRAWLINE
	LOCALS = 没有%TEXTS("助手の名称")%
	PRINTFORM 　[ 0]%TEXT_LJ(LOCALS, 16)%
	IF CONFIG("探索同行者選択中") == 0
		IF CHARA_NUM("指示可能な助手")
			PRINTFORM [200]\@ CONFIG("助手への指示") ? 变更界面%BL(3)% # 指示界面%BL(3)% \@
		ELSE
			CLEARBIT FLAG:45, 0
		ENDIF
		PRINTFORM [300]\@ CONFIG("助手の探索能力表示") ? 调教时的能力%BL(3)% # 探索时的能力%BL(3)% \@
		PRINTFORM [400]%TEXTS("助手の名称")%职称的变更
	ENDIF
	PRINTL
CASE "調教対象変更"
	IF CONFIG("調教対象変更でアナライズ")
		PRINTL 要调教谁呢？（满点是100点，超过50点就可以跟MASTER叫板）
		DRAWLINE
		PRINTFORM  [200]显示普通项目
		PRINTPLAINFORM 　欲求不满　 好感度　 放置　 敏感度　 攻击力　 跟MASTER对比
	ELSE
		PRINTL 要调教谁呢？（☆标记：偏爱角色）
		DRAWLINE
		PRINTFORM  [200]显示欲求不满度、好感度、放置时间、具体的强度等…
	ENDIF
	PRINTL
CASE "売却"
	PRINTL 要将谁卖掉呢？
	PRINTL 计算公式大概就是 能力 × 固定素质 × 可变素质 ＝ 评价pt → 价格
CASE "探索"
	PRINTFORML 主人和%TEXTS("助手の名称")%都可以外出探索。要派谁去探索呢？
	IF ASSI
		PRINTPLAINFORM （选择[ 1]%NAME:MASTER%自己外出探索的话，
		SETCOLOR DEF_COLOR("イエロー")
		PRINTPLAINFORM %TEXTS("助手の名称")%[{ASSI, 2}]%NAME:ASSI%
		RESETCOLOR
		PRINTL 也会同行）
		PRINTFORML 点击%TEXTS("助手の名称")%的能力(如「财宝发现」之类的)就可以显示该能力的说明
	ENDIF
	;食器で回復量UP
	IF ITEM:ミスリルのティーカップ
		PRINTFORML 如果带上秘银茶杯，战斗结束时的休息效果提高６Ｐ。
	ELSEIF ITEM:ミスリルのスプーン
		PRINTFORML 如果带上秘银茶匙，战斗结束时的休息效果提高５Ｐ。
	ELSEIF ITEM:銀のティーカップ
		PRINTFORML 如果带上银制茶杯，战斗结束时的休息效果提高４Ｐ。
	ELSEIF ITEM:銀のスプーン
		PRINTFORML 如果带上银制茶匙，战斗结束时的休息效果提高３Ｐ。
	ELSEIF ITEM:陶磁器のティーカップ
		PRINTFORML 如果带上陶瓷茶杯，战斗结束时的休息效果提高２Ｐ。
	ELSEIF ITEM:陶磁器のスプーン
		PRINTFORML 如果带上陶瓷茶匙，战斗结束时的休息效果提高１Ｐ。
	ENDIF
CASE "お気に入り"
	PRINTFORML 要将谁设置为偏爱呢？
	PRINTFORML ※注意：这项功能一周以内只能变更一次※
	PRINTFORML (被标记为偏爱的角色带有☆。你可以从这个角色身上获得更多的快感…)
	PRINTFORML 还有，选项[ 0]和[ 1]可以设定%CALLNAME:MASTER%的弱点和性癖
	PRINTFORML (你可以从性癖匹配的角色身上获得更多的快感…)
	PRINTFORML
	PRINTFORM 　[ 0]弱点设定　现在的弱点是：
	SELECTCASE NUM_BIT(TALENT:MASTER:弱点)
	CASE NUM("弱点")
		PRINTFORM [弱点全选]
	CASE IS >= 4
		PRINTFORM 很多
	CASE 0
		PRINTFORM 无
	CASEELSE
		FOR LOCAL, 0, NUM("弱点")
			SIF GETBIT(TALENT:MASTER:弱点, LOCAL)
				PRINTFORM %NAMES("弱点", LOCAL)%
		NEXT
	ENDSELECT
	PRINTL

	PRINTFORM 　[ 1]性癖设定　现在的性癖是：
	SELECTCASE NUM_BIT(TALENT:MASTER:性癖)
	CASE IS >= 6
		PRINTFORM 很多
	CASE 0
		PRINTFORM 无
	CASEELSE
		FOR LOCAL, 0, NUM("属性")
			SIF GETBIT(TALENT:MASTER:性癖, LOCAL)
				PRINTFORM %NAMES("属性", LOCAL)%
		NEXT
	ENDSELECT
	PRINTL
CASE "名前変更"
	PRINTL 要变更谁的名称呢？
CASE "喫茶店"
	PRINTL ～店员名册～
	PRINTL 对服务态度有何指示呢？
CASE "アナライズ"
	PRINTL 要分析谁呢？
CASE "一晩開放許可"
	PRINTL 要允许谁外出了呢？
CASE "乳首選択"
	PRINTFORML 通过点击下面的文字，如肥大、凹陷、乳晕，
	PRINTFORML 就可以改变乳头肥大、乳头凹陷以及大乳晕的等级。
	PRINTFORML 点击１次提升１LV。
	DRAWLINE
	PRINTFORML 　　　　　　　　　　　　肥大 凹陷 乳晕
ENDSELECT

;-------------------------------------------------
;ステータス表示（Pは初期値0）
;-------------------------------------------------
@SHOW_CHARADATA, P, M
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "能力表示"
CALL SHOW_CHARA_LIST, "能力表示", P, M

$INPUT_LOOP
INPUT
IF RESULT == 100
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 109 && CHARA_NUM("助手可能") && CONFIG("調教相手変更不可") == 0
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	CALL SELECT_ASSI, 0, 0
	RETURN 0
ELSEIF RESULT == 110 && CHARA_NUM("控えキャラ") && CONFIG("調教相手変更不可") == 0
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	CALL CHANGE_TARGET, 0, 0
	RETURN 0
ELSEIF RESULT == 200 && CONFIG("３サイズ：表示")
	INVERTBIT FLAG:46, 0
	RESTART
;クリアキャラ表示（暫定）
ELSEIF RESULT == 999 && FLAG:31
	CALL SHOW_CHARA_CAPTURED
	RESTART
ELSEIF RESULT == 2000 && CONFIG("世代別表示")
	M = 0
	P = 0
	RESTART
ELSEIF RESULT == 2001 && CONFIG("世代別表示")
	M += 1
	P = 0
	RESTART
ELSEIF RESULT == 2002 && CONFIG("世代別表示")
	M -= 1
	P = 0
	RESTART
ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != RESULT - 1001
	P = RESULT - 1001
	RESTART
ELSEIF RESULT < MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

CALL NEW_PRINT_STATE, RESULT, "通常"

RESTART

;-------------------------------------------------
;助手選択
;-------------------------------------------------
@SELECT_ASSI, P, M
#DIM LCOUNT
#DIM CHOICE
#DIMS CHOICES, 2
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
VARSET CHOICES

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "助手変更"
CALL SHOW_CHARA_LIST, "助手変更", P, M

$INPUT_LOOP
INPUTS

SPLIT RESULTS, "/", CHOICES

SELECTCASE CHOICES
CASE "相性:☆"
	IF TARGET
		PRINTFORML %CALLNAME:TARGET%跟MASTER最相配！
		PRINTFORMW (指令的实行值+30bonus)
	ENDIF
CASE "相性:◎"
	IF TARGET
		PRINTFORML %CALLNAME:TARGET%跟MASTER很相配！
		PRINTFORMW (指令的实行值+15bonus)
	ENDIF
CASE "相性:〇"
	IF TARGET
		PRINTFORML %CALLNAME:TARGET%跟MASTER相配得还行
		PRINTFORMW (指令的实行值+5bonus)
	ENDIF
CASE "針"
	PRINTFORMW 「针」指令的效果提升
CASE "調合"
	PRINTFORML 「媚药」指令的效果显著提升
	PRINTFORMW 而且，还可以利用淫魔水滴调合各种药品
CASE "サド"
	PRINTFORMW 在ＳＭ调教中，实行Ｓ系指令会非常上手
CASE "妖狐"
	PRINTFORMW 调教结束时会送你媚药
CASE "スライム"
	PRINTFORMW 调教结束时会送你润滑液
CASE "ドライアド"
	PRINTFORMW 调教结束时会送你茉莉薰香
CASE "治療"
	PRINTFORMW 随着时间推进，调教对象可以恢复最多500点体力
CASE "鼓舞"
	PRINTFORML %TEXTS("助手の名称")%会给调教对象加油，让她充满干劲
	PRINTFORMW (指令的实行值+10bonus)
CASE "ふたなり"
	PRINTFORMW 就是扶她啦
CASE "♂"
	PRINTFORMW 是男孩子
CASE "禁断の知識"
	PRINTFORML 可以在店里购买危险的商品
	PRINTFORMW 而且，跟调合知识一样可以增强媚药的效果
CASE "具現"
	PRINTFORMW 让女性也可以长出阴茎
CASE "大魔法使い"
	PRINTFORMW 可以使用全部秘法。就是传说中最强的魔法使
CASE "魔法使い"
	PRINTFORML 可以通过阅读魔导书习得秘法。而且，使用秘法时消耗的精力也比一般人少
	PRINTFORML -%CALLNAME:TOINT(CHOICES:1)%已习得秘法一览-
	FOR LCOUNT, 230, 260
		SIF TALENT:TOINT(CHOICES:1):LCOUNT
			PRINTFORML %TEXT_LJ(TALENT_NAME(LCOUNT, TALENT:TOINT(CHOICES:1):LCOUNT, TOINT(CHOICES:1)), 20)% %TEXTS("アルカナ解説", LCOUNT)%
	NEXT
	WAIT
CASE "アルカナ"
	PRINTFORML 虽然不是魔法专家，但也还是可以使用一些秘法
	PRINTFORML -%CALLNAME:TOINT(CHOICES:1)%已习得秘法一览-
	FOR LCOUNT, 230, 260
		SIF TALENT:TOINT(CHOICES:1):LCOUNT
			PRINTFORML %TEXT_LJ(TALENT_NAME(LCOUNT, TALENT:TOINT(CHOICES:1):LCOUNT, TOINT(CHOICES:1)), 20)% %TEXTS("アルカナ解説", LCOUNT)%
	NEXT
	WAIT
CASE "愛撫・奉仕知識"
	PRINTFORML 爱抚侍奉时给予对方的快感增加50％，恐惧和反感一类的负面感情影响减半
	PRINTFORMW 但是，需要%TEXTS("助手の名称")%亲自调教对象才能生效
CASE "教え上手"
	PRINTFORML 当侍奉技能比被调教对象高时，对象获得的经验各+1点
	PRINTFORML 而且，调教对象获得的侍奉快乐度经验会变为1.5倍
	PRINTFORMW 但是，需要%TEXTS("助手の名称")%亲自调教对象才能生效
CASE "ＳＭ知識"
	PRINTFORML 在执行「打屁股」一类ＳＭ指令时才会生效
	PRINTFORML 欲情和屈服经验增加50％，恐惧和反感一类的负面感情影响减半
	PRINTFORML 而且，对象获得的嗜虐快乐经验和被虐快乐会变为1.5倍
	PRINTFORMW 但是，%TEXTS("助手の名称")%亲自调教对象才能生效
CASE "Ａ調教知識"
	PRINTFORML 肛门爱抚时，快Ａ和欲情会增加50％，
	PRINTFORML 痛苦和反感一类的负面感情影响减半
	PRINTFORMW 但是，%TEXTS("助手の名称")%亲自调教对象才能生效
CASE "性器拡張知識"
	PRINTFORML 进行性器扩张时，痛苦和恐惧减半、扩张经验的增加量会获得bonus
	PRINTFORMW 但是，%TEXTS("助手の名称")%亲自调教对象才能生效
CASE "道具使い"
	PRINTFORML 在使用震动棒一类的调教道具时，快乐和欲情会增加50％
	PRINTFORML 恐惧和反感一类的负面感情影响减半
	PRINTFORMW 但是，%TEXTS("助手の名称")%亲自调教对象才能生效
CASE "露出知識"
	PRINTFORML 调教对象的耻情增加50％，露出经验和羞耻快乐经验的获得量增加
	PRINTFORMW 这个知识，只要%TEXTS("助手の名称")%拥有就会一直生效
CASE "甘い言葉"
	PRINTFORMW 可以发动甜言蜜语
CASE "手加減"
	PRINTFORMW 可以实行高潮管理
CASE "歌唱"
	PRINTFORMW 可以咏唱带有各种BUFF的歌

;探索での能力
CASE "遭遇回避"
	CALL PRINT_STR, "梦魔遭遇率_黄色_下降５％__W"
CASE "遭遇増加"
	CALL PRINT_STR, "梦魔遭遇率_青_上升５％__W"
CASE "罠解除"
	CALL PRINT_STR, "开宝箱时的陷阱发动率变为_黄色_1／4__W"
CASE "罠誘発"
	CALL PRINT_STR, "开宝箱时的陷阱发动率变为_青_2.5倍__W"
CASE "財宝発見"
	CALL PRINT_STR, "宝箱发现率_黄色_上升５％__W"
CASE "財宝見逃し"
	CALL PRINT_STR, "宝箱发现率_青_下降５％__W"
CASE "階段発見"
	CALL PRINT_STR, "前往下一层的门扉的发现率_黄色_上升５％__W"
CASE "回復"
	CALL PRINT_STR, "每次行动后精力_黄色_回复１Ｐ_，战斗结束后精力_黄色_回复２Ｐ__W"
CASE "戦闘補助"
	CALL PRINT_STR, "探索者的战斗力有所增加_W"
CASE "状態異常無効"
	CALL PRINT_STR, "宝箱和陷阱的异常状态无效化_W"

;それ以外は数値入力へ変換
CASEELSE
	RESULT = TOINT(CHOICES)
	IF RESULT == 100
		REDRAW 1
		RETURN 0
	ELSEIF RESULT == 101 && CONFIG("探索同行者選択中") == 0
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		;1ページ目を表示
		CALL SHOW_CHARADATA, 0, 0
		RETURN 0
	ELSEIF RESULT == 200 && CHARA_NUM("指示可能な助手") && CONFIG("探索同行者選択中") == 0
		INVERTBIT FLAG:45, 0
		RESTART
	ELSEIF RESULT == 300 && CONFIG("探索同行者選択中") == 0
		CLEARBIT FLAG:45, 0
		INVERTBIT FLAG:45, 1
		RESTART
	ELSEIF RESULT == 400
		PRINTFORML "助手"这个职称是可以修改的
		PRINTFORML 现在的职称是『%TEXTS("助手の名称")%』，要如何变更职称呢？
		PRINTFORML  [ 0] 助手
		PRINTFORML  [ 1] 秘书
		PRINTFORML  [ 2] 副官
		PRINTFORML  [ 3] 女仆长
		PRINTFORML  [ 4] 亲信
		PRINTFORML  [ 5] 参谋
		PRINTFORML  [99] 直接输入
		PRINTFORML [100] 不作出更改

		CALL INPUT_SELECT, 6, 99, 100

		CHOICE = RESULT

		SELECTCASE RESULT
		CASE 100
			RESTART
		CASE 0
			LOCALS = 助手の名称：助手
		CASE 1
			LOCALS = 助手の名称：秘书
		CASE 2
			LOCALS = 助手の名称：副官
		CASE 3
			LOCALS = 助手の名称：女仆长
		CASE 4
			LOCALS = 助手の名称：亲信
		CASE 5
			LOCALS = 助手の名称：参谋
		CASE 99
			PRINTFORML 全只能输入最多８个全角字符(或16个半角字符)
			CALL PRINT_STR, "黄色_※推荐长度在５个全角字符以内※_L"
			PRINTFORML 什么都不输入就不会作出更改。

			CALL INPUTS_SELECT, 16

			SELECTCASE RESULTS
			CASE ""
				RESTART
			CASEELSE
				LOCALS = 助手の名称：%RESULTS%
			ENDSELECT
		ENDSELECT
		;助手の名称フラグリセット
		CALL DEL_CEVENT_GROUP, "助手の名称", MASTER
		CALL SET_CEVENT, LOCALS, MASTER

		PRINTFORMW 职称已修改为『%TEXTS("助手の名称")%』

		RESTART
	ELSEIF RESULT == 110 && CHARA_NUM("控えキャラ") && CONFIG("調教相手変更不可") == 0 && CONFIG("探索同行者選択中") == 0
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		CALL CHANGE_TARGET, 0, 0
		RETURN 0
	ELSEIF RESULT == 2000 && CONFIG("世代別表示")
		M = 0
		P = 0
		RESTART
	ELSEIF RESULT == 2001 && CONFIG("世代別表示")
		M += 1
		P = 0
		RESTART
	ELSEIF RESULT == 2002 && CONFIG("世代別表示")
		M -= 1
		P = 0
		RESTART
	ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != RESULT - 1001
		P = RESULT - 1001
		RESTART
	ELSEIF RESULT == 0
		ASSI = 0
		REDRAW 1
		RETURN 0
	ELSEIF RESULT < MASTER || RESULT >= CHARANUM
		GOTO INPUT_LOOP
	ELSEIF RESULT == ASSI

	ELSEIF COND("助手可能", RESULT) == 0
		GOTO INPUT_LOOP
	ELSEIF CONFIG("助手への指示") && ABL:RESULT:助手 == 0
		GOTO INPUT_LOOP
	ENDIF

	;指示変更
	IF CONFIG("助手への指示")
		CALL ASSI_CHANGE_INDICATE, RESULT
	;助手変更
	ELSEIF RESULT == ASSI
		ASSI = 0
	ELSE
		ASSI = RESULT
		SIF ASSI == TARGET
			TARGET = 0
	ENDIF
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;調教対象選択
;-------------------------------------------------
@CHANGE_TARGET, P, M
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "調教対象変更"
CALL SHOW_CHARA_LIST, "調教対象変更", P, M

$INPUT_LOOP
INPUT
IF RESULT == 100
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 101
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	;1ページ目を表示
	CALL SHOW_CHARADATA, 0, 0
	RETURN 0
ELSEIF RESULT == 109 && CHARA_NUM("助手可能") && CONFIG("調教相手変更不可") == 0
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	CALL SELECT_ASSI, 0, 0
	RETURN 0
ELSEIF RESULT == 200
	INVERTBIT FLAG:47, 0
	RESTART
ELSEIF RESULT == 2000 && CONFIG("世代別表示")
	M = 0
	P = 0
	RESTART
ELSEIF RESULT == 2001 && CONFIG("世代別表示")
	M += 1
	P = 0
	RESTART
ELSEIF RESULT == 2002 && CONFIG("世代別表示")
	M -= 1
	P = 0
	RESTART
ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != RESULT - 1001
	P = RESULT - 1001
	RESTART
ELSEIF RESULT <= MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

SIF RESULT != 0
	TARGET = RESULT
SIF TARGET == ASSI
	ASSI = 0

REDRAW 1
RETURN 1

;CLEARLINE LINECOUNT - MEMO_LINECOUNT
;RESTART

;-------------------------------------------------
;売却の処理
;-------------------------------------------------
@CHARA_SALE, P, M
CALL SHOW_CHARA_LIST_PRE, "売却"
CALL SHOW_CHARA_LIST, "売却", P, M

$INPUT_LOOP
INPUT

IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 2000 && CONFIG("世代別表示")
	M = 0
	P = 0
	RESTART
ELSEIF RESULT == 2001 && CONFIG("世代別表示")
	M += 1
	P = 0
	RESTART
ELSEIF RESULT == 2002 && CONFIG("世代別表示")
	M -= 1
	P = 0
	RESTART
ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != RESULT - 1001
	P = RESULT - 1001
	RESTART
ELSEIF RESULT <= MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF TALENT:RESULT:誓約 || TALENT:RESULT:相思相愛
	IF TALENT:RESULT:相思相愛
		IF TALENT:RESULT:夢魔 || TALENT:RESULT:上位夢魔 || TALENT:RESULT:眷属
			PRINTFORML 你怎么可以卖掉相思相爱的梦魔！
		ELSE
			PRINTFORML 你怎么可以卖掉相思相爱的人！
		ENDIF
	ELSEIF TALENT:RESULT:誓約
		PRINTFORML 誓约后的%NAME:RESULT%不会被卖掉
	ENDIF
	GOTO INPUT_LOOP
ELSEIF COND("売却可能", RESULT) == 0
	PRINTFORML 尚未满足可以出售的条件
	GOTO INPUT_LOOP
ELSE
	LOCAL = TARGET
	LOCAL:1 = RESULT
	TARGET = RESULT

	;内訳の表示
	PRINTFORML ～价格计算的详细～
	CALL PRIZE_CHARA, 2
	PRINTFORML 综合评价：{CFLAG:44}pt
	PRINTFORML 　　估价：%MONEY_C(CFLAG:40)%
	PRINTFORML 真的要卖掉[{TARGET}]%CALLNAME:TARGET%吗？
	PRINTL  [ 0]卖掉
	PRINTL [100]我再考虑一下
	CALL INPUT_SELECT, 1, 100
	IF RESULT == 100
		TARGET = LOCAL
		RESTART
	ENDIF

	LOCAL:2 = 0
	;目標金額に到達したか
	SIF MONEY < NUM("目標金額")
		LOCAL:2 = 1

	;売却処理・口上表示デバグ
	DRAWLINE
	CALL SELF_KOJO, "売却した"
	DRAWLINE
	PRINTFORMW 就这样，以%MONEY_C(CFLAG:40)%的价格，卖掉了%CALLNAME:TARGET%。
	MONEY += CFLAG:40

	TARGET = LOCAL

	CALL DELETE_CHARA, LOCAL:1

	;目標金額に到達したか
	IF MONEY >= NUM("目標金額") && LOCAL:2
		PRINTL
		; PRINTW ☆ＥＮＤ条件達成-SHOP画面[500]選択でENDINGとなります-☆
		PRINTW ☆达成ＥＮＤ条件 -在主菜单选择[600]就可以观看ENDING了-☆
	ENDIF
ENDIF

;能力×素質＝＄表示
@PRIZE_CHARA, ARG
;ARG = 0 テキスト表示なし
;ARG = 1 (能力)×(素質) = 表示
;ARG = 2 売却時の表示
;CFLAG:41に能力による価値が代入される
CALL CALC, "売却価格：能力", ARG
;CFLAG:43に獲得or喪失しうる素質による価値の倍率が代入される
CALL CALC, "売却価格：変動素質", ARG
;CFLAG:42に殆ど変動のない素質による価値の倍率が代入される
CALL CALC, "売却価格：基礎", ARG

IF ARG == 1
	SELECTCASE CFLAG:41
	;100万未満
	CASE IS < 1000000
		PRINTFORM  {CFLAG:41, 8}
	;100万以上、100億未満
	CASE IS < 1000000 * 10000
		PRINTFORM  {CFLAG:41/10000, 6}万
	;100億以上、100兆未満
	CASE IS < 1000000 * 10000 * 10000
		PRINTFORM  {CFLAG:41/(10000 * 10000), 6}亿
	;100兆以上、100京未満
	CASE IS < 1000000 * 10000 * 10000 * 10000
		PRINTFORM  {CFLAG:41/(10000 * 10000 * 10000), 6}兆
	CASEELSE
		PRINTFORM  {CFLAG:41/(10000 * 10000 * 10000 * 10000), 6}京
	ENDSELECT

	PRINTFORM ×{CFLAG:42 / 1000, 2}.{CFLAG:42%1000/100}{CFLAG:42%100/10}
	PRINTFORM ×{CFLAG:43 / 1000, 2}.{CFLAG:43%1000/100}{CFLAG:43%100/10}＝

	SELECTCASE CFLAG:44
	;100万未満
	CASE IS < 1000000
		PRINTFORM {CFLAG:44, 8}
	;100万以上、100億未満
	CASE IS < 1000000 * 10000
		PRINTFORM {CFLAG:44/10000, 6}万
	;100億以上、100兆未満
	CASE IS < 1000000 * 10000 * 10000
		PRINTFORM {CFLAG:44/(10000 * 10000), 6}亿
	;100兆以上、100京未満
	CASE IS < 1000000 * 10000 * 10000 * 10000
		PRINTFORM {CFLAG:44/(10000 * 10000 * 10000), 6}兆
	CASEELSE
		PRINTFORM {CFLAG:44/(10000 * 10000 * 10000 * 10000), 6}京
	ENDSELECT

	PRINTFORM pt → %MONEY_C(CFLAG:40, MONEY:1)%
ENDIF

;-------------------------------------------------
;コスチュームの一覧表示
;-------------------------------------------------
@COSPLAY_LIST, P, M
DRAWLINE
PRINTL □ COS服一览 □---------------------------------------------------------
CALL SHOW_CHARA_LIST, "コス確認", P, M

IF Z <= CONFIG("表示人数") || CONFIG("表示人数") == 0
	WAIT
	RETURN 0
ENDIF

$INPUT_LOOP
INPUT
IF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != RESULT - 1001
	P = RESULT - 1001
	RESTART
;100以外でもSHOP画面に戻します
ELSE
	RETURN 0
ENDIF


;-------------------------------------------------
;お気に入りの設定をする
;-------------------------------------------------
@SET_CHARA_LOVE, P, M
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "お気に入り"
CALL SHOW_CHARA_LIST, "お気に入り", P, M

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	FOR LCOUNT, 1, CHARANUM
		SELECTCASE LCOUNT
		CASE MASTER
			SIF TFLAG:0 == TALENT:MASTER:弱点 && TFLAG:1 == TALENT:MASTER:性癖
				CONTINUE
		CASEELSE
			SIF TFLAG:LCOUNT == TALENT:LCOUNT:お気に入り
				CONTINUE
		ENDSELECT
		PRINTW 变更已完成，本周以内这项设定已经不能再进行变更了
		FLAG:208 = 7
		BREAK
	NEXT
	REDRAW 1
	RETURN 0
ELSEIF CHOICE == 2000 && CONFIG("世代別表示")
	M = 0
	P = 0
ELSEIF CHOICE == 2001 && CONFIG("世代別表示")
	M += 1
	P = 0
ELSEIF CHOICE == 2002 && CONFIG("世代別表示")
	M -= 1
	P = 0
ELSEIF CHOICE >= 1001 && CHOICE <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != CHOICE - 1001
	P = CHOICE - 1001
	RESTART
ELSEIF CHOICE == 0
	CALL SETCONFIG, "弱点"
ELSEIF CHOICE == 1
	CALL SETCONFIG, "性癖"
ELSEIF CHOICE < MASTER || CHOICE >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF TALENT:CHOICE:お気に入り
	TALENT:CHOICE:お気に入り = 0
ELSE
	TALENT:CHOICE:お気に入り = 1
	PRINTFORMW 已将%NAME:CHOICE%标记为偏爱角色
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;ステータス表示（Pは初期値0）
@CAFE_LIST, P, M
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE, 2

MEMO_LINECOUNT = LINECOUNT

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "喫茶店"
CALL SHOW_CHARA_LIST, "喫茶店", P, M

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	REDRAW 1
	RETURN 0
ELSEIF CHOICE == 200
ELSEIF CHOICE == 2000 && CONFIG("世代別表示")
	M = 0
	P = 0
	RESTART
ELSEIF CHOICE == 2001 && CONFIG("世代別表示")
	M += 1
	P = 0
	RESTART
ELSEIF CHOICE == 2002 && CONFIG("世代別表示")
	M -= 1
	P = 0
	RESTART
ELSEIF CHOICE >= 1001 && CHOICE <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != CHOICE - 1001
	P = CHOICE - 1001
	RESTART
ELSEIF CHOICE < MASTER || CHOICE >= CHARANUM || COND("バイト可能", CHOICE) == 0
	GOTO INPUT_LOOP
ENDIF

IF CHOICE == 200
	PRINTFORML 全员服务态度指示（特殊服务上限设定）。
ELSE
	PRINTFORML 请对%NAME:CHOICE%服务态度（特殊服务上限设定）指示。
ENDIF
PRINTFORML  [ 0] 想干什么都可以
PRINTFORML  [ 1] 最多只能到口交
PRINTFORML  [ 2] 最多只能摸摸(例如袭胸)
PRINTFORML  [ 3] 最多只能露胖次
PRINTFORML  [10] 全面禁止不洁行为
; PRINTFORML  [99] バイトはさせない
PRINTFORML  [99] 不在咖啡店打工
PRINTFORML [100] 不作出更改

CALL INPUT_SELECT, 4, 10, 99, 100
CHOICE:1 = RESULT

IF CHOICE:1 < 100
	IF CHOICE == 200
		FOR LCOUNT, 2, CHARANUM
			SIF COND("一晩開放可能", LCOUNT)
				CFLAG:LCOUNT:31 = CHOICE:1
		NEXT
	ELSE
		CFLAG:CHOICE:31 = CHOICE:1
	ENDIF
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;一晩開放許可の設定をする
;-------------------------------------------------
@SET_CHARA_NIGHTWALKER, P, M
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE, 2

MEMO_LINECOUNT = LINECOUNT

REDRAW 0
CALL SHOW_CHARA_LIST_PRE, "一晩開放許可"
CALL SHOW_CHARA_LIST, "一晩開放許可", P, M

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	REDRAW 1
	RETURN 0
ELSEIF CHOICE == 200
ELSEIF CHOICE == 2000 && CONFIG("世代別表示")
	M = 0
	P = 0
	RESTART  ; original edition missing this
ELSEIF CHOICE == 2001 && CONFIG("世代別表示")
	M += 1
	P = 0
	RESTART  ; original edition missing this
ELSEIF CHOICE == 2002 && CONFIG("世代別表示")
	M -= 1
	P = 0
	RESTART  ; original edition missing this
ELSEIF CHOICE >= 1001 && CHOICE <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != CHOICE - 1001
	P = CHOICE - 1001
	RESTART
ELSEIF CHOICE <= MASTER || CHOICE >= CHARANUM || COND("一晩開放可能", CHOICE) == 0
	GOTO INPUT_LOOP
ENDIF

PRINTFORML 什么情况下允许外出？
PRINTFORML  [ 0] 禁止外出
PRINTFORML  [ 1] 想外出就可以去
PRINTFORML  [ 2] 避孕状态下可以外出
PRINTFORML  [ 3] 容易怀上的日子禁止外出（安全日 or 避孕状态的话就ＯＫ）
PRINTFORML  [ 4] 只在容易怀上的日子允许外出（危险日 or 发情日就ＯＫ，怀孕时禁止）
CALL INPUT_SELECT, 5
CHOICE:1 = RESULT

IF CHOICE == 200
	FOR LCOUNT, 2, CHARANUM
		SIF COND("一晩開放可能", LCOUNT)
			CFLAG:LCOUNT:49 = CHOICE:1
	NEXT
ELSE
	CFLAG:CHOICE:49 = CHOICE:1
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;乳首選択
;-------------------------------------------------
@RESELECT_ERECT, P, M
#DIM LCOUNT
#DIM CHOICE
#DIMS CHOICES, 2
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
VARSET CHOICES

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "乳首選択"
CALL SHOW_CHARA_LIST, "乳首選択", P, M

$INPUT_LOOP
INPUTS

SPLIT RESULTS, "/", CHOICES

CHOICE = TOINT(CHOICES:1)
IF CHOICE >= CHARANUM
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

SELECTCASE CHOICES
CASE "乳首肥大無し"
		CLEARBIT TALENT:CHOICE:乳首, 0
		SETBIT TALENT:CHOICE:乳首, 1
CASE "乳首肥大LV１"
		CLEARBIT TALENT:CHOICE:乳首, 1
		SETBIT TALENT:CHOICE:乳首, 2
CASE "乳首肥大LV２"
		CLEARBIT TALENT:CHOICE:乳首, 2
		SETBIT TALENT:CHOICE:乳首, 3
CASE "乳首肥大LV３"
		CLEARBIT TALENT:CHOICE:乳首, 3
		SETBIT TALENT:CHOICE:乳首, 0

CASE "普通乳首"
		CLEARBIT TALENT:CHOICE:乳首, 10
		SETBIT TALENT:CHOICE:乳首, 11
CASE "仮性陥没"
		CLEARBIT TALENT:CHOICE:乳首, 11
		SETBIT TALENT:CHOICE:乳首, 12
CASE "真性陥没"
		CLEARBIT TALENT:CHOICE:乳首, 12
		SETBIT TALENT:CHOICE:乳首, 10

CASE "巨乳輪無し"
		CLEARBIT TALENT:CHOICE:乳首, 20
		SETBIT TALENT:CHOICE:乳首, 21
CASE "巨乳輪LV１"
		CLEARBIT TALENT:CHOICE:乳首, 21
		SETBIT TALENT:CHOICE:乳首, 22
CASE "巨乳輪LV２"
		CLEARBIT TALENT:CHOICE:乳首, 22
		SETBIT TALENT:CHOICE:乳首, 23
CASE "巨乳輪LV３"
		CLEARBIT TALENT:CHOICE:乳首, 23
		SETBIT TALENT:CHOICE:乳首, 20

;それ以外は数値入力へ変換
CASEELSE
	RESULT = TOINT(CHOICES)
	IF RESULT == 100
		SETBIT FLAG:11, 2
		REDRAW 1
		RETURN 0
	ELSEIF RESULT == 2000 && CONFIG("世代別表示")
		M = 0
		P = 0
		RESTART  ; original edition may missing this
	ELSEIF RESULT == 2001 && CONFIG("世代別表示")
		M += 1
		P = 0
		RESTART  ; original edition may missing this
	ELSEIF RESULT == 2002 && CONFIG("世代別表示")
		M -= 1
		P = 0
		RESTART  ; original edition may missing this
	ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != RESULT - 1001
		P = RESULT - 1001
		RESTART  ; original edition may missing this
	ENDIF
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;恋慕or淫乱にしたキャラを一覧表示(名前の異なる上位夢魔などにも個別対応)
;-------------------------------------------------
@SHOW_CHARA_CAPTURED
#DIM LCOUNT
#DIM LCOUNT_2
#DIM NEXT_LINE
; PRINTL □ クリアキャラ一覧 □---------------------------------------------------------
PRINTL □ 攻略角色一览 □---------------------------------------------------------

;改行の判定
NEXT_LINE = 0

FOR LCOUNT, 1, 99
	VARSET LOCALS
	FOR LCOUNT_2, 0, 5
		SIF GETBIT(FLAG:(1099 + LCOUNT), LCOUNT_2) == 0
			CONTINUE

		LOCALS = %NAMES("種族", LCOUNT, LCOUNT_2)%
		;同じ名前なら飛ばす
		SIF LOCALS == LOCALS:1 || LOCALS == LOCALS:2 || LOCALS == LOCALS:3 || LOCALS == LOCALS:4 || LOCALS == LOCALS:5
			CONTINUE

		SIF FLAG:(1200 + LCOUNT) >= 1
			SETCOLOR DEF_COLOR("イエロー")
		PRINTFORM  %TEXT_LJ(LOCALS, 16)%
		SIF FLAG:(1200 + LCOUNT) >= 1
			RESETCOLOR

		SIF NEXT_LINE%4 == 3
			PRINTL
		NEXT_LINE += 1
		LOCALS:(LCOUNT_2 + 1) = %LOCALS%
	NEXT
NEXT

;最後のループで改行してなかったら改行
SIF NEXT_LINE%4
	PRINTL
WAIT
