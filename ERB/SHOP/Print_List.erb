;-------------------------------------------------
;キャラを羅列する表示はここで一括管理。種類はARGSで区別する。
;LOCAL:LCOUNTでLOCAL番のキャラが条件クリアかどうかを管理する。
;-------------------------------------------------
@SHOW_CHARA_LIST, ARGS, ARG, ARG:1
#DIM LCOUNT

IF ARGS != "コス確認" && CONFIG("世代別表示")
	IF ARG:1 == 0
		PRINTL [2001]子供の表示(第一世代)
	ELSEIF ARG:1 == 1
		PRINTL [2000]嫁の表示
		PRINTL [2001]次世代の子供の表示	
	ELSEIF ARG:1 > 1
		PRINTL [2000]嫁の表示
		PRINTL [2001]次世代の子供の表示	
		PRINTL [2002]前世代の子供の表示	
	ENDIF
ENDIF

VARSET LOCAL
FOR LCOUNT, 1, CHARANUM
	IF ARGS != "コス確認" && CONFIG("世代別表示")
		IF ARG:1 == 0
			SIF BASE:LCOUNT:世代
				CONTINUE
		ELSE
			SIF BASE:LCOUNT:世代 != ARG:1
				CONTINUE
		ENDIF
	ENDIF
	SELECTCASE ARGS
	CASE "助手変更"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "調教対象変更"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "売却"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "コス確認"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "探索"
		SIF LCOUNT > MASTER && COND("助手可能", LCOUNT) == 0
			CONTINUE
	CASE "お気に入り"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "喫茶店"
		SIF COND("バイト可能", LCOUNT) == 0
			CONTINUE
	CASE "アナライズ"
		SIF LCOUNT == MASTER
			CONTINUE
	CASE "一晩開放許可"
		SIF COND("一晩開放可能", LCOUNT) == 0
			CONTINUE
	ENDSELECT
	LOCAL:LCOUNT = 1
NEXT

Z = 0
DRAWLINE
FOR LCOUNT, 1, CHARANUM
	SIF LOCAL:LCOUNT == 0
		CONTINUE
	IF LCOUNT > MASTER
		Z += 1
		SIF (Z-1 < CONFIG("表示人数")*ARG || Z > CONFIG("表示人数")*(ARG+1)) && CONFIG("表示人数")
			CONTINUE
	ENDIF

	SELECTCASE ARGS
	CASE "能力表示"
		IF LCOUNT == MASTER
			IF CONFIG("能力表示で３サイズ閲覧") == 0 || TALENT:MASTER:オトコ
				PRINTFORM 　[{MASTER, 2}]
				IF STATE("悪酔い", MASTER)
					SETCOLOR DEF_COLOR("青")
				ELSEIF STATE("ドランク", MASTER)
					SETCOLOR DEF_COLOR("ハートピンク")
				ENDIF
				PRINTFORM %TEXT_LJ(NAME:MASTER, 16)%
				RESETCOLOR
			ELSE
				PRINTFORM %BL(22)%
			ENDIF
			IF FLAG:31
				PRINTFORM  [999]クリア履歴 
			ELSE
				PRINTFORM  %BL(16)%
			ENDIF
			IF CONFIG("３サイズ：表示")
				PRINTFORM [200]\@ CONFIG("能力表示で３サイズ閲覧") ? 体力等表示 # ３size表示 \@ %BL(12)%
			ELSE
				PRINTFORM %BL(28)%
			ENDIF
			SIF CONFIG("能力表示で３サイズ閲覧") == 0 && TALENT:MASTER:オトコ == 0
				CALL PRINT_N, LCOUNT
			PRINTL 
			IF CONFIG("能力表示で３サイズ閲覧") == 0 || TALENT:MASTER:オトコ
				DRAWLINE
				CONTINUE
			ENDIF
		ENDIF
		IF CONFIG("能力表示で３サイズ閲覧")
			CALL PRINT_NUM_NAME, "３サイズ閲覧", LCOUNT
		ELSE
			CALL PRINT_NUM_NAME, "能力表示", LCOUNT
		ENDIF
	CASE "調教対象変更"
		IF CONFIG("調教対象変更でアナライズ")
			CALL PRINT_NUM_NAME, "アナライズ", LCOUNT
		ELSE
			CALL PRINT_NUM_NAME, "調教対象変更", LCOUNT
		ENDIF
	CASE "お気に入り", "名前変更"
		CALL PRINT_NUM_NAME, "体力", LCOUNT
	CASEELSE
		CALL PRINT_NUM_NAME, ARGS, LCOUNT
	ENDSELECT
NEXT

;選択肢の表示
IF CONFIG("表示人数") && Z > CONFIG("表示人数")
	IF ARG
		PRINTFORM [{1000 + ARG}]前のpageへ　
	ELSE
		PRINTPLAINFORM [----]前のpageへ　
	ENDIF
ENDIF

SIF ARGS != "コス確認" || (CONFIG("表示人数") && Z > CONFIG("表示人数"))
	PRINTFORM  [100]戻る

SELECTCASE ARGS
CASE "喫茶店"
	SIF CHARA_NUM("バイト可能") >= 2
		PRINTFORM  [200]全キャラ一括指示
CASE "一晩開放許可"
	SIF CHARA_NUM("一晩開放可能") >= 2
		PRINTFORM  [200]全キャラ一括指示
CASE "能力表示"
	SIF CHARA_NUM("助手可能") && CONFIG("調教相手変更不可") == 0
		PRINTFORM  [109]助手変更
	SIF CHARA_NUM("控えキャラ") && CONFIG("調教相手変更不可") == 0
		PRINTFORM  [110]調教対象変更
CASE "助手変更"
	IF CONFIG("探索同行者選択中") == 0
		PRINTFORM  [101]能力の表示
		SIF CHARA_NUM("控えキャラ") && CONFIG("調教相手変更不可") == 0
			PRINTFORM  [110]調教対象変更
	ENDIF
CASE "調教対象変更"
	PRINTFORM  [101]能力の表示
	SIF CHARA_NUM("助手可能") && CONFIG("調教相手変更不可") == 0
		PRINTFORM  [109]%TEXTS("助手の名称")%変更
ENDSELECT

SIF CONFIG("表示人数") && Z > CONFIG("表示人数") * (ARG + 1)
	PRINTFORM 　[{1000 + ARG + 2}]次のpageへ

PRINTL 

;全ページ数
IF CONFIG("表示人数")
	Q = (Z / CONFIG("表示人数")) + 1
ELSE
	Q = 1
ENDIF

@SHOW_CHARA_LIST_PRE, ARGS
PRINTL 
SELECTCASE ARGS
CASE "喫茶店", "探索", "乳首選択"
CASEELSE
	SIF TARGET || (ASSI && TARGET <= 0)
		PRINTFORM 現在
	SIF TARGET
		PRINTFORM [{TARGET}]%NAME:TARGET%の相手をしていて、
	IF ASSI
		PRINTFORM [{ASSI}]%NAME:ASSI%が%TEXTS("助手の名称")%です
	ELSEIF TARGET
		PRINTFORM %TEXTS("助手の名称")%はつけていません
	ENDIF
	SIF ARGS == "売却"
		PRINTFORM  (所持金:%MONEY_C(MONEY)%)
	PRINTL 
ENDSELECT

SELECTCASE ARGS
CASE "能力表示"
	PRINTL 誰の能力を見ますか？
	IF CHARA_NUM("ドランク")
		PRINT ちなみに
		CALL PRINT_COLORTEXT, "色が青いキャラは悪酔い中", DEF_COLOR("青")
		PRINT 、
		CALL PRINT_COLORTEXT, "ピンクのキャラはドランク中", DEF_COLOR("ハートピンク")
		PRINTL です
	ENDIF
	PRINTL 能力表示画面の各項目(□ 素質 □など)はクリックすることにより出し入れできます
CASE "助手変更"
	IF CONFIG("助手への指示")
		PRINTL []の数だけ指示を与えることが出来ます。
		PRINTL 誰の指示を変更しますか？
	ELSE
		PRINTFORML 誰を%TEXTS("助手の名称")%にしますか？（選択済みのキャラを選ぶと%TEXTS("助手の名称")%無しになります）
		IF CONFIG("助手の探索能力表示")
			PRINTFORML %TEXTS("助手の名称")%の能力(財宝発見など)はクリックすることで効果の解説を見る事が出来ます
		ELSE
			PRINTFORML %TEXTS("助手の名称")%の能力(調合など)はクリックすることで効果の解説を見る事が出来ます
			PRINTFORML （覚えているアルカナを確認するには 魔法使い or アルカナ をクリックして下さい）
		ENDIF
	ENDIF
	DRAWLINE
	LOCALS = %TEXTS("助手の名称")%なし
	PRINTFORM 　[ 0]%TEXT_LJ(LOCALS, 16)% 
	IF CONFIG("探索同行者選択中") == 0
		IF CHARA_NUM("指示可能な助手")
			PRINTFORM [200]\@ CONFIG("助手への指示") ? 変更画面 # 指示画面 \@ 
		ELSE
			CLEARBIT FLAG:45, 0
		ENDIF
		PRINTFORM [300]\@ CONFIG("助手の探索能力表示") ? 調教時の能力 # 探索時の能力 \@ 
		PRINTFORM [400]%TEXTS("助手の名称")%の名称の変更
	ENDIF
	PRINTL 
CASE "調教対象変更"
	IF CONFIG("調教対象変更でアナライズ")
		PRINTL 誰を調教しますか？（強さは100点満点で、50点ならば主人と互角です）
		DRAWLINE
		PRINTFORM  [200]通常表示に戻す
		PRINTPLAINFORM 　欲求不満　 好感度　 放置　 敏感度　 攻撃力　主人基準での強さ
	ELSE
		PRINTL 誰を調教しますか？（☆マークはお気に入りのしるしです）
		DRAWLINE
		PRINTFORM  [200]欲求不満度、好感度、放置期間、具体的な強さ…等を表示する
	ENDIF
	PRINTL 
CASE "売却"
	PRINTL 誰を売りますか？
	PRINTL 計算式は 能力×変動しない素質×変動する素質＝評価pt→価格 となっています
CASE "探索"
	PRINTFORML 主人か%TEXTS("助手の名称")%に限り探索に出かける事ができます。誰が赴きますか？
	IF ASSI
		PRINTPLAINFORM （[ 1]%NAME:MASTER%を選択した際には、
		SETCOLOR DEF_COLOR("イエロー")
		PRINTPLAINFORM %TEXTS("助手の名称")%[{ASSI, 2}]%NAME:ASSI%
		RESETCOLOR
		PRINTL も同行します）
		PRINTFORML %TEXTS("助手の名称")%の能力(財宝発見など)はクリックすることで効果の解説を見る事が出来ます
	ENDIF
	;食器で回復量UP
	IF ITEM:ミスリルのティーカップ
		PRINTFORML ミスリルのティーカップを所持しているので、戦闘終了時の休憩の効果が6Ｐ高まっています。
	ELSEIF ITEM:ミスリルのスプーン
		PRINTFORML ミスリルのスプーンを所持しているので、戦闘終了時の休憩の効果が5Ｐ高まっています。
	ELSEIF ITEM:銀のティーカップ
		PRINTFORML 銀のティーカップを所持しているので、戦闘終了時の休憩の効果が4Ｐ高まっています。
	ELSEIF ITEM:銀のスプーン
		PRINTFORML 銀のスプーンを所持しているので、戦闘終了時の休憩の効果が3Ｐ高まっています。
	ELSEIF ITEM:陶磁器のティーカップ
		PRINTFORML 陶磁器のティーカップを所持しているので、戦闘終了時の休憩の効果が2Ｐ高まっています。
	ELSEIF ITEM:陶磁器のスプーン
		PRINTFORML 陶磁器のスプーンを所持しているので、戦闘終了時の休憩の効果が1Ｐ高まっています。
	ENDIF
CASE "お気に入り"
	PRINTFORML 誰をお気に入りにしますか？
	PRINTFORML ※設定を変更すると、一週間変更できなくなります※
	PRINTFORML (お気に入りは☆で記します。その相手からは、快楽を多めに受けることでしょう…)
	PRINTFORML また、[ 0]や[ 1]を入力する事で、%CALLNAME:MASTER%の弱点や性癖を設定する事も出来ます
	PRINTFORML (性癖にストライクな相手からは、これまた受ける快楽が多めとなります)
	PRINTFORML 
	PRINTFORM 　[ 0] 弱点を設定する　現在の弱点:
	SELECTCASE NUM_BIT(TALENT:MASTER:弱点)
	CASE NUM("弱点")
		PRINTFORM [全てに弱い]
	CASE IS >= 4
		PRINTFORM いっぱい
	CASE 0
		PRINTFORM 無し
	CASEELSE
		FOR LOCAL, 0, NUM("弱点")
			SIF GETBIT(TALENT:MASTER:弱点, LOCAL)
				PRINTFORM %NAMES("弱点", LOCAL)%
		NEXT
	ENDSELECT
	PRINTL 

	PRINTFORM 　[ 1] 性癖を設定する　現在の性癖:
	SELECTCASE NUM_BIT(TALENT:MASTER:性癖)
	CASE IS >= 6
		PRINTFORM いっぱい
	CASE 0
		PRINTFORM 無し
	CASEELSE
		FOR LOCAL, 0, NUM("属性")
			SIF GETBIT(TALENT:MASTER:性癖, LOCAL)
				PRINTFORM %NAMES("属性", LOCAL)%
		NEXT
	ENDSELECT
	PRINTL 
CASE "名前変更"
	PRINTL 誰の名前を変更しますか？
CASE "喫茶店"
	PRINTL ～店員名簿～
	PRINTL 勤務態度を指示しますか？
CASE "アナライズ"
	PRINTL 誰をアナライズしますか？
CASE "一晩開放許可"
	PRINTL 誰の外出を許可しますか？
CASE "乳首選択"
	PRINTFORML 肥大、陥没、乳輪の下の列の文字をクリックする事で、
	PRINTFORML 乳首肥大、乳首の陥没具合、乳輪の大きさを変化させることができます。
	PRINTFORML １クリックに付き１LV変化するのでカチカチ選択して調整してください。
	DRAWLINE
	PRINTFORML 　　　　　　　　　　　　肥大 陥没 乳輪
ENDSELECT

;-------------------------------------------------
;ステータス表示（Pは初期値0）
;-------------------------------------------------
@SHOW_CHARADATA, P, M
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "能力表示"
CALL SHOW_CHARA_LIST, "能力表示", P, M

$INPUT_LOOP
INPUT
IF RESULT == 100
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 109 && CHARA_NUM("助手可能") && CONFIG("調教相手変更不可") == 0
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	CALL SELECT_ASSI, 0, 0
	RETURN 0
ELSEIF RESULT == 110 && CHARA_NUM("控えキャラ") && CONFIG("調教相手変更不可") == 0
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	CALL CHANGE_TARGET, 0, 0
	RETURN 0
ELSEIF RESULT == 200 && CONFIG("３サイズ：表示")
	INVERTBIT FLAG:46, 0
	RESTART
;クリアキャラ表示（暫定）
ELSEIF RESULT == 999 && FLAG:31
	CALL SHOW_CHARA_CAPTURED
	RESTART
ELSEIF RESULT == 2000 && CONFIG("世代別表示")
	M = 0
	P = 0
	RESTART
ELSEIF RESULT == 2001 && CONFIG("世代別表示")
	M += 1
	P = 0
	RESTART
ELSEIF RESULT == 2002 && CONFIG("世代別表示")
	M -= 1
	P = 0
	RESTART
ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != RESULT - 1001
	P = RESULT - 1001
	RESTART
ELSEIF RESULT < MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

CALL NEW_PRINT_STATE, RESULT, "通常"

RESTART

;-------------------------------------------------
;助手選択
;-------------------------------------------------
@SELECT_ASSI, P, M
#DIM LCOUNT
#DIM CHOICE
#DIMS CHOICES, 2
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
VARSET CHOICES

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "助手変更"
CALL SHOW_CHARA_LIST, "助手変更", P, M

$INPUT_LOOP
INPUTS

SPLIT RESULTS, "/", CHOICES

SELECTCASE CHOICES
CASE "相性:☆"
	IF TARGET
		PRINTFORML %CALLNAME:TARGET%との相性が最高に良いです！
		PRINTFORMW (コマンドの実行値に30のボーナスが付きます)
	ENDIF
CASE "相性:◎"
	IF TARGET
		PRINTFORML %CALLNAME:TARGET%との相性が素晴らしく良いです！
		PRINTFORMW (コマンドの実行値に15のボーナスが付きます)
	ENDIF
CASE "相性:〇"
	IF TARGET
		PRINTFORML %CALLNAME:TARGET%との相性が良いです
		PRINTFORMW (コマンドの実行値に５のボーナスが付きます)
	ENDIF
CASE "針"
	PRINTFORMW 針コマンドの効果が上がります
CASE "調合"
	PRINTFORML 媚薬コマンドの効果が著しく上がります
	PRINTFORMW また、淫魔の雫を調合して様々な薬物を作ることができます
CASE "サド"
	PRINTFORMW 俗に言うＳＭにおけるＳなコマンドや行為を実行するのが得意です
CASE "妖狐"
	PRINTFORMW 調教終了時に媚薬を一つ貰えます
CASE "スライム"
	PRINTFORMW 調教終了時にローションを一つ貰えます
CASE "ドライアド"
	PRINTFORMW 調教終了時にジャスミンを一つ貰えます
CASE "治療"
	PRINTFORMW 時間経過ごとに調教対象の体力が500多く回復します
CASE "鼓舞"
	PRINTFORML %TEXTS("助手の名称")%が、がんばれがんばれできるできると横で応援してくれます
	PRINTFORMW (コマンドの実行値に10のボーナスが付きます)
CASE "ふたなり"
	PRINTFORMW ふたなりです
CASE "♂"
	PRINTFORMW オトコです
CASE "禁断の知識"
	PRINTFORML お店で危険な商品を売ってもらえるようになります
	PRINTFORMW また、調合知識と同様に媚薬の効果が強くなります
CASE "具現"
	PRINTFORMW 女性にペニスを生やすことができます
CASE "大魔法使い"
	PRINTFORMW なんと全てのアルカナを使う事が出来ます。魔法使いとしては最強と言って良いでしょう
CASE "魔法使い"
	PRINTFORML 魔導書を読むことでアルカナを覚える事が出来ます。また、消費精力も少なく済みます
	PRINTFORML -%CALLNAME:TOINT(CHOICES:1)%が覚えているアルカナの一覧-
	FOR LCOUNT, 230, 260
		SIF TALENT:TOINT(CHOICES:1):LCOUNT
			PRINTFORML %TEXT_LJ(TALENT_NAME(LCOUNT, TALENT:TOINT(CHOICES:1):LCOUNT, TOINT(CHOICES:1)), 20)% %TEXTS("アルカナ解説", LCOUNT)%
	NEXT
	WAIT
CASE "アルカナ"
	PRINTFORML 専門家ではありませんが、アルカナを使う事が出来ます
	PRINTFORML -%CALLNAME:TOINT(CHOICES:1)%が覚えているアルカナの一覧-
	FOR LCOUNT, 230, 260
		SIF TALENT:TOINT(CHOICES:1):LCOUNT
			PRINTFORML %TEXT_LJ(TALENT_NAME(LCOUNT, TALENT:TOINT(CHOICES:1):LCOUNT, TOINT(CHOICES:1)), 20)% %TEXTS("アルカナ解説", LCOUNT)%
	NEXT
	WAIT
CASE "愛撫・奉仕知識"
	PRINTFORML 奉仕や愛撫によって与える快感を５割増しにし、恐怖や反感などのマイナスパラメータを半減させます
	PRINTFORMW ただし、%TEXTS("助手の名称")%自身が相手を調教しないと意味がありません
CASE "教え上手"
	PRINTFORML 奉仕される際に相手よりも技巧LVなどが高いと、相手が獲得する経験を各+１させます
	PRINTFORML また、調教対象の獲得する奉仕快楽経験を1.5倍にします
	PRINTFORMW ただし、%TEXTS("助手の名称")%自身が相手を調教しないと意味がありません
CASE "ＳＭ知識"
	PRINTFORML スパンキングなど、ＳＭプレイに属するコマンドの実行時にのみ効果がある知識です
	PRINTFORML 欲情と屈服のパラメーターを５割増しにし、恐怖や反感を半減させます
	PRINTFORML また、相手の獲得する嗜虐快楽経験と被虐快楽経験を1.5倍にします
	PRINTFORMW ただし、%TEXTS("助手の名称")%自身が相手を調教しないと意味がありません
CASE "Ａ調教知識"
	PRINTFORML アナルを弄るコマンド実行時に快Ａや欲情のパラメーターを５割増しにし、
	PRINTFORML 苦痛や反感などのマイナスパラメーターの発生を半減させます
	PRINTFORMW ただし、%TEXTS("助手の名称")%自身が相手を調教しないと意味がありません
CASE "性器拡張知識"
	PRINTFORML 性器を拡張させる際の苦痛や恐怖を半減させ、拡張経験の増加量にボーナスを得ます
	PRINTFORMW ただし、%TEXTS("助手の名称")%自身が相手を調教しないと意味がありません
CASE "道具使い"
	PRINTFORML バイブなど道具を用いたコマンド実行時に快楽や欲情のパラメーターを５割増しにし、
	PRINTFORML 恐怖や反感などのマイナスパラメーターの発生を半減させます
	PRINTFORMW ただし、%TEXTS("助手の名称")%自身が相手を調教しないと意味がありません
CASE "露出知識"
	PRINTFORML 調教対象の恥情のパラメータを常に５割増しにし、露出経験と羞恥快楽経験の獲得量を増やします
	PRINTFORMW この知識は、%TEXTS("助手の名称")%が持っているだけでも効果を発揮します
CASE "甘い言葉"
	PRINTFORMW 甘い言葉を囁くことができます
CASE "手加減"
	PRINTFORMW 手加減を行うことができます
CASE "歌唱"
	PRINTFORMW 様々な効果の歌を演奏することができます

;探索での能力
CASE "遭遇回避"
	CALL PRINT_STR, "夢魔との遭遇率が_黄色_５％減少_します_W"
CASE "遭遇増加"
	CALL PRINT_STR, "夢魔との遭遇率が_青_５％上昇_します_W"
CASE "罠解除"
	CALL PRINT_STR, "宝箱を開ける際のトラップ発動率が_黄色_４分の一_になります_W"
CASE "罠誘発"
	CALL PRINT_STR, "宝箱を開ける際のトラップ発動率が_青_2.5倍_になります_W"
CASE "財宝発見"
	CALL PRINT_STR, "宝箱を見つける確率が_黄色_５％上昇_します_W"
CASE "財宝見逃し"
	CALL PRINT_STR, "宝箱を見つける確率が_青_５％減少_します_W"
CASE "階段発見"
	CALL PRINT_STR, "次の階層へのドアを見つける確率が_黄色_５％上昇_します_W"
CASE "回復"
	CALL PRINT_STR, "各行動ごとに精力が_黄色_１Ｐ回復_し、戦闘終了時は_黄色_２Ｐ多く回復_します_W"
CASE "戦闘補助"
	CALL PRINT_STR, "数値の分だけ探索者の戦闘力が増えます_W"
CASE "状態異常無効"
	CALL PRINT_STR, "宝箱の罠での状態異常を無効化します_W"

;それ以外は数値入力へ変換
CASEELSE
	RESULT = TOINT(CHOICES)
	IF RESULT == 100
		REDRAW 1
		RETURN 0
	ELSEIF RESULT == 101 && CONFIG("探索同行者選択中") == 0
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		;1ページ目を表示
		CALL SHOW_CHARADATA, 0, 0
		RETURN 0
	ELSEIF RESULT == 200 && CHARA_NUM("指示可能な助手") && CONFIG("探索同行者選択中") == 0
		INVERTBIT FLAG:45, 0
		RESTART
	ELSEIF RESULT == 300 && CONFIG("探索同行者選択中") == 0
		CLEARBIT FLAG:45, 0
		INVERTBIT FLAG:45, 1
		RESTART
	ELSEIF RESULT == 400
		PRINTFORML いわゆる"助手"の役職名を変更します
		PRINTFORML 現在は『%TEXTS("助手の名称")%』ですが、何に変更しますか？
		PRINTFORML  [ 0] 助手
		PRINTFORML  [ 1] 秘書
		PRINTFORML  [ 2] 副官
		PRINTFORML  [ 3] メイド長
		PRINTFORML  [ 4] 側近
		PRINTFORML  [ 5] 参謀
		PRINTFORML  [99] 直接入力する
		PRINTFORML [100] 変更しない

		CALL INPUT_SELECT, 6, 99, 100

		CHOICE = RESULT

		SELECTCASE RESULT
		CASE 100
			RESTART
		CASE 0
			LOCALS = 助手の名称：助手
		CASE 1
			LOCALS = 助手の名称：秘書
		CASE 2
			LOCALS = 助手の名称：副官
		CASE 3
			LOCALS = 助手の名称：メイド長
		CASE 4
			LOCALS = 助手の名称：側近
		CASE 5
			LOCALS = 助手の名称：参謀
		CASE 99
			PRINTFORML 全角８文字(半角なら16文字)以内で入力してください
			CALL PRINT_STR, "黄色_※推奨は全角で５文字以内です※_L"
			PRINTFORML 何も入力しない場合は変更しません。

			CALL INPUTS_SELECT, 16

			SELECTCASE RESULTS
			CASE ""
				RESTART
			CASEELSE
				LOCALS = 助手の名称：%RESULTS%
			ENDSELECT
		ENDSELECT
		;助手の名称フラグリセット
		CALL DEL_CEVENT_GROUP, "助手の名称", MASTER
		CALL SET_CEVENT, LOCALS, MASTER

		PRINTFORMW 役職名を『%TEXTS("助手の名称")%』としました

		RESTART
	ELSEIF RESULT == 110 && CHARA_NUM("控えキャラ") && CONFIG("調教相手変更不可") == 0 && CONFIG("探索同行者選択中") == 0
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		CALL CHANGE_TARGET, 0, 0
		RETURN 0
	ELSEIF RESULT == 2000 && CONFIG("世代別表示")
		M = 0
		P = 0
		RESTART
	ELSEIF RESULT == 2001 && CONFIG("世代別表示")
		M += 1
		P = 0
		RESTART
	ELSEIF RESULT == 2002 && CONFIG("世代別表示")
		M -= 1
		P = 0
		RESTART
	ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != RESULT - 1001
		P = RESULT - 1001
		RESTART
	ELSEIF RESULT == 0
		ASSI = 0
		REDRAW 1
		RETURN 0
	ELSEIF RESULT < MASTER || RESULT >= CHARANUM
		GOTO INPUT_LOOP
	ELSEIF RESULT == ASSI

	ELSEIF COND("助手可能", RESULT) == 0
		GOTO INPUT_LOOP
	ELSEIF CONFIG("助手への指示") && ABL:RESULT:助手 == 0
		GOTO INPUT_LOOP
	ENDIF

	;指示変更
	IF CONFIG("助手への指示")
		CALL ASSI_CHANGE_INDICATE, RESULT
	;助手変更
	ELSEIF RESULT == ASSI
		ASSI = 0
	ELSE
		ASSI = RESULT
		SIF ASSI == TARGET
			TARGET = 0
	ENDIF
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;調教対象選択
;-------------------------------------------------
@CHANGE_TARGET, P, M
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "調教対象変更"
CALL SHOW_CHARA_LIST, "調教対象変更", P, M

$INPUT_LOOP
INPUT
IF RESULT == 100
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 101
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	;1ページ目を表示
	CALL SHOW_CHARADATA, 0, 0
	RETURN 0
ELSEIF RESULT == 109 && CHARA_NUM("助手可能") && CONFIG("調教相手変更不可") == 0
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	CALL SELECT_ASSI, 0, 0
	RETURN 0
ELSEIF RESULT == 200
	INVERTBIT FLAG:47, 0
	RESTART
ELSEIF RESULT == 2000 && CONFIG("世代別表示")
	M = 0
	P = 0
	RESTART
ELSEIF RESULT == 2001 && CONFIG("世代別表示")
	M += 1
	P = 0
	RESTART
ELSEIF RESULT == 2002 && CONFIG("世代別表示")
	M -= 1
	P = 0
	RESTART
ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != RESULT - 1001
	P = RESULT - 1001
	RESTART
ELSEIF RESULT <= MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

SIF RESULT != 0
	TARGET = RESULT
SIF TARGET == ASSI
	ASSI = 0

REDRAW 1
RETURN 1

;CLEARLINE LINECOUNT - MEMO_LINECOUNT
;RESTART

;-------------------------------------------------
;売却の処理
;-------------------------------------------------
@CHARA_SALE, P, M
CALL SHOW_CHARA_LIST_PRE, "売却"
CALL SHOW_CHARA_LIST, "売却", P, M

$INPUT_LOOP
INPUT

IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 2000 && CONFIG("世代別表示")
	M = 0
	P = 0
	RESTART
ELSEIF RESULT == 2001 && CONFIG("世代別表示")
	M += 1
	P = 0
	RESTART
ELSEIF RESULT == 2002 && CONFIG("世代別表示")
	M -= 1
	P = 0
	RESTART
ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != RESULT - 1001
	P = RESULT - 1001
	RESTART
ELSEIF RESULT <= MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF TALENT:RESULT:誓約 || TALENT:RESULT:相思相愛
	IF TALENT:RESULT:相思相愛
		IF TALENT:RESULT:夢魔 || TALENT:RESULT:上位夢魔 || TALENT:RESULT:眷属
			PRINTFORML 愛する夢魔を売るなんてとんでもない！
		ELSE
			PRINTFORML 愛する人を売るなんてとんでもない！
		ENDIF
	ELSEIF TALENT:RESULT:誓約
		PRINTFORML 誓約により%NAME:RESULT%を売ることはできません
	ENDIF
	GOTO INPUT_LOOP
ELSEIF COND("売却可能", RESULT) == 0
	PRINTFORML 売却条件を満たしていません
	GOTO INPUT_LOOP
ELSE
	LOCAL = TARGET
	LOCAL:1 = RESULT
	TARGET = RESULT

	;内訳の表示
	PRINTFORML ～価格計算の詳細～
	CALL PRIZE_CHARA, 2
	PRINTFORML 総合評価：{CFLAG:44}pt
	PRINTFORML 　評価額：%MONEY_C(CFLAG:40)%
	PRINTFORML 本当に[{TARGET}]%CALLNAME:TARGET%を売りますか？
	PRINTL  [ 0] 売る
	PRINTL [100] やっぱやめる
	CALL INPUT_SELECT, 1, 100
	IF RESULT == 100
		TARGET = LOCAL
		RESTART
	ENDIF

	LOCAL:2 = 0
	;目標金額に到達したか
	SIF MONEY < NUM("目標金額")
		LOCAL:2 = 1

	;売却処理・口上表示デバグ
	DRAWLINE
	CALL SELF_KOJO, "売却した"
	DRAWLINE
	PRINTFORMW 結果、%CALLNAME:TARGET%は%MONEY_C(CFLAG:40)%で売れました。
	MONEY += CFLAG:40

	TARGET = LOCAL

	CALL DELETE_CHARA, LOCAL:1

	;目標金額に到達したか
	IF MONEY >= NUM("目標金額") && LOCAL:2
		PRINTL 
		PRINTW ☆ＥＮＤ条件達成-SHOP画面[500]選択でENDINGとなります-☆
	ENDIF
ENDIF

;能力×素質＝＄表示
@PRIZE_CHARA, ARG
;ARG = 0 テキスト表示なし
;ARG = 1 (能力)×(素質) = 表示
;ARG = 2 売却時の表示
;CFLAG:41に能力による価値が代入される
CALL CALC, "売却価格：能力", ARG
;CFLAG:43に獲得or喪失しうる素質による価値の倍率が代入される
CALL CALC, "売却価格：変動素質", ARG
;CFLAG:42に殆ど変動のない素質による価値の倍率が代入される
CALL CALC, "売却価格：基礎", ARG

IF ARG == 1
	SELECTCASE CFLAG:41
	;100万未満
	CASE IS < 1000000
		PRINTFORM  {CFLAG:41, 8}
	;100万以上、100億未満
	CASE IS < 1000000 * 10000
		PRINTFORM  {CFLAG:41/10000, 6}万
	;100億以上、100兆未満
	CASE IS < 1000000 * 10000 * 10000
		PRINTFORM  {CFLAG:41/(10000 * 10000), 6}億
	;100兆以上、100京未満
	CASE IS < 1000000 * 10000 * 10000 * 10000
		PRINTFORM  {CFLAG:41/(10000 * 10000 * 10000), 6}兆
	CASEELSE
		PRINTFORM  {CFLAG:41/(10000 * 10000 * 10000 * 10000), 6}京
	ENDSELECT

	PRINTFORM ×{CFLAG:42 / 1000, 2}.{CFLAG:42%1000/100}{CFLAG:42%100/10}
	PRINTFORM ×{CFLAG:43 / 1000, 2}.{CFLAG:43%1000/100}{CFLAG:43%100/10}＝

	SELECTCASE CFLAG:44
	;100万未満
	CASE IS < 1000000
		PRINTFORM {CFLAG:44, 8}
	;100万以上、100億未満
	CASE IS < 1000000 * 10000
		PRINTFORM {CFLAG:44/10000, 6}万
	;100億以上、100兆未満
	CASE IS < 1000000 * 10000 * 10000
		PRINTFORM {CFLAG:44/(10000 * 10000), 6}億
	;100兆以上、100京未満
	CASE IS < 1000000 * 10000 * 10000 * 10000
		PRINTFORM {CFLAG:44/(10000 * 10000 * 10000), 6}兆
	CASEELSE
		PRINTFORM {CFLAG:44/(10000 * 10000 * 10000 * 10000), 6}京
	ENDSELECT

	PRINTFORM pt → %MONEY_C(CFLAG:40, MONEY:1)%
ENDIF

;-------------------------------------------------
;コスチュームの一覧表示
;-------------------------------------------------
@COSPLAY_LIST, P, M
DRAWLINE
PRINTL □ コスチューム一覧 □---------------------------------------------------------
CALL SHOW_CHARA_LIST, "コス確認", P, M

IF Z <= CONFIG("表示人数") || CONFIG("表示人数") == 0
	WAIT
	RETURN 0
ENDIF

$INPUT_LOOP
INPUT
IF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != RESULT - 1001
	P = RESULT - 1001
	RESTART
;100以外でもSHOP画面に戻します
ELSE
	RETURN 0
ENDIF


;-------------------------------------------------
;お気に入りの設定をする
;-------------------------------------------------
@SET_CHARA_LOVE, P, M
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "お気に入り"
CALL SHOW_CHARA_LIST, "お気に入り", P, M

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	FOR LCOUNT, 1, CHARANUM
		SELECTCASE LCOUNT
		CASE MASTER
			SIF TFLAG:0 == TALENT:MASTER:弱点 && TFLAG:1 == TALENT:MASTER:性癖
				CONTINUE
		CASEELSE
			SIF TFLAG:LCOUNT == TALENT:LCOUNT:お気に入り
				CONTINUE
		ENDSELECT
		PRINTW 設定を変更したので、一週間の間再設定できなくなりました
		FLAG:208 = 7
		BREAK
	NEXT
	REDRAW 1
	RETURN 0
ELSEIF CHOICE == 2000 && CONFIG("世代別表示")
	M = 0
	P = 0
ELSEIF CHOICE == 2001 && CONFIG("世代別表示")
	M += 1
	P = 0
ELSEIF CHOICE == 2002 && CONFIG("世代別表示")
	M -= 1
	P = 0
ELSEIF CHOICE >= 1001 && CHOICE <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != CHOICE - 1001
	P = CHOICE - 1001
	RESTART
ELSEIF CHOICE == 0
	CALL SETCONFIG, "弱点"
ELSEIF CHOICE == 1
	CALL SETCONFIG, "性癖"
ELSEIF CHOICE < MASTER || CHOICE >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF TALENT:CHOICE:お気に入り
	TALENT:CHOICE:お気に入り = 0
ELSE
	TALENT:CHOICE:お気に入り = 1
	PRINTFORMW %NAME:CHOICE%をお気に入りにしました
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;ステータス表示（Pは初期値0）
@CAFE_LIST, P, M
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE, 2

MEMO_LINECOUNT = LINECOUNT

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "喫茶店"
CALL SHOW_CHARA_LIST, "喫茶店", P, M

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	REDRAW 1
	RETURN 0
ELSEIF CHOICE == 200
ELSEIF CHOICE == 2000 && CONFIG("世代別表示")
	M = 0
	P = 0
	RESTART
ELSEIF CHOICE == 2001 && CONFIG("世代別表示")
	M += 1
	P = 0
	RESTART
ELSEIF CHOICE == 2002 && CONFIG("世代別表示")
	M -= 1
	P = 0
	RESTART
ELSEIF CHOICE >= 1001 && CHOICE <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != CHOICE - 1001
	P = CHOICE - 1001
	RESTART
ELSEIF CHOICE < MASTER || CHOICE >= CHARANUM || COND("バイト可能", CHOICE) == 0
	GOTO INPUT_LOOP
ENDIF

IF CHOICE == 200
	PRINTFORML 全員の勤務態度（エッチな行動の上限等）を指示してください。
ELSE
	PRINTFORML %NAME:CHOICE%の勤務態度（エッチな行動の上限等）を指示してください。
ENDIF
PRINTFORML  [ 0] 好きにさせる
PRINTFORML  [ 1] フェラ程度まで
PRINTFORML  [ 2] おさわり（胸を当てたり）程度まで
PRINTFORML  [ 3] パンチラなど見せる行為程度まで
PRINTFORML  [10] エッチなことは全面禁止
PRINTFORML  [99] バイトはさせない
PRINTFORML [100] 変更しない

CALL INPUT_SELECT, 4, 10, 99, 100
CHOICE:1 = RESULT

IF CHOICE:1 < 100
	IF CHOICE == 200
		FOR LCOUNT, 2, CHARANUM
			SIF COND("一晩開放可能", LCOUNT)
				CFLAG:LCOUNT:31 = CHOICE:1
		NEXT
	ELSE
		CFLAG:CHOICE:31 = CHOICE:1
	ENDIF
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;一晩開放許可の設定をする
;-------------------------------------------------
@SET_CHARA_NIGHTWALKER, P, M
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE, 2

MEMO_LINECOUNT = LINECOUNT

REDRAW 0
CALL SHOW_CHARA_LIST_PRE, "一晩開放許可"
CALL SHOW_CHARA_LIST, "一晩開放許可", P, M

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	REDRAW 1
	RETURN 0
ELSEIF CHOICE == 200
ELSEIF CHOICE == 2000 && CONFIG("世代別表示")
	M = 0
	P = 0
ELSEIF CHOICE == 2001 && CONFIG("世代別表示")
	M += 1
	P = 0
ELSEIF CHOICE == 2002 && CONFIG("世代別表示")
	M -= 1
	P = 0
ELSEIF CHOICE >= 1001 && CHOICE <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != CHOICE - 1001
	P = CHOICE - 1001
	RESTART
ELSEIF CHOICE <= MASTER || CHOICE >= CHARANUM || COND("一晩開放可能", CHOICE) == 0
	GOTO INPUT_LOOP
ENDIF

PRINTFORML どのように条件をつけますか？
PRINTFORML  [ 0] 外出させない
PRINTFORML  [ 1] 好きに外出させる
PRINTFORML  [ 2] 避妊中のみ許可する
PRINTFORML  [ 3] 妊娠しやすい日は外出させない（安全日or避妊中ならＯＫ）
PRINTFORML  [ 4] 妊娠しやすい日のみ許可する（危険日や発情中ならＯＫ。避妊中はダメ）
CALL INPUT_SELECT, 5
CHOICE:1 = RESULT

IF CHOICE == 200
	FOR LCOUNT, 2, CHARANUM
		SIF COND("一晩開放可能", LCOUNT)
			CFLAG:LCOUNT:49 = CHOICE:1
	NEXT
ELSE
	CFLAG:CHOICE:49 = CHOICE:1
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;乳首選択
;-------------------------------------------------
@RESELECT_ERECT, P, M
#DIM LCOUNT
#DIM CHOICE
#DIMS CHOICES, 2
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
VARSET CHOICES

REDRAW 0

CALL SHOW_CHARA_LIST_PRE, "乳首選択"
CALL SHOW_CHARA_LIST, "乳首選択", P, M

$INPUT_LOOP
INPUTS

SPLIT RESULTS, "/", CHOICES

CHOICE = TOINT(CHOICES:1)
IF CHOICE >= CHARANUM
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

SELECTCASE CHOICES
CASE "乳首肥大無し"
		CLEARBIT TALENT:CHOICE:乳首, 0
		SETBIT TALENT:CHOICE:乳首, 1
CASE "乳首肥大LV１"
		CLEARBIT TALENT:CHOICE:乳首, 1
		SETBIT TALENT:CHOICE:乳首, 2
CASE "乳首肥大LV２"
		CLEARBIT TALENT:CHOICE:乳首, 2
		SETBIT TALENT:CHOICE:乳首, 3
CASE "乳首肥大LV３"
		CLEARBIT TALENT:CHOICE:乳首, 3
		SETBIT TALENT:CHOICE:乳首, 0

CASE "普通乳首"
		CLEARBIT TALENT:CHOICE:乳首, 10
		SETBIT TALENT:CHOICE:乳首, 11
CASE "仮性陥没"
		CLEARBIT TALENT:CHOICE:乳首, 11
		SETBIT TALENT:CHOICE:乳首, 12
CASE "真性陥没"
		CLEARBIT TALENT:CHOICE:乳首, 12
		SETBIT TALENT:CHOICE:乳首, 10

CASE "巨乳輪無し"
		CLEARBIT TALENT:CHOICE:乳首, 20
		SETBIT TALENT:CHOICE:乳首, 21
CASE "巨乳輪LV１"
		CLEARBIT TALENT:CHOICE:乳首, 21
		SETBIT TALENT:CHOICE:乳首, 22
CASE "巨乳輪LV２"
		CLEARBIT TALENT:CHOICE:乳首, 22
		SETBIT TALENT:CHOICE:乳首, 23
CASE "巨乳輪LV３"
		CLEARBIT TALENT:CHOICE:乳首, 23
		SETBIT TALENT:CHOICE:乳首, 20

;それ以外は数値入力へ変換
CASEELSE
	RESULT = TOINT(CHOICES)
	IF RESULT == 100
		SETBIT FLAG:11, 2
		REDRAW 1
		RETURN 0
	ELSEIF RESULT == 2000 && CONFIG("世代別表示")
		M = 0
		P = 0
	ELSEIF RESULT == 2001 && CONFIG("世代別表示")
		M += 1
		P = 0
	ELSEIF RESULT == 2002 && CONFIG("世代別表示")
		M -= 1
		P = 0
	ELSEIF RESULT >= 1001 && RESULT <= 1000+Q && CONFIG("表示人数") && Q >= 2 && P != RESULT - 1001
		P = RESULT - 1001
	ENDIF
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;恋慕or淫乱にしたキャラを一覧表示(名前の異なる上位夢魔などにも個別対応)
;-------------------------------------------------
@SHOW_CHARA_CAPTURED
#DIM LCOUNT
#DIM LCOUNT_2
#DIM NEXT_LINE
PRINTL □ クリアキャラ一覧 □---------------------------------------------------------

;改行の判定
NEXT_LINE = 0

FOR LCOUNT, 1, 99
	VARSET LOCALS
	FOR LCOUNT_2, 0, 5
		SIF GETBIT(FLAG:(1099 + LCOUNT), LCOUNT_2) == 0
			CONTINUE

		LOCALS = %NAMES("種族", LCOUNT, LCOUNT_2)%
		;同じ名前なら飛ばす
		SIF LOCALS == LOCALS:1 || LOCALS == LOCALS:2 || LOCALS == LOCALS:3 || LOCALS == LOCALS:4 || LOCALS == LOCALS:5
			CONTINUE

		SIF FLAG:(1200 + LCOUNT) >= 1
			SETCOLOR DEF_COLOR("イエロー")
		PRINTFORM  %TEXT_LJ(LOCALS, 16)%
		SIF FLAG:(1200 + LCOUNT) >= 1
			RESETCOLOR

		SIF NEXT_LINE%4 == 3
			PRINTL 
		NEXT_LINE += 1
		LOCALS:(LCOUNT_2 + 1) = %LOCALS%
	NEXT
NEXT

;最後のループで改行してなかったら改行
SIF NEXT_LINE%4
	PRINTL 
WAIT

