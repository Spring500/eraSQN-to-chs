;=============================================================================
;召喚事故用ステータス生成処理
;=============================================================================
@CHAR_SAMON_T, ARG
;現在起こりうる事故(ビットで管理)
;0	処女
;1	上級夢魔
;2	夢魔でない
;3	魔法を使える
;4	歌が歌える
;5	変わり種
;6	惚れっぽい
;7	レズ嗜好
;8	尽くすタイプ
;9	倒錯的
;10	高難易度
;11	巨乳
;12 	貧乳
;13 	長身
;14	小柄
;15	獣耳
;16	男の娘
;17	ふたなり

P = 1p10
R = 2
;処女
IF TALENT:处女 == 0
	SIF RAND:R == 0
		P = 1p0
	R += 1
ENDIF
;上位夢魔
IF TALENT:梦魔
	SIF RAND:R == 0
		P = 1p1
	R += 1
ENDIF
;夢魔で無い（これだけ確率が低い）
IF TALENT:梦魔 + TALENT:上位梦魔 && RAND:10 == 0
	SIF RAND:R == 0
		P = 1p2
	R += 1
ENDIF
;魔法使い
IF TALENT:魔法使 == 0
	SIF RAND:R == 0
		P = 1p3
	R += 1
ENDIF
;歌唱技能
IF TALENT:歌唱技能 == 0
	SIF RAND:R == 0
		P = 1p4
	R += 1
ENDIF
;変り種
	SIF RAND:R == 0
		P = 1p5
	R += 1
;惚れっぽい
IF TALENT:纯情 == 0
	SIF RAND:R == 0
		P = 1p6
	R += 1
ENDIF
;レズッ気
IF TALENT:双性恋 == 0
	SIF RAND:R == 0
		P = 1p7
	R += 1
ENDIF
;尽くすタイプ
IF TALENT:献身 == 0
	SIF RAND:R == 0
		P = 1p8
	R += 1
ENDIF
;倒錯的
IF TALENT:性变态 == 0
	SIF RAND:R == 0
		P = 1p9
	R += 1
ENDIF

SIF FLAG:666 && TALENT:梦魔
	P = 1p1

;巨乳
IF TALENT:巨乳 < 2 && RAND:5 == 0
	SETBIT P, 11
;貧乳
ELSEIF TALENT:贫乳 == 0 && RAND:5 == 0
	SETBIT P, 12
ENDIF
;長身
IF TALENT:高挑体型 + TALENT:妖精 == 0 && RAND:5 == 0
	SETBIT P, 13
;小柄
ELSEIF TALENT:娇小体型 == 0 && RAND:5 == 0
	SETBIT P, 14
ENDIF
;獣耳
SIF COND("人外度") == 0 && RAND:5 == 0
	SETBIT P, 15
;男の娘とふたなり
IF PENIS(TARGET) == 0 && RAND:5 == 0
	IF CONFIG("召喚事故：男の娘") && AGE(TARGET) < 3 && RAND:2
		SETBIT P, 16
		CLEARBIT P, 0
		CLEARBIT P, 7
		CLEARBIT P, 11
		CLEARBIT P, 12
		CLEARBIT P, 13
		CLEARBIT P, 17
	ENDIF
	IF CONFIG("召喚事故：ふたなり") && RAND:2
		SETBIT P, 17
		CLEARBIT P, 16
	ENDIF
ENDIF

;召喚の天才
IF TALENT:MASTER:天才召唤师 && FLAG:8
	P = 0
	CALL CHAR_SAMON_T_EX
	SIF P == 0
		RETURN 0
ENDIF

VARSET LOCALS
;ここのツリーではLOCALS:1～LOCALS:5を使う
;上級夢魔
IF GETBIT(P, 1)
	LOCALS:1 = 予定よりも上位の夢魔が召喚されたようだ
	;その種に上位夢魔がいるかをチェック
	IF TALENT:上位梦魔
		LOCALS:2 = …どうやら、大した事故ではなかったようだ
	ELSE
		IF IS_HIGH_RANKER(ARG)
			;名前や能力が変わるかも？
			E = RAND:3 + 2
			CALL HIGH_RANKER, ARG, E
			CALL SET_NEWNAME
		ENDIF
		SIF CFLAG:8 == 0
			CFLAG:8 = RAND:4 + 1
	ENDIF
	ABL:ＬＶ = 0
	MAXBASE:体力 += 500
	MAXBASE:気力 += 300
	TALENT:梦魔 = 0
	TALENT:上位梦魔 = 1
	;TALENT:强硬 = 1
	SIF RAND:10 == 0
		TALENT:禁忌知识 = 1
	SIF RAND:10 == 0
		TALENT:魅力 = 1
	SIF RAND:10 == 0
		TALENT:魅惑 = 1

	IF TALENT:魔法使 == 1
		LOCALS:3 = 既に結構な数の魔法も使えるようだ！
		TALENT:小根术 = 1
		TALENT:高敏术 = 1
		TALENT:精强术 = 1
		TALENT:解强化术 = RAND:2
		TALENT:解弱化术 = RAND:2
		CALL GET_MAGIC, TARGET
	ENDIF
;夢魔でない
ELSEIF GETBIT(P, 2)
	LOCALS:1 = 夢魔となっていない魔物が召喚された
	SIF ARG == 1 || ARG == 2 || ARG == 34 || ARG == 35 || ARG == 48 || ARG == 50
		LOCALS:2 = …………………え？
	MAXBASE:体力 -= 50
	MAXBASE:気力 += 100
	TALENT:上位梦魔 = 0
	TALENT:梦魔 = 0
	TALENT:不重贞操 = 0
	TALENT:不知羞耻 = 0
;魔法を使える
ELSEIF GETBIT(P, 3)
	LOCALS:1 = …魔法を使える夢魔が召喚された
	TALENT:魔法使 = 1
	CALL GET_MAGIC, TARGET
	MAXBASE:気力 += 300
;歌が歌える
ELSEIF GETBIT(P, 4)
	LOCALS:1 =  …歌を歌える夢魔が召喚された
	TALENT:歌唱技能 = 1
	MAXBASE:気力 += 300
;変わり種
ELSEIF GETBIT(P, 5)
	LOCALS:1 =  ちょっと変わり種を召喚してしまった
	MAXBASE:体力 += 100 * RAND:11
	MAXBASE:気力 += 100 * RAND:11

	REPEAT 22
		R = 0
		SELECTCASE COUNT
		CASE 0
			R = 23
		CASE 1
			R = 10
		CASE 2
			R = 25
		CASE 3
			R = 35
		CASE 4
			R = 40
		CASE 5
			R = 42
		CASE 6
			R = 50
		CASE 7
			R = 61
		CASE 8
			R = 11
		CASE 9
			R = 101
		CASE 10
			R = 103
		CASE 11
			R = 105
		CASE 12
			R = 107
		CASE 13
			R = 111
		CASE 14
			R = 32
		CASE 15
			R = 115
		CASE 16
			R = 70
		CASE 17
			R = 81
			;幼稚・小悪魔
			IF RAND:4 == 0
				TALENT:幼稚 = 1
			ELSEIF RAND:4 == 0
				TALENT:小恶魔 = 1
			ENDIF
		CASE 18
			R = 15
		CASE 19
			R = 30
		CASE 20
			;マイナス素質のオンパレード
			IF RAND:2 == 0
				;嫉妬
				SIF RAND:4 == 0
					TALENT:嫉妒 = 1
				SIF RAND:4 == 0
					TALENT:底线明确 = 1
				;抵抗
				SIF RAND:8 == 0
					TALENT:抵抗 = 1
				;感情乏しい
				SIF RAND:8 == 0 && TALENT:感情贫乏 == 0
					TALENT:感情贫乏 = 1
			ENDIF
		CASE 21
			;プラス素質のオンパレード
			;鼓舞
			SIF RAND:4 == 0
				TALENT:鼓舞 = 1
			;針さばき
			SIF RAND:10 == 0
				TALENT:针术 = 1
			;倒錯的
			SIF RAND:10 == 0
				TALENT:性变态 = 1
			SIF RAND:4 == 0
				TALENT:抖Ｓ = 1
			;魅力・両刀
			SIF RAND:4 == 0
				TALENT:魅力 = 1
		ENDSELECT

		SIF RAND:4 || R == 0 || CALCF("逆の素質", R) == 0
			CONTINUE

		SELECTCASE RAND:3
		CASE 2
			TALENT:R = 1
			TALENT:CALCF("逆の素質", R) = 0
		CASE 1
			TALENT:R = 0
			TALENT:CALCF("逆の素質", R) = 0
		CASE 0
			TALENT:R = 0
			TALENT:CALCF("逆の素質", R) = 1
		ENDSELECT
	REND
;惚れっぽい
ELSEIF GETBIT(P, 6)
	LOCALS:1 = ちょっと惚れっぽい夢魔を召還してしまった。
	TALENT:纯情 = 1
;レズ・両刀
ELSEIF GETBIT(P, 7)
	LOCALS:1 = 百合に目覚めてる夢魔が召喚された
	ABL:百合倾向 = 1 + RAND:3
	EXP:百合经验 = 10 * RAND:5 + 1
	IF RAND:2 == 0
		TALENT:讨厌男性 = 1
		TALENT:双性恋 = 0
	ELSE
		TALENT:双性恋 = 1
		TALENT:讨厌男性 = 0
	ENDIF
;尽くすタイプ
ELSEIF GETBIT(P, 8)
	LOCALS:1 = 尽くす事を喜びとする夢魔が召喚された
	MAXBASE:体力 -= 100
	MAXBASE:気力 += 200 * RAND:3
	TALENT:坦率 = 1
	TALENT:聪慧 = 1
	TALENT:献身 = 1
	TALENT:治愈 = 1
	TALENT:鼓舞 = 1

	TALENT:叛逆 = 0
	TALENT:自大 = 0
	TALENT:糊涂 = 0
;倒錯的
ELSEIF GETBIT(P, 9)
	LOCALS:1 = 倒錯的な性格の夢魔が召喚された
	TALENT:性变态 = 1
	IF RAND:3 == 0
		LOCALS:2 = …しかも、マゾ気質のようだ！
		ABL:受虐倾向 += RAND:3 + 1
		SIF ABL:受虐倾向 >= 5
			ABL:受虐倾向 = 5
		ABL:施虐倾向 -= RAND:3
		IF ABL:施虐倾向 <= 0
			ABL:施虐倾向 = 0
			TALENT:抖Ｓ = 0
		ENDIF
		SIF RAND:10 < ABL:受虐倾向
			TALENT:抖Ｍ = 1
	ELSEIF RAND:2 == 0
		LOCALS:2 = …しかも、サド気質のようだ！
		ABL:施虐倾向 += RAND:3 + 1
		SIF ABL:施虐倾向 >= 5
			ABL:施虐倾向 = 5
		ABL:受虐倾向 -= RAND:3
		IF ABL:受虐倾向 <= 0
			ABL:受虐倾向 = 0
			TALENT:抖Ｍ = 0
		ENDIF
		SIF RAND:10 < ABL:施虐倾向
			TALENT:抖Ｓ = 1
	ENDIF
;難易度高
ELSEIF GETBIT(P, 10)
	LOCALS:1 = 一見、普通に見えるが、ちょっと厄介なタイプのようだ
	MAXBASE:体力 += 100 * RAND:6
	MAXBASE:気力 += 300 * RAND:4
	;男嫌い・嫉妬
	IF RAND:4 == 0
		TALENT:讨厌男性 = 1
	ELSEIF RAND:5 == 0
		TALENT:嫉妒 = 1
	ENDIF
	;快感の否定・一線越えない
	IF RAND:8 == 0
		TALENT:否认快感 = 1
		TALENT:CALCF("逆の素質", 71) = 0
	ENDIF
	IF RAND:8 == 0
		TALENT:底线明确 = 1
		TALENT:CALCF("逆の素質", 27) = 0
	ENDIF
	;抑圧・保守的
	IF RAND:6 == 0
		TALENT:压抑 = 1
		TALENT:CALCF("逆の素質", 32) = 0
	ENDIF
	IF RAND:6 == 0
		TALENT:保守 = 1
		TALENT:CALCF("逆の素質", 24) = 0
	ENDIF
	;抵抗・反抗的
	SIF RAND:4 == 0 && TALENT:坦率 == 0
		TALENT:叛逆 = 1
	SIF RAND:4 == 0
		TALENT:抵抗 = 1
	;感情乏しい
	SIF RAND:8 == 0 && TALENT:感情贫乏 == 0
		TALENT:感情贫乏 = 1
ENDIF

;身体のサイズ変化
;LOCALS:6～LOCALS:10を用いる
SIF P&(1p11 + 1p12 + 1p13 + 1p14)
	LOCALS:6 = 同じ種族の中では、
;巨乳
IF GETBIT(P, 11)
	R = 1
	IF TALENT:贫乳
		R += 2
	ELSEIF TALENT:巨乳 == 0
		R += 1
	ENDIF
	R = 1 + RAND:R

	IF R == 1
		LOCALS:6 = %LOCALS:6%その胸の膨らみは豊か
		IF P&(1p13 + 1p14)
			LOCALS:6 = %LOCALS:6%で、
		ELSE
			LOCALS:6 = %LOCALS:6%なようだ
		ENDIF
	ELSE
		LOCALS:6 = %LOCALS:6%その胸の膨らみは群を抜いて
		IF P&(1p13 + 1p14)
			LOCALS:6 = %LOCALS:6%大きく、
		ELSE
			LOCALS:6 = %LOCALS:6%いるようだ
		ENDIF
	ENDIF

	IF (TALENT:贫乳 && R == 3) || TALENT:巨乳 || (TALENT:贫乳 + TALENT:巨乳 == 0 && R == 2)
		CALL CALC, "爆乳化", TARGET
	ELSEIF (TALENT:贫乳 && R == 2) || (TALENT:贫乳 + TALENT:巨乳 == 0 && R == 1)
		CALL CALC, "巨乳化", TARGET
	ELSE
		CALL CALC, "並乳化", TARGET
	ENDIF
;貧乳
ELSEIF GETBIT(P, 12)
	R = 1
	SIF TALENT:巨乳
		R += TALENT:巨乳
	R = 1 + RAND:R

	IF R == 1
		LOCALS:6 = %LOCALS:6%その胸の膨らみは大人しめ
		IF P&(1p13 + 1p14)
			LOCALS:6 = %LOCALS:6%で、
		ELSE
			LOCALS:6 = %LOCALS:6%なようだ
		ENDIF
	ELSE
		LOCALS:6 = %LOCALS:6%その胸の平らさは群を抜いて
		IF P&(1p13 + 1p14)
			LOCALS:6 = %LOCALS:6%おり、
		ELSE
			LOCALS:6 = %LOCALS:6%いるようだ
		ENDIF
	ENDIF

	IF TALENT:巨乳 >= 2 && R == 1
		CALL CALC, "巨乳化", TARGET
	ELSEIF TALENT:巨乳 && R == TALENT:巨乳
		CALL CALC, "並乳化", TARGET
	ELSE
		CALL CALC, "貧乳化", TARGET
	ENDIF
	;パイズリ経験軽減
	EXP:乳交经验 /= 3
ENDIF
;長身
IF GETBIT(P, 13)
	R = 1
	SIF TALENT:娇小体型
		R += 1
	R = 1 + RAND:R

	IF R == 1
		LOCALS:6 = %LOCALS:6%背が高めなようだ
	ELSE
		LOCALS:6 = %LOCALS:6%異常なほど背が高いようだ
	ENDIF
	IF TALENT:娇小体型 && R == 1
		CALL CALC, "普通体型化", TARGET
	ELSE
		CALL CALC, "長身化", TARGET
	ENDIF
;小柄
ELSEIF GETBIT(P, 14)
	R = 1
	SIF TALENT:高挑体型
		R += 1
	R = 1 + RAND:R

	IF R == 1
		LOCALS:6 = %LOCALS:6%ちょっと背が低いようだ
	ELSE
		LOCALS:6 = %LOCALS:6%例外的な身長の低さのようだ…
	ENDIF
	IF TALENT:高挑体型 && R == 1
		CALL CALC, "普通体型化", TARGET
	ELSE
		CALL CALC, "小柄化", TARGET
	ENDIF
ENDIF

;男の娘とふたなり
;LOCALS:7を使う
IF GETBIT(P, 16)
	TALENT:伪娘 = 1
	SIF TALENT:高挑体型
		CALL CALC, "普通体型化", TARGET
	TALENT:女性 = 0
	TALENT:扶她 = 0
	TALENT:男性 = 1
	EXP:Ｖ经验 = 0
	EXP:Ｖ性交经验 = 0
	EXP:膣内精液经验 = 0
	EXP:乳交经验 = 0
	EXP:百合经验 = 0
	EXP:Ｖ扩张经验 = 0
	EXP:Ｖ损伤经验 = 0
	EXP:Ｖ拳交经验 = 0
	LOCALS:7 = …ちょっとまて、これは……まさか…女かと思いきやオトコ？！
ELSEIF GETBIT(P, 17)
	TALENT:女性 = 0
	TALENT:扶她 = 1
	TALENT:男性 = 0
	LOCALS:7 = ！？…ちょっとまて、これは……有ってはならないものが付いてるようだ…！！
ENDIF

;獣耳
;LOCALS:11を使う
IF GETBIT(P, 15)
	TALENT:兽耳 = 1 + RAND:5
	TALENT:尾巴 = TALENT:兽耳
	LOCALS:11 = \@ P == 1p15 ? なんと # さらに \@、%NAMES("動物の種類", TALENT:兽耳)%の耳としっぽが生えている…！？
ENDIF

LOCAL = 0
;簡易処女化
;LOCALS:12,LOCALS:13を使う
IF P == 1
	LOCALS:12 = まだ男性経験のない夢魔が召喚されたようだ
	MAXBASE:体力 -= 200
	MAXBASE:気力 -= 100
	SIF RAND:5 == 0
		TALENT:胆怯 = 1
	SIF RAND:5 == 0
		TALENT:看重贞操 = 1
	SIF RAND:5 == 0
		TALENT:羞耻心强 = 1
	SIF RAND:5 == 0
		TALENT:刚强 = 0
	SIF RAND:5 == 0 || TALENT:不重贞操
		TALENT:不重贞操 = 0
	SIF RAND:5 == 0 || TALENT:羞耻心强
		TALENT:不知羞耻 = 0
	IF RAND:3 == 0
		LOCALS:13 = が、どうやら女性経験ならばあるようだ
		ABL:百合倾向 = 1 + RAND:4
		EXP:百合经验 = 10 * RAND:5+1
		IF RAND:5 == 0
			TALENT:讨厌男性 = 1
		ELSEIF RAND:4 == 0
			TALENT:双性恋 = 1
		ENDIF
	ENDIF
	LOCAL = 1
ELSEIF GETBIT(P, 0) || (RAND:4 == 0 && TALENT:处女 == 0 && TALENT:MASTER:天才召唤师 == 0)
	LOCALS:12 = ……しかも、まだ処女のようだ！！
	LOCAL = 1
ENDIF
IF LOCAL
	TALENT:处女 = 1
	TALENT:生产经历 = 0
	ABL:Ｖ感觉 = 0
	EXP:Ｖ经验 = 0
	EXP:Ｖ性交经验 = 0
	EXP:腰技经验 = 0
	EXP:Ｖ扩张经验 = 0
	BASE:胎児の父親 = 0
ENDIF

;身体のサイズ変化
IF P&(1p11 + 1p12 + 1p13 + 1p14)
	BASE:身長 = 0
	BASE:バスト = 0
	BASE:カップ = 0
	BASE:ウェスト = 0
	BASE:ヒップ = 0
	BASE:身長の追加要素 = 0
	CALL SET_3SIZE, TARGET
ENDIF

IF TALENT:男性 && EXP:绝顶经验
	EXP:射精经验 += EXP:绝顶经验
	EXP:绝顶经验 = 0
ENDIF

;体力、気力が上昇していた場合、全快させる
SIF BASE:体力 != MAXBASE:体力
	BASE:体力 = MAXBASE:体力
SIF BASE:気力 != MAXBASE:気力
	BASE:気力 = MAXBASE:気力

;文章を表示しないならばここまで
SIF FLAG:8 == 0
	RETURN 1

FOR LOCAL, 1, 15
	SIF LOCALS:LOCAL != ""
		PRINTFORMW %LOCALS:LOCAL%
NEXT

RETURN 1


@CHAR_SAMON_T_EX
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT
REDRAW 0
PRINTL どの召喚事故を起こしますか？
PRINTL なお、処女化、胸や身長のサイズの事故は、他と同時に発生可能です
PRINTL
SIF TALENT:处女 == 0
	PRINTFORML  [ 0] \@ GETBIT(P, 0) ? ○ # － \@ 処女
SIF TALENT:梦魔
	PRINTFORML  [ 1] \@ GETBIT(P, 1) ? ○ # － \@ 上位夢魔
SIF TALENT:梦魔 + TALENT:上位梦魔
	PRINTFORML  [ 2] \@ GETBIT(P, 2) ? ○ # － \@ 夢魔でなくなる
SIF TALENT:魔法使 == 0
	PRINTFORML  [ 3] \@ GETBIT(P, 3) ? ○ # － \@ 素質：魔法使い 持ち
SIF TALENT:歌唱技能 == 0
	PRINTFORML  [ 4] \@ GETBIT(P, 4) ? ○ # － \@ 素質：歌唱技能 持ち
	PRINTFORML  [ 5] \@ GETBIT(P, 5) ? ○ # － \@ 変り種（素質などが変）
SIF TALENT:纯情 == 0
	PRINTFORML  [ 6] \@ GETBIT(P, 6) ? ○ # － \@ 惚れっぽい
;レズッ気
SIF TALENT:双性恋 == 0
	PRINTFORML  [ 7] \@ GETBIT(P, 7) ? ○ # － \@ レズ志向
SIF TALENT:献身 == 0
	PRINTFORML  [ 8] \@ GETBIT(P, 8) ? ○ # － \@ 尽くすタイプ
SIF TALENT:性变态 == 0
	PRINTFORML  [ 9] \@ GETBIT(P, 9) ? ○ # － \@ 倒錯的
	PRINTFORML  [10] \@ GETBIT(P, 10) ? ○ # － \@ 調教難易度が高い
SIF TALENT:巨乳 < 2
	PRINTFORML  [11] \@ GETBIT(P, 11) ? ○ # － \@ 巨乳化
SIF TALENT:贫乳 == 0
	PRINTFORML  [12] \@ GETBIT(P, 12) ? ○ # － \@ 貧乳化
SIF TALENT:高挑体型 + TALENT:妖精 == 0
	PRINTFORML  [13] \@ GETBIT(P, 13) ? ○ # － \@ 長身化
SIF TALENT:娇小体型 == 0
	PRINTFORML  [14] \@ GETBIT(P, 14) ? ○ # － \@ 小柄化
SIF COND("人外度") == 0
	PRINTFORML  [15] \@ GETBIT(P, 15) ? ○ # － \@ 獣耳有り
SIF PENIS(TARGET) == 0 && AGE(TARGET) < 3
	PRINTFORML  [16] \@ GETBIT(P, 16) ? ○ # － \@ 男の娘(外見が若いキャラ限定)
SIF PENIS(TARGET) == 0
	PRINTFORML  [17] \@ GETBIT(P, 17) ? ○ # － \@ ふたなり化

PRINTFORML [100] \@ P ? これでＯＫ # 事故は起こさない \@

$INPUT_LOOP
INPUT

LOCAL = RESULT

IF LOCAL == 100
	REDRAW 1
	RETURN 0
ELSEIF LOCAL == 0 && TALENT:处女 == 0
	CLEARBIT P, 16
ELSEIF LOCAL == 1 && TALENT:梦魔
ELSEIF LOCAL == 2 && TALENT:梦魔 + TALENT:上位梦魔
ELSEIF LOCAL == 3 && TALENT:魔法使 == 0
ELSEIF LOCAL == 4 && TALENT:歌唱技能 == 0
ELSEIF LOCAL == 5
ELSEIF LOCAL == 6 && TALENT:纯情 == 0
ELSEIF LOCAL == 7 && TALENT:双性恋 == 0
	CLEARBIT P, 16
ELSEIF LOCAL == 8 && TALENT:献身 == 0
ELSEIF LOCAL == 9 && TALENT:性变态 == 0
ELSEIF LOCAL == 10
ELSEIF LOCAL == 11 && TALENT:巨乳 < 2
	CLEARBIT P, 12
	CLEARBIT P, 16
ELSEIF LOCAL == 12 && TALENT:贫乳 == 0
	CLEARBIT P, 11
	CLEARBIT P, 16
ELSEIF LOCAL == 13 && TALENT:高挑体型 + TALENT:妖精 == 0
	CLEARBIT P, 14
	CLEARBIT P, 16
ELSEIF LOCAL == 14 && TALENT:娇小体型 == 0
	CLEARBIT P, 13
ELSEIF LOCAL == 15 && COND("人外度") == 0
ELSEIF LOCAL == 16 && PENIS(TARGET) == 0 && AGE(TARGET) < 3
	CLEARBIT P, 0
	CLEARBIT P, 7
	CLEARBIT P, 11
	CLEARBIT P, 12
	CLEARBIT P, 13
	CLEARBIT P, 17
ELSEIF LOCAL == 17 && PENIS(TARGET) == 0
	CLEARBIT P, 16
ELSE
	GOTO INPUT_LOOP
ENDIF

INVERTBIT P, LOCAL

IF LOCAL >= 1 && LOCAL <= 10
	FOR LOCAL:1, 1, 11
		SIF LOCAL:1 == LOCAL
			CONTINUE
		CLEARBIT P, LOCAL:1
	NEXT
ENDIF
CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART
