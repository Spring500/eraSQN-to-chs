@EAT_LUNCH

REDRAW 0

PRINTL 
DRAWLINE
PRINTL  □ 酒場『一角獣の角』 □
DRAWLINE
PRINTFORML 普段の食事に何か一品加えて心身を充実させよう！ (所持金:%MONEY_C(MONEY)%)
PRINTFORML ちなみに食事による体力の回復は100％を越えます
PRINTFORML ドランクすると調教の実行値が上昇しますが、悪酔いすると変な行動を偶にとります
PRINTFORML （右端の[解説]をクリックor食事の名称を入力することで解説をON/OFFできます）
;食器で回復量UP
IF ITEM:ミスリルのスプーン
	PRINTFORML ミスリルのスプーンを所持しているので、体力＆精力回復効果が60％高まっています。
ELSEIF ITEM:銀のスプーン
	PRINTFORML 銀のスプーンを所持しているので、体力＆精力回復効果が40％高まっています。
ELSEIF ITEM:陶磁器のスプーン
	PRINTFORML 陶磁器のスプーンを所持しているので、体力＆精力回復効果20％高まっています。
ENDIF

CALL EAT_LUNCH_BUY

@EAT_LUNCH_BUY
#DIM MEMO_LINECOUNT
#DIM PRICE, 100

MEMO_LINECOUNT = LINECOUNT

PRINTL 
;メインディッシュ
PRINTFORM  [ 0] シプリサンド　　　　　＄９０　　%TEXT_LJ("体力小回復＆精力小回復", 33)%
PRINTBUTTON "[解説]", "シプリサンド"
PRINTL 
SIF GETBIT(FLAG:41, 0)
	CALL PRINTL_COLORTEXT, "　　　チーズとたっぷりの玉ネギを乗せたシプリ名物オープンサンド", DEF_COLOR("黄色")
PRICE:0 = 90

PRINTFORM  [ 1] マウンテンステーキ　　＄９０　　%TEXT_LJ("体力中回復", 33)%
PRINTBUTTON "[解説]", "マウンテンステーキ"
PRINTL 
SIF GETBIT(FLAG:41, 1)
	CALL PRINTL_COLORTEXT, "　　　山羊の肉をさまざまなハーブと共に焼いたステーキ", DEF_COLOR("黄色")
PRICE:1 = 90

;軽食やサラダ
PRINTFORM  [10] 木の実のサラダ　　　　＄６０　　%TEXT_LJ("体力小回復＆エオルグン", 33)%
PRINTBUTTON "[解説]", "木の実のサラダ"
PRINTL 
SIF GETBIT(FLAG:41, 10)
	CALL PRINTL_COLORTEXT, "　　　森で採れるナッツをふんだんに使ったサラダ", DEF_COLOR("黄色")
PRICE:10 = 60

PRINTFORM  [11] スモークサーモン　　　＄７５　　%TEXT_LJ("体力小回復＆猫大好き", 33)%
PRINTBUTTON "[解説]", "スモークサーモン"
PRINTL 
SIF GETBIT(FLAG:41, 11)
	CALL PRINTL_COLORTEXT, "　　　鮭の切り身を燻製にした料理。そのままでも美味だが、食材としても価値が高い", DEF_COLOR("黄色")
PRICE:11 = 75

;お菓子やデザート
PRINTFORM  [20] ハニービスケット　　　＄１０　　%TEXT_LJ("体力小回復＆好感度小上昇", 33)%
PRINTBUTTON "[解説]", "ハニービスケット"
PRINTL 
SIF GETBIT(FLAG:41, 20)
	CALL PRINTL_COLORTEXT, "　　　蜂蜜を塗って硬めに焼いた焼き菓子。手軽なおやつとして広く人気がある", DEF_COLOR("黄色")
PRICE:20 = 10

PRINTFORM  [21] ベイクドアップル　　　＄３０　　%TEXT_LJ("体力小回復＆好感度小上昇", 33)%
PRINTBUTTON "[解説]", "ベイクドアップル"
PRINTL 
SIF GETBIT(FLAG:41, 21)
	CALL PRINTL_COLORTEXT, "　　　シナモン等を詰めたリンゴを洋酒で香り付けしてオーブンで焼いたお菓子", DEF_COLOR("黄色")
PRICE:21 = 30

;お酒や飲み物
PRINTFORM  [30] ミョッズ　　　　　　　＄６０　　%TEXT_LJ("ドランク(悪酔い率：並)", 33)%
PRINTBUTTON "[解説]", "ミョッズ"
PRINTL 
SIF GETBIT(FLAG:41, 30)
	CALL PRINTL_COLORTEXT, "　　　蜂蜜を発酵させて作った蜜酒。全国的によく飲まれる一般的なお酒", DEF_COLOR("黄色")
PRICE:30 = 60

PRINTFORM  [31] ファドゥルサワー　　　＄４５　　%TEXT_LJ("ドランク(悪酔い率：高)", 33)%
PRINTBUTTON "[解説]", "ファドゥルサワー"
PRINTL 
SIF GETBIT(FLAG:41, 31)
	CALL PRINTL_COLORTEXT, "　　　ファドゥル麦を発酵させて作った麦酒。庶民に人気がある", DEF_COLOR("黄色")
PRICE:31 = 45

PRINTFORM  [32] マタタビワイン　　　　＄７０　　%TEXT_LJ("ドランク(悪酔い率：並)＆猫大好き", 33)%
PRINTBUTTON "[解説]", "マタタビワイン"
PRINTL 
SIF GETBIT(FLAG:41, 32)
	CALL PRINTL_COLORTEXT, "　　　マタタビの実から作った果実酒。滋養強壮に効果があり、猫の大好物でもある", DEF_COLOR("黄色")
PRICE:32 = 70

IF ITEM:ムーンドロップス
	PRINTFORM  [50] ムーンドロップス　　　
ELSE
	SETCOLOR DEF_COLOR("灰色")
	PRINTPLAINFORM  [×] ムーンドロップス　　　
ENDIF
PRINT 残り
SIF ITEM:ムーンドロップス < 10
	PRINT  
PRINTFORM {ITEM:ムーンドロップス}　　%TEXT_LJ("体力中回復＆好感度大上昇", 33)%
PRINTBUTTON "[解説]", "ムーンドロップス"
PRINTL 
SIF GETBIT(FLAG:41, 50)
	CALL PRINTL_COLORTEXT, "　　　夢魔の好む不思議なキャンディ。夢魔でない者は舐めることが出来ない", DEF_COLOR("黄色")

RESETCOLOR

PRINTFORM [100] 戻る　
PRINTBUTTON "[解説ＡＬＬオン]", "解説ＡＬＬオン"
PRINT 　
PRINTBUTTON "[解説ＡＬＬオフ]", "解説ＡＬＬオフ"
PRINTL 

$INPUT_LOOP

INPUTS

SELECTCASE RESULTS
CASE "解説ＡＬＬオン"
	FLAG:41 = SETBITS(FLAG:41, 0, 63)
CASE "解説ＡＬＬオフ"
	FLAG:41 = 0
;入力する人は居ないだろうが念のため
CASE "×"

CASE "シプリサンド"
	INVERTBIT FLAG:41, 0
CASE "マウンテンステーキ"
	INVERTBIT FLAG:41, 1

CASE "木の実のサラダ"
	INVERTBIT FLAG:41, 10
CASE "スモークサーモン"
	INVERTBIT FLAG:41, 11

CASE "ハニービスケット"
	INVERTBIT FLAG:41, 20
CASE "ベイクドアップル"
	INVERTBIT FLAG:41, 21

CASE "ミョッズ"
	INVERTBIT FLAG:41, 30
CASE "ファドゥルサワー"
	INVERTBIT FLAG:41, 31
CASE "マタタビワイン"
	INVERTBIT FLAG:41, 32

CASE "ムーンドロップス"
	INVERTBIT FLAG:41, 50

CASEELSE
	SELECTCASE TOINT(RESULTS)
	CASE  100
		REDRAW 1
		RETURN 0
	CASE 0 TO 1, 10 TO 11, 20 TO 21, 30 TO 32
		IF MONEY >= PRICE:TOINT(RESULTS)
			MONEY -= PRICE:TOINT(RESULTS)
		ELSE
			PRINTFORML お金が足りません
			GOTO INPUT_LOOP
		ENDIF
	CASE 50
		IF ITEM:ムーンドロップス
			ITEM:ムーンドロップス -= 1
		ELSE
			PRINTFORML ムーンドロップスを持っていません
			GOTO INPUT_LOOP
		ENDIF
	CASEELSE
		GOTO INPUT_LOOP
	ENDSELECT

	;食事フラグＯＮ
	SETBIT FLAG:40, TOINT(RESULTS)
	CALL EAT_LUNCH_2
	;一言イベントの発言フラグリセット
	FLAG:23 = 0
	REDRAW 1
	RETURN 0
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART


;食べたものはFLAG:40で記録されている
@EAT_LUNCH_2
#DIM LCOUNT
;0は体力％、1は精力％、2は好感度、3は悪酔い率
#DIM DYNAMIC LUNCH_BONUS, 5
#DIM MOON_DROP
#DIM CAT_FAVOR

MOON_DROP = 0
CAT_FAVOR = 0

;食事等による体力回復
SELECTCASE NAMES("食事", FLAG:40)
CASE "シプリサンド"
	LUNCH_BONUS:0 = 25
	LUNCH_BONUS:1 = 25
CASE "マウンテンステーキ"
	LUNCH_BONUS:0 = 50

CASE "木の実のサラダ"
	LUNCH_BONUS:0 = 25
CASE "スモークサーモン"
	LUNCH_BONUS:0 = 25
	CAT_FAVOR = 50

CASE "ハニービスケット"
	LUNCH_BONUS:0 = 25
	LUNCH_BONUS:2 = 50
CASE "ベイクドアップル"
	LUNCH_BONUS:0 = 25
	LUNCH_BONUS:2 = 50

CASE "ミョッズ"
	LUNCH_BONUS:3 = 25
CASE "ファドゥルサワー"
	LUNCH_BONUS:3 = 50
CASE "マタタビワイン"
	LUNCH_BONUS:3 = 25
	CAT_FAVOR = 100

CASE "ムーンドロップス"
	LUNCH_BONUS:0 = 50
	;これはムーンドロップ専用処理
	;LUNCH_BONUS:2 = 100
	MOON_DROP = 100
CASEELSE
	RETURN 0
ENDSELECT

;食器で回復量UP
IF ITEM:ミスリルのスプーン
	TIMES LUNCH_BONUS:0, 1.60
	TIMES LUNCH_BONUS:1, 1.60
ELSEIF ITEM:銀のスプーン
	TIMES LUNCH_BONUS:0, 1.40
	TIMES LUNCH_BONUS:1, 1.40
ELSEIF ITEM:陶磁器のスプーン
	TIMES LUNCH_BONUS:0, 1.20
	TIMES LUNCH_BONUS:1, 1.20
ENDIF

FOR LCOUNT, 1, CHARANUM
	;体力回復
	CALL CALC, "体力回復上限無視", MAXBASE:LCOUNT:体力 * LUNCH_BONUS:0 / 100, LCOUNT

	;精力回復
	CALL CALC, "精力回復", MAXBASE:LCOUNT:精力 * LUNCH_BONUS:1 / 100, LCOUNT

	;酔っ払い
	SIF LUNCH_BONUS:3
		CALL ADD_STATE, LCOUNT, "ドランク"

	;MASTERは好感度関係なし
	SIF LCOUNT == MASTER
		CONTINUE

	;猫専用メニュー
	SIF CAT_FAVOR && BASE:LCOUNT:種族 == 6
		CFLAG:LCOUNT:2 += CAT_FAVOR

	;ムーンドロップス
	SIF MOON_DROP && COND("夢魔", LCOUNT)
		CFLAG:LCOUNT:2 += MOON_DROP

	;好感度ＵＰ
	CFLAG:LCOUNT:2 += LUNCH_BONUS:2
NEXT

IF MOON_DROP
	PRINTFORMW 夢魔たちにムーンドロップスを振る舞った！
ELSEIF LUNCH_BONUS:3
	PRINTFORML 皆で%NAMES("食事", FLAG:40)%を楽しんだ！
	CALL PRINT_STR, "ピンク_※ドランク状態だと調教がムードが高い状態で開始されます※_W"
ELSE
	PRINTFORMW 皆で%NAMES("食事", FLAG:40)%に舌鼓を打った！
ENDIF

LOCALS = 
SIF LUNCH_BONUS:0
	LOCALS = %LOCALS%\@ LOCALS != "" ? ＆ # \@体力{LUNCH_BONUS:0}％回復！
SIF LUNCH_BONUS:1
	LOCALS = %LOCALS%\@ LOCALS != "" ? ＆ # \@精力{LUNCH_BONUS:1}％回復！

IF LUNCH_BONUS:2
	LOCALS = %LOCALS%\@ LOCALS != "" ? ＆ # \@好感度{LUNCH_BONUS:2}ＵＰ！
ELSEIF MOON_DROP
	LOCALS = %LOCALS%\@ LOCALS != "" ? ＆ # \@夢魔の好感度{MOON_DROP}ＵＰ！
ELSEIF CAT_FAVOR
	LOCALS = %LOCALS%\@ LOCALS != "" ? ＆ # \@猫の好感度{CAT_FAVOR}ＵＰ！
ENDIF

SIF LOCALS != ""
	PRINTFORMW %LOCALS%


;お酒飲んだら悪酔い判定
IF LUNCH_BONUS:3
	LOCALS = 
	FOR LCOUNT, 1, CHARANUM
		;悪酔い判定(薬毒耐性持ちは悪酔いしづらい)
		IF RAND:100 * (1 + TALENT:LCOUNT:薬毒耐性) < LUNCH_BONUS:3
			CALL ADD_STATE, LCOUNT, "悪酔い"
			LOCALS = %LOCALS%\@ LOCALS != "" ? と # …… \@%CALLNAME:LCOUNT%
		ENDIF
	NEXT
	SIF LOCALS != ""
		PRINTFORMW %LOCALS%は、悪酔いしてしまった…
ENDIF

