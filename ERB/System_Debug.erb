;SHOP画面で88224646でここに来ます
@DEBUG_MODE
#DIM LCOUNT

PRINTL 
IF TARGET
	PRINTFORML  正在编辑的对象：%CALLNAME:TARGET%
	PRINTL  [ 0] CFLAG编辑
	PRINTL  [ 1] ABL编辑
	PRINTL  [ 2] EXP编辑
	PRINTL  [ 3] MARK编辑
	PRINTL  [ 4] TALENT编辑
	PRINTL  [ 5] BASE编辑
	PRINTL  [ 6] JUEL编辑
	PRINTL  [ 7] 尿道扩张的相关设置
	PRINTL  [ 8] 确认CSTR
	PRINTL  [18] ABL、EXP、TALENT、JUEL初始化
	SIF BASE:胎儿 == 0
		SETCOLOR DEF_COLOR("灰色")
	PRINTL  [19] 反转TARGET胎儿的性别（当然得是怀孕中才能用）
	RESETCOLOR
ENDIF
PRINTL  [20] FLAG编辑
PRINTL  [21] 金钱编辑
PRINTL  [22] ITEM编辑
PRINTL  [23] 日期变更

PRINTL  [90] 模拟探索战斗
PRINTL  [99] 修改TARGET
PRINTL [100] 返回上层菜单

$INPUT_LOOP
INPUT

SELECTCASE RESULT
CASE 0 TO 6
	SIF TARGET == 0
		GOTO INPUT_LOOP
CASE 8
	SIF TARGET == 0
		GOTO INPUT_LOOP
	CALL DEBUG_CSTR
	RESTART
CASE 7
	SIF TARGET == 0
		GOTO INPUT_LOOP
	CALL DEBUG_U
	RESTART
CASE 18
	SIF TARGET == 0
		GOTO INPUT_LOOP
	VARSET TALENT
	VARSET EXP
	VARSET ABL
	VARSET JUEL
	PRINTW 初始化完成
	RESTART
CASE 19
	IF TARGET <= 0
		PRINTFORMW 请先选择对象
	ELSEIF BASE:胎儿 == 0
		PRINTFORMW 并非怀孕中
	ELSEIF COND("妊娠中：息子")
		PRINTFORMW 肚子里的孩子已经变成了[女孩子]
		CLEARBIT BASE:胎儿, 20
	ELSE
		PRINTFORMW 肚子里的孩子已经变成了[男孩子]
		SETBIT BASE:胎儿, 20
	ENDIF
	RESTART

CASE 20, 22
CASE 21
	CALL DEBUG_CHANGE_MONEY
	RESTART
CASE 23
	CALL DEBUG_CHANGE_DAY
	RESTART

CASE 90
	;経験の保存。童貞？知らんなぁ～
	FOR LCOUNT, 0, 100
		LOCAL:LCOUNT = EXP:PLAYER:LCOUNT
	NEXT

	CALL SEXBATTLE_TEST

	FOR LCOUNT, 0, 100
		EXP:PLAYER:LCOUNT = LOCAL:LCOUNT
	NEXT
	RESTART
CASE 99
	CALL TARGET_CHANGE_DEBUG_MODE, 0, 0
	RESTART
CASE 100
	RETURN 0
CASEELSE
	GOTO INPUT_LOOP
ENDSELECT

CALL DEBUG_FLAG, RESULT

RESTART

@DEBUG_CSTR
#DIM LCOUNT
;改行するかの判定に使う
#DIM NEXT_LINE
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT

PRINTL 

NEXT_LINE = 0

FOR LCOUNT, 0, 100

	SIF STRLENSU(CSTRNAME:LCOUNT) == 0
		CONTINUE

	NEXT_LINE += 1

	PRINTFORM %BL(1)%[{LCOUNT, 2}] %TEXT_RJ(CSTRNAME:LCOUNT, 16)%%BL(4)%

	SIF NEXT_LINE%3 == 0
		PRINTL 
NEXT

SIF NEXT_LINE%3
	PRINTL 
PRINTFORML [999] 返回上层菜单
PRINTFORML 选择要显示的CSTR

CALL INPUT_SELECT, 100, 999

SELECTCASE RESULT
CASE 999
	RETURN 0
CASEELSE
	SIF CSTRNAME:RESULT != ""
		PRINTFORMW %CSTR:RESULT%
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@DEBUG_U
#DIM MEMO_LINECOUNT

PRINTFORML [{TARGET, 2}]%CALLNAME:TARGET%现在尿道的大小是{CALCF("尿道径")}mm，尿道扩张等级LV{ABL:尿道扩张}
MEMO_LINECOUNT = LINECOUNT
PRINTFORML 要怎么办呢？
PRINTFORML  [ 0]  +1mm
PRINTFORML  [ 1]  +5mm
PRINTFORML  [ 2] +10mm
PRINTFORML  [ 3]  -1mm
PRINTFORML  [ 4]  -5mm
PRINTFORML  [ 5] -10mm
PRINTFORML [100] 完成

CALL INPUT_SELECT, 6, 100

SELECTCASE RESULT
CASE 100
	RETURN 0
CASE 0
	IF PENIS(TARGET)
		CSTR:4 = %ADDNUM_STR(CSTR:4, 1)%
	ELSE
		CSTR:3 = %ADDNUM_STR(CSTR:3, 1)%
	ENDIF
CASE 1
	IF PENIS(TARGET)
		CSTR:4 = %ADDNUM_STR(CSTR:4, 5)%
	ELSE
		CSTR:3 = %ADDNUM_STR(CSTR:3, 5)%
	ENDIF
CASE 2
	IF PENIS(TARGET)
		CSTR:4 = %ADDNUM_STR(CSTR:4, 10)%
	ELSE
		CSTR:3 = %ADDNUM_STR(CSTR:3, 10)%
	ENDIF
CASE 3
	IF PENIS(TARGET)
		CSTR:4 = %ADDNUM_STR(CSTR:4, -1)%
	ELSE
		CSTR:3 = %ADDNUM_STR(CSTR:3, -1)%
	ENDIF
CASE 4
	IF PENIS(TARGET)
		CSTR:4 = %ADDNUM_STR(CSTR:4, -5)%
	ELSE
		CSTR:3 = %ADDNUM_STR(CSTR:3, -5)%
	ENDIF
CASE 5
	IF PENIS(TARGET)
		CSTR:4 = %ADDNUM_STR(CSTR:4, -10)%
	ELSE
		CSTR:3 = %ADDNUM_STR(CSTR:3, -10)%
	ENDIF
ENDSELECT

CALL GET_EXABL_U, TARGET

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@TARGET_CHANGE_DEBUG_MODE, P, M
X = 1
CALL SHOW_CHARA_LIST, "", P, M
PRINTFORML 请选择对象的编号({MASTER}～{CHARANUM - 1})。输入大于{CHARANUM}的编号将会返回上层菜单。

$INPUT_LOOP
INPUT

SIF RESULT < MASTER
	GOTO INPUT_LOOP
SIF RESULT >= CHARANUM
	RETURN 0

TARGET = RESULT

@DEBUG_CHANGE_MONEY
PRINTL 要将金钱修改为多少？

$INPUT_LOOP
INPUT

IF RESULT >= 0
	MONEY = RESULT
	PRINTFORMW 金钱已变为%MONEY_C(MONEY)%。
ELSE
	GOTO INPUT_LOOP
ENDIF


@DEBUG_CHANGE_DAY
#DIM LCOUNT
#DIM MEMO_DAY

MEMO_DAY = DAY

PRINTFORML 请输入日期。现在的日期是{DAY}日
PRINTFORML （以下的日子前后会发生对应的事件）
PRINTFORML [{5 + 120 * (DAY/120)}] 白色情人节
PRINTFORML [{46 + 120 * (DAY/120)}] 海之日
PRINTFORML [{80 + 120 * (DAY/120)}] 万圣节
PRINTFORML [{91 + 120 * (DAY/120)}] 圣诞节前一周
PRINTFORML [{98 + 120 * (DAY/120)}] 圣诞节
PRINTFORML [{99 + 120 * (DAY/120)}] 圣诞节次日
PRINTFORML [{101 + 120 * (DAY/120)}] 正月
PRINTFORML [{114 + 120 * (DAY/120)}] 情人节前一天
PRINTFORML [{115 + 120 * (DAY/120)}] 亲人节
PRINTFORML [{116 + 120 * (DAY/120)}] 情人节次日

$INPUT_LOOP
INPUT

IF RESULT >= 0
	DAY = RESULT
	PRINTFORMW 日期已更改为{DAY}日。
	;未来に飛んだ際には各キャラのご無沙汰の数値を新しいDAYに合わせて変わらないようにする
	IF DAY > MEMO_DAY
		FOR LCOUNT, 2, CHARANUM
			CFLAG:LCOUNT:97 += DAY - MEMO_DAY
		NEXT
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

;ARGに選んだ選択肢番号が入っている
@DEBUG_FLAG, ARG
#DIM LCOUNT
;改行するかの判定に使う
#DIM NEXT_LINE
;変数ごとの最大値
#DIM MAX_VAR
;変更する変数
#DIMS VARS
;変更する変数名
#DIMS VAR_NAME, 1000
;変更する変数の値
#DIM VAR_VALUE, 1000
;選んだ番号
#DIM VAR_CHOSE

NEXT_LINE = 0

PRINTL 
SELECTCASE ARG
CASE 0
	VARS = CFLAG
	MAX_VAR = 999
CASE 1
	VARS = ABL
	MAX_VAR = 99

	FOR LCOUNT, 0, 100
		VAR_NAME:LCOUNT = %ABLNAME:LCOUNT%
		VAR_VALUE:LCOUNT = ABL:LCOUNT

		SELECTCASE LCOUNT
		CASE IS < 40, IS >= 50
			SIF STRLENSU(ABLNAME:LCOUNT) == 0
				CONTINUE

			NEXT_LINE += 1

			IF ABL:LCOUNT >= 100
				PRINTFORM %BL(2)%[{LCOUNT, 2}]%TEXT_RJ(ABLNAME:LCOUNT, 10)%: {ABL:LCOUNT, 4}%BL(4)%
			ELSE
				PRINTFORM %BL(2)%[{LCOUNT, 2}]%TEXT_RJ(ABLNAME:LCOUNT, 10)%: {ABL:LCOUNT, 4}%BL(4)%
			ENDIF

			SIF NEXT_LINE%4 == 0
				PRINTL 
		ENDSELECT
	NEXT

	SIF NEXT_LINE%4
		PRINTL

CASE 2
	VARS = EXP
	MAX_VAR = 99

	FOR LCOUNT, 0, 100
		VAR_NAME:LCOUNT = %EXPNAME:LCOUNT%
		VAR_VALUE:LCOUNT = EXP:LCOUNT

		SIF STRLENSU(EXPNAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM %BL(2)%[{LCOUNT, 2}]%TEXT_RJ(EXPNAME:LCOUNT, 14)%: {EXP:LCOUNT, 5}%BL(4)%

		SIF NEXT_LINE%3 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%3
		PRINTL

CASE 3
	VARS = MARK
	MAX_VAR = 19

	FOR LCOUNT, 0, 20
		VAR_NAME:LCOUNT = %MARKNAME:LCOUNT%
		VAR_VALUE:LCOUNT = MARK:LCOUNT

		SIF STRLENSU(MARKNAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM %BL(1)%[{LCOUNT, 2}]%TEXT_RJ(MARKNAME:LCOUNT, 18)%: LV{MARK:LCOUNT, 3}%BL(10)%

		SIF NEXT_LINE%2 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%2
		PRINTL

CASE 4
	VARS = TALENT
	MAX_VAR = 599

	FOR LCOUNT, 0, 600
		VAR_NAME:LCOUNT = %TALENTNAME:LCOUNT%
		VAR_VALUE:LCOUNT = TALENT:LCOUNT

		SIF TALENT:LCOUNT == 0
			CONTINUE

		SIF STRLENSU(TALENTNAME:LCOUNT) == 0
			CONTINUE

		SELECTCASE TALENT:LCOUNT
		CASE IS >= 2
			LOCALS = %BL(1)%[{LCOUNT, 3}]%TEXT_RJ(TALENTNAME:LCOUNT, 14)%: {TALENT:LCOUNT}
		CASEELSE
			LOCALS = %BL(1)%[{LCOUNT, 3}]%TEXT_RJ(TALENTNAME:LCOUNT, 14)%
		ENDSELECT

		PRINTFORM %LOCALS%
		PRINTL 
	NEXT

CASE 5
	VARS = BASE
	MAX_VAR = 99

	FOR LCOUNT, 0, 100
		VAR_NAME:LCOUNT = %BASENAME:LCOUNT%
		VAR_VALUE:LCOUNT = BASE:LCOUNT

		SIF STRLENSU(BASENAME:LCOUNT) == 0
			CONTINUE

		LOCALS = %BL(1)%[{LCOUNT, 3}]%TEXT_RJ(BASENAME:LCOUNT, 18)%: {BASE:LCOUNT, 5}

		PRINTFORM %LOCALS%

		PRINTL 
	NEXT

CASE 6
	VARS = JUEL
	MAX_VAR = 19

	FOR LCOUNT, 0, 20
		SELECTCASE LCOUNT
		CASE 3, 11 TO 13, 16 TO 19
			CONTINUE
		ENDSELECT

		VAR_NAME:LCOUNT = %PALAMNAME:LCOUNT%之珠
		VAR_VALUE:LCOUNT = JUEL:LCOUNT

		SIF STRLENSU(PALAMNAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM %BL(1)%[{LCOUNT,3}] %PALAMNAME:LCOUNT%之珠: {JUEL:LCOUNT, 8}%BL(4)%

		SIF NEXT_LINE%3 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%3
		PRINTL

CASE 20
	VARS = FLAG
	MAX_VAR = 9999
CASE 22
	VARS = ITEM
	MAX_VAR = 599

	FOR LCOUNT, 0, 600
		VAR_NAME:LCOUNT = %ITEMNAME:LCOUNT%
		VAR_VALUE:LCOUNT = ITEM:LCOUNT

		SELECTCASE LCOUNT
		CASE 50 TO 95, 98, 100 TO 199, 330 TO 349, 400 TO 499
			CONTINUE
		CASE 30, 40, 96, 300, 350, 500
			IF NEXT_LINE%2
				PRINTL 
				NEXT_LINE = 0
			ENDIF
			PRINTL 
		ENDSELECT

		SIF STRLENSU(ITEMNAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM  [{LCOUNT, 3}]%TEXT_RJ(ITEMNAME:LCOUNT, 15)%: {ITEM:LCOUNT, 3}%BL(4)%

		SIF NEXT_LINE%2 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%2
		PRINTL 
ENDSELECT

PRINTFORML [9999] 返回上层菜单
PRINTFORML 输入要更改的%VARS%（最大{MAX_VAR}）
SIF VARS == "BASE"
	PRINTL 体力期气力的最大值会一起变更。

$INPUT_LOOP_1
INPUT

SELECTCASE RESULT
CASE 9999
	RETURN 0
CASE IS < 0, IS > MAX_VAR
	GOTO INPUT_LOOP_1
ENDSELECT

VAR_CHOSE = RESULT

;FLAGとCFLAGには名前がない
SELECTCASE ARG
CASE 0
	PRINTFORML CFLAG:{VAR_CHOSE} = {CFLAG:VAR_CHOSE}的变更
	PRINTL CFLAG可以设置成负值
CASE 20
	PRINTFORML FLAG:{VAR_CHOSE} = {FLAG:VAR_CHOSE}的变更
CASEELSE
	SIF VAR_NAME:VAR_CHOSE == ""
		GOTO INPUT_LOOP_1
	PRINTFORML %VARS%:%VAR_NAME:VAR_CHOSE% = {VAR_VALUE:VAR_CHOSE}的变更
ENDSELECT


$INPUT_LOOP_2
INPUT

SELECTCASE RESULT
CASE IS < 0
	SIF VARS != "CFLAG"
		GOTO INPUT_LOOP_2
ENDSELECT

SELECTCASE ARG
CASE 0
	CFLAG:VAR_CHOSE = RESULT
CASE 1
	ABL:VAR_CHOSE = RESULT
CASE 2
	EXP:VAR_CHOSE = RESULT
CASE 3
	MARK:VAR_CHOSE = RESULT
CASE 4
	TALENT:VAR_CHOSE = RESULT
CASE 5
	BASE:VAR_CHOSE = RESULT
	;体力気力は最大値も変更
	SIF VAR_CHOSE == 0 || VAR_CHOSE == 1
		MAXBASE:VAR_CHOSE = RESULT
CASE 6
	JUEL:VAR_CHOSE = RESULT
CASE 20
	FLAG:VAR_CHOSE = RESULT
CASE 22
	ITEM:VAR_CHOSE = RESULT
ENDSELECT

;FLAGとCFLAGには名前がない
SELECTCASE ARG
CASE 0, 20
	PRINTFORMW %VARS%:{VAR_CHOSE}已变更为{RESULT}
CASEELSE
	PRINTFORMW %VARS%:%VAR_NAME:VAR_CHOSE%已变更为{RESULT}
ENDSELECT


RESTART

@SEXBATTLE_TEST
PRINTL 即将进行探索战斗模拟
PRINTL 要设计几个敌人呢？
PRINTL  [ 0] 一人
PRINTL  [ 1] 二人
PRINTL  [ 2] 三人
PRINTL [999] 还是不要了吧

CALL INPUT_SELECT, 3, 999

SIF RESULT == 999
	RETURN 0

PRINTL 指定敌人的方法：第XX号的上位LV为Yの的对手应输入YXX指定
PRINTL Y为0～4，当为0时可以省略
PRINTL 例如：魅魔的编号为[1]号，上位等级为[0]，那么就直接指定为[1]
PRINTL 阿斯摩太是梦魔族的上位LV3的角色，那么就应该指定为[301]
PRINTL 请格外小心，不要指定超过5个上位角色或不存在的角色
PRINTL 

LOCAL = RESULT

$START_BATTLE_SLG
VARSET LOCAL, 0, 1, 4

PRINTL
PRINTFORML 还剩{LOCAL + 1}个对手需要指定。请输入对手的编号
PRINTFORML (输入[100]返回人数选择，输入[999]结束战斗测试)

INPUT

SIF RESULT == 100
	RESTART
SIF RESULT == 999
	RETURN 0

LOCAL:1 = RESULT

IF LOCAL >= 1
	PRINTFORML 第1个人是%CSVNAME(LOCAL:1%100, 0)%\@ LOCAL:1/100 ? 的上位种LV{LOCAL:1/100} # \@。
	PRINTFORML \@ LOCAL >= 2 ? 还需要选择2人 # 还需要选择1人 \@，请输入对手的编号(输入[100]返回)
	INPUT
	SIF RESULT == 100
		RESTART
	SIF RESULT == 999
		RETURN 0
	LOCAL:2 = RESULT
ENDIF
IF LOCAL >= 2
	PRINTFORML 第2人是%CSVNAME(LOCAL:2%100, 0)%\@ LOCAL:2/100 ? 的上位种LV{LOCAL:2/100} # \@。
	PRINTFORML 还需要选择1人，请输入对手的编号(输入[100]返回)
	INPUT
	SIF RESULT == 100
		RESTART
	SIF RESULT == 999
		RETURN 0
	LOCAL:3 = RESULT
ENDIF
CALL SEXBATTLE_SLG, LOCAL:1, LOCAL:2, LOCAL:3

GOTO START_BATTLE_SLG


@SEXBATTLE_SLG, ARG, ARG:1, ARG:2
#DIM MEMO_TARGET
#DIMS NAME_1
#DIMS NAME_2
#DIMS NAME_3

MEMO_TARGET = TARGET

;戦闘パラメーターの初期化
VARSET TFLAG, 0, 15, 30

;キャラ番号ARGの夢魔を設定
CALL ADD_CHARACTER_EXPLORER, ARG
NAME_1 = %NAME:TARGET%(LV:{ABL:ＬＶ})

SIF ARG:1 > 0
	CALL ADD_CHARACTER_EXPLORER, ARG:1
SIF ARG:2 > 0
	CALL ADD_CHARACTER_EXPLORER, ARG:2

SIF PALAM_EXPLORER("敵キャラ数") == 3
	NAME_3 = %NAME:PALAM_EXPLORER("三人目")%(LV:{ABL:PALAM_EXPLORER("三人目"):90})
SIF PALAM_EXPLORER("敵キャラ数") >= 2
	NAME_2 = %NAME:PALAM_EXPLORER("二人目")%(LV:{ABL:PALAM_EXPLORER("二人目"):90})

SETCOLOR DEF_COLOR("イエロー")

PRINTL

;全員の名前一緒
IF NAME_1 == NAME_2 && NAME_1 == NAME_3
	PRINTFORM %NAME_1%×３
;二人の名前が一緒
ELSEIF NAME_1 == NAME_2
	PRINTFORM %NAME_1%×２\@ PALAM_EXPLORER("敵キャラ数") == 3 ? 和%NAME_3% # \@
ELSEIF NAME_1 == NAME_3
	PRINTFORM %NAME_1%×２和%NAME_2%
ELSEIF PALAM_EXPLORER("敵キャラ数") >= 2 && NAME_2 == NAME_3
	PRINTFORM %NAME_1%和%NAME_2%×２
;全員別
ELSE
	PRINTFORM %NAME_1%
	SIF PALAM_EXPLORER("敵キャラ数") >= 2
		PRINTFORM 和%NAME_2%
	SIF PALAM_EXPLORER("敵キャラ数") >= 3
		PRINTFORM 和%NAME_3%
ENDIF
PRINTFORML 出现了！

RESETCOLOR


;遭遇口上を喋るのは一人目が代表
TRYCALL KOJO_MESSAGE_SEXBATTLE_A, NO:PALAM_EXPLORER("一人目")

;ムード 0,1～4,5～8,9以上でランク分け。
CALL SET_PALAM_EXPLORER, "ムード", TALENT:PLAYER:魅惑+TALENT:PLAYER:谜之魅力+TALENT:PLAYER:魅力

PRINTFORM 精力:{BASE:PALAM_EXPLORER("一人目"):6} 
SIF ARG:1
	PRINTFORM 精力:{BASE:PALAM_EXPLORER("二人目"):6} 
SIF ARG:2
	PRINTFORM 精力:{BASE:PALAM_EXPLORER("三人目"):6}
PRINTL 

$START_BATTLE
SIF BASE:精力 != MAXBASE:精力 || BASE:PLAYER:精力 != MAXBASE:PLAYER:精力
	PRINTFORML 你的精力:{BASE:PLAYER:精力}/{MAXBASE:PLAYER:精力}({BASE:PLAYER:STOCK}/{MAXBASE:PLAYER:STOCK}) , 敌人的精力:{BASE:精力}
;プレイヤーが攻撃選択
CALL SELECTCOM_SEXBATTLE_EXPLORER

;相手がイってない
IF BASE:精力 > 0
	;相手の反撃
	CALL COUNTER_SEXBATTLE_EXPLORER

	;イかされてしまったなら、ストックの判定
	SIF BASE:PLAYER:精力 <= 0
		CALL TEXT_LOSE_EXPLORER

	;勝負がついていないなら続行
	SIF BASE:PLAYER:精力 > 0
		GOTO START_BATTLE
;相手をイかせた
ELSEIF BASE:精力 <= 0
	CALL TEXT_WIN_EXPLORER
	;勝負がついていないなら続行
	SIF BASE:精力 > 0
		GOTO START_BATTLE
ENDIF

PRINTFORML 你的精力{BASE:PLAYER:精力}/{MAXBASE:PLAYER:精力}({BASE:PLAYER:STOCK}/{MAXBASE:PLAYER:STOCK}) , 敌人的精力:{BASE:精力}

CALL DELMONSTER

BASE:PLAYER:精力 = MAXBASE:PLAYER:精力
BASE:PLAYER:STOCK = MAXBASE:PLAYER:STOCK

VARSET TFLAG
TARGET = MEMO_TARGET
