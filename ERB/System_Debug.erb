;SHOP画面で88224646でここに来ます
@DEBUG_MODE
#DIM LCOUNT

PRINTL 
IF TARGET
	PRINTFORML  現在の編集対象：%CALLNAME:TARGET%
	PRINTL  [ 0] CFLAG編集
	PRINTL  [ 1] ABL編集
	PRINTL  [ 2] EXP編集
	PRINTL  [ 3] MARK編集
	PRINTL  [ 4] TALENT編集
	PRINTL  [ 5] BASE編集
	PRINTL  [ 6] JUEL編集
	PRINTL  [ 7] 尿道の拡張度合い変更処理（仮）
	PRINTL  [ 8] CSTR確認
	PRINTL  [18] ABL、EXP、TALENT、JUEL初期化
	SIF BASE:胎児 == 0
		SETCOLOR DEF_COLOR("灰色")
	PRINTL  [19] TARGETの胎児の性別反転（当然妊婦のみ）
	RESETCOLOR
	PRINTL 
ENDIF
PRINTL  [20] FLAG編集
PRINTL  [21] 所持金編集
PRINTL  [22] ITEM編集
PRINTL  [23] 日付変更

PRINTL  [90] 探索戦闘シミュレーター
PRINTL  [99] TARGET変更
PRINTL [100] 戻る

$INPUT_LOOP
INPUT

SELECTCASE RESULT
CASE 0 TO 6
	SIF TARGET == 0
		GOTO INPUT_LOOP
CASE 8
	SIF TARGET == 0
		GOTO INPUT_LOOP
	CALL DEBUG_CSTR
	RESTART
CASE 7
	SIF TARGET == 0
		GOTO INPUT_LOOP
	CALL DEBUG_U
	RESTART
CASE 18
	SIF TARGET == 0
		GOTO INPUT_LOOP
	VARSET TALENT
	VARSET EXP
	VARSET ABL
	VARSET JUEL
	PRINTW 初期化完了
	RESTART
CASE 19
	IF TARGET <= 0
		PRINTFORMW キャラを選んでください
	ELSEIF BASE:胎児 == 0
		PRINTFORMW 妊娠してないようです
	ELSEIF COND("妊娠中：息子")
		PRINTFORMW お腹の中の子供は、『女の子』になりました
		CLEARBIT BASE:胎児, 20
	ELSE
		PRINTFORMW お腹の中の子供は、『男の子』になりました
		SETBIT BASE:胎児, 20
	ENDIF
	RESTART

CASE 20, 22
CASE 21
	CALL DEBUG_CHANGE_MONEY
	RESTART
CASE 23
	CALL DEBUG_CHANGE_DAY
	RESTART

CASE 90
	;経験の保存。童貞？知らんなぁ～
	FOR LCOUNT, 0, 100
		LOCAL:LCOUNT = EXP:PLAYER:LCOUNT
	NEXT

	CALL SEXBATTLE_TEST

	FOR LCOUNT, 0, 100
		EXP:PLAYER:LCOUNT = LOCAL:LCOUNT
	NEXT
	RESTART
CASE 99
	CALL TARGET_CHANGE_DEBUG_MODE, 0, 0
	RESTART
CASE 100
	RETURN 0
CASEELSE
	GOTO INPUT_LOOP
ENDSELECT

CALL DEBUG_FLAG, RESULT

RESTART

@DEBUG_CSTR
#DIM LCOUNT
;改行するかの判定に使う
#DIM NEXT_LINE
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT

PRINTL 

NEXT_LINE = 0

FOR LCOUNT, 0, 100

	SIF STRLENS(CSTRNAME:LCOUNT) == 0
		CONTINUE

	NEXT_LINE += 1

	PRINTFORM  [{LCOUNT, 2}] %TEXT_RJ(CSTRNAME:LCOUNT, 20)%

	SIF NEXT_LINE%3 == 0
		PRINTL 
NEXT

SIF NEXT_LINE%3
	PRINTL 
PRINTFORML [999] 戻る
PRINTFORML 表示したいCSTRを選択してください

CALL INPUT_SELECT, 100, 999

SELECTCASE RESULT
CASE 999
	RETURN 0
CASEELSE
	SIF CSTRNAME:RESULT != ""
		PRINTFORMW %CSTR:RESULT%
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@DEBUG_U
#DIM MEMO_LINECOUNT

PRINTFORML [{TARGET, 2}]%CALLNAME:TARGET%の現在の尿道の太さは{CALCF("尿道径")}mmで、尿道拡張レベルは{ABL:尿道拡張}です
MEMO_LINECOUNT = LINECOUNT
PRINTFORML どうしますか？
PRINTFORML  [ 0]  +1mm
PRINTFORML  [ 1]  +5mm
PRINTFORML  [ 2] +10mm
PRINTFORML  [ 3]  -1mm
PRINTFORML  [ 4]  -5mm
PRINTFORML  [ 5] -10mm
PRINTFORML [100] 終了

CALL INPUT_SELECT, 6, 100

SELECTCASE RESULT
CASE 100
	RETURN 0
CASE 0
	IF PENIS(TARGET)
		CSTR:4 = %ADDNUM_STR(CSTR:4, 1)%
	ELSE
		CSTR:3 = %ADDNUM_STR(CSTR:3, 1)%
	ENDIF
CASE 1
	IF PENIS(TARGET)
		CSTR:4 = %ADDNUM_STR(CSTR:4, 5)%
	ELSE
		CSTR:3 = %ADDNUM_STR(CSTR:3, 5)%
	ENDIF
CASE 2
	IF PENIS(TARGET)
		CSTR:4 = %ADDNUM_STR(CSTR:4, 10)%
	ELSE
		CSTR:3 = %ADDNUM_STR(CSTR:3, 10)%
	ENDIF
CASE 3
	IF PENIS(TARGET)
		CSTR:4 = %ADDNUM_STR(CSTR:4, -1)%
	ELSE
		CSTR:3 = %ADDNUM_STR(CSTR:3, -1)%
	ENDIF
CASE 4
	IF PENIS(TARGET)
		CSTR:4 = %ADDNUM_STR(CSTR:4, -5)%
	ELSE
		CSTR:3 = %ADDNUM_STR(CSTR:3, -5)%
	ENDIF
CASE 5
	IF PENIS(TARGET)
		CSTR:4 = %ADDNUM_STR(CSTR:4, -10)%
	ELSE
		CSTR:3 = %ADDNUM_STR(CSTR:3, -10)%
	ENDIF
ENDSELECT

CALL GET_EXABL_U, TARGET

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@TARGET_CHANGE_DEBUG_MODE, P, M
X = 1
CALL SHOW_CHARA_LIST, "", P, M
PRINTFORML 対象の番号入力({MASTER}～{CHARANUM - 1})。{CHARANUM}以上の値を入力すると前の画面に戻る。

$INPUT_LOOP
INPUT

SIF RESULT < MASTER
	GOTO INPUT_LOOP
SIF RESULT >= CHARANUM
	RETURN 0

TARGET = RESULT

@DEBUG_CHANGE_MONEY
PRINTL 所持金をいくらにしますか？

$INPUT_LOOP
INPUT

IF RESULT >= 0
	MONEY = RESULT
	PRINTFORMW 所持金を%MONEY_C(MONEY)%にしました。
ELSE
	GOTO INPUT_LOOP
ENDIF


@DEBUG_CHANGE_DAY
#DIM LCOUNT
#DIM MEMO_DAY

MEMO_DAY = DAY

PRINTFORML 日付を入力してください。現在は{DAY}日です
PRINTFORML （日付の近いイベント発生日は以下）
PRINTFORML [{5 + 120 * (DAY/120)}] ホワイトデー
PRINTFORML [{46 + 120 * (DAY/120)}] 海の日
PRINTFORML [{80 + 120 * (DAY/120)}] ハロウィン
PRINTFORML [{91 + 120 * (DAY/120)}] クリスマス一週間前
PRINTFORML [{98 + 120 * (DAY/120)}] クリスマス
PRINTFORML [{99 + 120 * (DAY/120)}] クリスマス翌日
PRINTFORML [{101 + 120 * (DAY/120)}] お正月
PRINTFORML [{114 + 120 * (DAY/120)}] バレンタイン前日
PRINTFORML [{115 + 120 * (DAY/120)}] バレンタイン
PRINTFORML [{116 + 120 * (DAY/120)}] バレンタイン翌日

$INPUT_LOOP
INPUT

IF RESULT >= 0
	DAY = RESULT
	PRINTFORMW 日付を{DAY}にしました。
	;未来に飛んだ際には各キャラのご無沙汰の数値を新しいDAYに合わせて変わらないようにする
	IF DAY > MEMO_DAY
		FOR LCOUNT, 2, CHARANUM
			CFLAG:LCOUNT:97 += DAY - MEMO_DAY
		NEXT
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

;ARGに選んだ選択肢番号が入っている
@DEBUG_FLAG, ARG
#DIM LCOUNT
;改行するかの判定に使う
#DIM NEXT_LINE
;変数ごとの最大値
#DIM MAX_VAR
;変更する変数
#DIMS VARS
;変更する変数名
#DIMS VAR_NAME, 1000
;変更する変数の値
#DIM VAR_VALUE, 1000
;選んだ番号
#DIM VAR_CHOSE

NEXT_LINE = 0

PRINTL 
SELECTCASE ARG
CASE 0
	VARS = CFLAG
	MAX_VAR = 999
CASE 1
	VARS = ABL
	MAX_VAR = 99

	FOR LCOUNT, 0, 100
		VAR_NAME:LCOUNT = %ABLNAME:LCOUNT%
		VAR_VALUE:LCOUNT = ABL:LCOUNT

		SELECTCASE LCOUNT
		CASE IS < 40, IS >= 50
			SIF STRLENS(ABLNAME:LCOUNT) == 0
				CONTINUE

			NEXT_LINE += 1

			IF ABL:LCOUNT >= 100
				PRINTFORM  [{LCOUNT, 2}]%TEXT_RJ(ABLNAME:LCOUNT, 10)%:{ABL:LCOUNT}
			ELSE
				PRINTFORM  [{LCOUNT, 2}]%TEXT_RJ(ABLNAME:LCOUNT, 10)%:{ABL:LCOUNT, 2} 
			ENDIF

			SIF NEXT_LINE%4 == 0
				PRINTL 
		ENDSELECT
	NEXT

	SIF NEXT_LINE%4
		PRINTL 
CASE 2
	VARS = EXP
	MAX_VAR = 99

	FOR LCOUNT, 0, 100
		VAR_NAME:LCOUNT = %EXPNAME:LCOUNT%
		VAR_VALUE:LCOUNT = EXP:LCOUNT

		SIF STRLENS(EXPNAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM  [{LCOUNT, 2}]%TEXT_RJ(EXPNAME:LCOUNT, 14)%:{EXP:LCOUNT, 4}

		SIF NEXT_LINE%3 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%3
		PRINTL 
CASE 3
	VARS = MARK
	MAX_VAR = 19

	FOR LCOUNT, 0, 20
		VAR_NAME:LCOUNT = %MARKNAME:LCOUNT%
		VAR_VALUE:LCOUNT = MARK:LCOUNT

		SIF STRLENS(MARKNAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM   [{LCOUNT, 2}]%TEXT_RJ(MARKNAME:LCOUNT, 18)%:LV{MARK:LCOUNT, 2}

		SIF NEXT_LINE%2 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%2
		PRINTL 
CASE 4
	VARS = TALENT
	MAX_VAR = 599

	FOR LCOUNT, 0, 600
		VAR_NAME:LCOUNT = %TALENTNAME:LCOUNT%
		VAR_VALUE:LCOUNT = TALENT:LCOUNT

		SIF TALENT:LCOUNT == 0
			CONTINUE

		SIF STRLENS(TALENTNAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		SELECTCASE TALENT:LCOUNT
		CASE IS >= 2
			LOCALS = %BL(1)%[{LCOUNT, 3}]%TEXT_RJ(TALENTNAME:LCOUNT, 14)%:{TALENT:LCOUNT, 3}
		CASEELSE
			LOCALS = %BL(1)%[{LCOUNT, 3}]%TEXT_RJ(TALENTNAME:LCOUNT, 14)%%BL(4)%
		ENDSELECT

		SELECTCASE STRLENS(LOCALS)
		CASE IS > 24
			SELECTCASE NEXT_LINE%3
			CASE 0
				PRINTL 
			CASE 1
				NEXT_LINE += 2
			CASE 2
				NEXT_LINE += 1
			ENDSELECT
		ENDSELECT

		PRINTFORM %LOCALS%

		SIF NEXT_LINE%3 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%3
		PRINTL 
CASE 5
	VARS = BASE
	MAX_VAR = 99

	FOR LCOUNT, 0, 100
		VAR_NAME:LCOUNT = %BASENAME:LCOUNT%
		VAR_VALUE:LCOUNT = BASE:LCOUNT

		SIF STRLENS(BASENAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		LOCALS = %BL(1)%[{LCOUNT, 3}]%TEXT_RJ(BASENAME:LCOUNT, 14)%:{BASE:LCOUNT, 5}

		SELECTCASE STRLENS(LOCALS)
		CASE IS > 27
			SELECTCASE NEXT_LINE%3
			CASE 0
				PRINTL 
			CASE 1
				NEXT_LINE += 2
			CASE 2
				NEXT_LINE += 1
			ENDSELECT
		ENDSELECT

		PRINTFORM %LOCALS%

		SIF NEXT_LINE%3 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%3
		PRINTL 
CASE 6
	VARS = JUEL
	MAX_VAR = 19

	FOR LCOUNT, 0, 20
		SELECTCASE LCOUNT
		CASE 3, 11 TO 13, 16 TO 19
			CONTINUE
		ENDSELECT

		VAR_NAME:LCOUNT = %PALAMNAME:LCOUNT%の珠
		VAR_VALUE:LCOUNT = JUEL:LCOUNT

		SIF STRLENS(PALAMNAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM   [{LCOUNT,3}] %PALAMNAME:LCOUNT%の珠:{JUEL:LCOUNT, 8}

		SIF NEXT_LINE%3 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%3
		PRINTL 
CASE 20
	VARS = FLAG
	MAX_VAR = 9999
CASE 22
	VARS = ITEM
	MAX_VAR = 599

	FOR LCOUNT, 0, 600
		VAR_NAME:LCOUNT = %ITEMNAME:LCOUNT%
		VAR_VALUE:LCOUNT = ITEM:LCOUNT

		SELECTCASE LCOUNT
		CASE 50 TO 95, 98, 100 TO 199, 330 TO 349, 400 TO 499
			CONTINUE
		CASE 30, 40, 96, 300, 350, 500
			IF NEXT_LINE%2
				PRINTL 
				NEXT_LINE = 0
			ENDIF
			PRINTL 
		ENDSELECT

		SIF STRLENS(ITEMNAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM  [{LCOUNT, 3}]%TEXT_RJ(ITEMNAME:LCOUNT, 22)%:{ITEM:LCOUNT, 3}　

		SIF NEXT_LINE%2 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%2
		PRINTL 
ENDSELECT

PRINTFORML [9999] 戻る
PRINTFORML 変更したい%VARS%を入力してください（最大{MAX_VAR}）
SIF VARS == "BASE"
	PRINTL なお、体力と気力については、最大値も一緒に変更されます

$INPUT_LOOP_1
INPUT

SELECTCASE RESULT
CASE 9999
	RETURN 0
CASE IS < 0, IS > MAX_VAR
	GOTO INPUT_LOOP_1
ENDSELECT

VAR_CHOSE = RESULT

;FLAGとCFLAGには名前がない
SELECTCASE ARG
CASE 0
	PRINTFORML CFLAG:{VAR_CHOSE} = {CFLAG:VAR_CHOSE} の変更
	PRINTL CFLAGに関しては負の値も入力可能です
CASE 20
	PRINTFORML FLAG:{VAR_CHOSE} = {FLAG:VAR_CHOSE} の変更
CASEELSE
	SIF VAR_NAME:VAR_CHOSE == ""
		GOTO INPUT_LOOP_1
	PRINTFORML %VARS%:%VAR_NAME:VAR_CHOSE% = {VAR_VALUE:VAR_CHOSE} の変更
ENDSELECT


$INPUT_LOOP_2
INPUT

SELECTCASE RESULT
CASE IS < 0
	SIF VARS != "CFLAG"
		GOTO INPUT_LOOP_2
ENDSELECT

SELECTCASE ARG
CASE 0
	CFLAG:VAR_CHOSE = RESULT
CASE 1
	ABL:VAR_CHOSE = RESULT
CASE 2
	EXP:VAR_CHOSE = RESULT
CASE 3
	MARK:VAR_CHOSE = RESULT
CASE 4
	TALENT:VAR_CHOSE = RESULT
CASE 5
	BASE:VAR_CHOSE = RESULT
	;体力気力は最大値も変更
	SIF VAR_CHOSE == 0 || VAR_CHOSE == 1
		MAXBASE:VAR_CHOSE = RESULT
CASE 6
	JUEL:VAR_CHOSE = RESULT
CASE 20
	FLAG:VAR_CHOSE = RESULT
CASE 22
	ITEM:VAR_CHOSE = RESULT
ENDSELECT

;FLAGとCFLAGには名前がない
SELECTCASE ARG
CASE 0, 20
	PRINTFORMW %VARS%:{VAR_CHOSE}を {RESULT} に変更しました
CASEELSE
	PRINTFORMW %VARS%:%VAR_NAME:VAR_CHOSE%を {RESULT} に変更しました
ENDSELECT


RESTART

@SEXBATTLE_TEST
PRINTL 探索での戦闘テストを行います
PRINTL 相手は何人にしますか？
PRINTL  [ 0] 一人
PRINTL  [ 1] 二人
PRINTL  [ 2] 三人
PRINTL [999] やめる

CALL INPUT_SELECT, 3, 999

SIF RESULT == 999
	RETURN 0

PRINTL 敵指定の法則：番号XX番の相手の上位レベルYのキャラを指定するときにはYXXとする
PRINTL Yについては0～4であり、0の時には勿論省略してよい
PRINTL たとえば、サキュバスは番号一番で上位レベル0なので、 1 を指定する
PRINTL アスモデはサキュバス族の上位レベル3のキャラなので、 301 を指定すればよい
PRINTL まだ居ないキャラや、5以上の上位レベルを指定するのは自重して下さい
PRINTL 

LOCAL = RESULT

$START_BATTLE_SLG
VARSET LOCAL, 0, 1, 4

PRINTFORML 相手は{LOCAL + 1}人です。相手の番号を入力してください
PRINTFORML (100で人数選択へ戻り、999で戦闘テストを終了します)

INPUT

SIF RESULT == 100
	RESTART
SIF RESULT == 999
	RETURN 0

LOCAL:1 = RESULT

IF LOCAL >= 1
	PRINTFORML 一人目は%CSVNAME(LOCAL:1%100, 0)%\@ LOCAL:1/100 ? の上位種LV{LOCAL:1/100} # \@です
	PRINTFORML \@ LOCAL >= 2 ? あと二人 # もう一人 \@選択してください(100で戻ります)
	INPUT
	SIF RESULT == 100
		RESTART
	SIF RESULT == 999
		RETURN 0
	LOCAL:2 = RESULT
ENDIF
IF LOCAL >= 2
	PRINTFORML 二人目は%CSVNAME(LOCAL:2%100, 0)%\@ LOCAL:2/100 ? の上位種LV{LOCAL:2/100} # \@です
	PRINTFORML もう一人選択してください(100で戻ります)
	INPUT
	SIF RESULT == 100
		RESTART
	SIF RESULT == 999
		RETURN 0
	LOCAL:3 = RESULT
ENDIF
CALL SEXBATTLE_SLG, LOCAL:1, LOCAL:2, LOCAL:3

GOTO START_BATTLE_SLG


@SEXBATTLE_SLG, ARG, ARG:1, ARG:2
#DIM MEMO_TARGET
#DIMS NAME_1
#DIMS NAME_2
#DIMS NAME_3

MEMO_TARGET = TARGET

;戦闘パラメーターの初期化
VARSET TFLAG, 0, 15, 30

;キャラ番号ARGの夢魔を設定
CALL ADD_CHARACTER_EXPLORER, ARG
NAME_1 = %NAME:TARGET%(LV:{ABL:ＬＶ})

SIF ARG:1 > 0
	CALL ADD_CHARACTER_EXPLORER, ARG:1
SIF ARG:2 > 0
	CALL ADD_CHARACTER_EXPLORER, ARG:2

SIF PALAM_EXPLORER("敵キャラ数") == 3
	NAME_3 = %NAME:PALAM_EXPLORER("三人目")%(LV:{ABL:PALAM_EXPLORER("三人目"):90})
SIF PALAM_EXPLORER("敵キャラ数") >= 2
	NAME_2 = %NAME:PALAM_EXPLORER("二人目")%(LV:{ABL:PALAM_EXPLORER("二人目"):90})

SETCOLOR DEF_COLOR("イエロー")

;全員の名前一緒
IF NAME_1 == NAME_2 && NAME_1 == NAME_3
	PRINTFORM %NAME_1%×３
;二人の名前が一緒
ELSEIF NAME_1 == NAME_2
	PRINTFORM %NAME_1%×２\@ PALAM_EXPLORER("敵キャラ数") == 3 ? と%NAME_3% # \@
ELSEIF NAME_1 == NAME_3
	PRINTFORM %NAME_1%×２と%NAME_2%
ELSEIF PALAM_EXPLORER("敵キャラ数") >= 2 && NAME_2 == NAME_3
	PRINTFORM %NAME_1%と%NAME_2%×２
;全員別
ELSE
	PRINTFORM %NAME_1%
	SIF PALAM_EXPLORER("敵キャラ数") >= 2
		PRINTFORM と%NAME_2%
	SIF PALAM_EXPLORER("敵キャラ数") >= 3
		PRINTFORM と%NAME_3%
ENDIF
PRINTFORML が現れた！

RESETCOLOR


;遭遇口上を喋るのは一人目が代表
TRYCALL KOJO_MESSAGE_SEXBATTLE_A, NO:PALAM_EXPLORER("一人目")

;ムード 0,1～4,5～8,9以上でランク分け。
CALL SET_PALAM_EXPLORER, "ムード", TALENT:PLAYER:魅惑+TALENT:PLAYER:謎の魅力+TALENT:PLAYER:魅力

PRINTFORM 精力:{BASE:PALAM_EXPLORER("一人目"):6} 
SIF ARG:1
	PRINTFORM 精力:{BASE:PALAM_EXPLORER("二人目"):6} 
SIF ARG:2
	PRINTFORM 精力:{BASE:PALAM_EXPLORER("三人目"):6}
PRINTL 

$START_BATTLE
SIF BASE:精力 != MAXBASE:精力 || BASE:PLAYER:精力 != MAXBASE:PLAYER:精力
	PRINTFORML あなたの精力:{BASE:PLAYER:精力}/{MAXBASE:PLAYER:精力}({BASE:PLAYER:ストック}/{MAXBASE:PLAYER:ストック}) , 敵の精力:{BASE:精力}
;プレイヤーが攻撃選択
CALL SELECTCOM_SEXBATTLE_EXPLORER

;相手がイってない
IF BASE:精力 > 0
	;相手の反撃
	CALL COUNTER_SEXBATTLE_EXPLORER

	;イかされてしまったなら、ストックの判定
	SIF BASE:PLAYER:精力 <= 0
		CALL TEXT_LOSE_EXPLORER

	;勝負がついていないなら続行
	SIF BASE:PLAYER:精力 > 0
		GOTO START_BATTLE
;相手をイかせた
ELSEIF BASE:精力 <= 0
	CALL TEXT_WIN_EXPLORER
	;勝負がついていないなら続行
	SIF BASE:精力 > 0
		GOTO START_BATTLE
ENDIF

PRINTFORML あなたの精力:{BASE:PLAYER:精力}/{MAXBASE:PLAYER:精力}({BASE:PLAYER:ストック}/{MAXBASE:PLAYER:ストック}) , 敵の精力:{BASE:精力}

CALL DELMONSTER

BASE:PLAYER:精力 = MAXBASE:PLAYER:精力
BASE:PLAYER:ストック = MAXBASE:PLAYER:ストック

VARSET TFLAG
TARGET = MEMO_TARGET
